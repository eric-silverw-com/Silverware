OBJECT Page 1702 Deferral Schedule
{
  OBJECT-PROPERTIES
  {
    Date=11/27/18;
    Time=11:10:42 PM;
    Version List=;
  }
  PROPERTIES
  {
    CaptionML=ENU=Deferral Schedule;
    InsertAllowed=No;
    DeleteAllowed=No;
    LinksAllowed=No;
    SourceTable=Table1701;
    DataCaptionFields=Start Date;
    PageType=Worksheet;
    ShowFilter=No;
    OnOpenPage=BEGIN
                 InitForm;
               END;

    OnInsertRecord=BEGIN
                     Changed := TRUE;
                   END;

    OnModifyRecord=BEGIN
                     Changed := TRUE;
                   END;

    OnDeleteRecord=BEGIN
                     Changed := TRUE;
                   END;

    OnQueryClosePage=VAR
                       DeferralHeader@1003 : Record 1701;
                       DeferralLine@1001 : Record 1702;
                       DeferralUtilities@1005 : Codeunit 1720;
                       EarliestPostingDate@1002 : Date;
                       RecCount@1004 : Integer;
                       ExpectedCount@1006 : Integer;
                     BEGIN
                       // Prevent closing of the window if the sum of the periods does not equal the Amount to Defer
                       IF DeferralHeader.GET("Deferral Doc. Type",
                            "Gen. Jnl. Template Name",
                            "Gen. Jnl. Batch Name",
                            "Document Type",
                            "Document No.","Line No.")
                       THEN BEGIN
                         CALCFIELDS("Schedule Line Total");
                         IF "Schedule Line Total" <> DeferralHeader."Amount to Defer" THEN
                           ERROR(TotalToDeferErr);
                       END;

                       DeferralLine.SETRANGE("Deferral Doc. Type","Deferral Doc. Type");
                       DeferralLine.SETRANGE("Gen. Jnl. Template Name","Gen. Jnl. Template Name");
                       DeferralLine.SETRANGE("Gen. Jnl. Batch Name","Gen. Jnl. Batch Name");
                       DeferralLine.SETRANGE("Document Type","Document Type");
                       DeferralLine.SETRANGE("Document No.","Document No.");
                       DeferralLine.SETRANGE("Line No.","Line No.");

                       RecCount := DeferralLine.COUNT;
                       ExpectedCount := DeferralUtilities.CalcDeferralNoOfPeriods("Calc. Method","No. of Periods","Start Date");
                       IF ExpectedCount <> RecCount THEN
                         FIELDERROR("No. of Periods");

                       DeferralLine.SETFILTER("Posting Date",'>%1',0D);
                       IF DeferralLine.FINDFIRST THEN BEGIN
                         EarliestPostingDate := DeferralLine."Posting Date";
                         IF EarliestPostingDate <> DeferralHeader."Start Date" THEN
                           ERROR(PostingDateErr);
                       END;
                     END;

    ActionList=ACTIONS
    {
      { 8       ;    ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 9       ;1   ;ActionGroup;
                      CaptionML=ENU=Actions }
      { 10      ;2   ;Action    ;
                      Name=CalculateSchedule;
                      CaptionML=ENU=Calculate Schedule;
                      ToolTipML=ENU=Calculate the deferral schedule by which revenue or expense amounts will be distributed over multiple accounting periods.;
                      ApplicationArea=#Suite;
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=CalculateCalendar;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 Changed := CalculateSchedule;
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 2   ;0   ;Container ;
                ContainerType=ContentArea }

    { 1   ;1   ;Group     ;
                GroupType=Group }

    { 3   ;2   ;Field     ;
                ToolTipML=ENU=Specifies the amount to defer per period.;
                ApplicationArea=#Suite;
                SourceExpr="Amount to Defer" }

    { 4   ;2   ;Field     ;
                ToolTipML=ENU=Specifies how the Amount field for each period is calculated. Straight-Line: Calculated per the number of periods, distributed by period length. Equal Per Period: Calculated per the number of periods, distributed evenly on periods. Days Per Period: Calculated per the number of days in the period. User-Defined: Not calculated. You must manually fill the Amount field for each period.;
                ApplicationArea=#Suite;
                SourceExpr="Calc. Method" }

    { 5   ;2   ;Field     ;
                ToolTipML=ENU=Specifies when to start calculating deferral amounts.;
                ApplicationArea=#Suite;
                SourceExpr="Start Date" }

    { 6   ;2   ;Field     ;
                ToolTipML=ENU=Specifies how many accounting periods the total amounts will be deferred to.;
                ApplicationArea=#Suite;
                SourceExpr="No. of Periods" }

    { 7   ;1   ;Part      ;
                Name=DeferralSheduleSubform;
                ApplicationArea=#Suite;
                SubPageLink=Deferral Doc. Type=FIELD(Deferral Doc. Type),
                            Gen. Jnl. Template Name=FIELD(Gen. Jnl. Template Name),
                            Gen. Jnl. Batch Name=FIELD(Gen. Jnl. Batch Name),
                            Document Type=FIELD(Document Type),
                            Document No.=FIELD(Document No.),
                            Line No.=FIELD(Line No.);
                PagePartID=Page1703;
                PartType=Page }

  }
  CODE
  {
    VAR
      TotalToDeferErr@1000 : TextConst 'ENU=The sum of the deferred amounts must be equal to the amount in the Amount to Defer field.';
      Changed@1002 : Boolean;
      DisplayDeferralDocType@1005 : 'Purchase,Sales,G/L';
      DisplayGenJnlTemplateName@1012 : Code[10];
      DisplayGenJnlBatchName@1011 : Code[10];
      DisplayDocumentType@1010 : Integer;
      DisplayDocumentNo@1009 : Code[20];
      DisplayLineNo@1008 : Integer;
      PostingDateErr@1001 : TextConst 'ENU=You cannot specify a posting date that is earlier than the start date.';

    [External]
    PROCEDURE SetParameter@1(DeferralDocType@1001 : Integer;GenJnlTemplateName@1006 : Code[10];GenJnlBatchName@1005 : Code[10];DocumentType@1003 : Integer;DocumentNo@1002 : Code[20];LineNo@1004 : Integer);
    BEGIN
      DisplayDeferralDocType := DeferralDocType;
      DisplayGenJnlTemplateName := GenJnlTemplateName;
      DisplayGenJnlBatchName := GenJnlBatchName;
      DisplayDocumentType := DocumentType;
      DisplayDocumentNo := DocumentNo;
      DisplayLineNo := LineNo;
    END;

    PROCEDURE GetParameter@2() : Boolean;
    BEGIN
      EXIT(Changed OR CurrPage.DeferralSheduleSubform.PAGE.GetChanged)
    END;

    [External]
    PROCEDURE InitForm@3();
    BEGIN
      GET(DisplayDeferralDocType,DisplayGenJnlTemplateName,DisplayGenJnlBatchName,DisplayDocumentType,DisplayDocumentNo,DisplayLineNo);
    END;

    BEGIN
    END.
  }
}

