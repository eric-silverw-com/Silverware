OBJECT Codeunit 89 Sales-Post + Email
{
  OBJECT-PROPERTIES
  {
    Date=11/27/18;
    Time=11:07:50 PM;
    Version List=;
  }
  PROPERTIES
  {
    TableNo=36;
    OnRun=BEGIN
            SalesHeader.COPY(Rec);
            Code;
            Rec := SalesHeader;
          END;

  }
  CODE
  {
    VAR
      PostAndSendInvoiceQst@1001 : TextConst 'ENU=Do you want to post and send the %1?';
      SalesHeader@1003 : Record 36;
      SalesInvHeader@1005 : Record 112;
      SalesCrMemoHeader@1006 : Record 114;
      FileManagement@1009 : Codeunit 419;
      IdentityManagement@1008 : Codeunit 9801;
      HideMailDialog@1007 : Boolean;
      PostAndSaveInvoiceQst@1000 : TextConst 'ENU=Do you want to post and save the %1?';
      NotSupportedDocumentTypeSendingErr@1004 : TextConst 'ENU=The %1 is not posted because sending document of type %1 is not supported.';
      NotSupportedDocumentTypeSavingErr@1002 : TextConst 'ENU=The %1 is not posted because saving document of type %1 is not supported.';

    LOCAL PROCEDURE Code@2();
    VAR
      HideDialog@1000 : Boolean;
      IsHandled@1001 : Boolean;
    BEGIN
      HideDialog := FALSE;
      IsHandled := FALSE;
      OnBeforePostAndEMail(SalesHeader,HideDialog,IsHandled);
      IF IsHandled THEN
        EXIT;

      IF NOT HideDialog THEN
        WITH SalesHeader DO
          CASE "Document Type" OF
            "Document Type"::Invoice,
            "Document Type"::"Credit Memo":
              IF NOT ConfirmPostAndDistribute(SalesHeader) THEN
                EXIT;
            ELSE
              ErrorPostAndDistribute(SalesHeader);
          END;

      OnAfterConfirmPost(SalesHeader);

      CODEUNIT.RUN(CODEUNIT::"Sales-Post",SalesHeader);
      COMMIT;
      SendDocumentReport(SalesHeader);
    END;

    LOCAL PROCEDURE SendDocumentReport@3(VAR SalesHeader@1000 : Record 36);
    VAR
      ShowDialog@1001 : Boolean;
    BEGIN
      WITH SalesHeader DO
        CASE "Document Type" OF
          "Document Type"::Invoice:
            BEGIN
              IF "Last Posting No." = '' THEN
                SalesInvHeader."No." := "No."
              ELSE
                SalesInvHeader."No." := "Last Posting No.";
              SalesInvHeader.FIND;
              SalesInvHeader.SETRECFILTER;
              ShowDialog := (NOT IdentityManagement.IsInvAppId) AND NOT HideMailDialog;
              SalesInvHeader.EmailRecords(ShowDialog);
            END;
          "Document Type"::"Credit Memo":
            BEGIN
              IF "Last Posting No." = '' THEN
                SalesCrMemoHeader."No." := "No."
              ELSE
                SalesCrMemoHeader."No." := "Last Posting No.";
              SalesCrMemoHeader.FIND;
              SalesCrMemoHeader.SETRECFILTER;
              SalesCrMemoHeader.EmailRecords(NOT HideMailDialog);
            END
        END
    END;

    [External]
    PROCEDURE InitializeFrom@10(NewHideMailDialog@1001 : Boolean);
    BEGIN
      HideMailDialog := NewHideMailDialog;
    END;

    LOCAL PROCEDURE ConfirmPostAndDistribute@1(VAR SalesHeader@1000 : Record 36) : Boolean;
    VAR
      PostAndDistributeQuestion@1001 : Text;
      ConfirmOK@1002 : Boolean;
    BEGIN
      IF IdentityManagement.IsInvAppId THEN
        EXIT(TRUE);

      IF FileManagement.IsWebClient THEN
        PostAndDistributeQuestion := PostAndSaveInvoiceQst
      ELSE
        PostAndDistributeQuestion := PostAndSendInvoiceQst;

      ConfirmOK := CONFIRM(PostAndDistributeQuestion,FALSE,SalesHeader."Document Type");

      EXIT(ConfirmOK);
    END;

    LOCAL PROCEDURE ErrorPostAndDistribute@4(VAR SalesHeader@1000 : Record 36);
    VAR
      NotSupportedDocumentType@1001 : Text;
    BEGIN
      IF FileManagement.IsWebClient THEN
        NotSupportedDocumentType := NotSupportedDocumentTypeSavingErr
      ELSE
        NotSupportedDocumentType := NotSupportedDocumentTypeSendingErr;

      ERROR(NotSupportedDocumentType,SalesHeader."Document Type");
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterConfirmPost@6(SalesHeader@1000 : Record 36);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforePostAndEMail@5(VAR SalesHeader@1000 : Record 36;VAR HideDialog@1001 : Boolean;VAR IsHandled@1002 : Boolean);
    BEGIN
    END;

    BEGIN
    END.
  }
}

