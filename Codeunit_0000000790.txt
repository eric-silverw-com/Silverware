OBJECT Codeunit 790 IC Inbox Outbox Subscribers
{
  OBJECT-PROPERTIES
  {
    Date=11/22/17;
    Time=12:00:00 PM;
    Version List=NAVW111.00;
  }
  PROPERTIES
  {
    TableNo=418;
    OnRun=BEGIN
            SETRECFILTER;
            "Line Action" := "Line Action"::Accept;
            MODIFY;
            REPORT.RUN(REPORT::"Complete IC Inbox Action",FALSE,FALSE,Rec);
            RESET;
          END;

  }
  CODE
  {

    [EventSubscriber(Report,513,OnICInboxTransactionCreated)]
    PROCEDURE AcceptOnAfterInsertICInboxTransaction@3(VAR Sender@1000 : Report 513;VAR ICInboxTransaction@1001 : Record 418;PartnerCompanyName@1006 : Text);
    VAR
      CompanyInformation@1002 : Record 79;
      ICPartner@1005 : Record 413;
    BEGIN
      CompanyInformation.GET;
      ICPartner.CHANGECOMPANY(PartnerCompanyName);

      IF NOT ICPartner.GET(CompanyInformation."IC Partner Code") THEN
        EXIT;

      IF ICPartner."Auto. Accept Transactions" THEN
        TASKSCHEDULER.CREATETASK(CODEUNIT::"IC Inbox Outbox Subscribers",0,
          TRUE,PartnerCompanyName,0DT,ICInboxTransaction.RECORDID);
    END;

    [EventSubscriber(Codeunit,427,ICOutboxTransactionCreated)]
    PROCEDURE AcceptOnAfterInsertICOutboxTransaction@6(ICOutboxTransaction@1003 : Record 414);
    VAR
      CompanyInformation@1001 : Record 79;
      ICOutboxExport@1002 : Codeunit 431;
    BEGIN
      CompanyInformation.GET;
      IF CompanyInformation."Auto. Send Transactions" THEN BEGIN
        ICOutboxTransaction."Line Action" := ICOutboxTransaction."Line Action"::"Send to IC Partner";
        ICOutboxTransaction.MODIFY;
        ICOutboxExport.RunOutboxTransactions(ICOutboxTransaction);
      END;
    END;

    BEGIN
    END.
  }
}

