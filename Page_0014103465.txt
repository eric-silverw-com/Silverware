OBJECT Page 14103465 EasyPDF Batch Queues
{
  OBJECT-PROPERTIES
  {
    Date=02/15/18;
    Time=[ 9:47:48 AM];
    Modified=Yes;
    Version List=EZP2.22.00;
  }
  PROPERTIES
  {
    CaptionML=ENU=Easy PDF Batch;
    SaveValues=Yes;
    InsertAllowed=No;
    DeleteAllowed=No;
    SourceTable=Table14103463;
    DataCaptionExpr=EasyPDFQueueHeader.BatchId;
    SourceTableView=WHERE(EntryType=CONST(Header));
    PageType=Document;
    PromotedActionCategoriesML=ENU=New,Process,Report,Set Delivery Method;
    OnInit=VAR
             FileMgt@1000000000 : Codeunit 419;
           BEGIN

             EasyPDF.GetSetup(EasyPDFSetup);
             IsWindowsClient := FileMgt.IsWindowsClient();
           END;

    OnOpenPage=BEGIN

                 EasyPDFQueueHeader.INIT;
                 EasyPDFQueueHeader.SETVIEW('SORTING(CreatedDate) ORDER(Descending)');
                 EasyPDFQueueHeader.SETRANGE(EntryType, EasyPDFQueueHeader.EntryType::Header);
                 EasyPDFQueueHeader.SETRANGE(Processed, FALSE);
                 EasyPDFQueueHeader.SETRANGE(BatchId, GETFILTER(BatchId));

                 IF NOT EasyPDFQueueHeader.FIND('-') THEN
                 BEGIN
                   EasyPDFQueueHeader.SETRANGE(BatchId);
                   IF NOT EasyPDFQueueHeader.FIND('-') THEN
                     EasyPDFQueueHeader.BatchId := TEXT0002;
                 END;

                 SETRANGE(BatchId, EasyPDFQueueHeader.BatchId);

                 CountAttachments();
               END;

    ActionList=ACTIONS
    {
      { 1000000008;  ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 1000000011;1 ;Action    ;
                      Name=SendBatch;
                      CaptionML=ENU=Send Batch;
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=SendTo;
                      PromotedCategory=Process;
                      OnAction=BEGIN

                                 IF EasyPDFQueueHeader.EntryNo <> 0 THEN BEGIN

                                   CheckEmailAddresses(FALSE);

                                   Rec.SETVIEW(CurrPage.EasyPDFQueueListPart.PAGE.GetView());  // retrieve the table view from the listpart
                                   EasyPDF.SendBatch(EasyPDFQueueHeader.BatchId, Rec, EasyPDFQueueHeader.ConsolidateEmail);

                                   IF COUNT = 0 THEN BEGIN

                                     EasyPDFQueueHeader.INIT;
                                     EasyPDFQueueHeader.SETRANGE(EntryType, EntryType::Header);
                                     IF EasyPDFQueueHeader.FIND('-') THEN ;

                                     SETRANGE(BatchId, EasyPDFQueueHeader.BatchId);
                                     CountAttachments();

                                     CurrPage.UPDATE;

                                   END;

                                 END;
                               END;
                                }
      { 1000000009;1 ;Action    ;
                      Name=DeleteBatch;
                      CaptionML=ENU=Delete Batch;
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=Delete;
                      PromotedCategory=Process;
                      OnAction=BEGIN

                                 IF EasyPDFQueueHeader.EntryNo <> 0 THEN BEGIN

                                   IF CONFIRM(TEXT0001) THEN BEGIN

                                     EasyPDF.DeleteBatch(EasyPDFQueueHeader.BatchId);

                                     EasyPDFQueueHeader.RESET;
                                     EasyPDFQueueHeader.SETRANGE(EntryType, EntryType::Header);
                                     EasyPDFQueueHeader.SETRANGE(Processed, FALSE);
                                     IF NOT EasyPDFQueueHeader.FIND('-') THEN
                                     BEGIN
                                       CLEAR(EasyPDFQueueHeader);
                                       EasyPDFQueueHeader.BatchId := TEXT0002;
                                     END;

                                     SETRANGE(BatchId, EasyPDFQueueHeader.BatchId);
                                     CountAttachments();

                                     CurrPage.UPDATE;

                                   END;

                                 END;
                               END;
                                }
      { 1000000018;1 ;Action    ;
                      Name=SelectBatch;
                      CaptionML=ENU=Select Batch;
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=SelectEntries;
                      PromotedCategory=Process;
                      OnAction=BEGIN

                                 SelectBatch();
                               END;
                                }
      { 1000000012;1 ;Action    ;
                      Name=CheckEmailAddresses;
                      CaptionML=ENU=Check E-Mail Addresses;
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=Approve;
                      PromotedCategory=Process;
                      OnAction=BEGIN

                                 CheckEmailAddresses(TRUE);
                               END;
                                }
      { 1000000016;1 ;Action    ;
                      Name=AddAttachments;
                      CaptionML=ENU=Add Attachments;
                      Promoted=Yes;
                      Visible=IsWindowsClient;
                      PromotedIsBig=Yes;
                      Image=AddAction;
                      PromotedCategory=Process;
                      OnAction=BEGIN

                                 ShowAttachmentsPage(EasyPDFQueueHeader.EntryNo, 'Additional Attachments');
                               END;
                                }
      { 1000000031;1 ;ActionGroup;
                      Name=Set Delivery Method;
                      ActionContainerType=NewDocumentItems }
      { 1000000030;2 ;Action    ;
                      Name=Print;
                      Promoted=Yes;
                      Image=Print;
                      PromotedCategory=Category4;
                      OnAction=BEGIN

                                 EasyPDFQueueRec.RESET;
                                 CurrPage.EasyPDFQueueListPart.PAGE.SetSelectionFilterEx(EasyPDFQueueRec);

                                 IF EasyPDFQueueHeader.EntryNo <> 0 THEN
                                   IF CONFIRM(TEXT0003) THEN
                                     EasyPDF.UpdateBatchDeliveryMethod( BatchId, EasyPDFQueueRec, EasyPDFQueue.DeliveryMethod::Print );
                               END;
                                }
      { 1000000029;2 ;Action    ;
                      Name=E-Mail;
                      Promoted=Yes;
                      Image=SendTo;
                      PromotedCategory=Category4;
                      OnAction=BEGIN

                                 EasyPDFQueueRec.RESET;
                                 CurrPage.EasyPDFQueueListPart.PAGE.SetSelectionFilterEx(EasyPDFQueueRec);

                                 IF EasyPDFQueueHeader.EntryNo <> 0 THEN
                                   IF CONFIRM(TEXT0004) THEN
                                     EasyPDF.UpdateBatchDeliveryMethod( BatchId, EasyPDFQueueRec, EasyPDFQueue.DeliveryMethod::Email );
                               END;
                                }
      { 1000000028;2 ;Action    ;
                      Name=FAX;
                      Promoted=Yes;
                      Image=PrintForm;
                      PromotedCategory=Category4;
                      OnAction=BEGIN

                                 EasyPDFQueueRec.RESET;
                                 CurrPage.EasyPDFQueueListPart.PAGE.SetSelectionFilterEx(EasyPDFQueueRec);

                                 IF EasyPDFQueueHeader.EntryNo <> 0 THEN
                                   IF CONFIRM(TEXT0005) THEN
                                     EasyPDF.UpdateBatchDeliveryMethod( BatchId, EasyPDFQueueRec, EasyPDFQueue.DeliveryMethod::Fax );
                               END;
                                }
      { 1000000019;2 ;Action    ;
                      Name=No Action;
                      Promoted=Yes;
                      Image=VoidElectronicDocument;
                      PromotedCategory=Category4;
                      OnAction=BEGIN

                                 EasyPDFQueueRec.RESET;
                                 CurrPage.EasyPDFQueueListPart.PAGE.SetSelectionFilterEx(EasyPDFQueueRec);

                                 IF EasyPDFQueueHeader.EntryNo <> 0 THEN
                                   IF CONFIRM(TEXT0006) THEN
                                     EasyPDF.UpdateBatchDeliveryMethod( BatchId, EasyPDFQueueRec, EasyPDFQueue.DeliveryMethod::"No Action" );
                               END;
                                }
      { 1000000020;1 ;Action    ;
                      Name=ShowDocument;
                      ShortCutKey=Shift+F5;
                      CaptionML=ENU=Show Document;
                      Promoted=Yes;
                      Visible=IsWindowsClient;
                      PromotedIsBig=Yes;
                      Image=Document;
                      PromotedCategory=Report;
                      OnAction=VAR
                                 Q@1100774000 : Record 14103463;
                               BEGIN

                                 InvokeActions('^+L', '^+{F6}');
                               END;
                                }
      { 1000000021;1 ;Action    ;
                      Name=ShowRecipient;
                      ShortCutKey=Ctrl+F5;
                      CaptionML=ENU=Show Recipient;
                      Promoted=Yes;
                      Visible=IsWindowsClient;
                      PromotedIsBig=Yes;
                      Image=View;
                      PromotedCategory=Report;
                      OnAction=BEGIN

                                 InvokeActions('^+L', '^+{F7}');
                               END;
                                }
      { 1000000022;1 ;Action    ;
                      Name=ShowReport;
                      ShortCutKey=Shift+Ctrl+F5;
                      CaptionML=ENU=Show Report;
                      Promoted=Yes;
                      Visible=IsWindowsClient;
                      PromotedIsBig=Yes;
                      Image=Report;
                      PromotedCategory=Report;
                      OnAction=BEGIN

                                 InvokeActions('^+L', '^+{F8}');
                               END;
                                }
      { 1000000005;1 ;ActionGroup;
                      Name=Subpage Actions;
                      ActionContainerType=NewDocumentItems }
      { 1000000002;2 ;Action    ;
                      Name=DoShowDocument;
                      ShortCutKey=Shift+Ctrl+F6;
                      Visible=IsWindowsClient;
                      OnAction=BEGIN

                                 IF EasyPDFQueueHeader.EntryNo = 0 THEN
                                   EXIT;

                                 ViewStr := CurrPage.EasyPDFQueueListPart.PAGE.GetSelectedView();

                                 IF STRPOS(ViewStr,'EntryNo=') = 0 THEN
                                   EXIT;

                                 EasyPDFQueue.RESET;
                                 EasyPDFQueue.SETVIEW(ViewStr);
                                 IF EasyPDFQueue.FINDSET AND (EasyPDFQueue.DocNo <> '') THEN
                                 BEGIN
                                   EasyPDFCustomization.SetPrimaryKey(EasyPDFQueue.PrimaryKey);
                                   EasyPDFCustomization.ShowDocument(EasyPDFQueue.DocCode, EasyPDFQueue.DocNo);
                                 END;
                               END;
                                }
      { 1000000003;2 ;Action    ;
                      Name=DoShowRecipient;
                      ShortCutKey=Shift+Ctrl+F7;
                      Visible=IsWindowsClient;
                      OnAction=BEGIN

                                 IF EasyPDFQueueHeader.EntryNo = 0 THEN
                                   EXIT;

                                 ViewStr := CurrPage.EasyPDFQueueListPart.PAGE.GetSelectedView();

                                 IF STRPOS(ViewStr,'EntryNo=') = 0 THEN
                                   EXIT;

                                 EasyPDFQueue.RESET;
                                 EasyPDFQueue.SETVIEW(ViewStr);
                                 IF EasyPDFQueue.FINDSET AND (EasyPDFQueue.DocNo <> '') THEN
                                 BEGIN
                                   EasyPDFCustomization.SetPrimaryKey(EasyPDFQueue.PrimaryKey);
                                   EasyPDFCustomization.ShowRecipient(EasyPDFQueue.DocCode, EasyPDFQueue.DocNo);
                                 END;
                               END;
                                }
      { 1000000004;2 ;Action    ;
                      Name=DoShowReport;
                      ShortCutKey=Shift+Ctrl+F8;
                      Visible=IsWindowsClient;
                      OnAction=VAR
                                 Language@1000000000 : Record 8;
                                 ERR001@1000000001 : TextConst 'ENU=Batch language mismatch';
                               BEGIN

                                 IF EasyPDFQueueHeader.EntryNo = 0 THEN
                                   EXIT;

                                 IF EasyPDFQueueHeader.LanguageCode <> Language.GetUserLanguage() THEN
                                   ERROR(ERR001);

                                 ViewStr := CurrPage.EasyPDFQueueListPart.PAGE.GetSelectedView();

                                 IF STRPOS(ViewStr,'EntryNo=') = 0 THEN
                                   EXIT;

                                 EasyPDFQueue.RESET;
                                 EasyPDFQueue.SETVIEW(ViewStr);
                                 IF EasyPDFQueue.FINDSET AND (EasyPDFQueue.DocNo <> '') THEN
                                 BEGIN
                                   EasyPDF.GetDocumentSetup(EasyPDFDocument, EasyPDFQueue.DocCode);
                                   IF EasyPDFCustomization.InitializeRecord(
                                       EasyPDFQueue.DocCode,
                                       EasyPDFQueue.DocNo,
                                       EasyPDFQueueHeader.BatchId,
                                       EasyPDFQueue.PrimaryKey) THEN
                                     EasyPDFCustomization.ReportExecute(EasyPDFQueue.DocCode, EasyPDFDocument."Report ID");
                                 END;
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1000000000;;Container;
                CaptionML=ENU=General;
                ContainerType=ContentArea }

    { 1000000027;1;Group  ;
                CaptionML=ENU=Options;
                GroupType=Group }

    { 1000000014;2;Field  ;
                Name=BatchId;
                ExtendedDatatype=None;
                Lookup=Yes;
                CaptionML=ENU=Batch Name;
                SourceExpr=EasyPDFQueueHeader.BatchId;
                Importance=Promoted;
                Editable=FALSE;
                LookupPageID=EasyPDF Queue;
                OnLookup=VAR
                           EasyPDFQueuePage@1000000002 : Page 14103466;
                         BEGIN

                           SelectBatch();
                           Text := EasyPDFQueueHeader.BatchId;
                           CurrPage.UPDATE;
                           EXIT(TRUE);
                         END;
                          }

    { 1000000006;2;Field  ;
                Name=AttachmentCount;
                CaptionML=ENU=Additional Attachments;
                SourceExpr=AttachmentCount;
                Editable=FALSE;
                LookupPageID=EasyPDF Attachment;
                MultiLine=Yes;
                OnLookup=BEGIN

                           ShowAttachmentsPage(EasyPDFQueueHeader.EntryNo, 'Additional Attachments');
                           Text := FORMAT(AttachmentCount);
                           EXIT(TRUE);
                         END;
                          }

    { 1000000013;2;Field  ;
                Name=ConsolidateEmail;
                CaptionML=ENU=Consolidate Email;
                SourceExpr=EasyPDFQueueHeader.ConsolidateEmail;
                OnValidate=BEGIN

                             IF EasyPDFQueueHeader.EntryNo <> 0 THEN
                               EasyPDFQueueHeader.MODIFY;
                           END;
                            }

    { 1000000015;2;Field  ;
                Name=MergePdfAttachments;
                CaptionML=ENU=Merge PDF Attachments;
                SourceExpr=EasyPDFQueueHeader.MergePdfAttachments;
                OnValidate=BEGIN

                             IF EasyPDFQueueHeader.EntryNo <> 0 THEN
                               EasyPDFQueueHeader.MODIFY;
                           END;
                            }

    { 1000000001;1;Part   ;
                Name=EasyPDFQueueListPart;
                CaptionML=ENU=Queue;
                SubPageView=SORTING(EntryNo)
                            ORDER(Ascending)
                            WHERE(EntryType=CONST(Line));
                SubPageLink=BatchId=FIELD(BatchId);
                PagePartID=Page37062302;
                Editable=TRUE;
                PartType=Page;
                ShowFilter=Yes }

  }
  CODE
  {
    VAR
      EasyPDF@1000000001 : Codeunit 37062300;
      EasyPDFCustomization@1000000004 : Codeunit 14103460;
      EasyPDFQueue@1000000002 : Record 14103463;
      EasyPDFQueueHeader@1000000009 : Record 14103463;
      EasyPDFSetup@1000000006 : Record 14103460;
      TEXT0001@1000000007 : TextConst 'ENU=Please confirm batch delete';
      EasyPDFAttachment@1000000011 : Record 14103466;
      EasyPDFDocument@1000000012 : Record 14103461;
      AttachmentCount@1000000010 : Integer;
      TEXT0002@1100774000 : TextConst 'ENU=Undefined';
      ViewStr@1000000005 : Text[255];
      AddAttachmentsEnabled@1000000000 : Boolean;
      EasyPDFQueueRec@1000000003 : Record 14103463;
      TEXT0003@1000000008 : TextConst 'ENU=Change the delivery method of the selected batch entries to Print?';
      TEXT0004@1000000013 : TextConst 'ENU=Change the delivery method of the selected batch entries to E-Mail?';
      TEXT0005@1000000014 : TextConst 'ENU=Change the delivery method of the selected batch entries to FAX?';
      TEXT0006@1000000015 : TextConst 'ENU=Change the delivery method of the selected batch entries to No Action?';
      IsWindowsClient@1000000016 : Boolean INDATASET;

    PROCEDURE CheckEmailAddresses@1000000000(ShowResult@1000000009 : Boolean);
    VAR
      EmailTo@1000000001 : Text[250];
      EmailCc@1000000002 : Text[250];
      EmailBcc@1000000003 : Text[250];
      Window@1000000006 : Dialog;
      CounterTotal@1000000005 : Integer;
      Counter@1000000004 : Integer;
      Text001@1000000008 : TextConst 'ENU=Checking queue entries  #1###################### @2@@@@@@@@@@@@@';
      Text002@1000000007 : TextConst 'ENU=E-Mail Addresses are valid';
    BEGIN

      IF EasyPDFQueueHeader.EntryNo = 0 THEN
        EXIT;

      EasyPDFQueue.RESET;
      EasyPDFQueue.SETVIEW(CurrPage.EasyPDFQueueListPart.PAGE.GetView());
      EasyPDFQueue.SETRANGE(BatchId, EasyPDFQueueHeader.BatchId);
      EasyPDFQueue.SETRANGE(EasyPDFQueue.EntryType, EasyPDFQueue.EntryType::Line);
      EasyPDFQueue.SETRANGE(EasyPDFQueue.DeliveryMethod, EasyPDFQueue.DeliveryMethod::Email);
      EasyPDFQueue.SETRANGE(Processed, FALSE);

      IF EasyPDFQueue.FIND('-') THEN
      BEGIN

        CounterTotal := EasyPDFQueue.COUNT;
        Window.OPEN(Text001);

        REPEAT

          Counter := Counter + 1;
          Window.UPDATE(1,EasyPDFQueue.DocNo+','+EasyPDFQueue.RecipientName);
          Window.UPDATE(2,ROUND(Counter / CounterTotal * 10000,1));

          EmailTo := '';
          EmailCc := '';
          EmailBcc := '';

          EasyPDFCustomization.InitializeRecord(EasyPDFQueue.DocCode, EasyPDFQueue.DocNo, '', EasyPDFQueue.PrimaryKey);
          EasyPDFCustomization.GetEmailRecipients(EasyPDFQueue.DocCode, EasyPDFQueue.DocNo, EmailTo, EmailCc, EmailBcc);

          IF (EmailTo <> EasyPDFQueue.RecipientAddress) THEN
            IF EasyPDFQueue.AddressSpecified THEN
            BEGIN
              EmailTo := EasyPDFQueue.RecipientAddress;
            END ELSE BEGIN
              EasyPDFQueue.RecipientAddress := EmailTo;
              EasyPDFQueue.MODIFY;
            END;

          IF STRLEN(EmailTo) = 0 THEN
            IF EasyPDFQueue.MailTransport = EasyPDFQueue.MailTransport::Smtp THEN
              IF EasyPDFSetup.SilentlyIgnoreEmptyAddresses THEN
                EmailTo := 'silently@ignore.me';

          EasyPDF.CheckValidEmailAddresses(EmailTo,STRSUBSTNO('Document# %1',EasyPDFQueue.DocNo));
          IF (EmailCc <> '') THEN
            EasyPDF.CheckValidEmailAddresses(EmailCc,STRSUBSTNO('Document# %1',EasyPDFQueue.DocNo));
          IF (EmailBcc <> '') THEN
            EasyPDF.CheckValidEmailAddresses(EmailBcc,STRSUBSTNO('Document# %1',EasyPDFQueue.DocNo));

        UNTIL EasyPDFQueue.NEXT = 0;

        Window.CLOSE;

      END;

      IF ShowResult THEN
        MESSAGE(Text002);
    END;

    PROCEDURE CountAttachments@1000000003();
    BEGIN

      AttachmentCount := 0;
      IF EasyPDFQueueHeader.EntryNo = 0 THEN
        EXIT;

      EasyPDFQueueHeader.CALCFIELDS(Attachments);
      AttachmentCount := EasyPDFQueueHeader.Attachments;
    END;

    PROCEDURE ShowAttachmentsPage@1000000004(EntryNo@1000000001 : Integer;SubTitle@1000000002 : Text[100]);
    VAR
      EasyPDFAttachmentsPage@1000000000 : Page 14103469;
    BEGIN

      IF EasyPDFQueueHeader.EntryNo = 0 THEN
        EXIT;

      EasyPDFAttachment.RESET;
      EasyPDFAttachment.SETRANGE(AttachmentType, EasyPDFAttachment.AttachmentType::Batch);
      EasyPDFAttachment.SETFILTER(BatchId, EasyPDFQueueHeader.BatchId);
      EasyPDFAttachment.SETRANGE(BatchEntryNo, EasyPDFQueueHeader.EntryNo);

      EasyPDFAttachmentsPage.SETTABLEVIEW(EasyPDFAttachment);
      EasyPDFAttachmentsPage.CAPTION := EasyPDFQueueHeader.BatchId + ' - ' + SubTitle;
      EasyPDFAttachmentsPage.SetAddAsBlob(TRUE);
      EasyPDFAttachmentsPage.RUNMODAL();

      CountAttachments();
    END;

    PROCEDURE SelectBatch@1000000034();
    VAR
      EasyPDFQueuePage@1000000000 : Page 14103466;
    BEGIN

      EasyPDFQueueHeader.RESET;
      EasyPDFQueueHeader.SETRANGE(EntryType,EasyPDFQueueHeader.EntryType::Header);
      EasyPDFQueuePage.SETTABLEVIEW(EasyPDFQueueHeader);
      EasyPDFQueuePage.LOOKUPMODE(TRUE);

      IF EasyPDFQueuePage.RUNMODAL = ACTION::LookupOK THEN BEGIN
        EasyPDFQueuePage.GETRECORD(EasyPDFQueueHeader);
        IF EasyPDFQueueHeader.BatchId <> '' THEN
        BEGIN
          Rec.SETRANGE(BatchId,EasyPDFQueueHeader.BatchId);
          Rec.SETRANGE(Processed);
          CountAttachments();
        END ELSE BEGIN
          EasyPDFQueueHeader.BatchId := TEXT0002;
        END;
        CurrPage.UPDATE;
      END;

      AddAttachmentsEnabled := EasyPDFQueueHeader.BatchId <> TEXT0002;
    END;

    PROCEDURE InvokeActions@1000000001(Shortcut1@1000000000 : Text[10];Shortcut2@1000000001 : Text[10]);
    VAR
      WSHShell@1000000002 : Automation "{F935DC20-1CF0-11D0-ADB9-00C04FD58A0B} 1.0:{72C24DD5-D70A-438B-8A42-98424B88AFB8}:'Windows Script Host Object Model'.WshShell";
    BEGIN

      CREATE(WSHShell, FALSE, TRUE);
      IF NOT ISCLEAR(WSHShell) THEN
      BEGIN
        WSHShell.SendKeys(Shortcut1);
        WSHShell.SendKeys(Shortcut2);
      END;
      CLEAR(WSHShell);
    END;

    BEGIN
    END.
  }
}

