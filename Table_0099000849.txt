OBJECT Table 99000849 Action Message Entry
{
  OBJECT-PROPERTIES
  {
    Date=11/27/18;
    Time=11:15:58 PM;
    Version List=;
  }
  PROPERTIES
  {
    CaptionML=ENU=Action Message Entry;
  }
  FIELDS
  {
    { 1   ;   ;Entry No.           ;Integer       ;CaptionML=ENU=Entry No. }
    { 2   ;   ;Type                ;Option        ;CaptionML=ENU=Type;
                                                   OptionCaptionML=ENU=,New,Change Qty.,Reschedule,Resched. & Chg. Qty.,Cancel;
                                                   OptionString=,New,Change Qty.,Reschedule,Resched. & Chg. Qty.,Cancel }
    { 3   ;   ;Reservation Entry   ;Integer       ;TableRelation="Reservation Entry"."Entry No.";
                                                   CaptionML=ENU=Reservation Entry }
    { 4   ;   ;Quantity            ;Decimal       ;CaptionML=ENU=Quantity;
                                                   DecimalPlaces=0:5 }
    { 5   ;   ;New Date            ;Date          ;CaptionML=ENU=New Date }
    { 6   ;   ;Calculation         ;Option        ;CaptionML=ENU=Calculation;
                                                   OptionCaptionML=ENU=Sum,None;
                                                   OptionString=Sum,None }
    { 7   ;   ;Suppressed Action Msg.;Boolean     ;CaptionML=ENU=Suppressed Action Msg. }
    { 10  ;   ;Source Type         ;Integer       ;CaptionML=ENU=Source Type }
    { 11  ;   ;Source Subtype      ;Option        ;CaptionML=ENU=Source Subtype;
                                                   OptionCaptionML=ENU=0,1,2,3,4,5,6,7,8,9,10;
                                                   OptionString=0,1,2,3,4,5,6,7,8,9,10 }
    { 12  ;   ;Source ID           ;Code20        ;CaptionML=ENU=Source ID }
    { 13  ;   ;Source Batch Name   ;Code10        ;CaptionML=ENU=Source Batch Name }
    { 14  ;   ;Source Prod. Order Line;Integer    ;CaptionML=ENU=Source Prod. Order Line }
    { 15  ;   ;Source Ref. No.     ;Integer       ;CaptionML=ENU=Source Ref. No. }
    { 16  ;   ;Location Code       ;Code10        ;TableRelation=Location;
                                                   CaptionML=ENU=Location Code }
    { 17  ;   ;Bin Code            ;Code20        ;TableRelation=Bin.Code WHERE (Location Code=FIELD(Location Code));
                                                   CaptionML=ENU=Bin Code }
    { 18  ;   ;Variant Code        ;Code10        ;TableRelation="Item Variant".Code WHERE (Item No.=FIELD(Item No.));
                                                   CaptionML=ENU=Variant Code }
    { 19  ;   ;Item No.            ;Code20        ;TableRelation=Item;
                                                   CaptionML=ENU=Item No. }
  }
  KEYS
  {
    {    ;Entry No.                               ;Clustered=Yes }
    {    ;Reservation Entry,Calculation           ;SumIndexFields=Quantity }
    {    ;Source Type,Source Subtype,Source ID,Source Batch Name,Source Prod. Order Line,Source Ref. No.,Item No.,Location Code,Bin Code,Variant Code;
                                                   SumIndexFields=Quantity }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      ComponentBinding@1001 : Boolean;
      FirstDate@1002 : Date;
      FirstTime@1003 : Time;

    [External]
    PROCEDURE SumUp@1(VAR ActionMessageEntry@1000 : Record 99000849);
    VAR
      ActionMessageEntry2@1001 : Record 99000849;
      ReservEntry@1002 : Record 337;
      ReservEntry2@1003 : Record 337;
      ProdOrderComp@1004 : Record 5407;
      TypeArray@1005 : ARRAY [5] OF Boolean;
    BEGIN
      ActionMessageEntry2 := ActionMessageEntry;

      WITH ActionMessageEntry2 DO BEGIN
        SetSourceFilterFromActionEntry(Rec);
        SETRANGE("Location Code","Location Code");
        SETRANGE("Bin Code","Bin Code");
        SETRANGE("Variant Code","Variant Code");
        SETRANGE("Item No.","Item No.");
        ActionMessageEntry."New Date" := 0D;
        ActionMessageEntry.Quantity := 0;
        ActionMessageEntry.Type := 0;
        IF FINDSET THEN
          REPEAT
            IF Quantity <> 0 THEN BEGIN
              ActionMessageEntry.Quantity += Quantity;
              TypeArray[2] := TRUE;
            END;
            IF "New Date" <> 0D THEN BEGIN
              ActionMessageEntry."New Date" := "New Date";
              TypeArray[3] := TRUE;
            END;
          UNTIL NEXT = 0;

        IF TypeArray[2] THEN
          ActionMessageEntry.Type := ActionMessageEntry.Type::"Change Qty.";

        IF TypeArray[3] THEN
          ActionMessageEntry.Type := ActionMessageEntry.Type::Reschedule;

        IF TypeArray[2] AND TypeArray[3] THEN
          ActionMessageEntry.Type := ActionMessageEntry.Type::"Resched. & Chg. Qty.";

        IF TypeArray[1] THEN
          ActionMessageEntry.Type := ActionMessageEntry.Type::New;

        IF TypeArray[5] THEN
          ActionMessageEntry.Type := ActionMessageEntry.Type::Cancel;

        "New Date" := ActionMessageEntry."New Date";
        Quantity := ActionMessageEntry.Quantity;
        Type := ActionMessageEntry.Type;
        ActionMessageEntry := ActionMessageEntry2;

        ComponentBinding := FALSE;
        IF ActionMessageEntry."Source Type" = DATABASE::"Prod. Order Line" THEN BEGIN
          FirstDate := DMY2DATE(31,12,9999);
          ActionMessageEntry.FilterToReservEntry(ReservEntry);
          ReservEntry.SETRANGE(Binding,ReservEntry.Binding::"Order-to-Order");
          IF ReservEntry.FINDSET THEN
            REPEAT
              IF ReservEntry2.GET(ReservEntry."Entry No.",FALSE) THEN
                IF (ReservEntry2."Source Type" = DATABASE::"Prod. Order Component") AND
                   (ReservEntry2."Source Subtype" = ReservEntry."Source Subtype") AND
                   (ReservEntry2."Source ID" = ReservEntry."Source ID")
                THEN
                  IF ProdOrderComp.GET(
                       ReservEntry2."Source Subtype",ReservEntry2."Source ID",
                       ReservEntry2."Source Prod. Order Line",ReservEntry2."Source Ref. No.")
                  THEN BEGIN
                    ComponentBinding := TRUE;
                    IF ProdOrderComp."Due Date" < FirstDate THEN BEGIN
                      FirstDate := ProdOrderComp."Due Date";
                      FirstTime := ProdOrderComp."Due Time";
                    END;
                  END;
            UNTIL ReservEntry.NEXT = 0;
        END;
      END;
    END;

    [External]
    PROCEDURE TransferFromReservEntry@2(VAR ReservEntry@1000 : Record 337);
    BEGIN
      "Reservation Entry" := ReservEntry."Entry No.";
      SetSourceFromReservEntry(ReservEntry);
      "Location Code" := ReservEntry."Location Code";
      "Variant Code" := ReservEntry."Variant Code";
      "Item No." := ReservEntry."Item No.";
    END;

    [External]
    PROCEDURE FilterFromReservEntry@3(VAR ReservEntry@1000 : Record 337);
    BEGIN
      SetSourceFilterFromReservEntry(ReservEntry);
      SETRANGE("Location Code",ReservEntry."Location Code");
      SETRANGE("Variant Code",ReservEntry."Variant Code");
      SETRANGE("Item No.",ReservEntry."Item No.");
    END;

    [External]
    PROCEDURE FilterToReservEntry@7(VAR ReservEntry@1000 : Record 337);
    BEGIN
      ReservEntry.SetSourceFilter("Source Type","Source Subtype","Source ID","Source Ref. No.",TRUE);
      ReservEntry.SetSourceFilter2("Source Batch Name","Source Prod. Order Line");
    END;

    [External]
    PROCEDURE BoundToComponent@6() : Boolean;
    BEGIN
      EXIT(ComponentBinding);
    END;

    [External]
    PROCEDURE ComponentDueDate@4() : Date;
    BEGIN
      EXIT(FirstDate);
    END;

    [External]
    PROCEDURE ComponentDueTime@5() : Time;
    BEGIN
      EXIT(FirstTime);
    END;

    [External]
    PROCEDURE SetSource@22(SourceType@1000 : Integer;SourceSubtype@1001 : Integer;SourceID@1002 : Code[20];SourceRefNo@1003 : Integer;SourceBatchName@1004 : Code[10];SourceProdOrderLine@1005 : Integer);
    BEGIN
      "Source Type" := SourceType;
      "Source Subtype" := SourceSubtype;
      "Source ID" := SourceID;
      "Source Ref. No." := SourceRefNo;
      "Source Batch Name" := SourceBatchName;
      "Source Prod. Order Line" := SourceProdOrderLine;
    END;

    [External]
    PROCEDURE SetSourceFromReservEntry@21(ReservEntry@1000 : Record 337);
    BEGIN
      "Source Type" := ReservEntry."Source Type";
      "Source Subtype" := ReservEntry."Source Subtype";
      "Source ID" := ReservEntry."Source ID";
      "Source Ref. No." := ReservEntry."Source Ref. No.";
      "Source Batch Name" := ReservEntry."Source Batch Name";
      "Source Prod. Order Line" := ReservEntry."Source Prod. Order Line";
    END;

    [External]
    PROCEDURE SetSourceFilter@31(SourceType@1011 : Integer;SourceSubtype@1010 : Integer;SourceID@1009 : Code[20];SourceRefNo@1008 : Integer;SourceKey@1000 : Boolean);
    BEGIN
      IF SourceKey THEN
        SETCURRENTKEY(
          "Source ID","Source Type","Source Subtype","Source Batch Name",
          "Source Prod. Order Line","Source Ref. No.");
      SETRANGE("Source Type",SourceType);
      IF SourceSubtype >= 0 THEN
        SETRANGE("Source Subtype",SourceSubtype);
      SETRANGE("Source ID",SourceID);
      IF SourceRefNo >= 0 THEN
        SETRANGE("Source Ref. No.",SourceRefNo);
    END;

    [External]
    PROCEDURE SetSourceFilter2@33(SourceBatchName@1007 : Code[10];SourceProdOrderLine@1006 : Integer);
    BEGIN
      SETRANGE("Source Batch Name",SourceBatchName);
      SETRANGE("Source Prod. Order Line",SourceProdOrderLine);
    END;

    [External]
    PROCEDURE SetSourceFilterFromActionEntry@11(ActionMessageEntry@1000 : Record 99000849);
    BEGIN
      SETRANGE("Source Type",ActionMessageEntry."Source Type");
      SETRANGE("Source Subtype",ActionMessageEntry."Source Subtype");
      SETRANGE("Source ID",ActionMessageEntry."Source ID");
      SETRANGE("Source Ref. No.",ActionMessageEntry."Source Ref. No.");
      SETRANGE("Source Batch Name",ActionMessageEntry."Source Batch Name");
      SETRANGE("Source Prod. Order Line",ActionMessageEntry."Source Prod. Order Line");
    END;

    [External]
    PROCEDURE SetSourceFilterFromReservEntry@9(ReservEntry@1000 : Record 337);
    BEGIN
      SETRANGE("Source Type",ReservEntry."Source Type");
      SETRANGE("Source Subtype",ReservEntry."Source Subtype");
      SETRANGE("Source ID",ReservEntry."Source ID");
      SETRANGE("Source Ref. No.",ReservEntry."Source Ref. No.");
      SETRANGE("Source Batch Name",ReservEntry."Source Batch Name");
      SETRANGE("Source Prod. Order Line",ReservEntry."Source Prod. Order Line");
    END;

    [External]
    PROCEDURE ClearSourceFilter@8();
    BEGIN
      SETRANGE("Source Type");
      SETRANGE("Source Subtype");
      SETRANGE("Source ID");
      SETRANGE("Source Batch Name");
      SETRANGE("Source Prod. Order Line");
      SETRANGE("Source Ref. No.");
    END;

    BEGIN
    END.
  }
}

