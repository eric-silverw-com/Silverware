OBJECT Page 1816 Job Creation Wizard
{
  OBJECT-PROPERTIES
  {
    Date=11/22/17;
    Time=12:00:00 PM;
    Version List=NAVW111.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Create New Job;
               ESM=Crear un proyecto nuevo;
               FRC=Cr‚er de nouveaux projets;
               ENC=Create New Job];
    SourceTable=Table167;
    DelayedInsert=Yes;
    PageType=NavigatePage;
    OnInit=BEGIN
             LoadTopBanners;
           END;

    OnOpenPage=BEGIN
                 INIT;

                 Step := Step::Start;
                 EnableControls;
               END;

    ActionList=ACTIONS
    {
      { 10      ;    ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 15      ;1   ;Action    ;
                      Name=ActionBack;
                      CaptionML=[ENU=Back;
                                 ESM=Atr s;
                                 FRC=Pr‚c‚dent;
                                 ENC=Back];
                      ApplicationArea=#Basic,#Suite;
                      Enabled=BackActionEnabled;
                      InFooterBar=Yes;
                      Image=PreviousRecord;
                      OnAction=BEGIN
                                 NextStep(TRUE);
                               END;
                                }
      { 14      ;1   ;Action    ;
                      Name=ActionNext;
                      CaptionML=[ENU=Next;
                                 ESM=Siguiente;
                                 FRC=Suivant;
                                 ENC=Next];
                      ApplicationArea=#Basic,#Suite;
                      Enabled=NextActionEnabled;
                      InFooterBar=Yes;
                      Image=NextRecord;
                      OnAction=BEGIN
                                 NextStep(FALSE);
                               END;
                                }
      { 11      ;1   ;Action    ;
                      Name=ActionFinish;
                      CaptionML=[ENU=Finish;
                                 ESM=Terminar;
                                 FRC=Terminer;
                                 ENC=Finish];
                      ApplicationArea=#Basic,#Suite;
                      Enabled=FinishActionEnabled;
                      InFooterBar=Yes;
                      Image=Approve;
                      OnAction=BEGIN
                                 FinishAction;
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1   ;0   ;Container ;
                ContainerType=ContentArea }

    { 96  ;1   ;Group     ;
                Visible=TopBannerVisible AND NOT FinalStepVisible;
                Editable=FALSE;
                GroupType=Group }

    { 97  ;2   ;Field     ;
                ApplicationArea=#Jobs;
                SourceExpr=MediaResourcesStandard."Media Reference";
                Editable=FALSE;
                ShowCaption=No }

    { 98  ;1   ;Group     ;
                Visible=TopBannerVisible AND FinalStepVisible;
                Editable=FALSE;
                GroupType=Group }

    { 99  ;2   ;Field     ;
                ApplicationArea=#Jobs;
                SourceExpr=MediaResourcesDone."Media Reference";
                Editable=FALSE;
                ShowCaption=No }

    { 25  ;1   ;Group     ;
                Visible=FirstStepVisible;
                GroupType=Group }

    { 24  ;2   ;Group     ;
                CaptionML=[ENU=Welcome to Create New Job;
                           ESM=Crear un proyecto nuevo;
                           FRC=Bienvenue dans Cr‚er un nouveau projet;
                           ENC=Welcome to Create New Job];
                Visible=FirstStepVisible;
                GroupType=Group }

    { 23  ;3   ;Group     ;
                Visible=FirstStepVisible;
                GroupType=Group;
                InstructionalTextML=[ENU=Do you want to create a new job from an existing job?;
                                     ESM=¨Quiere crear un nuevo proyecto a partir de uno existente?;
                                     FRC=Souhaitez-vous cr‚er un nouveau projet … partir d'un projet existantÿ?;
                                     ENC=Do you want to create a new job from an existing job?] }

    { 21  ;4   ;Field     ;
                Name=YesCheckbox;
                CaptionML=[ENU=Yes;
                           ESM=S¡;
                           FRC=Oui;
                           ENC=Yes];
                ApplicationArea=#Jobs;
                SourceExpr=FromExistingJobYes;
                OnValidate=BEGIN
                             IF FromExistingJobYes THEN
                               FromExistingJobNo := FALSE;
                           END;
                            }

    { 22  ;4   ;Field     ;
                Name=NoCheckbox;
                CaptionML=[ENU=No;
                           ESM=No;
                           FRC=Non;
                           ENC=No];
                ApplicationArea=#Jobs;
                SourceExpr=FromExistingJobNo;
                OnValidate=BEGIN
                             IF FromExistingJobNo THEN
                               FromExistingJobYes := FALSE;
                           END;
                            }

    { 19  ;1   ;Group     ;
                Visible=CreationStepVisible;
                GroupType=Group }

    { 20  ;2   ;Group     ;
                CaptionML=[ENU=Welcome to Create New Job;
                           ESM=Crear un proyecto nuevo;
                           FRC=Bienvenue dans Cr‚er un nouveau projet;
                           ENC=Welcome to Create New Job];
                Visible=CreationStepVisible;
                GroupType=Group }

    { 18  ;3   ;Group     ;
                Visible=CreationStepVisible;
                GroupType=Group;
                InstructionalTextML=[ENU=Fill in the following fields for the new job.;
                                     ESM=Rellene los siguientes campos del nuevo proyecto.;
                                     FRC=Renseignez les champs suivants pour le nouveau projet.;
                                     ENC=Fill in the following fields for the new job.] }

    { 16  ;4   ;Field     ;
                CaptionML=[ENU=No.;
                           ESM=N§;
                           FRC=Nø;
                           ENC=No.];
                ApplicationArea=#Jobs;
                SourceExpr="No.";
                OnAssistEdit=BEGIN
                               IF AssistEdit(xRec) THEN
                                 CurrPage.UPDATE;
                             END;
                              }

    { 26  ;4   ;Field     ;
                CaptionML=[ENU=Description;
                           ESM=Descripci¢n;
                           FRC=Description;
                           ENC=Description];
                ApplicationArea=#Jobs;
                SourceExpr=Description }

    { 27  ;4   ;Field     ;
                CaptionML=[ENU=Bill-to Customer No.;
                           ESM=Factura-a N§ cliente;
                           FRC=Nø client factur‚;
                           ENC=Bill-to Customer No.];
                ApplicationArea=#Jobs;
                SourceExpr="Bill-to Customer No.";
                TableRelation=Customer }

    { 9   ;3   ;Group     ;
                GroupType=Group;
                InstructionalTextML=[ENU=To select the tasks to copy from an existing job, choose Next.;
                                     ESM=Para seleccionar las tareas que se copiar n de un proyecto existente, elija Siguiente.;
                                     FRC=Pour s‚lectionner les tƒches d'un projet existant … copier, choisissez Suivant.;
                                     ENC=To select the tasks to copy from an existing job, choose Next.] }

    { 8   ;1   ;Group     ;
                Visible=FinalStepVisible;
                GroupType=Group }

    { 12  ;2   ;Group     ;
                CaptionML=[ENU=That's it!;
                           ESM=Eso es todo.;
                           FRC=Voil…ÿ!;
                           ENC=That's it!];
                GroupType=Group }

    { 4   ;3   ;Group     ;
                GroupType=Group;
                InstructionalTextML=[ENU=To view your new job, choose Finish.;
                                     ESM=Para ver el nuevo proyecto, elija Terminar.;
                                     FRC=Pour afficher votre nouveau projet, choisissez Terminer.;
                                     ENC=To view your new job, choose Finish.] }

  }
  CODE
  {
    VAR
      MediaRepositoryStandard@1040 : Record 9400;
      MediaRepositoryDone@1041 : Record 9400;
      MediaResourcesStandard@1002 : Record 2000000182;
      MediaResourcesDone@1001 : Record 2000000182;
      ClientTypeManagement@1003 : Codeunit 4;
      Step@1012 : 'Start,Creation,Finish';
      TopBannerVisible@1042 : Boolean;
      FirstStepVisible@1000 : Boolean;
      CreationStepVisible@1010 : Boolean;
      FinalStepVisible@1015 : Boolean;
      FinishActionEnabled@1009 : Boolean;
      BackActionEnabled@1013 : Boolean;
      NextActionEnabled@1014 : Boolean;
      FromExistingJobYes@1022 : Boolean;
      FromExistingJobNo@1021 : Boolean;
      SelectYesNoMsg@1025 : TextConst 'ENU=To continue, specify if you want to create the new job based on an existing job.;ESM=Para continuar, especifique si quiere crear el nuevo proyecto a partir de un proyecto existente.;FRC=Pour continuer, indiquez si vous souhaitez cr‚er le nouveau projet … partir d''un projet existant.;ENC=To continue, specify if you want to create the new job based on an existing job.';
      SelectJobNumberMsg@1026 : TextConst 'ENU=To continue, specify the job number that you want to copy.;ESM=Para continuar, especifique el n£mero del proyecto que quiere copiar.;FRC=Pour continuer, sp‚cifiez le num‚ro du projet que vous souhaitez copier.;ENC=To continue, specify the job number that you want to copy.';
      SelectCustomerNumberMsg@1027 : TextConst 'ENU=To continue, specify the customer of the new job.;ESM=Para continuar, especifique el cliente del nuevo proyecto.;FRC=Pour continuer, sp‚cifiez le client du nouveau projet.;ENC=To continue, specify the customer of the new job.';

    LOCAL PROCEDURE EnableControls@2();
    BEGIN
      IF Step = Step::Creation THEN
        IF (NOT FromExistingJobNo) AND (NOT FromExistingJobYes) THEN BEGIN
          MESSAGE(SelectYesNoMsg);
          Step := Step - 1;
          EXIT;
        END;

      IF Step = Step::Finish THEN BEGIN
        IF "No." = '' THEN BEGIN
          MESSAGE(SelectJobNumberMsg);
          Step := Step - 1;
          EXIT;
        END;

        IF "Bill-to Customer No." = '' THEN BEGIN
          MESSAGE(SelectCustomerNumberMsg);
          Step := Step - 1;
          EXIT;
        END;
      END;

      ResetControls;

      CASE Step OF
        Step::Start:
          ShowStartStep;
        Step::Creation:
          ShowCreationStep;
        Step::Finish:
          ShowFinalStep;
      END;
    END;

    LOCAL PROCEDURE FinishAction@12();
    BEGIN
      PAGE.RUN(PAGE::"Job Card",Rec);
      CurrPage.CLOSE;
    END;

    LOCAL PROCEDURE NextStep@41(Backwards@1000 : Boolean);
    BEGIN
      IF Backwards THEN
        Step := Step - 1
      ELSE
        Step := Step + 1;

      EnableControls;
    END;

    LOCAL PROCEDURE ShowStartStep@24();
    BEGIN
      FirstStepVisible := TRUE;
      FinishActionEnabled := FALSE;
      BackActionEnabled := FALSE;
      FromExistingJobYes := TRUE;
      FromExistingJobNo := FALSE;
    END;

    LOCAL PROCEDURE ShowCreationStep@33();
    BEGIN
      CreationStepVisible := TRUE;
      FinishActionEnabled := FALSE;

      // If user clicked "Back", the Job will already exist, so don't try to create it again.
      IF "No." = '' THEN BEGIN
        INSERT(TRUE);
        COMMIT;
      END;

      IF FromExistingJobNo THEN
        FinishAction;
    END;

    LOCAL PROCEDURE ShowFinalStep@34();
    VAR
      CopyJobTasks@1000 : Page 1041;
    BEGIN
      FinalStepVisible := TRUE;
      BackActionEnabled := FALSE;
      NextActionEnabled := FALSE;

      CopyJobTasks.SetToJob(Rec);
      CopyJobTasks.RUN;
    END;

    LOCAL PROCEDURE ResetControls@1();
    BEGIN
      FinishActionEnabled := TRUE;
      BackActionEnabled := TRUE;
      NextActionEnabled := TRUE;

      FirstStepVisible := FALSE;
      CreationStepVisible := FALSE;
      FinalStepVisible := FALSE;
    END;

    LOCAL PROCEDURE LoadTopBanners@40();
    BEGIN
      IF MediaRepositoryStandard.GET('AssistedSetup-NoText-400px.png',FORMAT(ClientTypeManagement.GetCurrentClientType)) AND
         MediaRepositoryDone.GET('AssistedSetupDone-NoText-400px.png',FORMAT(ClientTypeManagement.GetCurrentClientType))
      THEN
        IF MediaResourcesStandard.GET(MediaRepositoryStandard."Media Resources Ref") AND
           MediaResourcesDone.GET(MediaRepositoryDone."Media Resources Ref")
        THEN
          TopBannerVisible := MediaResourcesDone."Media Reference".HASVALUE;
    END;

    BEGIN
    END.
  }
}

