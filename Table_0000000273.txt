OBJECT Table 273 Bank Acc. Reconciliation
{
  OBJECT-PROPERTIES
  {
    Date=09/19/18;
    Time=12:00:00 PM;
    Version List=NAVW113.00,NAVNA13.00;
  }
  PROPERTIES
  {
    Permissions=TableData 270=rm,
                TableData 1220=rimd,
                TableData 10120=r;
    DataCaptionFields=Bank Account No.,Statement No.;
    OnInsert=BEGIN
               TESTFIELD("Statement No.");
               TESTFIELD("Bank Account No.");
               BankAcc.GET("Bank Account No.");
               CASE "Statement Type" OF
                 "Statement Type"::"Bank Reconciliation":
                   BEGIN
                     IF PostedBankAccStmt.GET("Bank Account No.","Statement No.") THEN
                       ERROR(DuplicateStatementErr,"Statement No.");
                     BankAcc."Last Statement No." := "Statement No.";
                   END;
                 "Statement Type"::"Payment Application":
                   BEGIN
                     IF PostedPaymentReconHdr.GET("Bank Account No.","Statement No.") THEN
                       ERROR(DuplicateStatementErr,"Statement No.");
                     BankAcc."Last Payment Statement No." := "Statement No.";
                   END;
               END;

               BankAcc.MODIFY;
             END;

    OnDelete=BEGIN
               IF BankAccReconLine.LinesExist(Rec) THEN
                 BankAccReconLine.DELETEALL(TRUE);
             END;

    OnRename=BEGIN
               ERROR(RenameErr,TABLECAPTION);
             END;

    CaptionML=[ENU=Bank Acc. Reconciliation;
               ESM=Conciliaci¢n banco;
               FRC=Rapprochement compte bancaire;
               ENC=Bank Acc. Reconciliation];
    LookupPageID=Page388;
  }
  FIELDS
  {
    { 1   ;   ;Bank Account No.    ;Code20        ;TableRelation="Bank Account";
                                                   OnValidate=BEGIN
                                                                IF "Statement No." = '' THEN BEGIN
                                                                  BankAcc.GET("Bank Account No.");

                                                                  IF "Statement Type" = "Statement Type"::"Payment Application" THEN BEGIN
                                                                    SetLastPaymentStatementNo(BankAcc);
                                                                    "Statement No." := INCSTR(BankAcc."Last Payment Statement No.");
                                                                  END ELSE BEGIN
                                                                    SetLastStatementNo(BankAcc);
                                                                    "Statement No." := INCSTR(BankAcc."Last Statement No.");
                                                                  END;

                                                                  "Balance Last Statement" := BankAcc."Balance Last Statement";
                                                                END;

                                                                CreateDim(DATABASE::"Bank Account",BankAcc."No.");
                                                              END;

                                                   CaptionML=[ENU=Bank Account No.;
                                                              ESM=C¢d. cuenta banco;
                                                              FRC=Nø compte bancaire;
                                                              ENC=Bank Account No.];
                                                   NotBlank=Yes }
    { 2   ;   ;Statement No.       ;Code20        ;OnValidate=BEGIN
                                                                TESTFIELD("Bank Account No.");
                                                              END;

                                                   CaptionML=[ENU=Statement No.;
                                                              ESM=N§ estado de cta. banco;
                                                              FRC=Nø de relev‚;
                                                              ENC=Statement No.];
                                                   NotBlank=Yes;
                                                   Editable=No }
    { 3   ;   ;Statement Ending Balance;Decimal   ;CaptionML=[ENU=Statement Ending Balance;
                                                              ESM=Saldo final estado de cuenta;
                                                              FRC=Solde final du relev‚;
                                                              ENC=Statement Ending Balance];
                                                   AutoFormatType=1;
                                                   AutoFormatExpr=GetCurrencyCode }
    { 4   ;   ;Statement Date      ;Date          ;CaptionML=[ENU=Statement Date;
                                                              ESM=Fecha estado de cuenta banco;
                                                              FRC=Date du relev‚;
                                                              ENC=Statement Date] }
    { 5   ;   ;Balance Last Statement;Decimal     ;OnValidate=BEGIN
                                                                TESTFIELD("Statement Type","Statement Type"::"Bank Reconciliation");
                                                                BankAcc.GET("Bank Account No.");
                                                                IF "Balance Last Statement" <> BankAcc."Balance Last Statement" THEN
                                                                  IF NOT
                                                                     CONFIRM(
                                                                       BalanceQst,FALSE,
                                                                       FIELDCAPTION("Balance Last Statement"),BankAcc.FIELDCAPTION("Balance Last Statement"),
                                                                       BankAcc.TABLECAPTION)
                                                                  THEN
                                                                    "Balance Last Statement" := xRec."Balance Last Statement";
                                                              END;

                                                   CaptionML=[ENU=Balance Last Statement;
                                                              ESM=Saldo £lt. edo. cta. banco;
                                                              FRC=Solde dernier relev‚;
                                                              ENC=Balance Last Statement];
                                                   AutoFormatType=1;
                                                   AutoFormatExpr=GetCurrencyCode }
    { 6   ;   ;Bank Statement      ;BLOB          ;CaptionML=[ENU=Bank Statement;
                                                              ESM=Estado de cuenta banco;
                                                              FRC=Relev‚ bancaire;
                                                              ENC=Bank Statement] }
    { 7   ;   ;Total Balance on Bank Account;Decimal;
                                                   FieldClass=FlowField;
                                                   CalcFormula=Sum("Bank Account Ledger Entry".Amount WHERE (Bank Account No.=FIELD(Bank Account No.)));
                                                   CaptionML=[ENU=Total Balance on Bank Account;
                                                              ESM=Saldo total en cuenta bancaria;
                                                              FRC=Solde total sur compte bancaire;
                                                              ENC=Total Balance on Bank Account];
                                                   Editable=No;
                                                   AutoFormatType=1;
                                                   AutoFormatExpr=GetCurrencyCode }
    { 8   ;   ;Total Applied Amount;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Bank Acc. Reconciliation Line"."Applied Amount" WHERE (Statement Type=FIELD(Statement Type),
                                                                                                                           Bank Account No.=FIELD(Bank Account No.),
                                                                                                                           Statement No.=FIELD(Statement No.)));
                                                   CaptionML=[ENU=Total Applied Amount;
                                                              ESM=Importe liquidado total;
                                                              FRC=Montant affect‚ total;
                                                              ENC=Total Applied Amount];
                                                   Editable=No;
                                                   AutoFormatExpr=GetCurrencyCode }
    { 9   ;   ;Total Transaction Amount;Decimal   ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Bank Acc. Reconciliation Line"."Statement Amount" WHERE (Statement Type=FIELD(Statement Type),
                                                                                                                             Bank Account No.=FIELD(Bank Account No.),
                                                                                                                             Statement No.=FIELD(Statement No.)));
                                                   CaptionML=[ENU=Total Transaction Amount;
                                                              ESM=Importe total de la transacci¢n;
                                                              FRC=Montant transaction total;
                                                              ENC=Total Transaction Amount];
                                                   Editable=No;
                                                   AutoFormatExpr=GetCurrencyCode }
    { 10  ;   ;Total Unposted Applied Amount;Decimal;
                                                   FieldClass=FlowField;
                                                   CalcFormula=Sum("Bank Acc. Reconciliation Line"."Applied Amount" WHERE (Statement Type=FIELD(Statement Type),
                                                                                                                           Bank Account No.=FIELD(Bank Account No.),
                                                                                                                           Statement No.=FIELD(Statement No.),
                                                                                                                           Account Type=FILTER(<>Bank Account)));
                                                   CaptionML=[ENU=Total Unposted Applied Amount;
                                                              ESM=Importe liquidado total no registrado;
                                                              FRC=Montant total affect‚ non report‚;
                                                              ENC=Total Unposted Applied Amount];
                                                   Editable=No;
                                                   AutoFormatExpr=GetCurrencyCode }
    { 11  ;   ;Total Difference    ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Bank Acc. Reconciliation Line".Difference WHERE (Statement Type=FIELD(Statement Type),
                                                                                                                     Bank Account No.=FIELD(Bank Account No.),
                                                                                                                     Statement No.=FIELD(Statement No.)));
                                                   CaptionML=[ENU=Total Difference;
                                                              ESM=Diferencia total;
                                                              FRC=Total diff‚rence;
                                                              ENC=Total Difference];
                                                   Editable=No;
                                                   AutoFormatExpr=GetCurrencyCode }
    { 20  ;   ;Statement Type      ;Option        ;CaptionML=[ENU=Statement Type;
                                                              ESM=Tipo de estado de cuenta;
                                                              FRC=Type relev‚;
                                                              ENC=Statement Type];
                                                   OptionCaptionML=[ENU=Bank Reconciliation,Payment Application;
                                                                    ESM=Conciliaci¢n banco,liquidaci¢n de pago;
                                                                    FRC=Rapprochement bancaire,Affectation paiement;
                                                                    ENC=Bank Reconciliation,Payment Application];
                                                   OptionString=Bank Reconciliation,Payment Application }
    { 21  ;   ;Shortcut Dimension 1 Code;Code20   ;TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(1));
                                                   OnValidate=BEGIN
                                                                ValidateShortcutDimCode(1,"Shortcut Dimension 1 Code");
                                                              END;

                                                   CaptionML=[ENU=Shortcut Dimension 1 Code;
                                                              ESM=C¢d. dim. acceso dir. 1;
                                                              FRC=Code raccourci de dimension 1;
                                                              ENC=Shortcut Dimension 1 Code];
                                                   CaptionClass='1,2,1' }
    { 22  ;   ;Shortcut Dimension 2 Code;Code20   ;TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(2));
                                                   OnValidate=BEGIN
                                                                ValidateShortcutDimCode(2,"Shortcut Dimension 2 Code");
                                                              END;

                                                   CaptionML=[ENU=Shortcut Dimension 2 Code;
                                                              ESM=C¢d. dim. acceso dir. 2;
                                                              FRC=Code raccourci de dimension 2;
                                                              ENC=Shortcut Dimension 2 Code];
                                                   CaptionClass='1,2,2' }
    { 23  ;   ;Post Payments Only  ;Boolean       ;CaptionML=[ENU=Post Payments Only;
                                                              ESM=Registrar solamente pagos;
                                                              FRC=Reporter les paiements uniquement;
                                                              ENC=Post Payments Only] }
    { 24  ;   ;Import Posted Transactions;Option  ;CaptionML=[ENU=Import Posted Transactions;
                                                              ESM=Importar transacciones registradas;
                                                              FRC=Importer les transactions report‚es;
                                                              ENC=Import Posted Transactions];
                                                   OptionCaptionML=[ENU=" ,Yes,No";
                                                                    ESM=" ,S¡,No";
                                                                    FRC=" ,Oui,Non";
                                                                    ENC=" ,Yes,No"];
                                                   OptionString=[ ,Yes,No] }
    { 25  ;   ;Total Outstd Bank Transactions;Decimal;
                                                   FieldClass=FlowField;
                                                   CalcFormula=Sum("Bank Account Ledger Entry".Amount WHERE (Bank Account No.=FIELD(Bank Account No.),
                                                                                                             Open=CONST(Yes),
                                                                                                             Check Ledger Entries=CONST(0)));
                                                   CaptionML=[ENU=Total Outstd Bank Transactions;
                                                              ESM=Total de transacciones bancarias pendientes;
                                                              FRC=Total transactions bancaires restantes;
                                                              ENC=Total Outstd Bank Transactions];
                                                   Editable=No;
                                                   AutoFormatExpr=GetCurrencyCode }
    { 26  ;   ;Total Outstd Payments;Decimal      ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Bank Account Ledger Entry".Amount WHERE (Bank Account No.=FIELD(Bank Account No.),
                                                                                                             Open=CONST(Yes),
                                                                                                             Check Ledger Entries=FILTER(>0)));
                                                   CaptionML=[ENU=Total Outstd Payments;
                                                              ESM=Total de pagos pendientes;
                                                              FRC=Total paiements restants;
                                                              ENC=Total Outstd Payments];
                                                   Editable=No;
                                                   AutoFormatExpr=GetCurrencyCode }
    { 27  ;   ;Total Applied Amount Payments;Decimal;
                                                   FieldClass=FlowField;
                                                   CalcFormula=Sum("Bank Acc. Reconciliation Line"."Applied Amount" WHERE (Statement Type=FIELD(Statement Type),
                                                                                                                           Bank Account No.=FIELD(Bank Account No.),
                                                                                                                           Statement No.=FIELD(Statement No.),
                                                                                                                           Type=CONST(Check Ledger Entry)));
                                                   CaptionML=[ENU=Total Applied Amount Payments;
                                                              ESM=Total de pagos correspondientes al importe liquidado;
                                                              FRC=Montant total des paiements affect‚s;
                                                              ENC=Total Applied Amount Payments];
                                                   Editable=No;
                                                   AutoFormatExpr=GetCurrencyCode }
    { 28  ;   ;Bank Account Balance (LCY);Decimal ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Bank Account Ledger Entry"."Amount (LCY)" WHERE (Bank Account No.=FIELD(Bank Account No.)));
                                                   CaptionML=[ENU=Bank Account Balance ($);
                                                              ESM=Saldo de la cuenta bancaria ($);
                                                              FRC=Solde compte bancaire ($);
                                                              ENC=Bank Account Balance ($)];
                                                   Editable=No }
    { 29  ;   ;Total Positive Adjustments;Decimal ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Bank Acc. Reconciliation Line"."Applied Amount" WHERE (Statement Type=FIELD(Statement Type),
                                                                                                                           Bank Account No.=FIELD(Bank Account No.),
                                                                                                                           Statement No.=FIELD(Statement No.),
                                                                                                                           Account Type=FILTER(<>Bank Account),
                                                                                                                           Statement Amount=FILTER(>0)));
                                                   CaptionML=[ENU=Total Positive Adjustments;
                                                              ESM=Ajustes positivos totales;
                                                              FRC=Ajustements positifs totaux;
                                                              ENC=Total Positive Adjustments];
                                                   Editable=No;
                                                   AutoFormatExpr=GetCurrencyCode }
    { 30  ;   ;Total Negative Adjustments;Decimal ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Bank Acc. Reconciliation Line"."Applied Amount" WHERE (Statement Type=FIELD(Statement Type),
                                                                                                                           Bank Account No.=FIELD(Bank Account No.),
                                                                                                                           Statement No.=FIELD(Statement No.),
                                                                                                                           Account Type=FILTER(<>Bank Account),
                                                                                                                           Statement Amount=FILTER(<0)));
                                                   CaptionML=[ENU=Total Negative Adjustments;
                                                              ESM=Ajustes negativos totales;
                                                              FRC=Ajustements n‚gatifs totaux;
                                                              ENC=Total Negative Adjustments];
                                                   Editable=No;
                                                   AutoFormatExpr=GetCurrencyCode }
    { 31  ;   ;Total Positive Difference;Decimal  ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Bank Acc. Reconciliation Line"."Applied Amount" WHERE (Account Type=FIELD(Statement Type),
                                                                                                                           Bank Account No.=FIELD(Bank Account No.),
                                                                                                                           Statement No.=FIELD(Statement No.),
                                                                                                                           Type=CONST(Difference),
                                                                                                                           Applied Amount=FILTER(>0)));
                                                   CaptionML=[ENU=Total Positive Difference;
                                                              ESM=Diferencia positiva total;
                                                              FRC=Diff‚rence positive totale;
                                                              ENC=Total Positive Difference];
                                                   Editable=No;
                                                   AutoFormatExpr=GetCurrencyCode }
    { 32  ;   ;Total Negative Difference;Decimal  ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Bank Acc. Reconciliation Line"."Applied Amount" WHERE (Account Type=FIELD(Statement Type),
                                                                                                                           Bank Account No.=FIELD(Bank Account No.),
                                                                                                                           Statement No.=FIELD(Statement No.),
                                                                                                                           Type=CONST(Difference),
                                                                                                                           Applied Amount=FILTER(<0)));
                                                   CaptionML=[ENU=Total Negative Difference;
                                                              ESM=Diferencia negativa total;
                                                              FRC=Diff‚rence n‚gative totale;
                                                              ENC=Total Negative Difference];
                                                   Editable=No;
                                                   AutoFormatExpr=GetCurrencyCode }
    { 33  ;   ;Copy VAT Setup to Jnl. Line;Boolean;InitValue=Yes;
                                                   CaptionML=[ENU=Copy Tax Setup to Jnl. Line;
                                                              ESM=Copiar conf. IVA a l¡n. diario;
                                                              FRC=Copier configuration TVA sur la ligne journal;
                                                              ENC=Copy Tax Setup to Jnl. Line] }
    { 480 ;   ;Dimension Set ID    ;Integer       ;TableRelation="Dimension Set Entry";
                                                   OnValidate=BEGIN
                                                                DimMgt.UpdateGlobalDimFromDimSetID("Dimension Set ID","Shortcut Dimension 1 Code","Shortcut Dimension 2 Code");
                                                              END;

                                                   OnLookup=BEGIN
                                                              ShowDocDim;
                                                            END;

                                                   CaptionML=[ENU=Dimension Set ID;
                                                              ESM=Id. grupo dimensiones;
                                                              FRC=Code ensemble de dimensions;
                                                              ENC=Dimension Set ID];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Statement Type,Bank Account No.,Statement No.;
                                                   Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      DuplicateStatementErr@1000 : TextConst '@@@="%1=Statement No. value";ENU=Statement %1 already exists.;ESM=El estado de cta. banco %1 ya existe.;FRC=Le relev‚ %1 existe d‚j….;ENC=Statement %1 already exists.';
      RenameErr@1001 : TextConst '@@@="%1=Table name caption";ENU=You cannot rename a %1.;ESM=No se puede cambiar el nombre a %1.;FRC=Vous ne pouvez pas renommer un %1.;ENC=You cannot rename a %1.';
      BalanceQst@1002 : TextConst '@@@="%1=Balance Last Statement field caption;%2=field caption;%3=table caption";ENU=%1 is different from %2 on the %3. Do you want to change the value?;ESM=%1 es diferente del %2 en el %3. ¨Confirma que desea cambiar el valor?;FRC=%1 est diff‚rent de %2 sur le %3. Voulez-vous changer la valeur?;ENC=%1 is different from %2 on the %3. Do you want to change the value?';
      BankAcc@1003 : Record 270;
      BankAccReconLine@1005 : Record 274;
      PostedBankAccStmt@1006 : Record 275;
      PostedPaymentReconHdr@1007 : Record 1295;
      DimMgt@1008 : Codeunit 408;
      YouChangedDimQst@1009 : TextConst 'ENU=You may have changed a dimension.\\Do you want to update the lines?;ESM=Puede que haya cambiado una dimensi¢n.\\¨Desea actualizar las l¡neas?;FRC=Vous avez probablement modifi‚ une dimension.\\Souhaitez-vous mettre … jour les lignes?;ENC=You may have changed a dimension.\\Do you want to update the lines?';
      NoBankAccountsMsg@1010 : TextConst 'ENU=You have not set up a bank account.\To use the payments import process, set up a bank account.;ESM=No ha configurado ninguna cuenta bancaria.\Para usar el proceso de importaci¢n de pagos, configure una cuenta bancaria.;FRC=Vous n''avez param‚tr‚ aucun compte bancaire.\Veuillez en param‚trer un pour utiliser le processus d''importation de paiements.;ENC=You have not set up a bank account.\To use the payments import process, set up a bank account.';
      NoBankAccWithFileFormatMsg@1011 : TextConst 'ENU=No bank account exists that is ready for import of bank statement files.\Fill the Bank Statement Import Format field on the card of the bank account that you want to use.;ESM=No existen cuentas bancarias listas para la importaci¢n de los archivos de estado de cuenta bancario.\Rellene el campo Formato de importaci¢n de estado de cuenta bancario de la ficha de la cuenta bancaria que desee usar.;FRC=Il n''existe aucun compte bancaire prˆt pour l''importation de fichiers de relev‚ bancaire.\Compl‚tez le champ Format importation relev‚ bancaire sur la fiche du compte bancaire que vous souhaitez utiliser.;ENC=No bank account exists that is ready for import of bank statement files.\Fill the Bank Statement Import Format field on the card of the bank account that you want to use.';
      PostHighConfidentLinesQst@1012 : TextConst 'ENU=All imported bank statement lines were applied with high confidence level.\Do you want to post the payment applications?;ESM=Todas las l¡neas del estado de cuenta bancario importado se aplicaron con un nivel de confianza elevado.\¨Desea registrar las liquidaciones de pago?;FRC=Toutes les lignes de relev‚ bancaire import‚es ont ‚t‚ lettr‚es avec un niveau de confiance ‚lev‚.\Voulez-vous valider les lettrages paiementÿ?;ENC=All imported bank statement lines were applied with high confidence level.\Do you want to post the payment applications?';
      MustHaveValueQst@1015 : TextConst 'ENU=The bank account must have a value in %1. Do you want to open the bank account card?;ESM=La cuenta bancaria debe tener un valor en %1. ¨Desea abrir la tarjeta de cuenta bancaria?;FRC=Le compte bancaire doit avoir une valeur dans %1. Voulez-vous ouvrir la fiche compte bancaire?;ENC=The bank account must have a value in %1. Do you want to open the bank account card?';
      NoTransactionsImportedMsg@1013 : TextConst 'ENU=No bank transactions were imported.;ESM=No se importaron transacciones bancarias.;FRC=Aucune transaction bancaire n''a ‚t‚ import‚e.;ENC=No bank transactions were imported.';

    [External]
    PROCEDURE CreateDim@22(Type1@1000 : Integer;No1@1001 : Code[20]);
    VAR
      SourceCodeSetup@1010 : Record 242;
      TableID@1011 : ARRAY [10] OF Integer;
      No@1012 : ARRAY [10] OF Code[20];
      OldDimSetID@1013 : Integer;
    BEGIN
      SourceCodeSetup.GET;
      TableID[1] := Type1;
      No[1] := No1;
      OnAfterCreateDimTableIDs(Rec,CurrFieldNo,TableID,No);

      "Shortcut Dimension 1 Code" := '';
      "Shortcut Dimension 2 Code" := '';
      OldDimSetID := "Dimension Set ID";
      "Dimension Set ID" :=
        DimMgt.GetRecDefaultDimID(
          Rec,CurrFieldNo,TableID,No,SourceCodeSetup."Payment Reconciliation Journal",
          "Shortcut Dimension 1 Code","Shortcut Dimension 2 Code",0,0);

      IF (OldDimSetID <> "Dimension Set ID") AND LinesExist THEN BEGIN
        MODIFY;
        UpdateAllLineDim("Dimension Set ID",OldDimSetID);
      END;
    END;

    LOCAL PROCEDURE GetCurrencyCode@1() : Code[10];
    VAR
      BankAcc2@1000 : Record 270;
    BEGIN
      IF "Bank Account No." = BankAcc2."No." THEN
        EXIT(BankAcc2."Currency Code");

      IF BankAcc2.GET("Bank Account No.") THEN
        EXIT(BankAcc2."Currency Code");

      EXIT('');
    END;

    [External]
    PROCEDURE MatchSingle@5(DateRange@1008 : Integer);
    VAR
      MatchBankRecLines@1000 : Codeunit 1252;
    BEGIN
      MatchBankRecLines.MatchSingle(Rec,DateRange);
    END;

    [External]
    PROCEDURE ImportBankStatement@6();
    VAR
      DataExch@1001 : Record 1220;
      ProcessBankAccRecLines@1000 : Codeunit 1248;
    BEGIN
      IF BankAccountCouldBeUsedForImport THEN BEGIN
        DataExch.INIT;
        ProcessBankAccRecLines.ImportBankStatement(Rec,DataExch);
      END;
    END;

    [External]
    PROCEDURE ValidateShortcutDimCode@7(FieldNumber@1000 : Integer;VAR ShortcutDimCode@1001 : Code[20]);
    VAR
      DimMgt@1002 : Codeunit 408;
      OldDimSetID@1003 : Integer;
    BEGIN
      OldDimSetID := "Dimension Set ID";
      DimMgt.ValidateShortcutDimValues(FieldNumber,ShortcutDimCode,"Dimension Set ID");

      IF OldDimSetID <> "Dimension Set ID" THEN BEGIN
        MODIFY;
        UpdateAllLineDim("Dimension Set ID",OldDimSetID);
      END;
    END;

    [External]
    PROCEDURE ShowDocDim@3();
    VAR
      OldDimSetID@1000 : Integer;
    BEGIN
      OldDimSetID := "Dimension Set ID";
      "Dimension Set ID" :=
        DimMgt.EditDimensionSet2(
          "Dimension Set ID",STRSUBSTNO('%1 %2',TABLECAPTION,"Statement No."),
          "Shortcut Dimension 1 Code","Shortcut Dimension 2 Code");

      IF OldDimSetID <> "Dimension Set ID" THEN BEGIN
        MODIFY;
        UpdateAllLineDim("Dimension Set ID",OldDimSetID);
      END;
    END;

    LOCAL PROCEDURE UpdateAllLineDim@34(NewParentDimSetID@1000 : Integer;OldParentDimSetID@1001 : Integer);
    VAR
      BankAccReconciliationLine@1003 : Record 274;
      NewDimSetID@1002 : Integer;
    BEGIN
      // Update all lines with changed dimensions.
      IF NewParentDimSetID = OldParentDimSetID THEN
        EXIT;

      BankAccReconciliationLine.LOCKTABLE;
      IF BankAccReconciliationLine.LinesExist(Rec) THEN BEGIN
        IF NOT CONFIRM(YouChangedDimQst) THEN
          EXIT;

        REPEAT
          NewDimSetID :=
            DimMgt.GetDeltaDimSetID(BankAccReconciliationLine."Dimension Set ID",NewParentDimSetID,OldParentDimSetID);
          IF BankAccReconciliationLine."Dimension Set ID" <> NewDimSetID THEN BEGIN
            BankAccReconciliationLine."Dimension Set ID" := NewDimSetID;
            DimMgt.UpdateGlobalDimFromDimSetID(
              BankAccReconciliationLine."Dimension Set ID",
              BankAccReconciliationLine."Shortcut Dimension 1 Code",
              BankAccReconciliationLine."Shortcut Dimension 2 Code");
            BankAccReconciliationLine.MODIFY;
          END;
        UNTIL BankAccReconciliationLine.NEXT = 0;
      END;
    END;

    [External]
    PROCEDURE OpenNewWorksheet@19();
    VAR
      BankAccount@1000 : Record 270;
      BankAccReconciliation@1002 : Record 273;
    BEGIN
      IF NOT SelectBankAccountToUse(BankAccount,FALSE) THEN
        EXIT;

      CreateNewBankPaymentAppBatch(BankAccount."No.",BankAccReconciliation);
      OpenWorksheet(BankAccReconciliation);
    END;

    PROCEDURE ImportAndProcessToNewStatement@8();
    VAR
      BankAccount@1000 : Record 270;
      BankAccReconciliation@1001 : Record 273;
      DataExch@1002 : Record 1220;
      DataExchDef@1003 : Record 1222;
      DummyBankAccReconciliationLine@1005 : Record 274;
      LastStatementNo@1004 : Code[20];
    BEGIN
      IF NOT SelectBankAccountToUse(BankAccount,TRUE) THEN
        EXIT;
      BankAccount.GetDataExchDef(DataExchDef);

      DataExch."Related Record" := BankAccount.RECORDID;
      IF NOT DataExch.ImportFileContent(DataExchDef) THEN
        EXIT;

      BankAccount.LOCKTABLE;
      LastStatementNo := BankAccount."Last Statement No.";
      CreateNewBankPaymentAppBatch(BankAccount."No.",BankAccReconciliation);

      IF NOT ImportStatement(BankAccReconciliation,DataExch) THEN BEGIN
        DeleteBankAccReconciliation(BankAccReconciliation,BankAccount,LastStatementNo);
        MESSAGE(NoTransactionsImportedMsg);
        EXIT;
      END;

      IF NOT DummyBankAccReconciliationLine.LinesExist(BankAccReconciliation) THEN BEGIN
        DeleteBankAccReconciliation(BankAccReconciliation,BankAccount,LastStatementNo);
        MESSAGE(NoTransactionsImportedMsg);
        EXIT;
      END;

      COMMIT;
      ProcessStatement(BankAccReconciliation);
    END;

    LOCAL PROCEDURE DeleteBankAccReconciliation@23(VAR BankAccReconciliation@1000 : Record 273;VAR BankAccount@1002 : Record 270;LastStatementNo@1001 : Code[20]);
    BEGIN
      BankAccReconciliation.DELETE;
      BankAccount.GET(BankAccount."No.");
      BankAccount."Last Statement No." := LastStatementNo;
      BankAccount.MODIFY;
      COMMIT;
    END;

    PROCEDURE ImportStatement@13(VAR BankAccReconciliation@1000 : Record 273;DataExch@1002 : Record 1220) : Boolean;
    VAR
      ProcessBankAccRecLines@1001 : Codeunit 1248;
    BEGIN
      EXIT(ProcessBankAccRecLines.ImportBankStatement(BankAccReconciliation,DataExch))
    END;

    [External]
    PROCEDURE ProcessStatement@20(VAR BankAccReconciliation@1000 : Record 273);
    BEGIN
      CODEUNIT.RUN(CODEUNIT::"Match Bank Pmt. Appl.",BankAccReconciliation);

      IF ConfidenceLevelPermitToPost(BankAccReconciliation) THEN
        CODEUNIT.RUN(CODEUNIT::"Bank Acc. Reconciliation Post",BankAccReconciliation)
      ELSE
        IF GUIALLOWED THEN
          OpenWorksheet(BankAccReconciliation);
    END;

    [External]
    PROCEDURE CreateNewBankPaymentAppBatch@16(BankAccountNo@1001 : Code[20];VAR BankAccReconciliation@1000 : Record 273);
    BEGIN
      BankAccReconciliation.INIT;
      BankAccReconciliation."Statement Type" := BankAccReconciliation."Statement Type"::"Payment Application";
      BankAccReconciliation.VALIDATE("Bank Account No.",BankAccountNo);
      BankAccReconciliation.INSERT(TRUE);
    END;

    PROCEDURE SelectBankAccountToUse@4(VAR BankAccount@1000 : Record 270;OnlyWithImportFormatSet@1002 : Boolean) : Boolean;
    VAR
      TempBankAccount@1003 : TEMPORARY Record 270;
      TempLinkedBankAccount@1001 : TEMPORARY Record 270;
      NoOfAccounts@1004 : Integer;
    BEGIN
      IF OnlyWithImportFormatSet THEN BEGIN
        // copy to temp as we need OR filter
        BankAccount.SETFILTER("Bank Statement Import Format",'<>%1','');
        CopyBankAccountsToTemp(TempBankAccount,BankAccount);

        BankAccount.GetLinkedBankAccounts(TempLinkedBankAccount);
        TempLinkedBankAccount.SETRANGE("Bank Statement Import Format",'');
        CopyBankAccountsToTemp(TempBankAccount,TempLinkedBankAccount);
        NoOfAccounts := TempBankAccount.COUNT;
      END ELSE
        NoOfAccounts := BankAccount.COUNT;

      CASE NoOfAccounts OF
        0:
          BEGIN
            IF NOT BankAccount.GET(CantFindBancAccToUseInPaymentFileImport) THEN
              EXIT(FALSE);

            EXIT(TRUE);
          END;
        1:
          BEGIN
            IF TempBankAccount.COUNT > 0 THEN BEGIN
              TempBankAccount.FINDFIRST;
              BankAccount.GET(TempBankAccount."No.");
            END ELSE
              BankAccount.FINDFIRST;
          END;
        ELSE BEGIN
          IF TempBankAccount.COUNT > 0 THEN BEGIN
            IF PAGE.RUNMODAL(PAGE::"Payment Bank Account List",TempBankAccount) = ACTION::LookupOK THEN BEGIN
              BankAccount.GET(TempBankAccount."No.");
              EXIT(TRUE)
            END;
            EXIT(FALSE);
          END;
          EXIT(PAGE.RUNMODAL(PAGE::"Payment Bank Account List",BankAccount) = ACTION::LookupOK);
        END;
      END;

      EXIT(TRUE);
    END;

    [External]
    PROCEDURE OpenWorksheet@14(BankAccReconciliation@1001 : Record 273);
    VAR
      BankAccReconciliationLine@1000 : Record 274;
    BEGIN
      SetFiltersOnBankAccReconLineTable(BankAccReconciliation,BankAccReconciliationLine);
      PAGE.RUN(PAGE::"Payment Reconciliation Journal",BankAccReconciliationLine);
    END;

    [External]
    PROCEDURE OpenList@12(BankAccReconciliation@1001 : Record 273);
    VAR
      BankAccReconciliationLine@1000 : Record 274;
    BEGIN
      SetFiltersOnBankAccReconLineTable(BankAccReconciliation,BankAccReconciliationLine);
      PAGE.RUN(PAGE::"Pmt. Recon. Journal Overview",BankAccReconciliationLine);
    END;

    LOCAL PROCEDURE CantFindBancAccToUseInPaymentFileImport@18() : Code[20];
    VAR
      BankAccount@1000 : Record 270;
    BEGIN
      IF BankAccount.COUNT = 0 THEN
        MESSAGE(NoBankAccountsMsg)
      ELSE
        MESSAGE(NoBankAccWithFileFormatMsg);

      IF PAGE.RUNMODAL(PAGE::"Payment Bank Account List",BankAccount) = ACTION::LookupOK THEN
        IF (BankAccount."Bank Statement Import Format" <> '') OR
           BankAccount.IsLinkedToBankStatementServiceProvider
        THEN
          EXIT(BankAccount."No.");

      EXIT('');
    END;

    LOCAL PROCEDURE SetLastPaymentStatementNo@9(VAR BankAccount@1000 : Record 270);
    BEGIN
      IF BankAccount."Last Payment Statement No." = '' THEN BEGIN
        BankAccount."Last Payment Statement No." := '0';
        BankAccount.MODIFY;
      END;
    END;

    LOCAL PROCEDURE SetLastStatementNo@17(VAR BankAccount@1000 : Record 270);
    BEGIN
      IF BankAccount."Last Statement No." = '' THEN BEGIN
        BankAccount."Last Statement No." := '0';
        BankAccount.MODIFY;
      END;
    END;

    [External]
    PROCEDURE SetFiltersOnBankAccReconLineTable@10(BankAccReconciliation@1001 : Record 273;VAR BankAccReconciliationLine@1000 : Record 274);
    BEGIN
      BankAccReconciliationLine.FILTERGROUP := 2;
      BankAccReconciliationLine.SETRANGE("Statement Type",BankAccReconciliation."Statement Type");
      BankAccReconciliationLine.SETRANGE("Bank Account No.",BankAccReconciliation."Bank Account No.");
      BankAccReconciliationLine.SETRANGE("Statement No.",BankAccReconciliation."Statement No.");
      BankAccReconciliationLine.FILTERGROUP := 0;
    END;

    LOCAL PROCEDURE ConfidenceLevelPermitToPost@25(BankAccReconciliation@1000 : Record 273) : Boolean;
    VAR
      BankAccReconciliationLine@1001 : Record 274;
    BEGIN
      SetFiltersOnBankAccReconLineTable(BankAccReconciliation,BankAccReconciliationLine);
      IF BankAccReconciliationLine.COUNT = 0 THEN
        EXIT(FALSE);

      BankAccReconciliationLine.SETFILTER("Match Confidence",'<>%1',BankAccReconciliationLine."Match Confidence"::High);
      IF BankAccReconciliationLine.COUNT <> 0 THEN
        EXIT(FALSE);

      IF CONFIRM(PostHighConfidentLinesQst) THEN
        EXIT(TRUE);

      EXIT(FALSE);
    END;

    LOCAL PROCEDURE LinesExist@11() : Boolean;
    VAR
      BankAccReconciliationLine@1000 : Record 274;
    BEGIN
      EXIT(BankAccReconciliationLine.LinesExist(Rec));
    END;

    LOCAL PROCEDURE BankAccountCouldBeUsedForImport@2() : Boolean;
    VAR
      BankAccount@1000 : Record 270;
    BEGIN
      BankAccount.GET("Bank Account No.");
      IF BankAccount."Bank Statement Import Format" <> '' THEN
        EXIT(TRUE);

      IF BankAccount.IsLinkedToBankStatementServiceProvider THEN
        EXIT(TRUE);

      IF NOT CONFIRM(MustHaveValueQst,TRUE,BankAccount.FIELDCAPTION("Bank Statement Import Format")) THEN
        EXIT(FALSE);

      IF PAGE.RUNMODAL(PAGE::"Payment Bank Account Card",BankAccount) = ACTION::LookupOK THEN
        IF BankAccount."Bank Statement Import Format" <> '' THEN
          EXIT(TRUE);

      EXIT(FALSE);
    END;

    [External]
    PROCEDURE GetTempCopy@1020000(VAR BankAccReconciliation@1020000 : Record 273);
    BEGIN
      IF BankAccReconciliation.HASFILTER THEN
        COPYFILTERS(BankAccReconciliation);

      SETRANGE("Statement Type","Statement Type"::"Bank Reconciliation");
      IF NOT FINDSET THEN
        EXIT;

      REPEAT
        BankAccReconciliation := Rec;
        BankAccReconciliation.INSERT;
      UNTIL NEXT = 0;
    END;

    [External]
    PROCEDURE GetTempCopyFromBankRecHeader@1020004(VAR BankAccReconciliation@1020000 : Record 273);
    VAR
      BankRecHeader@1020001 : Record 10120;
    BEGIN
      IF BankAccReconciliation.HASFILTER THEN BEGIN
        BankRecHeader.SETFILTER("Bank Account No.",BankAccReconciliation.GETFILTER("Bank Account No."));
        BankRecHeader.SETFILTER("Statement No.",BankAccReconciliation.GETFILTER("Statement No."));
        BankRecHeader.SETFILTER("Statement Date",BankAccReconciliation.GETFILTER("Statement Date"));
        BankRecHeader.SETFILTER("Statement Balance",BankAccReconciliation.GETFILTER("Balance Last Statement"));
      END;

      IF NOT BankRecHeader.FINDSET THEN
        EXIT;

      REPEAT
        BankAccReconciliation."Statement Type" := BankAccReconciliation."Statement Type"::"Bank Reconciliation";
        BankAccReconciliation."Bank Account No." := BankRecHeader."Bank Account No.";
        BankAccReconciliation."Statement No." := BankRecHeader."Statement No.";
        BankAccReconciliation."Statement Date" := BankRecHeader."Statement Date";
        BankAccReconciliation."Balance Last Statement" := BankRecHeader."Statement Balance";
        BankAccReconciliation."Statement Ending Balance" := BankRecHeader.CalculateEndingBalance;
        BankAccReconciliation.INSERT;
      UNTIL BankRecHeader.NEXT = 0;
    END;

    [External]
    PROCEDURE InsertRec@1020001(StatementType@1020000 : Option;BankAccountNo@1020001 : Code[20]);
    BEGIN
      INIT;
      VALIDATE("Statement Type",StatementType);
      VALIDATE("Bank Account No.",BankAccountNo);
      INSERT(TRUE);
    END;

    [External]
    PROCEDURE DrillDownOnBalanceOnBankAccount@38();
    VAR
      BankAccountLedgerEntry@1000 : Record 271;
    BEGIN
      BankAccountLedgerEntry.SETRANGE(Open,TRUE);
      BankAccountLedgerEntry.SETRANGE("Bank Account No.","Bank Account No.");
      PAGE.RUN(PAGE::"Bank Account Ledger Entries",BankAccountLedgerEntry);
    END;

    LOCAL PROCEDURE CopyBankAccountsToTemp@21(VAR TempBankAccount@1000 : TEMPORARY Record 270;VAR FromBankAccount@1001 : Record 270);
    BEGIN
      IF FromBankAccount.FINDSET THEN
        REPEAT
          TempBankAccount := FromBankAccount;
          IF TempBankAccount.INSERT THEN;
        UNTIL FromBankAccount.NEXT = 0;
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterCreateDimTableIDs@164(VAR BankAccReconciliation@1000 : Record 273;VAR FieldNo@1001 : Integer;VAR TableID@1003 : ARRAY [10] OF Integer;VAR No@1002 : ARRAY [10] OF Code[20]);
    BEGIN
    END;

    BEGIN
    END.
  }
}

