OBJECT Codeunit 412 SMTP Test Mail
{
  OBJECT-PROPERTIES
  {
    Date=09/19/18;
    Time=12:00:00 PM;
    Version List=NAVW113.00;
  }
  PROPERTIES
  {
    OnRun=VAR
            TempNameValueBuffer@1000 : TEMPORARY Record 823;
            Mail@1001 : Codeunit 397;
            AddressChoice@1003 : Text;
            ChosenAddress@1004 : ',ADMail,BasicAuthMail,UserTableMail,OtherMail';
            Address@1002 : Text;
          BEGIN
            Mail.CollectCurrentUserEmailAddresses(TempNameValueBuffer);
            TempNameValueBuffer.RESET;
            IF TempNameValueBuffer.FINDSET THEN
              REPEAT
                IF AddressChoice <> '' THEN
                  AddressChoice := AddressChoice + ',';
                AddressChoice := AddressChoice + TempNameValueBuffer.Value;
              UNTIL TempNameValueBuffer.NEXT = 0;

            AddressChoice := STRSUBSTNO('%1,%2',AddressChoice,TestMailOtherTxt);

            IF AddressChoice = ',' + TestMailOtherTxt THEN
              PromptAndSendEmail
            ELSE BEGIN
              ChosenAddress := STRMENU(AddressChoice,TempNameValueBuffer.COUNT + 1,TestMailChoiceTxt);
              IF ChosenAddress = 0 THEN
                EXIT;
              IF ChosenAddress < TempNameValueBuffer.COUNT + 1 THEN BEGIN
                Address := SELECTSTR(ChosenAddress,AddressChoice);
                SendTestMail(Address);
                MESSAGE(TestMailSuccessMsg,Address);
              END ELSE
                PromptAndSendEmail;
            END;
          END;

  }
  CODE
  {
    VAR
      TestMailChoiceTxt@1003 : TextConst 'ENU=Choose the email address that should receive a test email message:;ESM=Elija la direcci¢n de correo electr¢nico que recibir  un mensaje de correo electr¢nico de prueba:;FRC=S‚lectionnez l''adresse ‚lectronique … laquelle le courrier ‚lectronique de test sera envoy‚ÿ:;ENC=Choose the email address that should receive a test email message:';
      TestMailTitleTxt@1002 : TextConst 'ENU=SMTP Test Email Message;ESM=Mensaje de correo electr¢nico de prueba SMTP;FRC=Courriel de test SMTP;ENC=SMTP Test Email Message';

      TestMailBodyTxt@1001 : TextConst
        '@@@="{Locked=""p style="",""font-family:"",""font-size"",""pt"",""<b>"",""</b>"",""</p>"",""<BR>"",""SMTP""} %1 is an email address, such as user@domain.com; %2 is the name of a mail server, such as mail.live.com; %3 is the TCP port number, such as 25; %4 is the authentication method, such as Basic Authentication; %5 is a boolean value, such as True; %6 is a numeric ID such as 12; %7 is the name identifier of a tenant instance, such as ''MyTenant1'';"',
        'ENU="<p style=""font-family:Verdana,Arial;font-size:10pt""><b>This mail message has been generated by the user %1 for test purposes.</b></p><p style=""font-family:Verdana,Arial;font-size:9pt""><b>Sent through SMTP Server:</b> %2<BR><b>SMTP Port:</b> %3<BR><b>Authentication:</b> %4<BR><b>Using Secure Connection:</b> %5<BR><b>Server Instance ID:</b> %6<BR><b>Tenant ID:</b> %7</p>"',
        'ESM="<p style=""font-family:Verdana,Arial;font-size:10pt""><b>El usuario %1 gener¢ este mensaje de correo electr¢nico con fines de prueba.</b></p><p style=""font-family:Verdana,Arial;font-size:9pt""><b>Enviado a trav‚s del servidor SMTP:</b> %2<BR><b>Puerto SMTP:</b> %3<BR><b>Autenticaci¢n:</b> %4<BR><b>Uso de conexi¢n segura:</b> %5<BR><b>Id. de instancia de servidor:</b> %6<BR><b>Id. de suscriptor:</b> %7</p>"',
        'FRC="<p style=""font-family:Verdana,Arial;font-size:10pt""><b>Ce courriel a ‚t‚ g‚n‚r‚ par l''utilisateur %1 … des fins de test.</b></p><p style=""font-family:Verdana,Arial;font-size:9pt""><b>Envoy‚ via serveur SMTPÿ:</b> %2<BR><b>Port SMTPÿ:</b> %3<BR><b>Authentification:</b> %4<BR><b>Utilisation d''une connexion s‚curis‚e:</b> %5<BR><b>Code instance serveur:</b> %6<BR><b>Code abonn‚:</b> %7</p>"',
        'ENC="<p style=""font-family:Verdana,Arial;font-size:10pt""><b>This mail message has been generated by the user %1 for test purposes.</b></p><p style=""font-family:Verdana,Arial;font-size:9pt""><b>Sent through SMTP Server:</b> %2<BR><b>SMTP Port:</b> %3<BR><b>Authentication:</b> %4<BR><b>Using Secure Connection:</b> %5<BR><b>Server Instance ID:</b> %6<BR><b>Tenant ID:</b> %7</p>"';
      TestMailSuccessMsg@1000 : TextConst '@@@=%1 is an email address.;ENU=Test email has been sent to %1.\Check your email for messages to make sure that the email was delivered successfully.;ESM=Se ha enviado un correo electr¢nico de prueba a %1.\Compruebe los mensajes de correo electr¢nico para asegurarse de que el correo electr¢nico se entreg¢ correctamente.;FRC=Le courriel test a ‚t‚ envoy‚ … ®ÿ%1ÿ¯.\Contr“lez vos messages pour v‚rifier que vous avez bien re‡u ce courriel.;ENC=Test email has been sent to %1.\Check your email for messages to make sure that the email was delivered successfully.';
      TestMailOtherTxt@1004 : TextConst 'ENU=Other...;ESM=Otros...;FRC=Autre...;ENC=Other...';

    [TryFunction]
    PROCEDURE SendTestMail@5(EmailAddress@1002 : Text);
    VAR
      SMTPMailSetup@1003 : Record 409;
      SMTPMail@1000 : Codeunit 400;
      MailManagement@1001 : Codeunit 9520;
    BEGIN
      SMTPMailSetup.GetSetup;

      SMTPMail.CreateMessage(
        '',
        MailManagement.GetSenderEmailAddress,
        EmailAddress,
        TestMailTitleTxt,
        STRSUBSTNO(
          TestMailBodyTxt,
          USERID,
          SMTPMailSetup."SMTP Server",
          FORMAT(SMTPMailSetup."SMTP Server Port"),
          SMTPMailSetup.Authentication,
          SMTPMailSetup."Secure Connection",
          SERVICEINSTANCEID,
          TENANTID),
        TRUE);

      SMTPMail.Send;
    END;

    LOCAL PROCEDURE PromptAndSendEmail@13();
    VAR
      SMTPUserSpecifiedAddress@1001 : Page 410;
      Address@1000 : Text;
    BEGIN
      IF SMTPUserSpecifiedAddress.RUNMODAL = ACTION::OK THEN BEGIN
        Address := SMTPUserSpecifiedAddress.GetEmailAddress;
        IF Address = '' THEN
          EXIT;
        SendTestMail(Address);
        MESSAGE(TestMailSuccessMsg,Address);
      END;
    END;

    BEGIN
    END.
  }
}

