OBJECT Table 6086308 Expense Attendee
{
  OBJECT-PROPERTIES
  {
    Date=01/27/17;
    Time=[ 1:00:00 AM];
    Version List=EMW16.00.10.2.50;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               TESTFIELD(Name);
               CheckExpense;

               IF "Entry No." = 0 THEN
                 "Entry No." := GetNextEntryNo;
             END;

    OnModify=BEGIN
               CheckExpense;
             END;

    OnDelete=BEGIN
               CheckExpense;
             END;

    OnRename=BEGIN
               CheckExpense;
             END;

    CaptionML=ENU=Expense Attendee;
    LookupPageID=Page6086325;
    DrillDownPageID=Page6086325;
  }
  FIELDS
  {
    { 1   ;   ;Expense Entry No.   ;Integer       ;TableRelation=Expense;
                                                   CaptionML=ENU=Expense Entry No. }
    { 2   ;   ;Entry No.           ;Integer       ;CaptionML=ENU=Entry No. }
    { 10  ;   ;Type                ;Option        ;CaptionML=ENU=Type;
                                                   OptionCaptionML=ENU=Employee,Guest;
                                                   OptionString=Employee,Guest }
    { 20  ;   ;Name                ;Text50        ;CaptionML=ENU=Name;
                                                   NotBlank=Yes }
    { 30  ;   ;Company Name        ;Text50        ;CaptionML=ENU=Company Name }
  }
  KEYS
  {
    {    ;Expense Entry No.,Entry No.             ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {

    PROCEDURE CheckExpense@1160040000();
    BEGIN
      TestNotPosted;
    END;

    LOCAL PROCEDURE TestNotPosted@37();
    VAR
      Expense@1160040000 : Record 6086320;
      Mileage@1160040001 : Record 6086338;
    BEGIN
      Expense.GET("Expense Entry No.");
      Expense.TESTFIELD(Posted,FALSE);
    END;

    PROCEDURE GetNextEntryNo@1160040001() : Integer;
    VAR
      ExpenseAttendee@1160040000 : Record 6086308;
    BEGIN
      ExpenseAttendee.SETRANGE("Expense Entry No.","Expense Entry No.");
      IF ExpenseAttendee.FINDLAST THEN
        EXIT(ExpenseAttendee."Entry No." + 1)
      ELSE
        EXIT(1);
    END;

    PROCEDURE AttendeesUpdated@1160040002(VAR EMAttendee@1160040000 : Record 6086308;ExpenseEntryNo@1160040005 : Integer) : Boolean;
    VAR
      xEMAttendee@1160040001 : Record 6086308;
    BEGIN
      EMAttendee.SETRANGE("Expense Entry No.",ExpenseEntryNo);

      xEMAttendee.SETRANGE("Expense Entry No.",ExpenseEntryNo);

      IF EMAttendee.FINDSET THEN
        REPEAT

          IF NOT xEMAttendee.GET(EMAttendee."Expense Entry No.",EMAttendee."Entry No.") THEN
            EXIT(TRUE);

          IF (EMAttendee.Type <> xEMAttendee.Type) OR
             (EMAttendee.Name <> xEMAttendee.Name) OR
             (EMAttendee."Company Name" <> xEMAttendee."Company Name")
          THEN
            EXIT(TRUE);

        UNTIL EMAttendee.NEXT = 0;

      IF xEMAttendee.FINDSET THEN
        REPEAT
          IF NOT EMAttendee.GET(xEMAttendee."Expense Entry No.",xEMAttendee."Entry No.") THEN
            EXIT(TRUE);
        UNTIL xEMAttendee.NEXT = 0;
    END;

    BEGIN
    END.
  }
}

