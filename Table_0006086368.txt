OBJECT Table 6086368 EM Default Dimension
{
  OBJECT-PROPERTIES
  {
    Date=01/27/17;
    Time=[ 1:00:00 AM];
    Version List=EMW16.00.10.2.50;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               GLSetup.GET;
               IF "Dimension Code" = GLSetup."Global Dimension 1 Code" THEN
                 UpdateGlobalDimCode(1,"Table ID","No.","Dimension Value Code");
               IF "Dimension Code" = GLSetup."Global Dimension 2 Code" THEN
                 UpdateGlobalDimCode(2,"Table ID","No.","Dimension Value Code");
             END;

    OnModify=BEGIN
               GLSetup.GET;
               IF "Dimension Code" = GLSetup."Global Dimension 1 Code" THEN
                 UpdateGlobalDimCode(1,"Table ID","No.","Dimension Value Code");
               IF "Dimension Code" = GLSetup."Global Dimension 2 Code" THEN
                 UpdateGlobalDimCode(2,"Table ID","No.","Dimension Value Code");
             END;

    OnDelete=BEGIN
               GLSetup.GET;
               IF "Dimension Code" = GLSetup."Global Dimension 1 Code" THEN
                 UpdateGlobalDimCode(1,"Table ID","No.",'');
               IF "Dimension Code" = GLSetup."Global Dimension 2 Code" THEN
                 UpdateGlobalDimCode(2,"Table ID","No.",'');
             END;

    OnRename=BEGIN
               ERROR(Text000,TABLECAPTION);
             END;

    CaptionML=ENU=Default Dimension;
  }
  FIELDS
  {
    { 1   ;   ;Table ID            ;Integer       ;TableRelation=Object.ID WHERE (Type=CONST(Table));
                                                   OnValidate=VAR
                                                                TempObject@1000 : TEMPORARY Record 2000000001;
                                                              BEGIN
                                                                CALCFIELDS("Table Name");
                                                                EMDimMgt.SetupObjectNoList(TempObject);
                                                                TempObject.Type := TempObject.Type::Table;
                                                                TempObject.ID := "Table ID";
                                                                IF NOT TempObject.FIND THEN
                                                                  FIELDERROR("Table ID");
                                                              END;

                                                   OnLookup=VAR
                                                              TempObject@1000 : TEMPORARY Record 2000000001;
                                                            BEGIN
                                                              CLEAR(TempObject);
                                                              EMDimMgt.SetupObjectNoList(TempObject);
                                                              IF PAGE.RUNMODAL(358,TempObject) = ACTION::LookupOK THEN BEGIN
                                                                "Table ID" := TempObject.ID;
                                                                VALIDATE("Table ID");
                                                              END;
                                                            END;

                                                   CaptionML=ENU=Table ID;
                                                   NotBlank=Yes }
    { 2   ;   ;No.                 ;Code20        ;TableRelation=IF (Table ID=CONST(6086307)) "Expense Type";
                                                   CaptionML=ENU=No. }
    { 3   ;   ;Dimension Code      ;Code20        ;TableRelation=Dimension;
                                                   OnValidate=BEGIN
                                                                IF NOT DimMgt.CheckDim("Dimension Code") THEN
                                                                  ERROR(DimMgt.GetDimErr);
                                                              END;

                                                   CaptionML=ENU=Dimension Code;
                                                   NotBlank=Yes }
    { 4   ;   ;Dimension Value Code;Code20        ;TableRelation="Dimension Value".Code WHERE (Dimension Code=FIELD(Dimension Code));
                                                   OnValidate=BEGIN
                                                                IF NOT DimMgt.CheckDimValue("Dimension Code","Dimension Value Code") THEN
                                                                  ERROR(DimMgt.GetDimErr);
                                                              END;

                                                   CaptionML=ENU=Dimension Value Code }
    { 5   ;   ;Value Posting       ;Option        ;CaptionML=ENU=Value Posting;
                                                   OptionCaptionML=ENU=" ,Code Mandatory,Same Code,No Code";
                                                   OptionString=[ ,Code Mandatory,Same Code,No Code] }
    { 6   ;   ;Table Name          ;Text249       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObjWithCaption."Object Caption" WHERE (Object Type=CONST(Table),
                                                                                                                Object ID=FIELD(Table ID)));
                                                   CaptionML=ENU=Table Name;
                                                   Editable=No }
    { 7   ;   ;Multi Selection Action;Option      ;CaptionML=ENU=Multi Selection Action;
                                                   OptionCaptionML=ENU=" ,Change,Delete";
                                                   OptionString=[ ,Change,Delete] }
  }
  KEYS
  {
    {    ;Table ID,No.,Dimension Code             ;Clustered=Yes }
    {    ;Dimension Code                           }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      GLSetup@1001 : Record 98;
      DimMgt@1160040000 : Codeunit 408;
      EMDimMgt@1002 : Codeunit 6086318;
      Text000@1160040001 : TextConst 'ENU=You cannot rename a %1.';

    PROCEDURE GetCaption@3() : Text[250];
    VAR
      ObjTransl@1000 : Record 377;
      SourceTableName@1004 : Text[100];
      NewNo@1003 : Code[20];
      CurrTableID@1001 : Integer;
      NewTableID@1002 : Integer;
    BEGIN
      IF NOT EVALUATE(NewTableID,GETFILTER("Table ID")) THEN
        EXIT('');

      IF NewTableID = 0 THEN
        IF GETRANGEMIN("Table ID") = GETRANGEMAX("Table ID") THEN
          NewTableID := GETRANGEMIN("Table ID")
        ELSE
          NewTableID := 0;

      IF NewTableID <> CurrTableID THEN
        SourceTableName := ObjTransl.TranslateObject(ObjTransl."Object Type"::Table,NewTableID);
      CurrTableID := NewTableID;

      IF GETFILTER("No.") <> '' THEN
        IF GETRANGEMIN("No.") = GETRANGEMAX("No.") THEN
          NewNo := GETRANGEMIN("No.")
        ELSE
          NewNo := '';

      IF NewTableID <> 0 THEN
        EXIT(STRSUBSTNO('%1 %2',SourceTableName,NewNo));

      EXIT('');
    END;

    LOCAL PROCEDURE UpdateGlobalDimCode@25(GlobalDimCodeNo@1000 : Integer;TableID@1001 : Integer;No@1002 : Code[20];NewDimValue@1003 : Code[20]);
    VAR
      ExpenseType@1004 : Record 6086307;
    BEGIN
      CASE "Table ID" OF

        DATABASE::"Expense Type":
          IF ExpenseType.GET(No) THEN BEGIN
            CASE GlobalDimCodeNo OF
              1:ExpenseType."Global Dimension 1 Code" := NewDimValue;
              2:ExpenseType."Global Dimension 2 Code" := NewDimValue;
            END;
            ExpenseType.MODIFY(TRUE);
          END;
      END;
    END;

    BEGIN
    END.
  }
}

