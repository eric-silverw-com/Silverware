OBJECT Page 6560 Document Line Tracking
{
  OBJECT-PROPERTIES
  {
    Date=09/19/18;
    Time=12:00:00 PM;
    Version List=NAVW113.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Document Line Tracking;
               ESM=Seguimiento de &l¡nea de documento;
               FRC=Tra‡abilit‚ ligne document;
               ENC=Document Line Tracking];
    SourceTable=Table265;
    DataCaptionExpr=DocumentCaption;
    PageType=List;
    OnInit=BEGIN
             ShowEnable := TRUE;
           END;

    OnOpenPage=BEGIN
                 IF (SourceDocNo = '') OR (SourceDocLineNo = 0) THEN
                   EXIT;

                 FindRecords;
               END;

    OnFindRecord=BEGIN
                   TempDocumentEntry := Rec;
                   IF NOT TempDocumentEntry.FIND(Which) THEN
                     EXIT(FALSE);
                   Rec := TempDocumentEntry;
                   EXIT(TRUE);
                 END;

    OnNextRecord=VAR
                   CurrentSteps@1000 : Integer;
                 BEGIN
                   TempDocumentEntry := Rec;
                   CurrentSteps := TempDocumentEntry.NEXT(Steps);
                   IF CurrentSteps <> 0 THEN
                     Rec := TempDocumentEntry;
                   EXIT(CurrentSteps);
                 END;

    ActionList=ACTIONS
    {
      { 15      ;0   ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 14      ;1   ;Action    ;
                      Name=Show;
                      CaptionML=[ENU=&Show;
                                 ESM=&Mostrar;
                                 FRC=&Afficher;
                                 ENC=&Show];
                      ToolTipML=[ENU=Show related document.;
                                 ESM=Mostrar documento relacionado.;
                                 FRC=Afficher le document associ‚.;
                                 ENC=Show related document.];
                      ApplicationArea=#Basic,#Suite;
                      Promoted=Yes;
                      Enabled=ShowEnable;
                      Image=View;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 ShowRecords;
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 13  ;0   ;Container ;
                ContainerType=ContentArea }

    { 12  ;1   ;Group     ;
                CaptionML=[ENU=General;
                           ESM=General;
                           FRC=G‚n‚ral;
                           ENC=General] }

    { 11  ;2   ;Field     ;
                Name=SourceDocLineNo;
                CaptionML=[ENU=Line No.;
                           ESM=N.§ l¡nea;
                           FRC=Nø ligne;
                           ENC=Line No.];
                ToolTipML=[ENU=Specifies the number of the tracked line.;
                           ESM=Especifica el n£mero de la l¡nea seguida.;
                           FRC=Sp‚cifie le num‚ro de la ligne suivie.;
                           ENC=Specifies the number of the tracked line.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr=SourceDocLineNo;
                Editable=FALSE }

    { 10  ;2   ;Field     ;
                Name=DocLineType;
                CaptionML=[ENU=Type;
                           ESM=Tipo;
                           FRC=Type;
                           ENC=Type];
                ToolTipML=[ENU="Specifies the type of the tracked document. ";
                           ESM="Especifica el tipo del documento seguido. ";
                           FRC="Sp‚cifie le type du document suivi. ";
                           ENC="Specifies the type of the tracked document. "];
                ApplicationArea=#Basic,#Suite;
                SourceExpr=DocLineType;
                Editable=FALSE }

    { 9   ;2   ;Field     ;
                Name=DocLineNo;
                CaptionML=[ENU=No.;
                           ESM=N.§;
                           FRC=Nø;
                           ENC=No.];
                ToolTipML=[ENU=Specifies the number of the tracked document line.;
                           ESM=Especifica el n£mero de la l¡nea de documento seguida.;
                           FRC=Sp‚cifie le num‚ro de la ligne document suivie.;
                           ENC=Specifies the number of the tracked document line.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr=DocLineNo;
                Editable=FALSE }

    { 8   ;2   ;Field     ;
                Name=DocLineDescription;
                CaptionML=[ENU=Description;
                           ESM=Descripci¢n;
                           FRC=Description;
                           ENC=Description];
                ToolTipML=[ENU=Specifies a description of the record.;
                           ESM=Especifica una descripci¢n del registro.;
                           FRC=Sp‚cifie la description de l'enregistrement.;
                           ENC=Specifies a description of the record.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr=DocLineDescription;
                Editable=FALSE }

    { 7   ;2   ;Field     ;
                Name=DocLineQuantity;
                CaptionML=[ENU=Quantity;
                           ESM=Cantidad;
                           FRC=Quantit‚;
                           ENC=Quantity];
                ToolTipML=[ENU=Specifies the quantity on the tracked document line.;
                           ESM=Especifica la cantidad en la l¡nea de documento seguida.;
                           FRC=Sp‚cifie la quantit‚ sur la ligne document suivie.;
                           ENC=Specifies the quantity on the tracked document line.];
                ApplicationArea=#Basic,#Suite;
                DecimalPlaces=0:5;
                SourceExpr=DocLineQuantity;
                Editable=FALSE }

    { 6   ;2   ;Field     ;
                Name=DocLineUnit;
                CaptionML=[ENU=Unit of Measure Code;
                           ESM=C¢d. unidad medida;
                           FRC=Code unit‚ de mesure;
                           ENC=Unit of Measure Code];
                ToolTipML=[ENU=Specifies the unit of measure that the item is shown in.;
                           ESM=Especifica la unidad de medida en la que se mostrar  el producto.;
                           FRC=Sp‚cifie l'unit‚ de mesure d'affichage de l'article.;
                           ENC=Specifies the unit of measure that the item is shown in.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr=DocLineUnit;
                Editable=FALSE }

    { 5   ;1   ;Group     ;
                Editable=FALSE;
                GroupType=Repeater }

    { 4   ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the number that is assigned to the entry.;
                           ESM=Especifica el n£mero asignado al movimiento.;
                           FRC=Sp‚cifie le num‚ro qui est affect‚ … l'‚criture.;
                           ENC=Specifies the number that is assigned to the entry.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Entry No.";
                Visible=FALSE }

    { 3   ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the ID of the table that stores the tracked document line.;
                           ESM=Especifica el ID de la tabla en la que se almacena la l¡nea de documento seguida.;
                           FRC=Sp‚cifie le code de la table qui stocke la ligne document suivie.;
                           ENC=Specifies the ID of the table that stores the tracked document line.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Table ID";
                Visible=FALSE }

    { 2   ;2   ;Field     ;
                Name=Table Name;
                ToolTipML=[ENU=Specifies the name of the table that stores the tracked document line.;
                           ESM=Especifica el nombre de la tabla en la que se almacena la l¡nea de documento seguida.;
                           FRC=Sp‚cifie le nom de la table qui stocke la ligne document suivie.;
                           ENC=Specifies the name of the table that stores the tracked document line.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Table Name" }

    { 1   ;2   ;Field     ;
                DrillDown=Yes;
                ToolTipML=[ENU=Specifies how many records contain the tracked document line.;
                           ESM=Especifica cu ntos registros contiene la l¡nea de documento seguida.;
                           FRC=Sp‚cifie le nombre d'enregistrements contenant la ligne document suivie.;
                           ENC=Specifies how many records contain the tracked document line.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr="No. of Records";
                OnDrillDown=BEGIN
                              ShowRecords;
                            END;
                             }

  }
  CODE
  {
    VAR
      CountingRecordsMsg@1000 : TextConst 'ENU=Counting records...;ESM=Contando registros...;FRC=Comptage des enregistrements...;ENC=Counting records...';
      SalesOrderLinesTxt@1001 : TextConst 'ENU=Sales Order Lines;ESM=L¡neas pedido venta;FRC=Lignes document de vente;ENC=Sales Order Lines';
      ArchivedSalesOrderLinesTxt@1002 : TextConst 'ENU=Archived Sales Order Lines;ESM=L¡neas pedido de ventas archivado;FRC=Lignes document de vente archiv‚;ENC=Archived Sales Order Lines';
      PostedSalesShipmentLinesTxt@1003 : TextConst 'ENU=Posted Sales Shipment Lines;ESM=Hist. l¡ns. albs. ventas;FRC=Lignes livraison de vente report‚e;ENC=Posted Sales Shipment Lines';
      PostedSalesInvoiceLinesTxt@1004 : TextConst 'ENU=Posted Sales Invoice Lines;ESM=Hist. l¡ns. facturas venta;FRC=Lignes facture vente report‚e;ENC=Posted Sales Invoice Lines';
      PurchaseOrderLinesTxt@1005 : TextConst 'ENU=Purchase Order Lines;ESM=L¡neas pedido compra;FRC=Lignes bon de commande;ENC=Purchase Order Lines';
      ArchivedPurchaseOrderLinesTxt@1006 : TextConst 'ENU=Archived Purchase Order Lines;ESM=L¡neas pedido de compra archivado;FRC=Lignes bon de commande archiv‚;ENC=Archived Purchase Order Lines';
      PostedPurchaseReceiptLinesTxt@1007 : TextConst 'ENU=Posted Purchase Receipt Lines;ESM=Hist. l¡ns. albs. compra;FRC=Lignes r‚ception achat report‚e;ENC=Posted Purchase Receipt Lines';
      PostedPurchaseInvoiceLinesTxt@1008 : TextConst 'ENU=Posted Purchase Invoice Lines;ESM=Hist. l¡ns. facturas compra;FRC=Lignes facture achat report‚e;ENC=Posted Purchase Invoice Lines';
      NoSalesOrderMsg@1009 : TextConst 'ENU=There is no Sales Order / Archived Sales Order with this Document Number and Document Line No.;ESM=No hay ning£n pedido de ventas / pedido ventas archivado con este n£mero de documento y este n£mero de l¡nea de documento.;FRC=Il n''existe aucun document de vente / document de vente archiv‚ portant ce num‚ro de document et ce Nø ligne document.;ENC=There is no Sales Order / Archived Sales Order with this Document Number and Document Line No.';
      NoPurchaseOrderMsg@1011 : TextConst 'ENU=There is no Purchase Order / Archived Purchase Order with this Document Number and Document Line No.;ESM=No hay ning£n pedido de compra / pedido de compra archivado con este n£mero de documento y este n£mero de l¡nea de documento.;FRC=Il n''existe aucun bon de commande / bon de commande archiv‚ portant ce num‚ro de document et ce Nø ligne document.;ENC=There is no Purchase Order / Archived Purchase Order with this Document Number and Document Line No.';
      ArchivedTxt@1012 : TextConst 'ENU=Archived;ESM=Archivado;FRC=Archiv‚;ENC=Archived';
      BlanketSalesOrderLinesTxt@1013 : TextConst 'ENU=Blanket Sales Order Lines;ESM=L¡neas de pedido de ventas abierto;FRC=Lignes commande permanente ventes;ENC=Blanket Sales Order Lines';
      ArchivedBlanketSalesOrderLinesTxt@1014 : TextConst 'ENU=Archived Blanket Sales Order Lines;ESM=L¡neas de pedido de venta abierto archivado;FRC=Lignes commande permanente ventes archiv‚e;ENC=Archived Blanket Sales Order Lines';
      BlanketPurchaseOrderLinesTxt@1015 : TextConst 'ENU=Blanket Purchase Order Lines;ESM=L¡neas de pedido de compra abierto;FRC=Lignes commande permanente achats;ENC=Blanket Purchase Order Lines';
      ArchivedBlanketPurchaseOrderLinesTxt@1016 : TextConst 'ENU=Archived Blanket Purchase Order Lines;ESM=L¡neas de pedido de compra abierto archivado;FRC=Lignes commande permanente achats archiv‚e;ENC=Archived Blanket Purchase Order Lines';
      SalesReturnOrderLinesTxt@1052 : TextConst 'ENU=Sales Return Order Lines;ESM=L¡neas de devoluci¢n ventas;FRC=Lignes retour vente;ENC=Sales Return Order Lines';
      ArchivedSalesReturnOrderLinesTxt@1053 : TextConst 'ENU=Archived Sales Return Order Lines;ESM=L¡neas de devoluci¢n ventas archivada;FRC=Lignes retour vente archiv‚;ENC=Archived Sales Return Order Lines';
      PostedReturnReceiptLinesTxt@1054 : TextConst 'ENU=Posted Return Receipt Lines;ESM=L¡ns. hist¢rico recep. dev.;FRC=Lignes r‚ception retour report‚e;ENC=Posted Return Receipt Lines';
      PostedSalesCreditMemoLinesTxt@1055 : TextConst 'ENU=Posted Sales Credit Memo Lines;ESM=L¡neas de notas de cr‚dito de ventas registradas;FRC=Lignes note de cr‚dit vente report‚e;ENC=Posted Sales Credit Memo Lines';
      PurchaseReturnOrderLinesTxt@1056 : TextConst 'ENU=Purchase Return Order Lines;ESM=L¡neas devoluci¢n compra;FRC=Lignes retour achat;ENC=Purchase Return Order Lines';
      ArchivedPurchaseReturnOrderLinesTxt@1057 : TextConst 'ENU=Archived Purchase Return Order Lines;ESM=L¡neas devoluci¢n compra archivada;FRC=Lignes retour achat archiv‚;ENC=Archived Purchase Return Order Lines';
      PostedReturnShipmentLinesTxt@1058 : TextConst 'ENU=Posted Return Shipment Lines;ESM=L¡ns. hist¢rico env¡o devol.;FRC=Lignes livraison retour report‚e;ENC=Posted Return Shipment Lines';
      PostedPurchaseCreditMemoLinesTxt@1059 : TextConst 'ENU=Posted Purchase Credit Memo Lines;ESM=L¡neas de notas de cr‚dito de compras registradas;FRC=Lignes note de cr‚dit achat report‚e;ENC=Posted Purchase Credit Memo Lines';
      SalesLine@1017 : Record 37;
      SalesShptLine@1018 : Record 111;
      SalesInvLine@1019 : Record 113;
      SalesCrMemoLine@1020 : Record 115;
      SalesLineArchive@1021 : Record 5108;
      PurchLine@1022 : Record 39;
      PurchRcptLine@1023 : Record 121;
      PurchInvLine@1024 : Record 123;
      PurchCrMemoLine@1025 : Record 125;
      PurchLineArchive@1026 : Record 5110;
      TempDocumentEntry@1027 : TEMPORARY Record 265;
      ReturnReceiptLine@1050 : Record 6661;
      ReturnShipmentLine@1051 : Record 6651;
      BlanketSalesOrderLine@1028 : Record 37;
      BlanketSalesOrderLineArchive@1029 : Record 5108;
      BlanketPurchOrderLine@1030 : Record 39;
      BlanketPurchOrderLineArchive@1031 : Record 5110;
      Window@1032 : Dialog;
      SourceDocType@1033 : 'SalesOrder,PurchaseOrder,BlanketSalesOrder,BlanketPurchaseOrder,SalesShipment,PurchaseReceipt,SalesInvoice,PurchaseInvoice,SalesReturnOrder,PurchaseReturnOrder,SalesCreditMemo,PurchaseCreditMemo,ReturnReceipt,ReturnShipment';
      SourceDocNo@1034 : Code[20];
      SourceDocBlanketOrderNo@1035 : Code[20];
      SourceDocOrderNo@1036 : Code[20];
      SourceDocLineNo@1039 : Integer;
      SourceDocBlanketOrderLineNo@1040 : Integer;
      SourceDocOrderLineNo@1041 : Integer;
      DocumentCaption@1042 : Text[60];
      DocNo@1037 : Code[20];
      DocLineNo@1010 : Code[20];
      DocType@1043 : Text[30];
      DocArchive@1044 : Text[30];
      DocLineType@1045 : Text[30];
      DocLineDescription@1046 : Text[50];
      DocLineUnit@1047 : Text[10];
      DocLineQuantity@1048 : Decimal;
      DocExists@1049 : Boolean;
      ShowEnable@19017131 : Boolean INDATASET;

    [External]
    PROCEDURE SetDoc@1000(NewSourceDocType@1000 : 'SalesOrder,PurchaseOrder,BlanketSalesOrder,BlanketPurchaseOrder,SalesShipment,PurchaseReceipt,SalesInvoice,PurchaseInvoice,SalesReturnOrder,PurchaseReturnOrder,SalesCreditMemo,PurchaseCreditMemo,ReturnReceipt,ReturnShipment';NewDocNo@1001 : Code[20];NewSourceDocLineNo@1002 : Integer;NewDocBlanketOrderNo@1003 : Code[20];NewDocBlanketOrderLineNo@1004 : Integer;NewDocOrderNo@1005 : Code[20];NewDocOrderLineNo@1006 : Integer);
    BEGIN
      SourceDocType := NewSourceDocType;
      SourceDocNo := NewDocNo;
      SourceDocLineNo := NewSourceDocLineNo;
      SourceDocBlanketOrderNo := NewDocBlanketOrderNo;
      SourceDocBlanketOrderLineNo := NewDocBlanketOrderLineNo;
      SourceDocOrderNo := NewDocOrderNo;
      SourceDocOrderLineNo := NewDocOrderLineNo;

      Rec := Rec;
    END;

    LOCAL PROCEDURE FindRecords@1001();
    BEGIN
      WITH TempDocumentEntry DO BEGIN
        Window.OPEN(CountingRecordsMsg);
        DELETEALL;
        "Entry No." := 0;

        CASE SourceDocType OF
          SourceDocType::SalesOrder:
            FindRecordsForSalesOrder;
          SourceDocType::PurchaseOrder:
            FindRecordsForPurchOrder;
          SourceDocType::BlanketSalesOrder:
            FindRecordsForBlanketSalesOrder;
          SourceDocType::BlanketPurchaseOrder:
            FindRecordsForBlanketPurchOrder;
          SourceDocType::SalesShipment:
            FindRecordsForSalesShipment;
          SourceDocType::PurchaseReceipt:
            FindRecordsForPurchaseReceipt;
          SourceDocType::SalesInvoice:
            FindRecordsForSalesInvoice;
          SourceDocType::PurchaseInvoice:
            FindRecordsForPurchInvoice;
          SourceDocType::SalesReturnOrder:
            FindRecordsForSalesReturnOrder;
          SourceDocType::PurchaseReturnOrder:
            FindRecordsForPurchReturnOrder;
          SourceDocType::SalesCreditMemo:
            FindRecordsForSalesCreditMemo;
          SourceDocType::PurchaseCreditMemo:
            FindRecordsForPurchCreditMemo;
          SourceDocType::ReturnReceipt:
            FindRecordsForReturnReceipt;
          SourceDocType::ReturnShipment:
            FindRecordsForReturnShipment;
        END;

        GetDocumentData;

        IF DocNo = '' THEN
          CASE SourceDocType OF
            SourceDocType::SalesOrder:
              MESSAGE(NoSalesOrderMsg);
            SourceDocType::PurchaseOrder:
              MESSAGE(NoPurchaseOrderMsg);
          END;

        DocExists := FIND('-');
        ShowEnable := DocExists;
        CurrPage.UPDATE(FALSE);
        DocExists := FIND('-');
        IF DocExists THEN;
        Window.CLOSE;
      END;
    END;

    LOCAL PROCEDURE FindRecordsForSalesOrder@1();
    BEGIN
      FindSalesBlanketOrderLines(SourceDocBlanketOrderNo,SourceDocBlanketOrderLineNo);
      FindSalesBlanketOrderLinesArchive(SourceDocBlanketOrderNo,SourceDocBlanketOrderLineNo);
      FindSalesOrderLines(SourceDocNo,SourceDocLineNo);
      FindSalesOrderLinesArchive(SourceDocNo,SourceDocLineNo);
      FindSalesShipmentLinesByOrder(SourceDocNo,SourceDocLineNo);
      FindSalesInvoiceLinesByOrder(SourceDocNo,SourceDocLineNo);
    END;

    LOCAL PROCEDURE FindRecordsForPurchOrder@3();
    BEGIN
      FindPurchBlanketOrderLines(SourceDocBlanketOrderNo,SourceDocBlanketOrderLineNo);
      FindPurchBlanketOrderLinesArchive(SourceDocBlanketOrderNo,SourceDocBlanketOrderLineNo);
      FindPurchOrderLines(SourceDocNo,SourceDocLineNo);
      FindPurchOrderLinesArchive(SourceDocNo,SourceDocLineNo);
      FindPurchReceiptLinesByOrder(SourceDocNo,SourceDocLineNo);
      FindPurchInvoiceLinesByOrder(SourceDocNo,SourceDocLineNo);
    END;

    LOCAL PROCEDURE FindRecordsForBlanketSalesOrder@4();
    BEGIN
      FindSalesBlanketOrderLines(SourceDocNo,SourceDocLineNo);
      FindSalesBlanketOrderLinesArchive(SourceDocNo,SourceDocLineNo);
      FindSalesOrderLinesByBlanketOrder(SourceDocNo,SourceDocLineNo);
      FindSalesOrderLinesArchiveByBlanketOrder(SourceDocNo,SourceDocLineNo);
      FindSalesShipmentLinesByBlanketOrder(SourceDocNo,SourceDocLineNo);
      FindSalesInvoiceLinesByBlanketOrder(SourceDocNo,SourceDocLineNo);
    END;

    LOCAL PROCEDURE FindRecordsForBlanketPurchOrder@5();
    BEGIN
      FindPurchBlanketOrderLines(SourceDocNo,SourceDocLineNo);
      FindPurchBlanketOrderLinesArchive(SourceDocNo,SourceDocLineNo);
      FindPurchOrderLinesByBlanketOrder(SourceDocNo,SourceDocLineNo);
      FindPurchOrderLinesArchiveByBlanketOrder(SourceDocNo,SourceDocLineNo);
      FindPurchReceiptLinesByBlanketOrder(SourceDocNo,SourceDocLineNo);
      FindPurchInvoiceLinesByBlanketOrder(SourceDocNo,SourceDocLineNo);
    END;

    LOCAL PROCEDURE FindRecordsForSalesShipment@6();
    BEGIN
      FindSalesBlanketOrderLines(SourceDocBlanketOrderNo,SourceDocBlanketOrderLineNo);
      FindSalesBlanketOrderLinesArchive(SourceDocBlanketOrderNo,SourceDocBlanketOrderLineNo);
      FindSalesOrderLines(SourceDocOrderNo,SourceDocOrderLineNo);
      FindSalesOrderLinesArchive(SourceDocOrderNo,SourceDocOrderLineNo);
      FindSalesShipmentLinesByOrder(SourceDocOrderNo,SourceDocOrderLineNo);
      FindSalesInvoiceLinesByOrder(SourceDocOrderNo,SourceDocOrderLineNo);
    END;

    LOCAL PROCEDURE FindRecordsForPurchaseReceipt@7();
    BEGIN
      FindPurchBlanketOrderLines(SourceDocBlanketOrderNo,SourceDocBlanketOrderLineNo);
      FindPurchBlanketOrderLinesArchive(SourceDocBlanketOrderNo,SourceDocBlanketOrderLineNo);
      FindPurchOrderLines(SourceDocOrderNo,SourceDocOrderLineNo);
      FindPurchOrderLinesArchive(SourceDocOrderNo,SourceDocOrderLineNo);
      FindPurchReceiptLinesByOrder(SourceDocOrderNo,SourceDocOrderLineNo);
      FindPurchInvoiceLinesByOrder(SourceDocOrderNo,SourceDocOrderLineNo);
    END;

    LOCAL PROCEDURE FindRecordsForSalesInvoice@9();
    BEGIN
      FindSalesBlanketOrderLines(SourceDocBlanketOrderNo,SourceDocBlanketOrderLineNo);
      FindSalesBlanketOrderLinesArchive(SourceDocBlanketOrderNo,SourceDocBlanketOrderLineNo);
      FindSalesOrderLines(SourceDocOrderNo,SourceDocOrderLineNo);
      FindSalesOrderLinesArchive(SourceDocOrderNo,SourceDocOrderLineNo);
      FindSalesShipmentLinesByOrder(SourceDocOrderNo,SourceDocOrderLineNo);
      FindSalesInvoiceLinesByOrder(SourceDocOrderNo,SourceDocOrderLineNo);
    END;

    LOCAL PROCEDURE FindRecordsForPurchInvoice@11();
    BEGIN
      FindPurchBlanketOrderLines(SourceDocBlanketOrderNo,SourceDocBlanketOrderLineNo);
      FindPurchBlanketOrderLinesArchive(SourceDocBlanketOrderNo,SourceDocBlanketOrderLineNo);
      FindPurchOrderLines(SourceDocOrderNo,SourceDocOrderLineNo);
      FindPurchOrderLinesArchive(SourceDocOrderNo,SourceDocOrderLineNo);
      FindPurchReceiptLinesByOrder(SourceDocOrderNo,SourceDocOrderLineNo);
      FindPurchInvoiceLinesByOrder(SourceDocOrderNo,SourceDocOrderLineNo);
    END;

    LOCAL PROCEDURE FindRecordsForSalesReturnOrder@13();
    BEGIN
      FindSalesReturnOrderLines(SourceDocNo,SourceDocLineNo);
      FindSalesReturnOrderLinesArchive(SourceDocNo,SourceDocLineNo);
      FindReturnReceiptLines(SourceDocNo,SourceDocLineNo);
      FindSalesCreditMemoLines(SourceDocNo,SourceDocLineNo);
    END;

    LOCAL PROCEDURE FindRecordsForPurchReturnOrder@14();
    BEGIN
      FindPurchReturnOrderLines(SourceDocNo,SourceDocLineNo);
      FindPurchReturnOrderLinesArchive(SourceDocNo,SourceDocLineNo);
      FindReturnShipmentLines(SourceDocNo,SourceDocLineNo);
      FindPurchCreditMemoLines(SourceDocNo,SourceDocLineNo);
    END;

    LOCAL PROCEDURE FindRecordsForSalesCreditMemo@15();
    BEGIN
      FindSalesReturnOrderLines(SourceDocOrderNo,SourceDocOrderLineNo);
      FindSalesReturnOrderLinesArchive(SourceDocOrderNo,SourceDocOrderLineNo);
      FindReturnReceiptLines(SourceDocOrderNo,SourceDocOrderLineNo);
      FindSalesCreditMemoLines(SourceDocOrderNo,SourceDocOrderLineNo);
    END;

    LOCAL PROCEDURE FindRecordsForPurchCreditMemo@16();
    BEGIN
      FindPurchReturnOrderLines(SourceDocOrderNo,SourceDocOrderLineNo);
      FindPurchReturnOrderLinesArchive(SourceDocOrderNo,SourceDocOrderLineNo);
      FindReturnShipmentLines(SourceDocOrderNo,SourceDocOrderLineNo);
      FindPurchCreditMemoLines(SourceDocOrderNo,SourceDocOrderLineNo);
    END;

    LOCAL PROCEDURE FindRecordsForReturnReceipt@17();
    BEGIN
      FindSalesReturnOrderLines(SourceDocBlanketOrderNo,SourceDocBlanketOrderLineNo);
      FindSalesReturnOrderLinesArchive(SourceDocBlanketOrderNo,SourceDocBlanketOrderLineNo);
      FindReturnReceiptLines(SourceDocBlanketOrderNo,SourceDocBlanketOrderLineNo);
      FindSalesCreditMemoLines(SourceDocBlanketOrderNo,SourceDocBlanketOrderLineNo);
    END;

    LOCAL PROCEDURE FindRecordsForReturnShipment@18();
    BEGIN
      FindPurchReturnOrderLines(SourceDocBlanketOrderNo,SourceDocBlanketOrderLineNo);
      FindPurchReturnOrderLinesArchive(SourceDocBlanketOrderNo,SourceDocBlanketOrderLineNo);
      FindReturnShipmentLines(SourceDocBlanketOrderNo,SourceDocBlanketOrderLineNo);
      FindPurchCreditMemoLines(SourceDocBlanketOrderNo,SourceDocBlanketOrderLineNo);
    END;

    LOCAL PROCEDURE InsertIntoDocEntry@1002(DocTableID@1000 : Integer;DocType@1001 : Option;DocTableName@1002 : Text[50];DocNoOfRecords@1003 : Integer);
    BEGIN
      IF DocNoOfRecords = 0 THEN
        EXIT;

      TempDocumentEntry.INIT;
      TempDocumentEntry."Entry No." := TempDocumentEntry."Entry No." + 1;
      TempDocumentEntry."Table ID" := DocTableID;
      TempDocumentEntry."Document Type" := DocType;
      TempDocumentEntry."Table Name" := COPYSTR(DocTableName,1,MAXSTRLEN(TempDocumentEntry."Table Name"));
      TempDocumentEntry."No. of Records" := DocNoOfRecords;
      TempDocumentEntry.INSERT;
    END;

    LOCAL PROCEDURE GetDocumentData@1004();
    BEGIN
      DocType := '';
      DocNo := '';
      DocArchive := '';
      DocLineType := '';
      DocLineNo := '';
      DocLineDescription := '';
      DocLineQuantity := 0;
      DocLineUnit := '';

      CASE SourceDocType OF
        SourceDocType::SalesOrder:
          IF SalesLine.FINDFIRST THEN
            AssignLineFields(
              FORMAT(SalesLine."Document Type"),SalesLine."Document No.",'',FORMAT(SalesLine.Type),
              SalesLine."No.",SalesLine.Description,SalesLine.Quantity,SalesLine."Unit of Measure Code")
          ELSE
            IF SalesLineArchive.FINDFIRST THEN
              AssignLineFields(
                FORMAT(SalesLineArchive."Document Type"),SalesLineArchive."Document No.",ArchivedTxt,
                FORMAT(SalesLineArchive.Type),SalesLineArchive."No.",SalesLineArchive.Description,
                SalesLineArchive.Quantity,SalesLineArchive."Unit of Measure Code");
        SourceDocType::BlanketSalesOrder:
          IF BlanketSalesOrderLine.FINDFIRST THEN
            AssignLineFields(
              FORMAT(BlanketSalesOrderLine."Document Type"),BlanketSalesOrderLine."Document No.",'',
              FORMAT(BlanketSalesOrderLine.Type),BlanketSalesOrderLine."No.",BlanketSalesOrderLine.Description,
              BlanketSalesOrderLine.Quantity,BlanketSalesOrderLine."Unit of Measure Code")
          ELSE
            IF BlanketSalesOrderLineArchive.FINDFIRST THEN
              AssignLineFields(
                FORMAT(BlanketSalesOrderLineArchive."Document Type"),BlanketSalesOrderLineArchive."Document No.",ArchivedTxt,
                FORMAT(BlanketSalesOrderLineArchive.Type),BlanketSalesOrderLineArchive."No.",BlanketSalesOrderLineArchive.Description,
                BlanketSalesOrderLineArchive.Quantity,BlanketSalesOrderLineArchive."Unit of Measure Code");
        SourceDocType::PurchaseOrder:
          IF PurchLine.FINDFIRST THEN
            AssignLineFields(
              FORMAT(PurchLine."Document Type"),PurchLine."Document No.",'',FORMAT(PurchLine.Type),
              PurchLine."No.",PurchLine.Description,PurchLine.Quantity,PurchLine."Unit of Measure Code")
          ELSE
            IF PurchLineArchive.FINDFIRST THEN
              AssignLineFields(
                FORMAT(PurchLineArchive."Document Type"),PurchLineArchive."Document No.",ArchivedTxt,FORMAT(PurchLineArchive.Type),
                PurchLineArchive."No.",PurchLineArchive.Description,PurchLineArchive.Quantity,PurchLineArchive."Unit of Measure Code");
        SourceDocType::BlanketPurchaseOrder:
          IF BlanketPurchOrderLine.FINDFIRST THEN
            AssignLineFields(
              FORMAT(BlanketPurchOrderLine."Document Type"),BlanketPurchOrderLine."Document No.",'',
              FORMAT(BlanketPurchOrderLine.Type),BlanketPurchOrderLine."No.",BlanketPurchOrderLine.Description,
              BlanketPurchOrderLine.Quantity,BlanketPurchOrderLine."Unit of Measure Code")
          ELSE
            IF BlanketPurchOrderLineArchive.FINDFIRST THEN
              AssignLineFields(
                FORMAT(BlanketPurchOrderLineArchive."Document Type"),BlanketPurchOrderLineArchive."Document No.",ArchivedTxt,
                FORMAT(BlanketPurchOrderLineArchive.Type),BlanketPurchOrderLineArchive."No.",BlanketPurchOrderLineArchive.Description,
                BlanketPurchOrderLineArchive.Quantity,BlanketPurchOrderLineArchive."Unit of Measure Code");
        SourceDocType::SalesShipment:
          IF SalesShptLine.FINDFIRST THEN
            AssignLineFields(
              SalesShptLine.TABLECAPTION,SalesShptLine."Document No.",'',FORMAT(SalesShptLine.Type),
              SalesShptLine."No.",SalesShptLine.Description,SalesShptLine.Quantity,SalesShptLine."Unit of Measure Code");
        SourceDocType::PurchaseReceipt:
          IF PurchRcptLine.FINDFIRST THEN
            AssignLineFields(
              PurchRcptLine.TABLECAPTION,PurchRcptLine."Document No.",'',FORMAT(PurchRcptLine.Type),
              PurchRcptLine."No.",PurchRcptLine.Description,PurchRcptLine.Quantity,PurchRcptLine."Unit of Measure Code");
        SourceDocType::SalesInvoice:
          IF SalesInvLine.FINDFIRST THEN
            AssignLineFields(
              SalesInvLine.TABLECAPTION,SalesInvLine."Document No.",'',FORMAT(SalesInvLine.Type),
              SalesInvLine."No.",SalesInvLine.Description,SalesInvLine.Quantity,SalesInvLine."Unit of Measure Code");
        SourceDocType::PurchaseInvoice:
          IF PurchInvLine.FINDFIRST THEN
            AssignLineFields(
              PurchInvLine.TABLECAPTION,PurchInvLine."Document No.",'',FORMAT(PurchInvLine.Type),
              PurchInvLine."No.",PurchInvLine.Description,PurchInvLine.Quantity,PurchInvLine."Unit of Measure Code");
      END;

      DocumentCaption := DELCHR(DocArchive + ' ' + DocType + ' ' + DocNo,'<',' ');
    END;

    LOCAL PROCEDURE ShowRecords@1005();
    BEGIN
      TempDocumentEntry := Rec;
      IF TempDocumentEntry.FIND THEN
        Rec := TempDocumentEntry;

      WITH TempDocumentEntry DO
        CASE "Table ID" OF
          DATABASE::"Sales Line":
            BEGIN
              IF "Document Type" = "Document Type"::"Blanket Order" THEN
                PAGE.RUNMODAL(PAGE::"Sales Lines",BlanketSalesOrderLine)
              ELSE
                PAGE.RUNMODAL(PAGE::"Sales Lines",SalesLine);
            END;
          DATABASE::"Sales Shipment Line":
            PAGE.RUNMODAL(0,SalesShptLine);
          DATABASE::"Sales Invoice Line":
            PAGE.RUNMODAL(0,SalesInvLine);
          DATABASE::"Sales Cr.Memo Line":
            PAGE.RUNMODAL(0,SalesCrMemoLine);
          DATABASE::"Sales Line Archive":
            BEGIN
              IF "Document Type" = "Document Type"::"Blanket Order" THEN
                PAGE.RUNMODAL(PAGE::"Sales Line Archive List",BlanketSalesOrderLineArchive)
              ELSE
                PAGE.RUNMODAL(PAGE::"Sales Line Archive List",SalesLineArchive);
            END;
          DATABASE::"Return Receipt Line":
            PAGE.RUNMODAL(0,ReturnReceiptLine);
          DATABASE::"Purchase Line":
            BEGIN
              IF "Document Type" = "Document Type"::"Blanket Order" THEN
                PAGE.RUNMODAL(PAGE::"Purchase Lines",BlanketPurchOrderLine)
              ELSE
                PAGE.RUNMODAL(PAGE::"Purchase Lines",PurchLine);
            END;
          DATABASE::"Purch. Rcpt. Line":
            PAGE.RUNMODAL(0,PurchRcptLine);
          DATABASE::"Purch. Inv. Line":
            PAGE.RUNMODAL(0,PurchInvLine);
          DATABASE::"Purch. Cr. Memo Line":
            PAGE.RUNMODAL(0,PurchCrMemoLine);
          DATABASE::"Purchase Line Archive":
            BEGIN
              IF "Document Type" = "Document Type"::"Blanket Order" THEN
                PAGE.RUNMODAL(PAGE::"Purchase Line Archive List",BlanketPurchOrderLineArchive)
              ELSE
                PAGE.RUNMODAL(PAGE::"Purchase Line Archive List",PurchLineArchive);
            END;
          DATABASE::"Return Shipment Line":
            PAGE.RUNMODAL(0,ReturnShipmentLine);
        END;
    END;

    LOCAL PROCEDURE AssignLineFields@21(NewDocType@1000 : Text[30];NewDocNo@1001 : Code[20];NewDocArchive@1002 : Text[30];NewDocLineType@1003 : Text[30];NewDocLineItemNo@1004 : Code[20];NewDocLineDescription@1005 : Text[50];NewDocLineQuantity@1006 : Decimal;NewDocLineUnit@1007 : Code[10]);
    BEGIN
      DocType := NewDocType;
      DocNo := NewDocNo;
      DocArchive := NewDocArchive;
      DocLineType := NewDocLineType;
      DocLineNo := NewDocLineItemNo;
      DocLineDescription := NewDocLineDescription;
      DocLineQuantity := NewDocLineQuantity;
      DocLineUnit := NewDocLineUnit;
    END;

    LOCAL PROCEDURE FindPurchCreditMemoLines@39(OrderNo@1001 : Code[20];OrderLineNo@1000 : Integer);
    BEGIN
      IF PurchCrMemoLine.READPERMISSION THEN BEGIN
        PurchCrMemoLine.RESET;
        PurchCrMemoLine.SETRANGE("Order No.",OrderNo);
        PurchCrMemoLine.SETRANGE("Order Line No.",OrderLineNo);
        InsertIntoDocEntry(
          DATABASE::"Purch. Cr. Memo Line",3,PostedPurchaseCreditMemoLinesTxt,PurchCrMemoLine.COUNT);
      END;
    END;

    LOCAL PROCEDURE FindPurchOrderLines@36(DocNo@1001 : Code[20];DocLineNo@1000 : Integer);
    BEGIN
      IF PurchLine.READPERMISSION THEN BEGIN
        PurchLine.RESET;
        PurchLine.SETRANGE("Document Type",PurchLine."Document Type"::Order);
        PurchLine.SETRANGE("Document No.",DocNo);
        PurchLine.SETRANGE("Line No.",DocLineNo);
        InsertIntoDocEntry(
          DATABASE::"Purchase Line",1,PurchaseOrderLinesTxt,PurchLine.COUNT);
      END;
    END;

    LOCAL PROCEDURE FindPurchOrderLinesByBlanketOrder@37(BlanketOrderNo@1001 : Code[20];BlanketOrderLineNo@1000 : Integer);
    BEGIN
      IF PurchLine.READPERMISSION THEN BEGIN
        PurchLine.RESET;
        PurchLine.SETRANGE("Document Type",PurchLine."Document Type"::Order);
        PurchLine.SETRANGE("Blanket Order No.",BlanketOrderNo);
        PurchLine.SETRANGE("Blanket Order Line No.",BlanketOrderLineNo);
        InsertIntoDocEntry(
          DATABASE::"Purchase Line",1,PurchaseOrderLinesTxt,PurchLine.COUNT);
      END;
    END;

    LOCAL PROCEDURE FindPurchBlanketOrderLines@53(DocNo@1001 : Code[20];DocLineNo@1000 : Integer);
    BEGIN
      IF BlanketPurchOrderLine.READPERMISSION THEN BEGIN
        BlanketPurchOrderLine.RESET;
        BlanketPurchOrderLine.SETRANGE("Document Type",BlanketPurchOrderLine."Document Type"::"Blanket Order");
        BlanketPurchOrderLine.SETRANGE("Document No.",DocNo);
        BlanketPurchOrderLine.SETRANGE("Line No.",DocLineNo);
        InsertIntoDocEntry(
          DATABASE::"Purchase Line",4,BlanketPurchaseOrderLinesTxt,BlanketPurchOrderLine.COUNT);
      END;
    END;

    LOCAL PROCEDURE FindPurchReturnOrderLines@50(DocNo@1001 : Code[20];DocLineNo@1000 : Integer);
    BEGIN
      IF PurchLine.READPERMISSION THEN BEGIN
        PurchLine.RESET;
        PurchLine.SETRANGE("Document Type",PurchLine."Document Type"::"Return Order");
        PurchLine.SETRANGE("Document No.",DocNo);
        PurchLine.SETRANGE("Line No.",DocLineNo);
        InsertIntoDocEntry(
          DATABASE::"Purchase Line",5,PurchaseReturnOrderLinesTxt,PurchLine.COUNT);
      END;
    END;

    LOCAL PROCEDURE FindPurchOrderLinesArchive@29(DocNo@1001 : Code[20];DocLineNo@1000 : Integer);
    BEGIN
      IF PurchLineArchive.READPERMISSION THEN BEGIN
        PurchLineArchive.RESET;
        PurchLineArchive.SETRANGE("Document Type",PurchLineArchive."Document Type"::Order);
        PurchLineArchive.SETRANGE("Document No.",DocNo);
        PurchLineArchive.SETRANGE("Line No.",DocLineNo);
        InsertIntoDocEntry(
          DATABASE::"Purchase Line Archive",1,ArchivedPurchaseOrderLinesTxt,PurchLineArchive.COUNT);
      END;
    END;

    LOCAL PROCEDURE FindPurchBlanketOrderLinesArchive@55(DocNo@1001 : Code[20];DocLineNo@1000 : Integer);
    BEGIN
      IF BlanketPurchOrderLineArchive.READPERMISSION THEN BEGIN
        BlanketPurchOrderLineArchive.RESET;
        BlanketPurchOrderLineArchive.SETRANGE("Document Type",BlanketPurchOrderLineArchive."Document Type"::"Blanket Order");
        BlanketPurchOrderLineArchive.SETRANGE("Document No.",DocNo);
        BlanketPurchOrderLineArchive.SETRANGE("Line No.",DocLineNo);
        InsertIntoDocEntry(
          DATABASE::"Purchase Line Archive",4,ArchivedBlanketPurchaseOrderLinesTxt,BlanketPurchOrderLineArchive.COUNT);
      END;
    END;

    LOCAL PROCEDURE FindPurchReturnOrderLinesArchive@67(DocNo@1001 : Code[20];DocLineNo@1000 : Integer);
    BEGIN
      IF PurchLineArchive.READPERMISSION THEN BEGIN
        PurchLineArchive.RESET;
        PurchLineArchive.SETRANGE("Document Type",PurchLineArchive."Document Type"::"Return Order");
        PurchLineArchive.SETRANGE("Document No.",DocNo);
        PurchLineArchive.SETRANGE("Line No.",DocLineNo);
        InsertIntoDocEntry(
          DATABASE::"Purchase Line Archive",5,ArchivedPurchaseReturnOrderLinesTxt,PurchLineArchive.COUNT);
      END;
    END;

    LOCAL PROCEDURE FindPurchOrderLinesArchiveByBlanketOrder@40(BlanketOrderNo@1001 : Code[20];BlanketOrderLineNo@1000 : Integer);
    BEGIN
      IF PurchLineArchive.READPERMISSION THEN BEGIN
        PurchLineArchive.RESET;
        PurchLineArchive.SETRANGE("Document Type",PurchLineArchive."Document Type"::Order);
        PurchLineArchive.SETRANGE("Blanket Order No.",BlanketOrderNo);
        PurchLineArchive.SETRANGE("Blanket Order Line No.",BlanketOrderLineNo);
        InsertIntoDocEntry(
          DATABASE::"Purchase Line Archive",1,ArchivedPurchaseOrderLinesTxt,PurchLineArchive.COUNT);
      END;
    END;

    LOCAL PROCEDURE FindPurchReceiptLinesByOrder@8(OrderNo@1001 : Code[20];OrderLineNo@1000 : Integer);
    BEGIN
      IF PurchRcptLine.READPERMISSION THEN BEGIN
        PurchRcptLine.RESET;
        PurchRcptLine.SETCURRENTKEY("Order No.","Order Line No.");
        PurchRcptLine.SETRANGE("Order No.",OrderNo);
        PurchRcptLine.SETRANGE("Order Line No.",OrderLineNo);
        InsertIntoDocEntry(
          DATABASE::"Purch. Rcpt. Line",0,PostedPurchaseReceiptLinesTxt,PurchRcptLine.COUNT);
      END;
    END;

    LOCAL PROCEDURE FindPurchReceiptLinesByBlanketOrder@10(BlanketOrderNo@1001 : Code[20];BlanketOrderLineNo@1000 : Integer);
    BEGIN
      IF PurchRcptLine.READPERMISSION THEN BEGIN
        PurchRcptLine.RESET;
        PurchRcptLine.SETRANGE("Blanket Order No.",BlanketOrderNo);
        PurchRcptLine.SETRANGE("Blanket Order Line No.",BlanketOrderLineNo);
        InsertIntoDocEntry(
          DATABASE::"Purch. Rcpt. Line",0,PostedPurchaseReceiptLinesTxt,PurchRcptLine.COUNT);
      END;
    END;

    LOCAL PROCEDURE FindPurchInvoiceLinesByOrder@19(OrderNo@1001 : Code[20];OrderLineNo@1000 : Integer);
    BEGIN
      IF PurchInvLine.READPERMISSION THEN BEGIN
        PurchInvLine.RESET;
        PurchInvLine.SETRANGE("Order No.",OrderNo);
        PurchInvLine.SETRANGE("Order Line No.",OrderLineNo);
        InsertIntoDocEntry(
          DATABASE::"Purch. Inv. Line",0,PostedPurchaseInvoiceLinesTxt,PurchInvLine.COUNT);
      END;
    END;

    LOCAL PROCEDURE FindPurchInvoiceLinesByBlanketOrder@23(BlanketOrderNo@1001 : Code[20];BlanketOrderLineNo@1000 : Integer);
    BEGIN
      IF PurchInvLine.READPERMISSION THEN BEGIN
        PurchInvLine.RESET;
        PurchInvLine.SETRANGE("Blanket Order No.",BlanketOrderNo);
        PurchInvLine.SETRANGE("Blanket Order Line No.",BlanketOrderLineNo);
        InsertIntoDocEntry(
          DATABASE::"Purch. Inv. Line",0,PostedPurchaseInvoiceLinesTxt,PurchInvLine.COUNT);
      END;
    END;

    LOCAL PROCEDURE FindReturnReceiptLines@31(ReturnOrderNo@1000 : Code[20];ReturnOrderLineNo@1001 : Integer);
    BEGIN
      IF ReturnReceiptLine.READPERMISSION THEN BEGIN
        ReturnReceiptLine.RESET;
        ReturnReceiptLine.SETRANGE("Return Order No.",ReturnOrderNo);
        ReturnReceiptLine.SETRANGE("Return Order Line No.",ReturnOrderLineNo);
        InsertIntoDocEntry(
          DATABASE::"Return Receipt Line",5,PostedReturnReceiptLinesTxt,ReturnReceiptLine.COUNT);
      END;
    END;

    LOCAL PROCEDURE FindReturnShipmentLines@38(ReturnOrderNo@1001 : Code[20];ReturnOrderLineNo@1000 : Integer);
    BEGIN
      IF ReturnShipmentLine.READPERMISSION THEN BEGIN
        ReturnShipmentLine.RESET;
        ReturnShipmentLine.SETRANGE("Return Order No.",ReturnOrderNo);
        ReturnShipmentLine.SETRANGE("Return Order Line No.",ReturnOrderLineNo);
        InsertIntoDocEntry(
          DATABASE::"Return Shipment Line",5,PostedReturnShipmentLinesTxt,ReturnShipmentLine.COUNT);
      END;
    END;

    LOCAL PROCEDURE FindSalesCreditMemoLines@34(OrderNo@1001 : Code[20];OrderLineNo@1000 : Integer);
    BEGIN
      IF SalesCrMemoLine.READPERMISSION THEN BEGIN
        SalesCrMemoLine.RESET;
        SalesCrMemoLine.SETRANGE("Order No.",OrderNo);
        SalesCrMemoLine.SETRANGE("Order Line No.",OrderLineNo);
        InsertIntoDocEntry(
          DATABASE::"Sales Cr.Memo Line",3,PostedSalesCreditMemoLinesTxt,SalesCrMemoLine.COUNT);
      END;
    END;

    LOCAL PROCEDURE FindSalesOrderLines@32(DocNo@1001 : Code[20];DocLineNo@1000 : Integer);
    BEGIN
      IF SalesLine.READPERMISSION THEN BEGIN
        SalesLine.RESET;
        SalesLine.SETRANGE("Document Type",SalesLine."Document Type"::Order);
        SalesLine.SETRANGE("Document No.",DocNo);
        SalesLine.SETRANGE("Line No.",DocLineNo);
        InsertIntoDocEntry(
          DATABASE::"Sales Line",1,SalesOrderLinesTxt,SalesLine.COUNT);
      END;
    END;

    LOCAL PROCEDURE FindSalesOrderLinesByBlanketOrder@41(BlanketOrderNo@1001 : Code[20];BlanketOrderLineNo@1000 : Integer);
    BEGIN
      IF SalesLine.READPERMISSION THEN BEGIN
        SalesLine.RESET;
        SalesLine.SETRANGE("Document Type",SalesLine."Document Type"::Order);
        SalesLine.SETRANGE("Blanket Order No.",BlanketOrderNo);
        SalesLine.SETRANGE("Blanket Order Line No.",BlanketOrderLineNo);
        InsertIntoDocEntry(
          DATABASE::"Sales Line",1,SalesOrderLinesTxt,SalesLine.COUNT);
      END;
    END;

    LOCAL PROCEDURE FindSalesBlanketOrderLines@60(DocNo@1001 : Code[20];DocLineNo@1000 : Integer);
    BEGIN
      IF BlanketSalesOrderLine.READPERMISSION THEN BEGIN
        BlanketSalesOrderLine.RESET;
        BlanketSalesOrderLine.SETRANGE("Document Type",BlanketSalesOrderLine."Document Type"::"Blanket Order");
        BlanketSalesOrderLine.SETRANGE("Document No.",DocNo);
        BlanketSalesOrderLine.SETRANGE("Line No.",DocLineNo);
        InsertIntoDocEntry(
          DATABASE::"Sales Line",4,BlanketSalesOrderLinesTxt,BlanketSalesOrderLine.COUNT);
      END;
    END;

    LOCAL PROCEDURE FindSalesReturnOrderLines@62(DocNo@1001 : Code[20];DocLineNo@1000 : Integer);
    BEGIN
      IF SalesLine.READPERMISSION THEN BEGIN
        SalesLine.RESET;
        SalesLine.SETRANGE("Document Type",SalesLine."Document Type"::"Return Order");
        SalesLine.SETRANGE("Document No.",DocNo);
        SalesLine.SETRANGE("Line No.",DocLineNo);
        InsertIntoDocEntry(
          DATABASE::"Sales Line",5,SalesReturnOrderLinesTxt,SalesLine.COUNT);
      END;
    END;

    LOCAL PROCEDURE FindSalesOrderLinesArchive@35(DocNo@1001 : Code[20];DocLineNo@1000 : Integer);
    BEGIN
      IF SalesLineArchive.READPERMISSION THEN BEGIN
        SalesLineArchive.RESET;
        SalesLineArchive.SETRANGE("Document Type",SalesLineArchive."Document Type"::Order);
        SalesLineArchive.SETRANGE("Document No.",DocNo);
        SalesLineArchive.SETRANGE("Line No.",DocLineNo);
        InsertIntoDocEntry(
          DATABASE::"Sales Line Archive",1,ArchivedSalesOrderLinesTxt,SalesLineArchive.COUNT);
      END;
    END;

    LOCAL PROCEDURE FindSalesBlanketOrderLinesArchive@61(DocNo@1001 : Code[20];DocLineNo@1000 : Integer);
    BEGIN
      IF BlanketSalesOrderLineArchive.READPERMISSION THEN BEGIN
        BlanketSalesOrderLineArchive.RESET;
        BlanketSalesOrderLineArchive.SETRANGE("Document Type",BlanketSalesOrderLineArchive."Document Type"::"Blanket Order");
        BlanketSalesOrderLineArchive.SETRANGE("Document No.",DocNo);
        BlanketSalesOrderLineArchive.SETRANGE("Line No.",DocLineNo);
        InsertIntoDocEntry(
          DATABASE::"Sales Line Archive",4,ArchivedBlanketSalesOrderLinesTxt,BlanketSalesOrderLineArchive.COUNT);
      END;
    END;

    LOCAL PROCEDURE FindSalesReturnOrderLinesArchive@63(DocNo@1001 : Code[20];DocLineNo@1000 : Integer);
    BEGIN
      IF SalesLineArchive.READPERMISSION THEN BEGIN
        SalesLineArchive.RESET;
        SalesLineArchive.SETRANGE("Document Type",SalesLineArchive."Document Type"::"Return Order");
        SalesLineArchive.SETRANGE("Document No.",DocNo);
        SalesLineArchive.SETRANGE("Line No.",DocLineNo);
        InsertIntoDocEntry(
          DATABASE::"Sales Line Archive",5,ArchivedSalesReturnOrderLinesTxt,SalesLineArchive.COUNT);
      END;
    END;

    LOCAL PROCEDURE FindSalesOrderLinesArchiveByBlanketOrder@43(BlanketOrderNo@1001 : Code[20];BlanketOrderLineNo@1000 : Integer);
    BEGIN
      IF SalesLineArchive.READPERMISSION THEN BEGIN
        SalesLineArchive.RESET;
        SalesLineArchive.SETRANGE("Document Type",SalesLineArchive."Document Type"::Order);
        SalesLineArchive.SETRANGE("Blanket Order No.",BlanketOrderNo);
        SalesLineArchive.SETRANGE("Blanket Order Line No.",BlanketOrderLineNo);
        InsertIntoDocEntry(
          DATABASE::"Sales Line Archive",1,ArchivedSalesOrderLinesTxt,SalesLineArchive.COUNT);
      END;
    END;

    LOCAL PROCEDURE FindSalesShipmentLinesByOrder@20(OrderNo@1001 : Code[20];OrderLineNo@1000 : Integer);
    BEGIN
      IF SalesShptLine.READPERMISSION THEN BEGIN
        SalesShptLine.RESET;
        SalesShptLine.SETRANGE("Order No.",OrderNo);
        SalesShptLine.SETRANGE("Order Line No.",OrderLineNo);
        InsertIntoDocEntry(
          DATABASE::"Sales Shipment Line",0,PostedSalesShipmentLinesTxt,SalesShptLine.COUNT);
      END;
    END;

    LOCAL PROCEDURE FindSalesShipmentLinesByBlanketOrder@22(BlanketOrderNo@1001 : Code[20];BlanketOrderLineNo@1000 : Integer);
    BEGIN
      IF SalesShptLine.READPERMISSION THEN BEGIN
        SalesShptLine.RESET;
        SalesShptLine.SETRANGE("Blanket Order No.",BlanketOrderNo);
        SalesShptLine.SETRANGE("Blanket Order Line No.",BlanketOrderLineNo);
        InsertIntoDocEntry(
          DATABASE::"Sales Shipment Line",0,PostedSalesShipmentLinesTxt,SalesShptLine.COUNT);
      END;
    END;

    LOCAL PROCEDURE FindSalesInvoiceLinesByOrder@25(OrderNo@1001 : Code[20];OrderLineNo@1000 : Integer);
    BEGIN
      IF SalesInvLine.READPERMISSION THEN BEGIN
        SalesInvLine.RESET;
        SalesInvLine.SETRANGE("Order No.",OrderNo);
        SalesInvLine.SETRANGE("Order Line No.",OrderLineNo);
        InsertIntoDocEntry(
          DATABASE::"Sales Invoice Line",0,PostedSalesInvoiceLinesTxt,SalesInvLine.COUNT);
      END;
    END;

    LOCAL PROCEDURE FindSalesInvoiceLinesByBlanketOrder@27(BlanketOrderNo@1001 : Code[20];BlanketOrderLineNo@1000 : Integer);
    BEGIN
      IF SalesInvLine.READPERMISSION THEN BEGIN
        SalesInvLine.RESET;
        SalesInvLine.SETRANGE("Blanket Order No.",BlanketOrderNo);
        SalesInvLine.SETRANGE("Blanket Order Line No.",BlanketOrderLineNo);
        InsertIntoDocEntry(
          DATABASE::"Sales Invoice Line",0,PostedSalesInvoiceLinesTxt,SalesInvLine.COUNT);
      END;
    END;

    BEGIN
    END.
  }
}

