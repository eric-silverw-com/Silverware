OBJECT Page 5930 Fault/Resol. Cod. Relationship
{
  OBJECT-PROPERTIES
  {
    Date=11/27/18;
    Time=11:11:17 PM;
    Version List=;
  }
  PROPERTIES
  {
    CaptionML=ENU=Fault/Resolution Codes Relationships;
    ApplicationArea=#Service;
    SourceTable=Table5920;
    DelayedInsert=Yes;
    SourceTableView=SORTING(Service Item Group Code,Fault Code,Occurrences)
                    ORDER(Descending);
    PageType=Worksheet;
    UsageCategory=Administration;
    OnQueryClosePage=BEGIN
                       IF CloseAction IN [ACTION::OK,ACTION::LookupOK] THEN
                         OKOnPush;
                     END;

  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 11  ;1   ;Group     ;
                CaptionML=ENU=General }

    { 12  ;2   ;Field     ;
                CaptionML=ENU=Service Item Group;
                ToolTipML=ENU=Specifies the code for the service item group for which you want to setup a new combination. For example: CD ROM.;
                ApplicationArea=#Service;
                SourceExpr=ServItemGroupCode;
                TableRelation="Service Item Group".Code;
                OnValidate=BEGIN
                             IF ServItemGroupCode <> '' THEN
                               SETRANGE("Service Item Group Code",ServItemGroupCode)
                             ELSE
                               SETRANGE("Service Item Group Code");
                             ServItemGroupCodeOnAfterValida;
                           END;
                            }

    { 14  ;2   ;Field     ;
                CaptionML=ENU=Fault Area Code;
                ToolTipML=ENU=Specifies a code for the fault area. For example: communication.;
                ApplicationArea=#Service;
                SourceExpr=FaultArea;
                TableRelation="Fault Area".Code;
                OnValidate=BEGIN
                             IF FaultArea <> '' THEN
                               SETRANGE("Fault Area Code",FaultArea)
                             ELSE
                               SETRANGE("Fault Area Code");
                             FaultAreaOnAfterValidate;
                           END;
                            }

    { 18  ;2   ;Field     ;
                CaptionML=ENU=Symptom Code;
                ToolTipML=ENU=Specifies a code for the symptom. For example: Quality;
                ApplicationArea=#Service;
                SourceExpr=SymptomCode;
                TableRelation="Symptom Code".Code;
                OnValidate=BEGIN
                             IF SymptomCode <> '' THEN
                               SETRANGE("Symptom Code",SymptomCode)
                             ELSE
                               SETRANGE("Symptom Code");
                             SymptomCodeOnAfterValidate;
                           END;
                            }

    { 16  ;2   ;Field     ;
                CaptionML=ENU=Fault Code;
                ToolTipML=ENU=Specifies a code for the fault. For example: transmission.;
                ApplicationArea=#Service;
                SourceExpr=FaultCode;
                OnValidate=BEGIN
                             IF FaultCode <> '' THEN BEGIN
                               FaultCodeRec.GET(FaultArea,SymptomCode,FaultCode);
                               SETRANGE("Fault Code",FaultCode);
                             END ELSE
                               SETRANGE("Fault Code");
                             FaultCodeOnAfterValidate;
                           END;

                OnLookup=BEGIN
                           FaultCodeRec.SETRANGE("Fault Area Code",FaultArea);
                           FaultCodeRec.SETRANGE("Symptom Code",SymptomCode);
                           IF NOT FaultCodeRec.GET(FaultArea,SymptomCode,FaultCode) THEN;
                           IF PAGE.RUNMODAL(0,FaultCodeRec) = ACTION::LookupOK THEN BEGIN
                             FaultCode := FaultCodeRec.Code;
                             SETRANGE("Fault Code",FaultCode);
                             CurrPage.UPDATE(FALSE);
                           END;
                         END;
                          }

    { 1   ;1   ;Group     ;
                GroupType=Repeater }

    { 22  ;2   ;Field     ;
                ToolTipML=ENU=Specifies the fault area code.;
                ApplicationArea=#Service;
                SourceExpr="Fault Area Code" }

    { 20  ;2   ;Field     ;
                ToolTipML=ENU=Specifies the symptom code.;
                ApplicationArea=#Service;
                SourceExpr="Symptom Code" }

    { 6   ;2   ;Field     ;
                ToolTipML=ENU=Specifies the fault code.;
                ApplicationArea=#Service;
                SourceExpr="Fault Code" }

    { 24  ;2   ;Field     ;
                ToolTipML=ENU=Specifies the resolution code.;
                ApplicationArea=#Service;
                SourceExpr="Resolution Code" }

    { 30  ;2   ;Field     ;
                ToolTipML=ENU=Specifies the code of the service item group linked to the relationship.;
                ApplicationArea=#Service;
                SourceExpr="Service Item Group Code" }

    { 28  ;2   ;Field     ;
                ToolTipML=ENU=Specifies a description of the relationship between the fault code and the resolution code.;
                ApplicationArea=#Service;
                SourceExpr=Description }

    { 26  ;2   ;Field     ;
                ToolTipML=ENU=Specifies the number of times the combination of fault code, symptom code, fault area, and resolution code occurs in the posted service lines.;
                ApplicationArea=#Service;
                SourceExpr=Occurrences }

    { 1900000007;0;Container;
                ContainerType=FactBoxArea }

    { 1900383207;1;Part   ;
                Visible=FALSE;
                PartType=System;
                SystemPartID=RecordLinks }

    { 1905767507;1;Part   ;
                Visible=FALSE;
                PartType=System;
                SystemPartID=Notes }

  }
  CODE
  {
    VAR
      FaultCodeRec@1010 : Record 5918;
      ServItemLine@1000 : Record 5901;
      ServInvLine@1001 : Record 5902;
      ServItemGroupCode@1002 : Code[10];
      FaultArea@1003 : Code[10];
      FaultCode@1004 : Code[10];
      SymptomCode@1005 : Code[10];
      ServTableID@1006 : Integer;
      ServDocumentType@1009 : Integer;
      ServDocumentNo@1007 : Code[20];
      ServLineNo@1008 : Integer;

    [External]
    PROCEDURE SetFilters@2(Symptom@1000 : Code[10];Fault@1001 : Code[10];Area@1002 : Code[10];ServItemGroup@1003 : Code[10]);
    BEGIN
      ServItemGroupCode := ServItemGroup;
      FaultArea := Area;
      FaultCode := Fault;
      SymptomCode := Symptom;
      IF Fault <> '' THEN
        SETRANGE("Fault Code",Fault)
      ELSE
        SETRANGE("Fault Code");
      IF Area <> '' THEN
        SETRANGE("Fault Area Code",Area)
      ELSE
        SETRANGE("Fault Area Code");
      IF Symptom <> '' THEN
        SETRANGE("Symptom Code",Symptom)
      ELSE
        SETRANGE("Symptom Code");
      IF ServItemGroup <> '' THEN
        SETRANGE("Service Item Group Code",ServItemGroup)
      ELSE
        SETRANGE("Service Item Group Code");
    END;

    [External]
    PROCEDURE SetDocument@1(TableID@1000 : Integer;DocumentType@1003 : Integer;DocumentNo@1001 : Code[20];LineNo@1002 : Integer);
    BEGIN
      ServDocumentType := DocumentType;
      ServDocumentNo := DocumentNo;
      ServLineNo := LineNo;
      ServTableID := TableID;
    END;

    LOCAL PROCEDURE UpdateOrginalRecord@3();
    BEGIN
      CASE ServTableID OF
        DATABASE::"Service Item Line":
          BEGIN
            ServItemLine.GET(ServDocumentType,ServDocumentNo,ServLineNo);
            ServItemLine."Fault Area Code" := "Fault Area Code";
            ServItemLine."Symptom Code" := "Symptom Code";
            ServItemLine."Fault Code" := "Fault Code";
            ServItemLine."Resolution Code" := "Resolution Code";
            ServItemLine.MODIFY(TRUE);
          END;
        DATABASE::"Service Line":
          BEGIN
            ServInvLine.GET(ServDocumentType,ServDocumentNo,ServLineNo);
            ServInvLine."Fault Area Code" := "Fault Area Code";
            ServInvLine."Symptom Code" := "Symptom Code";
            ServInvLine."Fault Code" := "Fault Code";
            ServInvLine."Resolution Code" := "Resolution Code";
            ServInvLine.MODIFY;
          END;
      END;
    END;

    LOCAL PROCEDURE FaultAreaOnAfterValidate@19004646();
    BEGIN
      CurrPage.UPDATE(FALSE);
    END;

    LOCAL PROCEDURE SymptomCodeOnAfterValidate@19047900();
    BEGIN
      CurrPage.UPDATE(FALSE);
    END;

    LOCAL PROCEDURE ServItemGroupCodeOnAfterValida@19062516();
    BEGIN
      CurrPage.UPDATE(FALSE);
    END;

    LOCAL PROCEDURE FaultCodeOnAfterValidate@19038782();
    BEGIN
      CurrPage.UPDATE(FALSE);
    END;

    LOCAL PROCEDURE OKOnPush@19066895();
    BEGIN
      UpdateOrginalRecord;
      CurrPage.CLOSE;
    END;

    BEGIN
    END.
  }
}

