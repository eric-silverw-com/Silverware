OBJECT Codeunit 9801 Identity Management
{
  OBJECT-PROPERTIES
  {
    Date=09/19/18;
    Time=12:00:00 PM;
    Version List=NAVW113.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      TenantManagementHelper@1005 : Codeunit 417;
      UserAccountHelper@1003 : DotNet "'Microsoft.Dynamics.Nav.NavUserAccount, Version=13.0.*.*, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.NavUserAccount.NavUserAccountHelper";
      NavTok@1000 : TextConst '@@@={Locked};ENU=NAV;ESM=NAV;FRC=NAV;ENC=NAV';
      InvoiceTok@1001 : TextConst '@@@={Locked};ENU=INV;ESM=INV;FRC=INV;ENC=INV';
      FinancialsTok@1002 : TextConst '@@@={Locked};ENU=FIN;ESM=FIN;FRC=FIN;ENC=FIN';
      C5Tok@1004 : TextConst '@@@={Locked};ENU=C5;ESM=C5;FRC=C5;ENC=C5';

    PROCEDURE SetAuthenticationKey@1(UserSecurityID@1000 : GUID;Key@1001 : Text[80]);
    BEGIN
      IF NOT UserAccountHelper.TrySetAuthenticationKey(UserSecurityID,Key) THEN
        ERROR(GETLASTERRORTEXT);
    END;

    PROCEDURE GetAuthenticationKey@14(UserSecurityID@1000 : GUID) Key : Text[80];
    BEGIN
      IF NOT UserAccountHelper.TryGetAuthenticationKey(UserSecurityID,Key) THEN
        Key := FORMAT(GETLASTERRORTEXT,80);
    END;

    PROCEDURE GetNameIdentifier@2(UserSecurityID@1000 : GUID) NameID : Text[250];
    BEGIN
      IF NOT UserAccountHelper.TryGetNameIdentifier(UserSecurityID,NameID) THEN
        NameID := FORMAT(GETLASTERRORTEXT,250);
    END;

    PROCEDURE GetObjectId@29(UserSecurityID@1000 : GUID) ObjectID : Text[250];
    BEGIN
      IF NOT UserAccountHelper.TryGetAuthenticationObjectId(UserSecurityID,ObjectID) THEN
        ObjectID := FORMAT(GETLASTERRORTEXT,250);
    END;

    PROCEDURE CreateWebServicesKey@8(UserSecurityID@1000 : GUID;ExpiryDate@1001 : DateTime) Key : Text[80];
    BEGIN
      IF NOT UserAccountHelper.TryCreateWebServicesKey(UserSecurityID,ExpiryDate,Key) THEN
        ERROR(GETLASTERRORTEXT);
    END;

    PROCEDURE GetPuid@23() : Text;
    BEGIN
      EXIT(UserAccountHelper.GetPuid);
    END;

    PROCEDURE CreateWebServicesKeyNoExpiry@13(UserSecurityID@1000 : GUID) Key : Text[80];
    VAR
      ExpiryDate@1001 : DateTime;
    BEGIN
      IF NOT UserAccountHelper.TryCreateWebServicesKey(UserSecurityID,ExpiryDate,Key) THEN
        ERROR(GETLASTERRORTEXT);
    END;

    PROCEDURE ClearWebServicesKey@22(UserSecurityID@1000 : GUID);
    BEGIN
      IF NOT UserAccountHelper.TryClearWebServicesKey(UserSecurityID) THEN
        ERROR(GETLASTERRORTEXT);
    END;

    PROCEDURE GetWebServicesKey@4(UserSecurityID@1000 : GUID) Key : Text[80];
    VAR
      ExpiryDate@1001 : DateTime;
    BEGIN
      IF NOT UserAccountHelper.TryGetWebServicesKey(UserSecurityID,Key,ExpiryDate) THEN
        Key := FORMAT(GETLASTERRORTEXT,80);
    END;

    PROCEDURE IsAzure@5() Ok : Boolean;
    BEGIN
      Ok := UserAccountHelper.IsAzure;
    END;

    PROCEDURE GetWebServiceExpiryDate@6(UserSecurityID@1000 : GUID) ExpiryDate : DateTime;
    VAR
      Key@1001 : Text[80];
    BEGIN
      IF NOT UserAccountHelper.TryGetWebServicesKey(UserSecurityID,Key,ExpiryDate) THEN
        ExpiryDate := CURRENTDATETIME;
    END;

    PROCEDURE GetACSStatus@7(UserSecurityID@1001 : GUID) ACSStatus : Integer;
    VAR
      ACSStatusOption@1000 : 'Disabled,Pending,Registered,Unknown';
      Key@1002 : Text[80];
      NameID@1003 : Text[250];
    BEGIN
      // Determines the status as follows:
      // If neither Nameidentifier, nor authentication key then Disabled
      // If authentiation key then Pending
      // If NameIdentifier then Registered
      // If no permission: Unknown

      IF NOT UserAccountHelper.TryGetAuthenticationKey(UserSecurityID,Key) THEN BEGIN
        ACSStatusOption := ACSStatusOption::Unknown;
        ACSStatus := ACSStatusOption;
        EXIT;
      END;

      IF NOT UserAccountHelper.TryGetNameIdentifier(UserSecurityID,NameID) THEN BEGIN
        ACSStatusOption := ACSStatusOption::Unknown;
        ACSStatus := ACSStatusOption;
        EXIT;
      END;

      IF NameID = '' THEN BEGIN
        IF Key = '' THEN
          ACSStatusOption := ACSStatusOption::Disabled
        ELSE
          ACSStatusOption := ACSStatusOption::Pending;
      END ELSE
        ACSStatusOption := ACSStatusOption::Registered;

      ACSStatus := ACSStatusOption;
    END;

    LOCAL PROCEDURE ValidateKeyStrength@9(Key@1001 : Text[250]) : Boolean;
    VAR
      i@1000 : Integer;
      KeyLen@1002 : Integer;
      HasUpper@1003 : Boolean;
      HasLower@1004 : Boolean;
      HasNumeric@1005 : Boolean;
    BEGIN
      KeyLen := STRLEN(Key);

      IF KeyLen < 8 THEN
        EXIT(FALSE);

      FOR i := 1 TO STRLEN(Key) DO BEGIN
        CASE Key[i] OF
          'A'..'Z':
            HasUpper := TRUE;
          'a'..'z':
            HasLower := TRUE;
          '0'..'9':
            HasNumeric := TRUE;
        END;

        IF HasUpper AND HasLower AND HasNumeric THEN
          EXIT(TRUE);
      END;

      EXIT(FALSE);
    END;

    [External]
    PROCEDURE ValidatePasswordStrength@10(Password@1000 : Text[250]) IsValid : Boolean;
    BEGIN
      IsValid := ValidateKeyStrength(Password);
    END;

    [External]
    PROCEDURE ValidateAuthKeyStrength@11(AuthKey@1000 : Text[250]) IsValid : Boolean;
    BEGIN
      IsValid := ValidateKeyStrength(AuthKey);
    END;

    PROCEDURE GetMaskedNavPassword@15(UserSecurityID@1000 : GUID) MaskedPassword : Text[80];
    BEGIN
      IF UserAccountHelper.IsPasswordSet(UserSecurityID) THEN
        MaskedPassword := '********'
      ELSE
        MaskedPassword := '';
    END;

    PROCEDURE IsWindowsAuthentication@16() Ok : Boolean;
    BEGIN
      Ok := UserAccountHelper.IsWindowsAuthentication;
    END;

    PROCEDURE IsUserNamePasswordAuthentication@17() Ok : Boolean;
    BEGIN
      Ok := UserAccountHelper.IsUserNamePasswordAuthentication;
    END;

    PROCEDURE IsAccessControlServiceAuthentication@18() Ok : Boolean;
    BEGIN
      Ok := UserAccountHelper.IsAccessControlServiceAuthentication;
    END;

    PROCEDURE UserName@19(Sid@1000 : Text) : Text[208];
    BEGIN
      IF Sid = '' THEN
        EXIT('');

      EXIT(UserAccountHelper.UserName(Sid));
    END;

    PROCEDURE SetAuthenticationEmail@12(UserSecurityId@1000 : GUID;AuthenticationEmail@1001 : Text[250]);
    BEGIN
      CLEARLASTERROR;
      IF NOT UserAccountHelper.TrySetAuthenticationEmail(UserSecurityId,AuthenticationEmail) THEN
        ERROR(GETLASTERRORTEXT);
    END;

    PROCEDURE GetAuthenticationStatus@21(UserSecurityId@1001 : GUID) O365AuthStatus : Integer;
    BEGIN
      O365AuthStatus := UserAccountHelper.GetAuthenticationStatus(UserSecurityId);
    END;

    [External]
    PROCEDURE GetAadTenantId@3() : Text;
    BEGIN
      EXIT(TenantManagementHelper.GetAadTenantId);
    END;

    [External]
    PROCEDURE IsInvAppId@20() : Boolean;
    VAR
      AppId@1000 : Text;
    BEGIN
      AppId := APPLICATIONIDENTIFIER;
      OnBeforeGetApplicationIdentifier(AppId);
      EXIT(AppId = InvoiceTok);
    END;

    [External]
    PROCEDURE IsFinAppId@24() : Boolean;
    VAR
      AppId@1000 : Text;
    BEGIN
      AppId := APPLICATIONIDENTIFIER;
      OnBeforeGetApplicationIdentifier(AppId);
      EXIT(AppId = FinancialsTok);
    END;

    [External]
    PROCEDURE IsNavAppId@25() : Boolean;
    VAR
      AppId@1000 : Text;
    BEGIN
      AppId := APPLICATIONIDENTIFIER;
      OnBeforeGetApplicationIdentifier(AppId);
      EXIT(AppId = NavTok);
    END;

    [External]
    PROCEDURE IsC5AppId@26() : Boolean;
    VAR
      AppId@1000 : Text;
    BEGIN
      AppId := APPLICATIONIDENTIFIER;
      OnBeforeGetApplicationIdentifier(AppId);
      EXIT(AppId = C5Tok);
    END;

    [Integration]
    [External]
    PROCEDURE OnBeforeGetApplicationIdentifier@27(VAR AppId@1000 : Text);
    BEGIN
    END;

    BEGIN
    END.
  }
}

