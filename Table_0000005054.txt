OBJECT Table 5054 Contact Business Relation
{
  OBJECT-PROPERTIES
  {
    Date=09/19/18;
    Time=12:00:00 PM;
    Version List=NAVW113.00;
  }
  PROPERTIES
  {
    OnInsert=VAR
               ContBusRel@1000 : Record 5054;
               Cont@1001 : Record 5050;
             BEGIN
               IF "No." <> '' THEN BEGIN
                 IF ContBusRel.FindByContact("Link to Table","Contact No.") THEN
                   ERROR(
                     Text000,
                     Cont.TABLECAPTION,"Contact No.",TABLECAPTION,"Link to Table",ContBusRel."No.");

                 IF ContBusRel.FindByRelation("Link to Table","No.") THEN
                   IF GetContactBusinessRelation(ContBusRel) THEN
                     ERROR(
                       Text000,
                       "Link to Table","No.",TABLECAPTION,Cont.TABLECAPTION,ContBusRel."Contact No.");

                 ContBusRel.RESET;
                 ContBusRel.SETRANGE("Contact No.","Contact No.");
                 ContBusRel.SETRANGE("Business Relation Code","Business Relation Code");
                 ContBusRel.SETRANGE("No.",'');
                 ContBusRel.DELETEALL;
               END;
             END;

    CaptionML=[ENU=Contact Business Relation;
               ESM=Relaci¢n negocio contacto;
               FRC=Relation d'affaires contact;
               ENC=Contact Business Relation];
    DrillDownPageID=Page5061;
  }
  FIELDS
  {
    { 1   ;   ;Contact No.         ;Code20        ;TableRelation=Contact WHERE (Type=CONST(Company));
                                                   OnValidate=BEGIN
                                                                IF "Contact No." <> '' THEN
                                                                  VALIDATE("Business Relation Code");
                                                              END;

                                                   CaptionML=[ENU=Contact No.;
                                                              ESM=N§ contacto;
                                                              FRC=Nø contact;
                                                              ENC=Contact No.];
                                                   NotBlank=Yes }
    { 2   ;   ;Business Relation Code;Code10      ;TableRelation="Business Relation";
                                                   OnValidate=VAR
                                                                RMSetup@1000 : Record 5079;
                                                                Cust@1001 : Record 18;
                                                                Vend@1002 : Record 23;
                                                                BankAcc@1003 : Record 270;
                                                              BEGIN
                                                                IF ("No." = '') AND
                                                                   ("Contact No." <> '') AND
                                                                   ("Business Relation Code" <> '') AND
                                                                   (CurrFieldNo <> 0)
                                                                THEN BEGIN
                                                                  RMSetup.GET;
                                                                  IF "Business Relation Code" = RMSetup."Bus. Rel. Code for Customers" THEN
                                                                    ERROR(Text001,
                                                                      FIELDCAPTION("Business Relation Code"),"Business Relation Code",
                                                                      Cont.TABLECAPTION,Cust.TABLECAPTION);
                                                                  IF "Business Relation Code" = RMSetup."Bus. Rel. Code for Vendors" THEN
                                                                    ERROR(Text001,
                                                                      FIELDCAPTION("Business Relation Code"),"Business Relation Code",
                                                                      Cont.TABLECAPTION,Vend.TABLECAPTION);
                                                                  IF "Business Relation Code" = RMSetup."Bus. Rel. Code for Bank Accs." THEN
                                                                    ERROR(Text001,
                                                                      FIELDCAPTION("Business Relation Code"),"Business Relation Code",
                                                                      Cont.TABLECAPTION,BankAcc.TABLECAPTION);
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Business Relation Code;
                                                              ESM=C¢d. relaci¢n negocio;
                                                              FRC=Code relation d'affaires;
                                                              ENC=Business Relation Code];
                                                   NotBlank=Yes }
    { 3   ;   ;Link to Table       ;Option        ;CaptionML=[ENU=Link to Table;
                                                              ESM=Relaciona con tabla;
                                                              FRC=Li‚ … la table;
                                                              ENC=Link to Table];
                                                   OptionCaptionML=[ENU=" ,Customer,Vendor,Bank Account";
                                                                    ESM=" ,Cliente,Proveedor,Banco";
                                                                    FRC=" ,Client,Fournisseur,Compte bancaire";
                                                                    ENC=" ,Customer,Vendor,Bank Account"];
                                                   OptionString=[ ,Customer,Vendor,Bank Account] }
    { 4   ;   ;No.                 ;Code20        ;TableRelation=IF (Link to Table=CONST(Customer)) Customer
                                                                 ELSE IF (Link to Table=CONST(Vendor)) Vendor
                                                                 ELSE IF (Link to Table=CONST(Bank Account)) "Bank Account";
                                                   CaptionML=[ENU=No.;
                                                              ESM=N§;
                                                              FRC=Nø;
                                                              ENC=No.] }
    { 5   ;   ;Business Relation Description;Text50;
                                                   FieldClass=FlowField;
                                                   CalcFormula=Lookup("Business Relation".Description WHERE (Code=FIELD(Business Relation Code)));
                                                   CaptionML=[ENU=Business Relation Description;
                                                              ESM=Descripci¢n relaci¢n negocio;
                                                              FRC=D‚signation relation affaires;
                                                              ENC=Business Relation Description];
                                                   Editable=No }
    { 6   ;   ;Contact Name        ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Contact.Name WHERE (No.=FIELD(Contact No.)));
                                                   CaptionML=[ENU=Contact Name;
                                                              ESM=Nombre contacto;
                                                              FRC=Nom du contact;
                                                              ENC=Contact Name];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Contact No.,Business Relation Code      ;Clustered=Yes }
    {    ;Link to Table,No.                        }
    {    ;Link to Table,Contact No.                }
    {    ;Business Relation Code                   }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=%1 %2 already has a %3 with %4 %5.;ESM=%1 %2 tiene ya un %3 con %4 %5.;FRC=L''enregistrement %1 %2 possŠde d‚j… un(e) %3 avec l''enregistrement %4 %5.;ENC=%1 %2 already has a %3 with %4 %5.';
      Text001@1001 : TextConst 'ENU=%1 %2 is used when a %3 is linked with a %4.;ESM=Se utiliza %1 %2 cuando un %3 est  relacionado con un %4.;FRC=La %1 %2 est utilis‚ quand un %3 est li‚ … un(e) %4.;ENC=%1 %2 is used when a %3 is linked with a %4.';
      Cont@1002 : Record 5050;

    LOCAL PROCEDURE GetContactBusinessRelation@2(ContactBusinessRelation@1000 : Record 5054) : Boolean;
    VAR
      Customer@1001 : Record 18;
      Vendor@1002 : Record 23;
      BankAccount@1003 : Record 270;
    BEGIN
      CASE ContactBusinessRelation."Link to Table" OF
        ContactBusinessRelation."Link to Table"::"Bank Account":
          EXIT(BankAccount.GET(ContactBusinessRelation."No."));
        ContactBusinessRelation."Link to Table"::Customer:
          EXIT(Customer.GET(ContactBusinessRelation."No."));
        ContactBusinessRelation."Link to Table"::Vendor:
          EXIT(Vendor.GET(ContactBusinessRelation."No."));
      END;
    END;

    PROCEDURE FindByContact@3(LinkType@1000 : Option;ContactNo@1001 : Code[20]) : Boolean;
    BEGIN
      RESET;
      SETCURRENTKEY("Link to Table","Contact No.");
      SETRANGE("Link to Table",LinkType);
      SETRANGE("Contact No.",ContactNo);
      EXIT(FINDFIRST);
    END;

    PROCEDURE FindByRelation@4(LinkType@1000 : Option;LinkNo@1001 : Code[20]) : Boolean;
    BEGIN
      RESET;
      SETCURRENTKEY("Link to Table","No.");
      SETRANGE("Link to Table",LinkType);
      SETRANGE("No.",LinkNo);
      EXIT(FINDFIRST);
    END;

    PROCEDURE GetContactNo@1(LinkType@1000 : Option;LinkNo@1001 : Code[20]) : Code[20];
    BEGIN
      IF FindByRelation(LinkType,LinkNo) THEN
        EXIT("Contact No.");
      EXIT('');
    END;

    PROCEDURE CreateRelation@5(ContactNo@1002 : Code[20];LinkNo@1004 : Code[20];LinkToTable@1003 : Option);
    BEGIN
      INIT;
      "Contact No." := ContactNo;
      "Business Relation Code" := GetBusinessRelationCodeFromSetup(LinkToTable);
      "Link to Table" := LinkToTable;
      "No." := LinkNo;
      INSERT(TRUE);
    END;

    PROCEDURE FindOrRestoreContactBusinessRelation@7(VAR Cont@1000 : Record 5050;RecVar@1006 : Variant;LinkToTable@1002 : Option);
    VAR
      ContCompany@1004 : Record 5050;
      CustContUpdate@1008 : Codeunit 5056;
      VendContUpdate@1005 : Codeunit 5057;
      RecRef@1007 : RecordRef;
      FieldRef@1010 : FieldRef;
    BEGIN
      RecRef.GETTABLE(RecVar);
      FieldRef := RecRef.FIELD(1);

      IF NOT FindByRelation(LinkToTable,FORMAT(FieldRef.VALUE)) THEN
        IF Cont.Type = Cont.Type::Person THEN
          IF ContCompany.GET(Cont."Company No.") THEN BEGIN
            ContCompany.CheckForExistingRelationships(LinkToTable);
            CreateRelation(ContCompany."No.",FORMAT(FieldRef.VALUE),LinkToTable);
          END ELSE BEGIN
            CASE RecRef.NUMBER OF
              DATABASE::Customer:
                CustContUpdate.OnInsert(RecVar);
              DATABASE::Vendor:
                VendContUpdate.OnInsert(RecVar);
            END;
            FINDFIRST;
            Cont.VALIDATE("Company No.","Contact No.");
            Cont.MODIFY(TRUE);
          END;
    END;

    LOCAL PROCEDURE GetBusinessRelationCodeFromSetup@6(LinkToTable@1002 : Option) : Code[10];
    VAR
      MarketingSetup@1000 : Record 5079;
      ContactBusinessRelation@1001 : Record 5054;
    BEGIN
      MarketingSetup.GET;
      CASE LinkToTable OF
        ContactBusinessRelation."Link to Table"::Customer:
          BEGIN
            MarketingSetup.TESTFIELD("Bus. Rel. Code for Customers");
            EXIT(MarketingSetup."Bus. Rel. Code for Customers");
          END;
        ContactBusinessRelation."Link to Table"::Vendor:
          BEGIN
            MarketingSetup.TESTFIELD("Bus. Rel. Code for Vendors");
            EXIT(MarketingSetup."Bus. Rel. Code for Vendors");
          END;
      END;
    END;

    BEGIN
    END.
  }
}

