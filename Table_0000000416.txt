OBJECT Table 416 Handled IC Outbox Trans.
{
  OBJECT-PROPERTIES
  {
    Date=11/27/18;
    Time=11:14:17 PM;
    Version List=;
  }
  PROPERTIES
  {
    OnDelete=VAR
               ICOutboxJnlLine@1000 : Record 417;
               ICOutboxSalesHdr@1001 : Record 430;
               ICOutboxPurchHdr@1002 : Record 432;
             BEGIN
               CASE "Source Type" OF
                 "Source Type"::Journal:
                   BEGIN
                     ICOutboxJnlLine.SETRANGE("Transaction No.","Transaction No.");
                     ICOutboxJnlLine.SETRANGE("IC Partner Code","IC Partner Code");
                     ICOutboxJnlLine.SETRANGE("Transaction Source","Transaction Source");
                     ICOutboxJnlLine.DELETEALL(TRUE);
                   END;
                 "Source Type"::"Sales Document":
                   IF ICOutboxSalesHdr.GET("Transaction No.","IC Partner Code","Transaction Source") THEN
                     ICOutboxSalesHdr.DELETE(TRUE);
                 "Source Type"::"Purchase Document":
                   IF ICOutboxPurchHdr.GET("Transaction No.","IC Partner Code","Transaction Source") THEN
                     ICOutboxPurchHdr.DELETE(TRUE);
               END;
               DeleteComments("Transaction No.","IC Partner Code");
             END;

    CaptionML=ENU=Handled IC Outbox Trans.;
  }
  FIELDS
  {
    { 1   ;   ;Transaction No.     ;Integer       ;CaptionML=ENU=Transaction No.;
                                                   Editable=No }
    { 2   ;   ;IC Partner Code     ;Code20        ;TableRelation="IC Partner";
                                                   CaptionML=ENU=IC Partner Code;
                                                   Editable=No }
    { 3   ;   ;Source Type         ;Option        ;CaptionML=ENU=Source Type;
                                                   OptionCaptionML=ENU=Journal,Sales Document,Purchase Document;
                                                   OptionString=Journal,Sales Document,Purchase Document;
                                                   Editable=No }
    { 5   ;   ;Document Type       ;Option        ;CaptionML=ENU=Document Type;
                                                   OptionCaptionML=ENU=" ,Payment,Invoice,Credit Memo,Refund,Order,Return Order";
                                                   OptionString=[ ,Payment,Invoice,Credit Memo,Refund,Order,Return Order];
                                                   Editable=No }
    { 6   ;   ;Document No.        ;Code20        ;CaptionML=ENU=Document No.;
                                                   Editable=No }
    { 7   ;   ;Posting Date        ;Date          ;CaptionML=ENU=Posting Date;
                                                   Editable=No }
    { 8   ;   ;Transaction Source  ;Option        ;CaptionML=ENU=Transaction Source;
                                                   OptionCaptionML=ENU=Rejected by Current Company,Created by Current Company;
                                                   OptionString=Rejected by Current Company,Created by Current Company;
                                                   Editable=No }
    { 9   ;   ;Document Date       ;Date          ;CaptionML=ENU=Document Date;
                                                   Editable=No }
    { 11  ;   ;Status              ;Option        ;CaptionML=ENU=Status;
                                                   OptionCaptionML=ENU=Sent to IC Partner,Rejection Sent to IC Partner,Canceled;
                                                   OptionString=Sent to IC Partner,Rejection Sent to IC Partner,Cancelled;
                                                   Editable=No }
    { 12  ;   ;IC Partner G/L Acc. No.;Code20     ;CaptionML=ENU=IC Partner G/L Acc. No. }
    { 13  ;   ;Source Line No.     ;Integer       ;CaptionML=ENU=Source Line No. }
  }
  KEYS
  {
    {    ;Transaction No.,IC Partner Code,Transaction Source;
                                                   Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {

    [External]
    PROCEDURE ShowDetails@1();
    VAR
      HandledICOutboxJnlLine@1000 : Record 417;
      HandledICOutboxSalesHeader@1001 : Record 430;
      HandledICOutboxPurchHdr@1002 : Record 432;
      HandledICOutboxJnlLines@1005 : Page 614;
      HandledICOutboxSalesDoc@1004 : Page 640;
      HandledICOutboxPurchDoc@1003 : Page 642;
    BEGIN
      CASE "Source Type" OF
        "Source Type"::Journal:
          BEGIN
            HandledICOutboxJnlLine.SETRANGE("Transaction No.","Transaction No.");
            HandledICOutboxJnlLine.SETRANGE("IC Partner Code","IC Partner Code");
            HandledICOutboxJnlLine.SETRANGE("Transaction Source","Transaction Source");
            CLEAR(HandledICOutboxJnlLines);
            HandledICOutboxJnlLines.SETTABLEVIEW(HandledICOutboxJnlLine);
            HandledICOutboxJnlLines.RUNMODAL;
          END;
        "Source Type"::"Sales Document":
          BEGIN
            HandledICOutboxSalesHeader.SETRANGE("IC Transaction No.","Transaction No.");
            HandledICOutboxSalesHeader.SETRANGE("IC Partner Code","IC Partner Code");
            HandledICOutboxSalesHeader.SETRANGE("Transaction Source","Transaction Source");
            CLEAR(HandledICOutboxSalesDoc);
            HandledICOutboxSalesDoc.SETTABLEVIEW(HandledICOutboxSalesHeader);
            HandledICOutboxSalesDoc.RUNMODAL;
          END;
        "Source Type"::"Purchase Document":
          BEGIN
            HandledICOutboxPurchHdr.SETRANGE("IC Partner Code","IC Partner Code");
            HandledICOutboxPurchHdr.SETRANGE("IC Transaction No.","Transaction No.");
            HandledICOutboxPurchHdr.SETRANGE("Transaction Source","Transaction Source");
            CLEAR(HandledICOutboxPurchDoc);
            HandledICOutboxPurchDoc.SETTABLEVIEW(HandledICOutboxPurchHdr);
            HandledICOutboxPurchDoc.RUNMODAL;
          END;
      END;
    END;

    LOCAL PROCEDURE DeleteComments@2(TransactionNo@1000 : Integer;ICPartnerCode@1001 : Code[20]);
    VAR
      ICCommentLine@1002 : Record 424;
    BEGIN
      ICCommentLine.SETRANGE("Table Name",ICCommentLine."Table Name"::"Handled IC Outbox Transaction");
      ICCommentLine.SETRANGE("Transaction No.",TransactionNo);
      ICCommentLine.SETRANGE("IC Partner Code",ICPartnerCode);
      ICCommentLine.DELETEALL;
    END;

    BEGIN
    END.
  }
}

