OBJECT Page 2812 Native - Sales Quotes
{
  OBJECT-PROPERTIES
  {
    Date=09/19/18;
    Time=12:00:00 PM;
    Version List=NAVW113.00;
  }
  PROPERTIES
  {
    CaptionML=[@@@={Locked};
               ENU=nativeInvoicingSalesQuotes;
               ESM=nativeInvoicingSalesQuotes;
               FRC=nativeInvoicingSalesQuotes;
               ENC=nativeInvoicingSalesQuotes];
    SourceTable=Table5505;
    DelayedInsert=Yes;
    PageType=List;
    OnOpenPage=BEGIN
                 BINDSUBSCRIPTION(NativeAPILanguageHandler);
                 SELECTLATESTVERSION;
               END;

    OnAfterGetRecord=VAR
                       SalesHeader@1001 : Record 36;
                       GraphMgtSalesQuoteBuffer@1000 : Codeunit 5506;
                       GraphMgtGeneralTools@1003 : Codeunit 5465;
                     BEGIN
                       IF NOT GetParentRecordNativeInvoicing(SalesHeader) THEN BEGIN
                         GraphMgtGeneralTools.CleanAggregateWithoutParent(Rec);
                         EXIT;
                       END;

                       SetCalculatedFields(SalesHeader);
                       GraphMgtSalesQuoteBuffer.RedistributeInvoiceDiscounts(Rec);
                     END;

    OnNewRecord=BEGIN
                  ClearCalculatedFields;
                END;

    OnInsertRecord=VAR
                     SalesHeader@1002 : Record 36;
                     GraphMgtSalesQuoteBuffer@1001 : Codeunit 5506;
                   BEGIN
                     CheckCustomer;
                     ProcessBillingPostalAddress;

                     GraphMgtSalesQuoteBuffer.PropagateOnInsert(Rec,TempFieldBuffer);
                     SetDates;

                     UpdateAttachments;
                     UpdateLines;
                     UpdateDiscount;
                     UpdateCoupons;
                     SetNoteForCustomer;

                     IF NOT GetParentRecordNativeInvoicing(SalesHeader) THEN
                       ERROR(AggregateParentIsMissingErr);

                     SetCalculatedFields(SalesHeader);

                     EXIT(FALSE);
                   END;

    OnModifyRecord=VAR
                     SalesHeader@1001 : Record 36;
                     GraphMgtSalesQuoteBuffer@1000 : Codeunit 5506;
                   BEGIN
                     IF xRec.Id <> Id THEN
                       ERROR(CannotChangeIDErr);

                     ProcessBillingPostalAddress;

                     GraphMgtSalesQuoteBuffer.PropagateOnModify(Rec,TempFieldBuffer);

                     UpdateAttachments;
                     UpdateLines;
                     UpdateDiscount;
                     UpdateCoupons;
                     SetNoteForCustomer;

                     IF NOT GetParentRecordNativeInvoicing(SalesHeader) THEN
                       ERROR(AggregateParentIsMissingErr);

                     SetCalculatedFields(SalesHeader);

                     EXIT(FALSE);
                   END;

    OnDeleteRecord=VAR
                     GraphMgtSalesQuoteBuffer@1000 : Codeunit 5506;
                   BEGIN
                     GraphMgtSalesQuoteBuffer.PropagateOnDelete(Rec);

                     EXIT(FALSE);
                   END;

    ODataKeyFields=Id;
  }
  CONTROLS
  {
    { 1   ;0   ;Container ;
                ContainerType=ContentArea }

    { 2   ;1   ;Group     ;
                Name=Group;
                GroupType=Repeater }

    { 18  ;2   ;Field     ;
                Name=id;
                CaptionML=[@@@={Locked};
                           ENU=id;
                           ESM=id;
                           FRC=id;
                           ENC=id];
                ApplicationArea=#All;
                SourceExpr=Id;
                OnValidate=BEGIN
                             RegisterFieldSet(FIELDNO(Id));
                           END;
                            }

    { 19  ;2   ;Field     ;
                Name=number;
                CaptionML=[@@@={Locked};
                           ENU=Number;
                           ESM=Number;
                           FRC=Number;
                           ENC=Number];
                ApplicationArea=#All;
                SourceExpr="No.";
                OnValidate=BEGIN
                             RegisterFieldSet(FIELDNO("No."));
                           END;
                            }

    { 16  ;2   ;Field     ;
                Name=quoteDate;
                CaptionML=[@@@={Locked};
                           ENU=quoteDate;
                           ESM=quoteDate;
                           FRC=quoteDate;
                           ENC=quoteDate];
                ApplicationArea=#All;
                SourceExpr="Document Date";
                OnValidate=BEGIN
                             DocumentDateVar := "Document Date";
                             DocumentDateSet := TRUE;

                             RegisterFieldSet(FIELDNO("Document Date"));
                             RegisterFieldSet(FIELDNO("Posting Date"));
                           END;
                            }

    { 10  ;2   ;Field     ;
                Name=dueDate;
                CaptionML=[@@@={Locked};
                           ENU=dueDate;
                           ESM=dueDate;
                           FRC=dueDate;
                           ENC=dueDate];
                ApplicationArea=#All;
                SourceExpr="Due Date";
                OnValidate=BEGIN
                             DueDateVar := "Due Date";
                             DueDateSet := TRUE;

                             RegisterFieldSet(FIELDNO("Due Date"));
                           END;
                            }

    { 24  ;2   ;Field     ;
                Name=validUntilDate;
                CaptionML=[@@@={Locked};
                           ENU=validUntilDate;
                           ESM=validUntilDate;
                           FRC=validUntilDate;
                           ENC=validUntilDate];
                ApplicationArea=#All;
                SourceExpr="Quote Valid Until Date";
                OnValidate=BEGIN
                             RegisterFieldSet(FIELDNO("Quote Valid Until Date"));
                           END;
                            }

    { 25  ;2   ;Field     ;
                Name=status;
                CaptionML=[@@@={Locked};
                           ENU=status;
                           ESM=status;
                           FRC=status;
                           ENC=status];
                ToolTipML=[ENU=Specifies the status of the Sales Quote (Draft,Sent,Accepted,Expired).;
                           ESM=Especifica el estado de la cotizaci¢n de venta (Borrador,Enviada,Aceptada,Vencida).;
                           FRC=Sp‚cifie l'‚tat de la demande de devis (Provisoire,Envoy‚,Accept‚,Expir‚).;
                           ENC=Specifies the status of the Sales Quote (Draft,Sent,Accepted,Expired).];
                ApplicationArea=#All;
                SourceExpr=Status;
                Editable=FALSE }

    { 36  ;2   ;Field     ;
                Name=accepted;
                CaptionML=[ENU=accepted;
                           ESM=aceptado;
                           FRC=accept‚;
                           ENC=accepted];
                ApplicationArea=#All;
                SourceExpr="Quote Accepted";
                OnValidate=BEGIN
                             RegisterFieldSet(FIELDNO("Quote Accepted"));
                           END;
                            }

    { 9   ;2   ;Field     ;
                Name=acceptedDate;
                CaptionML=[@@@={Locked};
                           ENU=acceptedDate;
                           ESM=acceptedDate;
                           FRC=acceptedDate;
                           ENC=acceptedDate];
                ApplicationArea=#All;
                SourceExpr="Quote Accepted Date" }

    { 7   ;2   ;Field     ;
                Name=customerId;
                CaptionML=[@@@={Locked};
                           ENU=customerId;
                           ESM=customerId;
                           FRC=customerId;
                           ENC=customerId];
                ApplicationArea=#All;
                SourceExpr="Customer Id";
                OnValidate=VAR
                             O365SalesInvoiceMgmt@1000 : Codeunit 2310;
                           BEGIN
                             Customer.SETRANGE(Id,"Customer Id");
                             IF NOT Customer.FINDFIRST THEN
                               ERROR(CannotFindCustomerErr);

                             O365SalesInvoiceMgmt.EnforceCustomerTemplateIntegrity(Customer);

                             "Sell-to Customer No." := Customer."No.";
                             RegisterFieldSet(FIELDNO("Customer Id"));
                             RegisterFieldSet(FIELDNO("Sell-to Customer No."));
                             CustomerIdSet := TRUE;
                           END;
                            }

    { 6   ;2   ;Field     ;
                Name=graphContactId;
                CaptionML=[@@@={Locked};
                           ENU=contactId;
                           ESM=contactId;
                           FRC=contactId;
                           ENC=contactId];
                ApplicationArea=#All;
                SourceExpr="Contact Graph Id";
                OnValidate=VAR
                             Contact@1000 : Record 5050;
                             Customer@1001 : Record 18;
                             GraphIntContact@1003 : Codeunit 5461;
                           BEGIN
                             IF ("Contact Graph Id" = '') AND CustomerIdSet THEN
                               EXIT;

                             RegisterFieldSet(FIELDNO("Contact Graph Id"));

                             IF "Contact Graph Id" = '' THEN
                               ERROR(ContactIdHasToHaveValueErr);

                             IF NOT GraphIntContact.FindOrCreateCustomerFromGraphContactSafe("Contact Graph Id",Customer,Contact) THEN
                               EXIT;

                             UpdateCustomerFromGraphContactId(Customer);

                             IF Contact."Company No." = Customer."No." THEN BEGIN
                               VALIDATE("Sell-to Contact No.",Contact."No.");
                               VALIDATE("Sell-to Contact",Contact.Name);

                               RegisterFieldSet(FIELDNO("Sell-to Contact No."));
                               RegisterFieldSet(FIELDNO("Sell-to Contact"));
                             END;
                           END;
                            }

    { 5   ;2   ;Field     ;
                Name=customerNumber;
                CaptionML=[@@@={Locked};
                           ENU=customerNumber;
                           ESM=customerNumber;
                           FRC=customerNumber;
                           ENC=customerNumber];
                ApplicationArea=#All;
                SourceExpr="Sell-to Customer No.";
                OnValidate=VAR
                             O365SalesInvoiceMgmt@1000 : Codeunit 2310;
                           BEGIN
                             IF Customer."No." <> '' THEN
                               EXIT;

                             IF NOT Customer.GET("Sell-to Customer No.") THEN
                               ERROR(CannotFindCustomerErr);

                             O365SalesInvoiceMgmt.EnforceCustomerTemplateIntegrity(Customer);

                             "Customer Id" := Customer.Id;
                             RegisterFieldSet(FIELDNO("Customer Id"));
                             RegisterFieldSet(FIELDNO("Sell-to Customer No."));
                           END;
                            }

    { 4   ;2   ;Field     ;
                Name=customerName;
                CaptionML=[@@@={Locked};
                           ENU=customerName;
                           ESM=customerName;
                           FRC=customerName;
                           ENC=customerName];
                ApplicationArea=#All;
                SourceExpr="Sell-to Customer Name";
                Editable=FALSE }

    { 26  ;2   ;Field     ;
                Name=customerEmail;
                CaptionML=[@@@={Locked};
                           ENU=customerEmail;
                           ESM=customerEmail;
                           FRC=customerEmail;
                           ENC=customerEmail];
                ToolTipML=[ENU=Specifies the email address of the customer;
                           ESM=Especifica la direcci¢n de correo electr¢nico del cliente.;
                           FRC=Sp‚cifie l'adresse ‚lectronique du client.;
                           ENC=Specifies the email address of the customer];
                ApplicationArea=#All;
                SourceExpr=CustomerEmail;
                Editable=FALSE }

    { 23  ;2   ;Field     ;
                Name=taxLiable;
                CaptionML=[ENU=taxLiable;
                           ESM=taxLiable;
                           FRC=taxLiable;
                           ENC=taxLiable];
                ToolTipML=[ENU=Specifies if the sales invoice contains sales tax.;
                           ESM=Especifica si la factura de venta incluye el impuesto sobre las ventas.;
                           FRC=Indique si la facture vente contient la taxe de vente.;
                           ENC=Specifies if the sales invoice contains sales tax.];
                ApplicationArea=#All;
                SourceExpr="Tax Liable";
                Importance=Additional;
                OnValidate=BEGIN
                             RegisterFieldSet(FIELDNO("Tax Liable"));
                           END;
                            }

    { 20  ;2   ;Field     ;
                Name=taxAreaId;
                CaptionML=[ENU=taxAreaId;
                           ESM=taxAreaId;
                           FRC=taxAreaId;
                           ENC=taxAreaId];
                ApplicationArea=#All;
                SourceExpr="Tax Area ID";
                OnValidate=BEGIN
                             RegisterFieldSet(FIELDNO("Tax Area ID"));

                             IF IsUsingVAT THEN
                               RegisterFieldSet(FIELDNO("VAT Bus. Posting Group"))
                             ELSE
                               RegisterFieldSet(FIELDNO("Tax Area Code"));
                           END;
                            }

    { 35  ;2   ;Field     ;
                Name=taxAreaDisplayName;
                CaptionML=[ENU=taxAreaDisplayName;
                           ESM=taxAreaDisplayName;
                           FRC=taxAreaDisplayName;
                           ENC=taxAreaDisplayName];
                ToolTipML=[ENU=Specifies the tax area display name.;
                           ESM=Especifica el nombre para mostrar en el  rea de impuestos.;
                           FRC=Sp‚cifie le nom d'affichage de la r‚gion fiscale.;
                           ENC=Specifies the tax area display name.];
                ApplicationArea=#All;
                SourceExpr=TaxAreaDisplayName;
                Editable=FALSE }

    { 13  ;2   ;Field     ;
                Name=taxRegistrationNumber;
                CaptionML=[ENU=taxRegistrationNumber;
                           ESM=taxRegistrationNumber;
                           FRC=taxRegistrationNumber;
                           ENC=taxRegistrationNumber];
                ApplicationArea=#All;
                SourceExpr="VAT Registration No.";
                OnValidate=BEGIN
                             RegisterFieldSet(FIELDNO("VAT Registration No."));
                           END;
                            }

    { 3   ;2   ;Field     ;
                Name=billingPostalAddress;
                CaptionML=[@@@={Locked};
                           ENU=billingPostalAddress;
                           ESM=billingPostalAddress;
                           FRC=billingPostalAddress;
                           ENC=billingPostalAddress];
                ToolTipML=[ENU=Specifies the billing address of the Sales Invoice.;
                           ESM=Especifica la direcci¢n de facturaci¢n de la factura de venta.;
                           FRC=Indique l'adresse de facturation de la facture vente.;
                           ENC=Specifies the billing address of the Sales Invoice.];
                ApplicationArea=#All;
                SourceExpr=BillingPostalAddressJSONText;
                OnValidate=BEGIN
                             BillingPostalAddressSet := TRUE;
                           END;

                ODataEDMType=POSTALADDRESS }

    { 29  ;2   ;Field     ;
                Name=pricesIncludeTax;
                CaptionML=[@@@={Locked};
                           ENU=pricesIncludeTax;
                           ESM=pricesIncludeTax;
                           FRC=pricesIncludeTax;
                           ENC=pricesIncludeTax];
                ApplicationArea=#All;
                SourceExpr="Prices Including VAT";
                Editable=FALSE;
                OnValidate=BEGIN
                             RegisterFieldSet(FIELDNO("Prices Including VAT"));
                           END;
                            }

    { 12  ;2   ;Field     ;
                Name=shipmentMethod;
                CaptionML=[@@@={Locked};
                           ENU=shipmentMethod;
                           ESM=shipmentMethod;
                           FRC=shipmentMethod;
                           ENC=shipmentMethod];
                ApplicationArea=#All;
                SourceExpr="Shipment Method Code";
                OnValidate=BEGIN
                             RegisterFieldSet(FIELDNO("Shipment Method Code"));
                           END;
                            }

    { 11  ;2   ;Field     ;
                Name=salesperson;
                CaptionML=[@@@={Locked};
                           ENU=salesperson;
                           ESM=salesperson;
                           FRC=salesperson;
                           ENC=salesperson];
                ApplicationArea=#All;
                SourceExpr="Salesperson Code";
                OnValidate=BEGIN
                             RegisterFieldSet(FIELDNO("Salesperson Code"));
                           END;
                            }

    { 39  ;2   ;Field     ;
                Name=currencyCode;
                CaptionML=[@@@={Locked};
                           ENU=currencyCode;
                           ESM=currencyCode;
                           FRC=currencyCode;
                           ENC=currencyCode];
                ToolTipML=[ENU=Specifies the currency code.;
                           ESM=Especifica el c¢digo de divisa.;
                           FRC=Sp‚cifie le code devise.;
                           ENC=Specifies the currency code.];
                ApplicationArea=#All;
                SourceExpr=CurrencyCodeTxt;
                Editable=FALSE }

    { 8   ;2   ;Field     ;
                Name=lines;
                CaptionML=[@@@={Locked};
                           ENU=lines;
                           ESM=lines;
                           FRC=lines;
                           ENC=lines];
                ToolTipML=[ENU=Specifies Sales Invoice Lines;
                           ESM=Especifica las l¡neas de factura de venta.;
                           FRC=Sp‚cifie les lignes facture vente;
                           ENC=Specifies Sales Invoice Lines];
                ApplicationArea=#All;
                SourceExpr=SalesQuoteLinesJSON;
                OnValidate=BEGIN
                             SalesLinesSet := PreviousSalesQuoteLinesJSON <> SalesQuoteLinesJSON;
                           END;

                ODataEDMType=Collection(NATIVE-SALESQUOTE-LINE) }

    { 30  ;2   ;Field     ;
                Name=subtotalAmount;
                CaptionML=[ENU=subtotalAmount;
                           ESM=subtotalAmount;
                           FRC=subtotalAmount;
                           ENC=subtotalAmount];
                ApplicationArea=#All;
                SourceExpr="Subtotal Amount";
                Editable=FALSe }

    { 14  ;2   ;Field     ;
                Name=discountAmount;
                CaptionML=[@@@={Locked};
                           ENU=discountAmount;
                           ESM=discountAmount;
                           FRC=discountAmount;
                           ENC=discountAmount];
                ApplicationArea=#All;
                SourceExpr="Invoice Discount Amount";
                Editable=False }

    { 32  ;2   ;Field     ;
                Name=discountAppliedBeforeTax;
                CaptionML=[@@@={Locked};
                           ENU=discountAppliedBeforeTax;
                           ESM=discountAppliedBeforeTax;
                           FRC=discountAppliedBeforeTax;
                           ENC=discountAppliedBeforeTax];
                ApplicationArea=#All;
                SourceExpr="Discount Applied Before Tax" }

    { 31  ;2   ;Field     ;
                Name=coupons;
                CaptionML=[@@@={Locked};
                           ENU=coupons;
                           ESM=coupons;
                           FRC=coupons;
                           ENC=coupons];
                ToolTipML=[ENU=Specifies Sales Invoice Coupons.;
                           ESM=Especifica los cupones de factura de venta.;
                           FRC=Sp‚cifie les coupons facture vente.;
                           ENC=Specifies Sales Invoice Coupons.];
                ApplicationArea=#All;
                SourceExpr=CouponsJSON;
                OnValidate=BEGIN
                             CouponsSet := PreviousCouponsJSON <> CouponsJSON;
                           END;

                ODataEDMType=Collection(NATIVE-SALESDOCUMENT-COUPON) }

    { 22  ;2   ;Field     ;
                Name=totalAmountExcludingTax;
                CaptionML=[@@@={Locked};
                           ENU=totalAmountExcludingTax;
                           ESM=totalAmountExcludingTax;
                           FRC=totalAmountExcludingTax;
                           ENC=totalAmountExcludingTax];
                ApplicationArea=#All;
                SourceExpr=Amount;
                Editable=FALSE }

    { 21  ;2   ;Field     ;
                Name=totalTaxAmount;
                CaptionML=[@@@={Locked};
                           ENU=totalTaxAmount;
                           ESM=totalTaxAmount;
                           FRC=totalTaxAmount;
                           ENC=totalTaxAmount];
                ToolTipML=[ENU=Specifies the total tax amount for the sales invoice.;
                           ESM=Especifica el importe total de impuestos de la factura de venta.;
                           FRC=Indique le montant total de taxe de la facture vente.;
                           ENC=Specifies the total tax amount for the sales invoice.];
                ApplicationArea=#All;
                SourceExpr="Total Tax Amount";
                Editable=FALSE;
                OnValidate=BEGIN
                             RegisterFieldSet(FIELDNO("Total Tax Amount"));
                           END;
                            }

    { 17  ;2   ;Field     ;
                Name=totalAmountIncludingTax;
                CaptionML=[@@@={Locked};
                           ENU=totalAmountIncludingTax;
                           ESM=totalAmountIncludingTax;
                           FRC=totalAmountIncludingTax;
                           ENC=totalAmountIncludingTax];
                ApplicationArea=#All;
                SourceExpr="Amount Including VAT";
                Editable=FALSE;
                OnValidate=BEGIN
                             RegisterFieldSet(FIELDNO("Amount Including VAT"));
                           END;
                            }

    { 27  ;2   ;Field     ;
                Name=noteForCustomer;
                CaptionML=[ENU=noteForCustomer;
                           ESM=noteForCustomer;
                           FRC=noteForCustomer;
                           ENC=noteForCustomer];
                ToolTipML=[ENU=Specifies the note for the customer.;
                           ESM=Especifica la nota para el cliente.;
                           FRC=Sp‚cifie la note pour le client.;
                           ENC=Specifies the note for the customer.];
                ApplicationArea=#All;
                SourceExpr=WorkDescription;
                OnValidate=BEGIN
                             NoteForCustomerSet := TRUE;
                           END;
                            }

    { 15  ;2   ;Field     ;
                Name=lastModifiedDateTime;
                CaptionML=[@@@={Locked};
                           ENU=lastModifiedDateTime;
                           ESM=lastModifiedDateTime;
                           FRC=lastModifiedDateTime;
                           ENC=lastModifiedDateTime];
                ApplicationArea=#All;
                SourceExpr="Last Modified Date Time";
                Editable=FALSE }

    { 33  ;2   ;Field     ;
                Name=attachments;
                CaptionML=[@@@={Locked};
                           ENU=attachments;
                           ESM=attachments;
                           FRC=attachments;
                           ENC=attachments];
                ToolTipML=[ENU=Specifies Attachments;
                           ESM=Especifica los datos adjuntos;
                           FRC=Sp‚cifie les piŠces jointes;
                           ENC=Specifies Attachments];
                ApplicationArea=#All;
                SourceExpr=AttachmentsJSON;
                OnValidate=BEGIN
                             IsAttachmentsSet := AttachmentsJSON <> PreviousAttachmentsJSON;
                           END;

                ODataEDMType=Collection(NATIVE-ATTACHMENT) }

    { 34  ;2   ;Field     ;
                Name=invoiceDiscountCalculation;
                CaptionML=[ENU=invoiceDiscountCalculation;
                           ESM=invoiceDiscountCalculation;
                           FRC=invoiceDiscountCalculation;
                           ENC=invoiceDiscountCalculation];
                OptionCaptionML=[@@@={Locked};
                                 ENU=,%,Amount;
                                 ESM=,%,Amount;
                                 FRC=,%,Amount;
                                 ENC=,%,Amount];
                ApplicationArea=#All;
                SourceExpr="Invoice Discount Calculation";
                OnValidate=BEGIN
                             RegisterFieldSet(FIELDNO("Invoice Discount Calculation"));
                             DiscountAmountSet := TRUE;
                           END;
                            }

    { 28  ;2   ;Field     ;
                Name=invoiceDiscountValue;
                CaptionML=[ENU=invoiceDiscountValue;
                           ESM=invoiceDiscountValue;
                           FRC=invoiceDiscountValue;
                           ENC=invoiceDiscountValue];
                ApplicationArea=#All;
                SourceExpr="Invoice Discount Value";
                OnValidate=BEGIN
                             RegisterFieldSet(FIELDNO("Invoice Discount Value"));
                             DiscountAmountSet := TRUE;
                           END;
                            }

    { 46  ;2   ;Field     ;
                Name=lastEmailSentStatus;
                CaptionML=[@@@={Locked};
                           ENU=lastEmailSentStatus;
                           ESM=lastEmailSentStatus;
                           FRC=lastEmailSentStatus;
                           ENC=lastEmailSentStatus];
                ToolTipML=[ENU=Specifies the status of the last sent email, Not Sent, In Process, Finished, or Error.;
                           ESM=Especifica el estado del £ltimo correo electr¢nico enviado (No enviado, En proceso, Finalizado o Error).;
                           FRC=Sp‚cifie l'‚tat du dernier courriel envoy‚ÿ: Non envoy‚, En cours, Termin‚ ou Erreur.;
                           ENC=Specifies the status of the last sent email, Not Sent, In Process, Finished, or Error.];
                ApplicationArea=#All;
                SourceExpr=LastEmailSentStatus;
                Editable=FALSE }

    { 37  ;2   ;Field     ;
                Name=lastEmailSentTime;
                CaptionML=[@@@={Locked};
                           ENU=lastEmailSentTime;
                           ESM=lastEmailSentTime;
                           FRC=lastEmailSentTime;
                           ENC=lastEmailSentTime];
                ToolTipML=[ENU=Specifies the time that the last email was sent.;
                           ESM=Especifica la hora de env¡o del £ltimo correo electr¢nico.;
                           FRC=Sp‚cifie l'heure … laquelle le dernier courriel a ‚t‚ envoy‚.;
                           ENC=Specifies the time that the last email was sent.];
                ApplicationArea=#All;
                SourceExpr=LastEmailSentTime;
                Editable=FALSE }

    { 38  ;2   ;Field     ;
                Name=isCustomerBlocked;
                CaptionML=[@@@={Locked};
                           ENU=isCustomerBlocked;
                           ESM=isCustomerBlocked;
                           FRC=isCustomerBlocked;
                           ENC=isCustomerBlocked];
                ToolTipML=[ENU=Specifies if the customer is blocked.;
                           ESM=Especifica si el cliente est  bloqueado.;
                           FRC=Sp‚cifie si le client est bloqu‚.;
                           ENC=Specifies if the customer is blocked.];
                ApplicationArea=#All;
                SourceExpr=IsCustomerBlocked;
                Editable=FALSE }

  }
  CODE
  {
    VAR
      TempFieldBuffer@1004 : TEMPORARY Record 8450;
      Customer@1006 : Record 18;
      DummySalesLine@1002 : Record 37;
      NativeAPILanguageHandler@1033 : Codeunit 2850;
      BillingPostalAddressJSONText@1000 : Text;
      CustomerEmail@1010 : Text;
      SalesQuoteLinesJSON@1003 : Text;
      PreviousSalesQuoteLinesJSON@1001 : Text;
      CouponsJSON@1022 : Text;
      PreviousCouponsJSON@1021 : Text;
      AttachmentsJSON@1024 : Text;
      TaxAreaDisplayName@1030 : Text;
      PreviousAttachmentsJSON@1025 : Text;
      BillingPostalAddressSet@1005 : Boolean;
      CannotFindCustomerErr@1008 : TextConst 'ENU=The customer cannot be found.;ESM=No se encuentra el cliente.;FRC=Impossible de trouver le client.;ENC=The customer cannot be found.';
      ContactIdHasToHaveValueErr@1007 : TextConst 'ENU=Contact Id must have a value set.;ESM=El id. de contacto debe tener un valor establecido.;FRC=Le code du contact doit avoir une valeur d‚finie.;ENC=Contact Id must have a value set.';
      CannotChangeIDErr@1009 : TextConst 'ENU=The id cannot be changed.;ESM=El id. no se puede cambiar.;FRC=Le code ne peut pas ˆtre modifi‚.;ENC=The id cannot be changed.';
      SalesLinesSet@1015 : Boolean;
      CouponsSet@1023 : Boolean;
      DiscountAmountSet@1014 : Boolean;
      IsAttachmentsSet@1026 : Boolean;
      InvoiceDiscountAmount@1013 : Decimal;
      WorkDescription@1012 : Text;
      CustomerNotProvidedErr@1016 : TextConst 'ENU=A customerNumber or a customerID must be provided.;ESM=Se debe proporcionar un valor de customerNumber o customerID.;FRC=Une valeur customerNumber ou customerID doit ˆtre fournie.;ENC=A customerNumber or a customerID must be provided.';
      NoteForCustomerSet@1017 : Boolean;
      DocumentDateSet@1020 : Boolean;
      DocumentDateVar@1019 : Date;
      DueDateSet@1018 : Boolean;
      DueDateVar@1011 : Date;
      CannotFindQuoteErr@1041 : TextConst 'ENU=The quote cannot be found.;ESM=No se encuentra la cotizaci¢n.;FRC=Impossible de trouver le devis.;ENC=The quote cannot be found.';
      EmptyEmailErr@1043 : TextConst 'ENU=The send-to email is empty. Specify email either for the customer or for the quote in email preview.;ESM=El correo electr¢nico del destinatario est  vac¡o. Especifique el correo electr¢nico del cliente o de la cotizaci¢n en la vista previa de correo electr¢nico.;FRC=Le courriel destinataire est vide. Sp‚cifiez le courriel pour le client ou pour le devis dans l''aper‡u du courriel.;ENC=The send-to email is empty. Specify email either for the customer or for the quote in email preview.';
      InvoiceDiscountPctMustBePositiveErr@1028 : TextConst 'ENU=Invoice discount percentage must be positive.;ESM=El porcentaje de descuento en factura debe ser positivo.;FRC=Le pourcentage d''escompte facture doit ˆtre positif.;ENC=Invoice discount percentage must be positive.';
      InvoiceDiscountPctMustBeBelowHundredErr@1027 : TextConst 'ENU=Invoice discount percentage must be below 100.;ESM=El porcentaje de descuento en factura debe ser inferior a 100.;FRC=Le pourcentage d''escompte facture doit ˆtre inf‚rieur … 100.;ENC=Invoice discount percentage must be below 100.';
      InvoiceDiscountAmtMustBePositiveErr@1029 : TextConst 'ENU=Invoice discount must be positive.;ESM=El descuento en factura debe ser positivo.;FRC=L''escompte facture doit ˆtre positif.;ENC=Invoice discount must be positive.';
      CustomerIdSet@1032 : Boolean;
      LastEmailSentTime@1031 : DateTime;
      LastEmailSentStatus@1034 : 'Not Sent,In Process,Finished,Error';
      AggregateParentIsMissingErr@1035 : TextConst 'ENU=Please try the operation again. If the error persists, please contact support.;ESM=Vuelva a intentar la operaci¢n. Si el error persiste, p¢ngase en contacto con el soporte t‚cnico.;FRC=Veuillez recommencer l''op‚ration. Si l''erreur persiste, contactez le support.;ENC=Please try the operation again. If the error persists, please contact support.';
      CurrencyCodeTxt@1038 : Text;
      LCYCurrencyCode@1037 : Code[10];
      IsCustomerBlocked@1036 : Boolean;

    LOCAL PROCEDURE SetCalculatedFields@6(VAR SalesHeader@1006 : Record 36);
    VAR
      TempSalesInvoiceLineAggregate@1002 : TEMPORARY Record 5476;
      DummyNativeAPITaxSetup@1005 : Record 2850;
      GraphMgtSalesQuote@1000 : Codeunit 5505;
      NativeEDMTypes@1001 : Codeunit 2801;
      NativeCoupons@1003 : Codeunit 2815;
      NativeAttachments@1004 : Codeunit 2820;
      GraphMgtGeneralTools@1007 : Codeunit 5465;
    BEGIN
      BillingPostalAddressJSONText := GraphMgtSalesQuote.BillToCustomerAddressToJSON(Rec);
      CurrencyCodeTxt := GraphMgtGeneralTools.TranslateNAVCurrencyCodeToCurrencyCode(LCYCurrencyCode,"Currency Code");

      IF "Sell-to Customer No." <> '' THEN
        IF Customer.GET("Sell-to Customer No.") THEN BEGIN
          CustomerEmail := Customer."E-Mail";
          IsCustomerBlocked := Customer.IsBlocked
        END ELSE BEGIN
          IsCustomerBlocked := FALSE;
          CustomerEmail := '';
        END;

      LoadLines(TempSalesInvoiceLineAggregate,Rec);
      SalesQuoteLinesJSON := NativeEDMTypes.WriteSalesLinesJSON(TempSalesInvoiceLineAggregate);
      PreviousSalesQuoteLinesJSON := SalesQuoteLinesJSON;

      CouponsJSON := NativeCoupons.WriteCouponsJSON(DummySalesLine."Document Type"::Quote,"No.",FALSE);
      PreviousCouponsJSON := CouponsJSON;

      AttachmentsJSON := NativeAttachments.GenerateAttachmentsJSON(Id);
      PreviousAttachmentsJSON := AttachmentsJSON;
      TaxAreaDisplayName := DummyNativeAPITaxSetup.GetTaxAreaDisplayName("Tax Area ID");

      GetNoteForCustomer(SalesHeader);
      GetLastEmailSentFields(SalesHeader);
    END;

    LOCAL PROCEDURE ClearCalculatedFields@10();
    BEGIN
      CLEAR(BillingPostalAddressJSONText);
      CLEAR(SalesQuoteLinesJSON);
      CLEAR(PreviousSalesQuoteLinesJSON);
      CLEAR(SalesLinesSet);
      CLEAR(AttachmentsJSON);
      CLEAR(PreviousAttachmentsJSON);
      CLEAR(IsAttachmentsSet);
      CLEAR(WorkDescription);
      CLEAR(TaxAreaDisplayName);
      CLEAR(LastEmailSentTime);
      CLEAR(LastEmailSentStatus);
      CLEAR(CurrencyCodeTxt);
      TempFieldBuffer.DELETEALL;
    END;

    LOCAL PROCEDURE RegisterFieldSet@11(FieldNo@1000 : Integer);
    VAR
      LastOrderNo@1001 : Integer;
    BEGIN
      LastOrderNo := 1;
      IF TempFieldBuffer.FINDLAST THEN
        LastOrderNo := TempFieldBuffer.Order + 1;

      CLEAR(TempFieldBuffer);
      TempFieldBuffer.Order := LastOrderNo;
      TempFieldBuffer."Table ID" := DATABASE::"Sales Quote Entity Buffer";
      TempFieldBuffer."Field ID" := FieldNo;
      TempFieldBuffer.INSERT;
    END;

    LOCAL PROCEDURE ProcessBillingPostalAddress@5();
    VAR
      GraphMgtSalesQuote@1000 : Codeunit 5505;
    BEGIN
      IF NOT BillingPostalAddressSet THEN
        EXIT;

      GraphMgtSalesQuote.ProcessComplexTypes(Rec,BillingPostalAddressJSONText);

      IF xRec."Sell-to Address" <> "Sell-to Address" THEN
        RegisterFieldSet(FIELDNO("Sell-to Address"));

      IF xRec."Sell-to Address 2" <> "Sell-to Address 2" THEN
        RegisterFieldSet(FIELDNO("Sell-to Address 2"));

      IF xRec."Sell-to City" <> "Sell-to City" THEN
        RegisterFieldSet(FIELDNO("Sell-to City"));

      IF xRec."Sell-to Country/Region Code" <> "Sell-to Country/Region Code" THEN
        RegisterFieldSet(FIELDNO("Sell-to Country/Region Code"));

      IF xRec."Sell-to Post Code" <> "Sell-to Post Code" THEN
        RegisterFieldSet(FIELDNO("Sell-to Post Code"));

      IF xRec."Sell-to County" <> "Sell-to County" THEN
        RegisterFieldSet(FIELDNO("Sell-to County"));
    END;

    LOCAL PROCEDURE UpdateCustomerFromGraphContactId@2(VAR Customer@1001 : Record 18);
    VAR
      O365SalesInvoiceMgmt@1002 : Codeunit 2310;
      UpdateCustomer@1000 : Boolean;
    BEGIN
      UpdateCustomer := "Sell-to Customer No." = '';
      IF NOT UpdateCustomer THEN BEGIN
        TempFieldBuffer.RESET;
        TempFieldBuffer.SETRANGE("Field ID",FIELDNO("Customer Id"));
        UpdateCustomer := NOT TempFieldBuffer.FINDFIRST;
        TempFieldBuffer.RESET;
      END;

      IF UpdateCustomer THEN BEGIN
        VALIDATE("Customer Id",Customer.Id);
        VALIDATE("Sell-to Customer No.",Customer."No.");
        RegisterFieldSet(FIELDNO("Customer Id"));
        RegisterFieldSet(FIELDNO("Sell-to Customer No."));
      END;

      O365SalesInvoiceMgmt.EnforceCustomerTemplateIntegrity(Customer);
    END;

    LOCAL PROCEDURE LoadLines@1(VAR TempSalesInvoiceLineAggregate@1000 : TEMPORARY Record 5476;VAR SalesQuoteEntityBuffer@1001 : Record 5505);
    VAR
      GraphMgtSalesQuoteBuffer@1002 : Codeunit 5506;
    BEGIN
      TempSalesInvoiceLineAggregate.SETRANGE("Document Id",SalesQuoteEntityBuffer.Id);
      GraphMgtSalesQuoteBuffer.LoadLines(TempSalesInvoiceLineAggregate,TempSalesInvoiceLineAggregate.GETFILTER("Document Id"));
    END;

    LOCAL PROCEDURE UpdateLines@4();
    VAR
      TempSalesInvoiceLineAggregate@1002 : TEMPORARY Record 5476;
      GraphMgtSalesQuoteBuffer@1000 : Codeunit 5506;
      NativeEDMTypes@1001 : Codeunit 2801;
    BEGIN
      IF NOT SalesLinesSet THEN
        EXIT;

      NativeEDMTypes.ParseSalesLinesJSON(
        DummySalesLine."Document Type"::Quote,SalesQuoteLinesJSON,TempSalesInvoiceLineAggregate,Id);
      TempSalesInvoiceLineAggregate.SETRANGE("Document Id",Id);
      GraphMgtSalesQuoteBuffer.PropagateMultipleLinesUpdate(TempSalesInvoiceLineAggregate);
      FIND;
    END;

    LOCAL PROCEDURE UpdateDiscount@13();
    VAR
      SalesHeader@1002 : Record 36;
      SalesLine@1009 : Record 37;
      TotalSalesLine@1008 : Record 37;
      CustInvDisc@1007 : Record 19;
      SalesCalcDiscountByType@1006 : Codeunit 56;
      O365Discounts@1005 : Codeunit 2155;
      DocumentTotals@1004 : Codeunit 57;
      GraphMgtSalesQuoteBuffer@1000 : Codeunit 5506;
      VatAmount@1003 : Decimal;
      InvoiceDiscountPct@1001 : Decimal;
    BEGIN
      IF SalesLinesSet AND (NOT DiscountAmountSet) THEN BEGIN
        GraphMgtSalesQuoteBuffer.RedistributeInvoiceDiscounts(Rec);
        EXIT;
      END;

      IF NOT DiscountAmountSet THEN
        EXIT;

      CASE "Invoice Discount Calculation" OF
        "Invoice Discount Calculation"::"%":
          BEGIN
            IF "Invoice Discount Value" < 0 THEN
              ERROR(InvoiceDiscountPctMustBePositiveErr);
            IF "Invoice Discount Value" > 100 THEN
              ERROR(InvoiceDiscountPctMustBeBelowHundredErr);
            InvoiceDiscountPct := "Invoice Discount Value";
          END;
        "Invoice Discount Calculation"::Amount:
          BEGIN
            InvoiceDiscountAmount := "Invoice Discount Value";
            IF "Invoice Discount Value" < 0 THEN
              ERROR(InvoiceDiscountAmtMustBePositiveErr);
          END;
      END;

      SalesHeader.GET("Document Type"::Quote,"No.");
      IF InvoiceDiscountPct <> 0 THEN BEGIN
        O365Discounts.ApplyInvoiceDiscountPercentage(SalesHeader,InvoiceDiscountPct);
        SalesLine.SETRANGE("Document Type",SalesHeader."Document Type");
        SalesLine.SETRANGE("Document No.",SalesHeader."No.");
        IF SalesLine.FINDFIRST THEN BEGIN
          GraphMgtSalesQuoteBuffer.RedistributeInvoiceDiscounts(Rec);
          DocumentTotals.CalculateSalesTotals(TotalSalesLine,VatAmount,SalesLine);
          "Invoice Discount Amount" := TotalSalesLine."Inv. Discount Amount";
        END;
      END ELSE BEGIN
        CustInvDisc.SETRANGE(Code,"No.");
        CustInvDisc.DELETEALL;
        SalesCalcDiscountByType.ApplyInvDiscBasedOnAmt(InvoiceDiscountAmount,SalesHeader);
        GraphMgtSalesQuoteBuffer.RedistributeInvoiceDiscounts(Rec);
      END;
    END;

    LOCAL PROCEDURE UpdateCoupons@14();
    VAR
      NativeEDMTypes@1000 : Codeunit 2801;
    BEGIN
      IF NOT CouponsSet THEN
        EXIT;

      NativeEDMTypes.ParseCouponsJSON("Contact Graph Id",DummySalesLine."Document Type"::Quote,"No.",CouponsJSON);
    END;

    LOCAL PROCEDURE UpdateAttachments@7();
    VAR
      NativeAttachments@1001 : Codeunit 2820;
    BEGIN
      IF NOT IsAttachmentsSet THEN
        EXIT;

      // Here we now know that user has specified different attachments than before.
      // We should patch the attachments. This means:
      // Delete the ones not in use anymore.
      // Add new ones.
      // Keep old ones.

      NativeAttachments.UpdateAttachments(Id,AttachmentsJSON,PreviousAttachmentsJSON);
    END;

    LOCAL PROCEDURE CheckCustomer@3();
    VAR
      BlankGUID@1000 : GUID;
    BEGIN
      IF ("Sell-to Customer No." = '') AND
         ("Customer Id" = BlankGUID)
      THEN
        ERROR(CustomerNotProvidedErr);
    END;

    LOCAL PROCEDURE GetLastEmailSentFields@19(VAR SalesHeader@1000 : Record 36);
    BEGIN
      LastEmailSentTime := SalesHeader."Last Email Sent Time";
      LastEmailSentStatus := SalesHeader."Last Email Sent Status";
    END;

    LOCAL PROCEDURE GetNoteForCustomer@9(VAR SalesHeader@1000 : Record 36);
    BEGIN
      CLEAR(WorkDescription);
      WorkDescription := SalesHeader.GetWorkDescriptionWorkDescriptionCalculated;
    END;

    LOCAL PROCEDURE SetNoteForCustomer@22();
    VAR
      SalesHeader@1000 : Record 36;
    BEGIN
      IF NOT NoteForCustomerSet THEN
        EXIT;

      SalesHeader.GET(SalesHeader."Document Type"::Quote,"No.");
      SalesHeader.SetWorkDescription(WorkDescription);
      SalesHeader.MODIFY(TRUE);
      FIND;
    END;

    LOCAL PROCEDURE SetDates@8();
    VAR
      GraphMgtSalesQuoteBuffer@1000 : Codeunit 5506;
    BEGIN
      IF NOT (DueDateSet OR DocumentDateSet) THEN
        EXIT;

      TempFieldBuffer.RESET;
      TempFieldBuffer.DELETEALL;

      IF DocumentDateSet THEN BEGIN
        "Document Date" := DocumentDateVar;
        "Posting Date" := DocumentDateVar;
        RegisterFieldSet(FIELDNO("Document Date"));
        RegisterFieldSet(FIELDNO("Posting Date"));
      END;

      IF DueDateSet THEN BEGIN
        "Due Date" := DueDateVar;
        RegisterFieldSet(FIELDNO("Due Date"));
      END;

      GraphMgtSalesQuoteBuffer.PropagateOnModify(Rec,TempFieldBuffer);
      FIND;
    END;

    LOCAL PROCEDURE GetQuote@36(VAR SalesHeader@1000 : Record 36);
    BEGIN
      SalesHeader.SETRANGE(Id,Id);
      IF NOT SalesHeader.FINDFIRST THEN
        ERROR(CannotFindQuoteErr);
    END;

    LOCAL PROCEDURE CheckAttachmentsSize@29(RecordVariant@1001 : Variant);
    VAR
      IncomingDocumentAttachment@1000 : Record 133;
      O365SalesAttachmentMgt@1002 : Codeunit 2112;
    BEGIN
      O365SalesAttachmentMgt.GetAttachments(RecordVariant,IncomingDocumentAttachment);
      O365SalesAttachmentMgt.AssertIncomingDocumentSizeBelowMax(IncomingDocumentAttachment);
    END;

    LOCAL PROCEDURE CheckSendToEmailAddress@21();
    BEGIN
      IF GetSendToEmailAddress = '' THEN
        ERROR(EmptyEmailErr);
    END;

    LOCAL PROCEDURE GetSendToEmailAddress@26() : Text[250];
    VAR
      EmailAddress@1000 : Text[250];
    BEGIN
      EmailAddress := GetDocumentEmailAddress;
      IF EmailAddress <> '' THEN
        EXIT(EmailAddress);
      EmailAddress := GetCustomerEmailAddress;
      EXIT(EmailAddress);
    END;

    LOCAL PROCEDURE GetCustomerEmailAddress@24() : Text[250];
    BEGIN
      IF NOT Customer.GET("Sell-to Customer No.") THEN
        EXIT('');
      EXIT(Customer."E-Mail");
    END;

    LOCAL PROCEDURE GetDocumentEmailAddress@58() : Text[250];
    VAR
      EmailParameter@1002 : Record 9510;
    BEGIN
      IF NOT EmailParameter.GET("No.","Document Type",EmailParameter."Parameter Type"::Address) THEN
        EXIT('');
      EXIT(EmailParameter."Parameter Value");
    END;

    LOCAL PROCEDURE SendQuote@34(VAR SalesHeader@1000 : Record 36);
    VAR
      DummyO365SalesDocument@1003 : Record 2103;
      LinesInstructionMgt@1002 : Codeunit 1320;
      O365SendResendInvoice@1001 : Codeunit 2104;
      O365SetupEmail@1004 : Codeunit 2135;
      O365SalesEmailManagement@1005 : Codeunit 2151;
    BEGIN
      O365SendResendInvoice.CheckDocumentIfNoItemsExists(SalesHeader,FALSE,DummyO365SalesDocument);
      LinesInstructionMgt.SalesCheckAllLinesHaveQuantityAssigned(SalesHeader);
      O365SetupEmail.SilentSetup;
      CheckSendToEmailAddress;

      O365SalesEmailManagement.NativeAPISaveEmailBodyText(Id);
      SalesHeader.SETRECFILTER;
      SalesHeader.EmailRecords(FALSE);
    END;

    LOCAL PROCEDURE SetActionResponse@47(VAR ActionContext@1004 : DotNet "'Microsoft.Dynamics.Nav.Ncl, Version=13.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Runtime.WebServiceActionContext";VAR SalesHeader@1000 : Record 36);
    VAR
      ODataActionManagement@1003 : Codeunit 6711;
    BEGIN
      ODataActionManagement.AddKey(FIELDNO(Id),SalesHeader.Id);
      IF SalesHeader."Document Type" = SalesHeader."Document Type"::Invoice THEN
        ODataActionManagement.SetDeleteResponseLocation(ActionContext,PAGE::"Native - Sales Inv. Entity")
      ELSE
        ODataActionManagement.SetDeleteResponseLocation(ActionContext,PAGE::"Native - Sales Quotes");
    END;

    [ServiceEnabled]
    [External]
    PROCEDURE MakeInvoice@15(VAR ActionContext@1000 : DotNet "'Microsoft.Dynamics.Nav.Ncl, Version=13.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Runtime.WebServiceActionContext");
    VAR
      SalesHeader@1001 : Record 36;
      SalesQuoteToInvoice@1005 : Codeunit 1305;
    BEGIN
      GetQuote(SalesHeader);
      SalesHeader.SETRECFILTER;
      SalesQuoteToInvoice.RUN(SalesHeader);
      SalesQuoteToInvoice.GetSalesInvoiceHeader(SalesHeader);
      SetActionResponse(ActionContext,SalesHeader);
    END;

    [ServiceEnabled]
    [External]
    PROCEDURE Send@17(VAR ActionContext@1000 : DotNet "'Microsoft.Dynamics.Nav.Ncl, Version=13.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Runtime.WebServiceActionContext");
    VAR
      SalesHeader@1005 : Record 36;
    BEGIN
      GetQuote(SalesHeader);
      CheckAttachmentsSize(SalesHeader);
      SendQuote(SalesHeader);
      SetActionResponse(ActionContext,SalesHeader);
    END;

    BEGIN
    END.
  }
}

