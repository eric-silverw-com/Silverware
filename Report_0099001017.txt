OBJECT Report 99001017 Calculate Plan - Plan. Wksh.
{
  OBJECT-PROPERTIES
  {
    Date=09/19/18;
    Time=12:00:00 PM;
    Version List=NAVW113.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Calculate Plan - Plan. Wksh.;
               ESM=Calcular plan - Hoja planif.;
               FRC=Calculer plan - F. planification;
               ENC=Calculate Plan - Plan. Wksh.];
    ProcessingOnly=Yes;
  }
  DATASET
  {
    { 8129;    ;DataItem;                    ;
               DataItemTable=Table27;
               DataItemTableView=SORTING(Low-Level Code)
                                 WHERE(Type=CONST(Inventory));
               OnPreDataItem=BEGIN
                               OpenWindow;
                               CLEAR(CalcItemPlan);
                               CalcItemPlan.SetTemplAndWorksheet(CurrTemplateName,CurrWorksheetName,NetChange);
                               CalcItemPlan.SetParm(UseForecast,ExcludeForecastBefore,Item);
                               CalcItemPlan.Initialize(FromDate,ToDate,MPS,MRP,RespectPlanningParm);

                               SetAtStartPosition := TRUE;

                               ReqLine.SETRANGE("Worksheet Template Name",CurrTemplateName);
                               ReqLine.SETRANGE("Journal Batch Name",CurrWorksheetName);
                               PlanningErrorLog.SETRANGE("Worksheet Template Name",CurrTemplateName);
                               PlanningErrorLog.SETRANGE("Journal Batch Name",CurrWorksheetName);
                               IF PlanningErrorLog.FINDFIRST AND ReqLine.FINDFIRST THEN
                                 SetAtStartPosition := NOT CONFIRM(Text009);

                               PlanningErrorLog.DELETEALL;
                               CLEARLASTERROR;

                               COMMIT;
                             END;

               OnAfterGetRecord=VAR
                                  ErrorText@1000 : Text[1000];
                                BEGIN
                                  UpdateWindow;

                                  IF NOT SetAtStartPosition THEN BEGIN
                                    SetAtStartPosition := TRUE;
                                    GET(PlanningErrorLog."Item No.");
                                    FIND('=<>');
                                  END;

                                  IF NoPlanningResiliency THEN BEGIN
                                    CalcItemPlan.RUN(Item);
                                    CounterOK := CounterOK + 1;
                                  END ELSE BEGIN
                                    CalcItemPlan.ClearInvtProfileOffsetting;
                                    CalcItemPlan.SetResiliencyOn;
                                    IF CalcItemPlan.RUN(Item) THEN
                                      CounterOK := CounterOK + 1
                                    ELSE
                                      IF NOT CalcItemPlan.GetResiliencyError(PlanningErrorLog) THEN BEGIN
                                        ErrorText := COPYSTR(GETLASTERRORTEXT,1,MAXSTRLEN(ErrorText));
                                        IF ErrorText = '' THEN
                                          ErrorText := Text011
                                        ELSE
                                          CLEARLASTERROR;
                                        PlanningErrorLog.SetJnlBatch(CurrTemplateName,CurrWorksheetName,"No.");
                                        PlanningErrorLog.SetError(
                                          COPYSTR(STRSUBSTNO(ErrorText,TABLECAPTION,"No."),1,250),0,GETPOSITION);
                                      END;
                                  END;

                                  COMMIT;
                                END;

               OnPostDataItem=BEGIN
                                CalcItemPlan.Finalize;
                                CloseWindow;
                              END;

               ReqFilterFields=No.,Search Description,Location Filter }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
      OnOpenPage=BEGIN
                   MfgSetup.GET;
                   UseForecast := MfgSetup."Current Production Forecast";
                   IF MfgSetup."Combined MPS/MRP Calculation" THEN BEGIN
                     MPS := TRUE;
                     MRP := TRUE;
                   END ELSE
                     MRP := NOT MPS;

                   OnAfterOnOpenPage;
                 END;

    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[ENU=Options;
                             ESM=Opciones;
                             FRC=Options;
                             ENC=Options] }

      { 10  ;2   ;Group     ;
                  CaptionML=[ENU=Calculate;
                             ESM=Calcular;
                             FRC=Calculer;
                             ENC=Calculate] }

      { 11  ;3   ;Field     ;
                  Name=MPS;
                  CaptionML=[ENU=MPS;
                             ESM=MPS;
                             FRC=PDP;
                             ENC=MPS];
                  ToolTipML=[ENU="Specifies whether to calculate a master production schedule (MPS) based on independent demand. ";
                             ESM="Especifica si se debe calcular una previsi¢n de producci¢n maestra (MPS) basada en la demanda independiente. ";
                             FRC="Sp‚cifie s'il faut calculer ou non une planification de production principale (PDP) sur la base d'une demande ind‚pendante. ";
                             ENC="Specifies whether to calculate a master production schedule (MPS) based on independent demand. "];
                  ApplicationArea=#Planning;
                  SourceExpr=MPS;
                  OnValidate=BEGIN
                               IF NOT MfgSetup."Combined MPS/MRP Calculation" THEN
                                 MRP := NOT MPS
                               ELSE
                                 IF NOT MPS THEN
                                   MRP := TRUE;
                             END;
                              }

      { 12  ;3   ;Field     ;
                  Name=MRP;
                  CaptionML=[ENU=MRP;
                             ESM=MRP;
                             FRC=PBM;
                             ENC=MRP];
                  ToolTipML=[ENU=Specifies whether to calculate an MRP, which will calculate dependent demand that is based on the MPS.;
                             ESM=Especifica si se debe calcular un MRP, que calcular  la demanda dependiente que se basa en MPS.;
                             FRC=Sp‚cifie s'il faut calculer ou non une planification des besoins de matiŠres (MRP), qui calcule la demande d‚pendante bas‚e sur la PDP.;
                             ENC=Specifies whether to calculate an MRP, which will calculate dependent demand that is based on the MPS.];
                  ApplicationArea=#Planning;
                  SourceExpr=MRP;
                  OnValidate=BEGIN
                               IF NOT MfgSetup."Combined MPS/MRP Calculation" THEN
                                 MPS := NOT MRP
                               ELSE
                                 IF NOT MRP THEN
                                   MPS := TRUE;
                             END;
                              }

      { 1   ;2   ;Field     ;
                  Name=StartingDate;
                  CaptionML=[ENU=Starting Date;
                             ESM=Fecha inicial;
                             FRC=Date d‚but;
                             ENC=Starting Date];
                  ToolTipML=[ENU=Specifies the date to use for new orders. This date is used to evaluate the inventory.;
                             ESM=Especifica la fecha para los pedidos nuevos. Esta fecha se utiliza para evaluar el inventario.;
                             FRC=Sp‚cifie la date … utiliser pour les nouvelles commandes. Cette date est utilis‚e pour ‚valuer l'inventaire.;
                             ENC=Specifies the date to use for new orders. This date is used to evaluate the inventory.];
                  ApplicationArea=#Planning;
                  SourceExpr=FromDate }

      { 3   ;2   ;Field     ;
                  Name=EndingDate;
                  CaptionML=[ENU=Ending Date;
                             ESM=Fecha final;
                             FRC=Date fin;
                             ENC=Ending Date];
                  ToolTipML=[ENU=Specifies the date where the planning period ends. Demand is not included beyond this date.;
                             ESM=Especifica la fecha en la que finaliza el periodo de planificaci¢n. No se incluye m s demanda a partir de esta fecha.;
                             FRC=Sp‚cifie la date de fin de la p‚riode de planification. La demande n'est pas incluse au-del… de cette date.;
                             ENC=Specifies the date where the planning period ends. Demand is not included beyond this date.];
                  ApplicationArea=#Planning;
                  SourceExpr=ToDate }

      { 16  ;2   ;Field     ;
                  CaptionML=[ENU=Stop and Show First Error;
                             ESM=Parar y mostrar primer error;
                             FRC=Arrˆter et montrer la premiŠre erreur;
                             ENC=Stop and Show First Error];
                  ToolTipML=[ENU=Specifies whether to stop the planning run when it encounters an error. If the planning run stops, then a message is displayed with information about the first error.;
                             ESM=Especifica si se debe detener la ejecuci¢n de la planificaci¢n cuando se produce un error. Si se detiene la ejecuci¢n de planificaci¢n, se mostrar  un mensaje con informaci¢n sobre el primer error.;
                             FRC=Sp‚cifie s'il faut arrˆter ou non l'ex‚cution de la planification lorsqu'elle d‚tecte une erreur. Si l'ex‚cution de la planification s'arrˆte, un message s'affiche avec des informations sur la premiŠre erreur.;
                             ENC=Specifies whether to stop the planning run when it encounters an error. If the planning run stops, then a message is displayed with information about the first error.];
                  ApplicationArea=#Planning;
                  SourceExpr=NoPlanningResiliency }

      { 5   ;2   ;Field     ;
                  CaptionML=[ENU=Use Forecast;
                             ESM=Previsi¢n de uso;
                             FRC=Utiliser la pr‚vision;
                             ENC=Use Forecast];
                  ToolTipML=[ENU=Specifies a forecast that should be included as demand when running the planning batch job.;
                             ESM=Especifica una previsi¢n que se debe incluir como demanda cuando se ejecute el trabajo por lotes de planificaci¢n.;
                             FRC=Sp‚cifie une pr‚vision … inclure en tant que demande lors de l'ex‚cution du traitement en lot de planification.;
                             ENC=Specifies a forecast that should be included as demand when running the planning batch job.];
                  ApplicationArea=#Planning;
                  SourceExpr=UseForecast;
                  TableRelation="Production Forecast Name".Name }

      { 7   ;2   ;Field     ;
                  CaptionML=[ENU=Exclude Forecast Before;
                             ESM=No incluir en la previsi¢n fechas anteriores al;
                             FRC=Exclure la pr‚vision ant‚rieure …;
                             ENC=Exclude Forecast Before];
                  ToolTipML=[ENU=Specifies how much of the selected forecast to include in the planning run, by entering a date before which forecast demand is not included.;
                             ESM=Especifica qu‚ parte de la previsi¢n seleccionada desea incluir en la planificaci¢n introduciendo una fecha anterior a la cual no se incluir  demanda de previsi¢n alguna.;
                             FRC=Sp‚cifie la date qui d‚terminera le d‚but de la p‚riode des pr‚visions s‚lectionn‚es prise en compte dans la planification.;
                             ENC=Specifies how much of the selected forecast to include in the planning run, by entering a date before which forecast demand is not included.];
                  ApplicationArea=#Planning;
                  SourceExpr=ExcludeForecastBefore }

      { 2   ;2   ;Field     ;
                  CaptionML=[ENU=Respect Planning Parameters for Exception Warnings;
                             ESM=Respetar par metros de planificaci¢n para las advertencias de excepci¢n;
                             FRC=Respecter les paramŠtres de planification pour les avertissements d'exception;
                             ENC=Respect Planning Parameters for Exception Warnings];
                  ToolTipML=[ENU=Specifies whether planning lines with Exception warnings will respect the planning parameters on the item or SKU card.;
                             ESM=Especifica si las l¡neas de planificaci¢n con advertencias de excepci¢n respetar n los par metros de planificaci¢n de la ficha de producto o de UA.;
                             FRC=Sp‚cifie si les lignes planification avec des avertissements d'exception respectent ou non les paramŠtres de planification sur la fiche article ou unit‚ de stock.;
                             ENC=Specifies whether planning lines with Exception warnings will respect the planning parameters on the item or SKU card.];
                  ApplicationArea=#Planning;
                  SourceExpr=RespectPlanningParm }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      MfgSetup@1004 : Record 99000765;
      PlanningErrorLog@1018 : Record 5430;
      ReqLine@1021 : Record 246;
      CalcItemPlan@1005 : Codeunit 5431;
      Window@1006 : Dialog;
      Counter@1023 : Integer;
      CounterOK@1017 : Integer;
      NoOfRecords@1012 : Integer;
      NetChange@1009 : Boolean;
      MPS@1003 : Boolean;
      MRP@1002 : Boolean;
      NoPlanningResiliency@1022 : Boolean;
      SetAtStartPosition@1001 : Boolean;
      FromDate@1011 : Date;
      ToDate@1010 : Date;
      ExcludeForecastBefore@1015 : Date;
      Text005@1014 : TextConst 'ENU=Calculating the plan...\\;ESM=Calculando el plan...\\;FRC=Calculer le plan...\\;ENC=Calculating the plan...\\';
      Text006@1025 : TextConst 'ENU=Progress;ESM=Progreso;FRC=Progression;ENC=Progress';
      Text007@1024 : TextConst 'ENU=Not all items were planned. A total of %1 items were not planned.;ESM=No se planificaron todos los productos. Faltan %1 productos.;FRC=Les articles n''ont pas tous ‚t‚ planifi‚s. Un total de %1 articles n''ont pas ‚t‚ planifi‚s.;ENC=Not all items were planned. A total of %1 items were not planned.';
      Text008@1020 : TextConst 'ENU=There is nothing to plan.;ESM=No hay nada que planificar.;FRC=Il n''y a rien … planifier.;ENC=There is nothing to plan.';
      CurrTemplateName@1008 : Code[10];
      CurrWorksheetName@1007 : Code[10];
      UseForecast@1013 : Code[10];
      Text009@1016 : TextConst 'ENU=The last time this batch was run, errors were encountered.\Do you want the batch to continue from where it left off?;ESM=La £ltima vez que se ejecut¢ este proceso se detectaron errores.\¨Desea continuar el proceso desde donde se interrumpi¢?;FRC=Des erreurs se sont produites lors de la derniŠre ex‚cution de ce traitement en lot.\Voulez-vous que celui-ci continue … partir du moment o— il s''est interrompuÿ?;ENC=The last time this batch was run, errors were encountered.\Do you want the batch to continue from where it left off?';
      Text011@1000 : TextConst 'ENU=An unidentified error occurred while planning %1 %2. Recalculate the plan with the option "Stop and Show Error".;ESM=Error no identificado al planificar %1 %2. Recalcule el plan con la opci¢n "Parar y mostrar error".;FRC=Une erreur non identifi‚e s''est produite pendant la planification %1 %2. Recalculer la planification avec l''option "Arrˆter et montrer erreur".;ENC=An unidentified error occurred while planning %1 %2. Recalculate the plan with the option "Stop and Show Error".';
      RespectPlanningParm@1026 : Boolean;

    [External]
    PROCEDURE SetTemplAndWorksheet@2(TemplateName@1000 : Code[10];WorksheetName@1001 : Code[10];Regenerative@1002 : Boolean);
    BEGIN
      CurrTemplateName := TemplateName;
      CurrWorksheetName := WorksheetName;
      NetChange := NOT Regenerative;
    END;

    [External]
    PROCEDURE InitializeRequest@3(NewFromDate@1001 : Date;NewToDate@1000 : Date;NewRespectPlanningParm@1002 : Boolean);
    BEGIN
      FromDate := NewFromDate;
      ToDate := NewToDate;
      RespectPlanningParm := NewRespectPlanningParm;

      MfgSetup.GET;
      IF MfgSetup."Combined MPS/MRP Calculation" THEN BEGIN
        MPS := TRUE;
        MRP := TRUE;
      END ELSE
        MRP := NOT MPS;
      UseForecast := MfgSetup."Current Production Forecast";
    END;

    [External]
    PROCEDURE OpenWindow@5();
    VAR
      Indentation@1000 : Integer;
    BEGIN
      Counter := 0;
      CounterOK := 0;
      NoOfRecords := Item.COUNT;
      Indentation := STRLEN(Text006);
      IF STRLEN(Item.FIELDCAPTION("Low-Level Code")) > Indentation THEN
        Indentation := STRLEN(Item.FIELDCAPTION("Low-Level Code"));
      IF STRLEN(Item.FIELDCAPTION("No.")) > Indentation THEN
        Indentation := STRLEN(Item.FIELDCAPTION("No."));

      Window.OPEN(
        Text005 +
        PADSTR(Text006,Indentation) + ' @1@@@@@@@@@@@@@\' +
        PADSTR(Item.FIELDCAPTION("Low-Level Code"),Indentation) + ' #2######\' +
        PADSTR(Item.FIELDCAPTION("No."),Indentation) + ' #3##########');
    END;

    [External]
    PROCEDURE UpdateWindow@6();
    BEGIN
      Counter := Counter + 1;
      Window.UPDATE(1,ROUND(Counter / NoOfRecords * 10000,1));
      Window.UPDATE(2,Item."Low-Level Code");
      Window.UPDATE(3,Item."No.");
    END;

    [External]
    PROCEDURE CloseWindow@1();
    BEGIN
      Window.CLOSE;

      IF Counter = 0 THEN
        MESSAGE(Text008);
      IF Counter > CounterOK THEN BEGIN
        MESSAGE(Text007,Counter - CounterOK);
        IF PlanningErrorLog.FINDFIRST THEN
          PAGE.RUNMODAL(0,PlanningErrorLog);
      END;
    END;

    [Integration(DEFAULT,TRUE)]
    LOCAL PROCEDURE OnAfterOnOpenPage@4();
    BEGIN
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

