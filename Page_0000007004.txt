OBJECT Page 7004 Sales Line Discounts
{
  OBJECT-PROPERTIES
  {
    Date=11/27/18;
    Time=11:11:36 PM;
    Version List=;
  }
  PROPERTIES
  {
    CaptionML=ENU=Sales Line Discounts;
    SaveValues=Yes;
    SourceTable=Table7004;
    DataCaptionExpr=GetCaption;
    DelayedInsert=Yes;
    PageType=List;
    ShowFilter=No;
    OnInit=BEGIN
             CodeFilterCtrlEnable := TRUE;
             SalesCodeFilterCtrlEnable := TRUE;
             SalesCodeEditable := TRUE;
           END;

    OnOpenPage=BEGIN
                 IsOnMobile := ClientTypeManagement.GetCurrentClientType = CLIENTTYPE::Phone;
                 GetRecFilters;
                 SetRecFilters;
               END;

    OnAfterGetCurrRecord=BEGIN
                           SetEditableFields;
                         END;

    ActionList=ACTIONS
    {
      { 11      ;    ;ActionContainer;
                      Name=Filtering;
                      ActionContainerType=ActionItems }
      { 7       ;1   ;Action    ;
                      Name=Filter;
                      CaptionML=ENU=Filter;
                      ToolTipML=ENU=Apply the filter.;
                      ApplicationArea=#Basic,#Suite;
                      Promoted=Yes;
                      Visible=IsOnMobile;
                      Image=Filter;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 FilterLines;
                               END;
                                }
      { 9       ;1   ;Action    ;
                      Name=ClearFilter;
                      CaptionML=ENU=Clear Filter;
                      ToolTipML=ENU=Clear filter.;
                      ApplicationArea=#Basic,#Suite;
                      Promoted=Yes;
                      Visible=IsOnMobile;
                      Image=ClearFilter;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 RESET;
                                 UpdateBasicRecFilters;
                                 SetEditableFields;
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 20  ;1   ;Group     ;
                CaptionML=ENU=General;
                Visible=NOT IsOnMobile }

    { 28  ;2   ;Field     ;
                CaptionML=ENU=Sales Type Filter;
                ToolTipML=ENU=Specifies a filter for which sales line discounts to display.;
                OptionCaptionML=ENU=Customer,Customer Discount Group,All Customers,Campaign,None;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=SalesTypeFilter;
                OnValidate=BEGIN
                             SalesTypeFilterOnAfterValidate;
                           END;
                            }

    { 26  ;2   ;Field     ;
                Name=SalesCodeFilterCtrl;
                CaptionML=ENU=Sales Code Filter;
                ToolTipML=ENU=Specifies a filter for which sales line discounts to display.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=SalesCodeFilter;
                Enabled=SalesCodeFilterCtrlEnable;
                OnValidate=BEGIN
                             SalesCodeFilterOnAfterValidate;
                           END;

                OnLookup=VAR
                           CustList@1005 : Page 22;
                           CustDiscGrList@1004 : Page 512;
                           CampaignList@1003 : Page 5087;
                         BEGIN
                           IF SalesTypeFilter = SalesTypeFilter::"All Customers" THEN
                             EXIT;

                           CASE SalesTypeFilter OF
                             SalesTypeFilter::Customer:
                               BEGIN
                                 CustList.LOOKUPMODE := TRUE;
                                 IF CustList.RUNMODAL = ACTION::LookupOK THEN
                                   Text := CustList.GetSelectionFilter
                                 ELSE
                                   EXIT(FALSE);
                               END;
                             SalesTypeFilter::"Customer Discount Group":
                               BEGIN
                                 CustDiscGrList.LOOKUPMODE := TRUE;
                                 IF CustDiscGrList.RUNMODAL = ACTION::LookupOK THEN
                                   Text := CustDiscGrList.GetSelectionFilter
                                 ELSE
                                   EXIT(FALSE);
                               END;
                             SalesTypeFilter::Campaign:
                               BEGIN
                                 CampaignList.LOOKUPMODE := TRUE;
                                 IF CampaignList.RUNMODAL = ACTION::LookupOK THEN
                                   Text := CampaignList.GetSelectionFilter
                                 ELSE
                                   EXIT(FALSE);
                               END;
                           END;

                           EXIT(TRUE);
                         END;
                          }

    { 34  ;2   ;Field     ;
                CaptionML=ENU=Starting Date Filter;
                ToolTipML=ENU=Specifies a filter for which sales line discounts to display.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=StartingDateFilter;
                OnValidate=VAR
                             TextManagement@1000 : Codeunit 41;
                           BEGIN
                             TextManagement.MakeDateFilter(StartingDateFilter);
                             StartingDateFilterOnAfterValid;
                           END;
                            }

    { 10  ;2   ;Field     ;
                CaptionML=ENU=Type Filter;
                ToolTipML=ENU=Specifies a filter for which sales line discounts to display.;
                OptionCaptionML=ENU=Item,Item Discount Group,None;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=ItemTypeFilter;
                OnValidate=BEGIN
                             ItemTypeFilterOnAfterValidate;
                           END;
                            }

    { 40  ;2   ;Field     ;
                Name=CodeFilterCtrl;
                CaptionML=ENU=Code Filter;
                ToolTipML=ENU=Specifies a filter for which sales line discounts to display.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=CodeFilter;
                Enabled=CodeFilterCtrlEnable;
                OnValidate=BEGIN
                             CodeFilterOnAfterValidate;
                           END;

                OnLookup=VAR
                           ItemList@1002 : Page 31;
                           ItemDiscGrList@1003 : Page 513;
                         BEGIN
                           CASE Type OF
                             Type::Item:
                               BEGIN
                                 ItemList.LOOKUPMODE := TRUE;
                                 IF ItemList.RUNMODAL = ACTION::LookupOK THEN
                                   Text := ItemList.GetSelectionFilter
                                 ELSE
                                   EXIT(FALSE);
                               END;
                             Type::"Item Disc. Group":
                               BEGIN
                                 ItemDiscGrList.LOOKUPMODE := TRUE;
                                 IF ItemDiscGrList.RUNMODAL = ACTION::LookupOK THEN
                                   Text := ItemDiscGrList.GetSelectionFilter
                                 ELSE
                                   EXIT(FALSE);
                               END;
                           END;

                           EXIT(TRUE);
                         END;
                          }

    { 16  ;2   ;Field     ;
                Name=SalesCodeFilterCtrl2;
                CaptionML=ENU=Currency Code Filter;
                ToolTipML=ENU=Specifies a filter for which sales line discounts to display.;
                ApplicationArea=#Suite;
                SourceExpr=CurrencyCodeFilter;
                OnValidate=BEGIN
                             CurrencyCodeFilterOnAfterValid;
                           END;

                OnLookup=VAR
                           CurrencyList@1000 : Page 5;
                         BEGIN
                           CurrencyList.LOOKUPMODE := TRUE;
                           IF CurrencyList.RUNMODAL = ACTION::LookupOK THEN
                             Text := CurrencyList.GetSelectionFilter
                           ELSE
                             EXIT(FALSE);

                           EXIT(TRUE);
                         END;
                          }

    { 3   ;1   ;Group     ;
                CaptionML=ENU=Filters;
                Visible=IsOnMobile;
                GroupType=Group }

    { 5   ;2   ;Field     ;
                ToolTipML=ENU=Specifies a filter for which sales line discounts to display.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=GetFilterDescription;
                Editable=FALSE;
                OnAssistEdit=BEGIN
                               FilterLines;
                               CurrPage.UPDATE(FALSE);
                             END;
                              }

    { 1   ;1   ;Group     ;
                GroupType=Repeater }

    { 32  ;2   ;Field     ;
                Name=SalesType;
                ToolTipML=ENU=Specifies the sales type of the sales line discount. The sales type defines whether the sales price is for an individual customer, customer discount group, all customers, or for a campaign.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Sales Type";
                OnValidate=BEGIN
                             SetEditableFields;
                           END;
                            }

    { 18  ;2   ;Field     ;
                Name=SalesCode;
                ToolTipML=ENU=Specifies one of the following values, depending on the value in the Sales Type field.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Sales Code";
                Editable=SalesCodeEditable }

    { 42  ;2   ;Field     ;
                ToolTipML=ENU=Specifies the type of item that the sales discount line is valid for. That is, either an item or an item discount group.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=Type }

    { 4   ;2   ;Field     ;
                ToolTipML=ENU=Specifies one of two values, depending on the value in the Type field.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=Code }

    { 30  ;2   ;Field     ;
                ToolTipML=ENU=Specifies the variant of the item on the line.;
                ApplicationArea=#Planning;
                SourceExpr="Variant Code";
                Visible=FALSE }

    { 14  ;2   ;Field     ;
                ToolTipML=ENU=Specifies the currency code of the sales line discount price.;
                ApplicationArea=#Suite;
                SourceExpr="Currency Code";
                Visible=FALSE }

    { 8   ;2   ;Field     ;
                ToolTipML=ENU=Specifies how each unit of the item or resource is measured, such as in pieces or hours. By default, the value in the Base Unit of Measure field on the item or resource card is inserted.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Unit of Measure Code" }

    { 12  ;2   ;Field     ;
                ToolTipML=ENU=Specifies the minimum quantity that the customer must purchase in order to gain the agreed discount.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Minimum Quantity" }

    { 6   ;2   ;Field     ;
                ToolTipML=ENU=Specifies the discount percentage that is granted for the item on the line.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Line Discount %" }

    { 36  ;2   ;Field     ;
                ToolTipML=ENU=Specifies the date from which the sales line discount is valid.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Starting Date" }

    { 2   ;2   ;Field     ;
                ToolTipML=ENU=Specifies the date to which the sales line discount is valid.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Ending Date" }

    { 1900000007;0;Container;
                ContainerType=FactBoxArea }

    { 1900383207;1;Part   ;
                Visible=FALSE;
                PartType=System;
                SystemPartID=RecordLinks }

    { 1905767507;1;Part   ;
                Visible=FALSE;
                PartType=System;
                SystemPartID=Notes }

  }
  CODE
  {
    VAR
      Cust@1001 : Record 18;
      CustDiscGr@1005 : Record 340;
      Campaign@1008 : Record 5071;
      Item@1004 : Record 27;
      ItemDiscGr@1013 : Record 341;
      ClientTypeManagement@1077 : Codeunit 4;
      SalesTypeFilter@1000 : 'Customer,Customer Discount Group,All Customers,Campaign,None';
      SalesCodeFilter@1003 : Text;
      ItemTypeFilter@1012 : 'Item,Item Discount Group,None';
      CodeFilter@1011 : Text;
      StartingDateFilter@1006 : Text[30];
      Text000@1007 : TextConst 'ENU=All Customers';
      CurrencyCodeFilter@1002 : Text;
      SalesCodeEditable@19031640 : Boolean INDATASET;
      SalesCodeFilterCtrlEnable@19013164 : Boolean INDATASET;
      CodeFilterCtrlEnable@19026169 : Boolean INDATASET;
      IsOnMobile@1009 : Boolean;

    LOCAL PROCEDURE GetRecFilters@2();
    BEGIN
      IF GETFILTERS <> '' THEN
        UpdateBasicRecFilters;
    END;

    [External]
    PROCEDURE SetRecFilters@1();
    BEGIN
      SalesCodeFilterCtrlEnable := TRUE;
      CodeFilterCtrlEnable := TRUE;

      IF SalesTypeFilter <> SalesTypeFilter::None THEN
        SETRANGE("Sales Type",SalesTypeFilter)
      ELSE
        SETRANGE("Sales Type");

      IF SalesTypeFilter IN [SalesTypeFilter::"All Customers",SalesTypeFilter::None] THEN BEGIN
        SalesCodeFilterCtrlEnable := FALSE;
        SalesCodeFilter := '';
      END;

      IF SalesCodeFilter <> '' THEN
        SETFILTER("Sales Code",SalesCodeFilter)
      ELSE
        SETRANGE("Sales Code");

      IF ItemTypeFilter <> ItemTypeFilter::None THEN
        SETRANGE(Type,ItemTypeFilter)
      ELSE
        SETRANGE(Type);

      IF ItemTypeFilter = ItemTypeFilter::None THEN BEGIN
        CodeFilterCtrlEnable := FALSE;
        CodeFilter := '';
      END;

      IF CodeFilter <> '' THEN BEGIN
        SETFILTER(Code,CodeFilter);
      END ELSE
        SETRANGE(Code);

      IF CurrencyCodeFilter <> '' THEN BEGIN
        SETFILTER("Currency Code",CurrencyCodeFilter);
      END ELSE
        SETRANGE("Currency Code");

      IF StartingDateFilter <> '' THEN
        SETFILTER("Starting Date",StartingDateFilter)
      ELSE
        SETRANGE("Starting Date");

      CurrPage.UPDATE(FALSE);
    END;

    LOCAL PROCEDURE GetCaption@3() : Text[250];
    BEGIN
      IF IsOnMobile THEN
        EXIT('');

      EXIT(GetFilterDescription);
    END;

    LOCAL PROCEDURE GetFilterDescription@4() : Text;
    VAR
      ObjTranslation@1000 : Record 377;
      SourceTableName@1002 : Text;
      SalesSrcTableName@1003 : Text;
      Description@1001 : Text;
    BEGIN
      GetRecFilters;

      SourceTableName := '';
      CASE ItemTypeFilter OF
        ItemTypeFilter::Item:
          BEGIN
            SourceTableName := ObjTranslation.TranslateObject(ObjTranslation."Object Type"::Table,27);
            Item.SETFILTER("No.",CodeFilter);
            IF NOT Item.FINDFIRST THEN
              CLEAR(Item);
          END;
        ItemTypeFilter::"Item Discount Group":
          BEGIN
            SourceTableName := ObjTranslation.TranslateObject(ObjTranslation."Object Type"::Table,341);
            ItemDiscGr.SETFILTER(Code,CodeFilter);
            IF NOT ItemDiscGr.FINDFIRST THEN
              CLEAR(ItemDiscGr);
          END;
      END;

      SalesSrcTableName := '';
      CASE SalesTypeFilter OF
        SalesTypeFilter::Customer:
          BEGIN
            SalesSrcTableName := ObjTranslation.TranslateObject(ObjTranslation."Object Type"::Table,18);
            Cust.SETFILTER("No.",SalesCodeFilter);
            IF Cust.FINDFIRST THEN
              Description := Cust.Name;
          END;
        SalesTypeFilter::"Customer Discount Group":
          BEGIN
            SalesSrcTableName := ObjTranslation.TranslateObject(ObjTranslation."Object Type"::Table,340);
            CustDiscGr.SETFILTER(Code,SalesCodeFilter);
            IF CustDiscGr.FINDFIRST THEN
              Description := CustDiscGr.Description;
          END;
        SalesTypeFilter::Campaign:
          BEGIN
            SalesSrcTableName := ObjTranslation.TranslateObject(ObjTranslation."Object Type"::Table,5071);
            Campaign.SETFILTER("No.",SalesCodeFilter);
            IF Campaign.FINDFIRST THEN
              Description := Campaign.Description;
          END;
        SalesTypeFilter::"All Customers":
          BEGIN
            SalesSrcTableName := Text000;
            Description := '';
          END;
      END;

      IF SalesSrcTableName = Text000 THEN
        EXIT(STRSUBSTNO('%1 %2 %3 %4 %5',SalesSrcTableName,SalesCodeFilter,Description,SourceTableName,CodeFilter));
      EXIT(STRSUBSTNO('%1 %2 %3 %4 %5',SalesSrcTableName,SalesCodeFilter,Description,SourceTableName,CodeFilter));
    END;

    LOCAL PROCEDURE SalesCodeFilterOnAfterValidate@19067727();
    BEGIN
      CurrPage.SAVERECORD;
      SetRecFilters;
    END;

    LOCAL PROCEDURE SalesTypeFilterOnAfterValidate@19024044();
    BEGIN
      CurrPage.SAVERECORD;
      SalesCodeFilter := '';
      SetRecFilters;
    END;

    LOCAL PROCEDURE StartingDateFilterOnAfterValid@19055286();
    BEGIN
      CurrPage.SAVERECORD;
      SetRecFilters;
    END;

    LOCAL PROCEDURE ItemTypeFilterOnAfterValidate@19002579();
    BEGIN
      CurrPage.SAVERECORD;
      CodeFilter := '';
      SetRecFilters;
    END;

    LOCAL PROCEDURE CodeFilterOnAfterValidate@19025728();
    BEGIN
      CurrPage.SAVERECORD;
      SetRecFilters;
    END;

    LOCAL PROCEDURE CurrencyCodeFilterOnAfterValid@19015290();
    BEGIN
      CurrPage.SAVERECORD;
      SetRecFilters;
    END;

    LOCAL PROCEDURE GetSalesTypeFilter@1000() : Integer;
    BEGIN
      CASE GETFILTER("Sales Type") OF
        FORMAT("Sales Type"::Customer):
          EXIT(0);
        FORMAT("Sales Type"::"Customer Disc. Group"):
          EXIT(1);
        FORMAT("Sales Type"::"All Customers"):
          EXIT(2);
        FORMAT("Sales Type"::Campaign):
          EXIT(3);
      END;
    END;

    LOCAL PROCEDURE GetTypeFilter@1001() : Integer;
    BEGIN
      CASE GETFILTER(Type) OF
        FORMAT(Type::Item):
          EXIT(0);
        FORMAT(Type::"Item Disc. Group"):
          EXIT(1);
      END;
    END;

    LOCAL PROCEDURE FilterLines@8();
    VAR
      FilterPageBuilder@1000 : FilterPageBuilder;
    BEGIN
      FilterPageBuilder.ADDTABLE(TABLECAPTION,DATABASE::"Sales Line Discount");
      FilterPageBuilder.SETVIEW(TABLECAPTION,GETVIEW);

      IF GETFILTER("Sales Type") = '' THEN
        FilterPageBuilder.ADDFIELDNO(TABLECAPTION,FIELDNO("Sales Type"));
      IF GETFILTER("Sales Code") = '' THEN
        FilterPageBuilder.ADDFIELDNO(TABLECAPTION,FIELDNO("Sales Code"));
      IF GETFILTER(Type) = '' THEN
        FilterPageBuilder.ADDFIELDNO(TABLECAPTION,FIELDNO(Type));
      IF GETFILTER(Code) = '' THEN
        FilterPageBuilder.ADDFIELDNO(TABLECAPTION,FIELDNO(Code));
      IF GETFILTER("Starting Date") = '' THEN
        FilterPageBuilder.ADDFIELDNO(TABLECAPTION,FIELDNO("Starting Date"));
      IF GETFILTER("Currency Code") = '' THEN
        FilterPageBuilder.ADDFIELDNO(TABLECAPTION,FIELDNO("Currency Code"));

      IF FilterPageBuilder.RUNMODAL THEN
        SETVIEW(FilterPageBuilder.GETVIEW(TABLECAPTION));

      UpdateBasicRecFilters;
      SetEditableFields;
    END;

    LOCAL PROCEDURE UpdateBasicRecFilters@14();
    BEGIN
      IF GETFILTER("Sales Type") <> '' THEN
        SalesTypeFilter := GetSalesTypeFilter
      ELSE
        SalesTypeFilter := SalesTypeFilter::None;

      IF GETFILTER(Type) <> '' THEN
        ItemTypeFilter := GetTypeFilter
      ELSE
        ItemTypeFilter := ItemTypeFilter::None;

      SalesCodeFilter := GETFILTER("Sales Code");
      CodeFilter := GETFILTER(Code);
      CurrencyCodeFilter := GETFILTER("Currency Code");
      EVALUATE(StartingDateFilter,GETFILTER("Starting Date"));
    END;

    LOCAL PROCEDURE SetEditableFields@18();
    BEGIN
      SalesCodeEditable := "Sales Type" <> "Sales Type"::"All Customers";
    END;

    BEGIN
    END.
  }
}

