OBJECT Page 6192773 Continia Product Activation
{
  OBJECT-PROPERTIES
  {
    Date=03/19/18;
    Time=[ 1:00:00 AM];
    Version List=DCW17.10.00.5.00;
  }
  PROPERTIES
  {
    Editable=No;
    CaptionML=ENU=Continia Product Activation;
    LinksAllowed=No;
    SourceTable=Table6192775;
    DataCaptionExpr=ContiniaProductName;
    PageType=StandardDialog;
    SourceTableTemporary=Yes;
    OnOpenPage=VAR
                 CurrEntryNo@6085573 : Integer;
               BEGIN
                 ContiniaCompanySetup.GET;
                 ContiniaOnlineLicenseMgt.GetCompanyActivations(Rec,FALSE);
                 IF FINDLAST THEN BEGIN
                   CurrEntryNo := "Entry No." + 1;
                   INIT;
                   "Entry No." := CurrEntryNo;
                   "Product Code" := ContiniaProductCode;
                   INSERT;

                   CurrEntryNo := CurrEntryNo + 1;
                 END;

                 INIT;
                 "Entry No." := CurrEntryNo;
                 "Company GUID" := NullGUID;
                 "Company Name" := NewActivationTxt;
                 "Product Code" := ContiniaProductCode;
                 INSERT;

                 SETRANGE("Company GUID",ContiniaCompanySetup."Company GUID");
                 IF NOT FINDFIRST THEN BEGIN
                   SETRANGE("Company GUID",NullGUID);
                   FINDFIRST;
                 END;

                 SETRANGE("Company GUID");
               END;

    OnAfterGetRecord=BEGIN
                       IF "Company Name" = NewActivationTxt THEN
                         HighlightRow := TRUE
                       ELSE
                         HighlightRow := FALSE;
                     END;

    OnQueryClosePage=BEGIN
                       IF CloseAction = ACTION::LookupOK THEN BEGIN
                         IF ("Company GUID" = NullGUID) AND ("Company Name" = '') THEN
                           EXIT(FALSE);

                         IF ("Company GUID" <> NullGUID) AND ("Company GUID" <> ContiniaCompanySetup."Company GUID") THEN
                           IF NOT CONFIRM(UpdateWarningQst) THEN
                             EXIT(FALSE);
                       END;

                       EXIT(TRUE);
                     END;

  }
  CONTROLS
  {
    { 1160040020;;Container;
                Name=ContentArea;
                ContainerType=ContentArea }

    { 1160040000;1;Field  ;
                Name=Control1160040000;
                CaptionClass=ContiniaProductName;
                Style=Strong;
                StyleExpr=TRUE }

    { 1160040001;1;Field  ;
                Name=Control1160040001;
                CaptionClass=InstructionsTxt1 }

    { 6085579;1;Field     ;
                Name=Control6085579;
                CaptionClass=InstructionsTxt2 }

    { 6085582;1;Field     ;
                Name=Control6085582;
                CaptionML=ENU="" }

    { 6085573;1;Group     ;
                Name=Control6085573;
                CaptionML=ENU=Lines;
                GroupType=Repeater }

    { 6085574;2;Field     ;
                Name=Company Name;
                SourceExpr="Company Name";
                Style=Strong;
                StyleExpr=HighlightRow }

    { 6085575;2;Field     ;
                Name=Control6085575;
                SourceExpr=GetCompanyGUIDAsText("Company GUID");
                CaptionClass=FIELDCAPTION("Company GUID");
                Visible=FALSE;
                Style=Strong;
                StyleExpr=HighlightRow }

    { 6085577;2;Field     ;
                Name=Product Code;
                SourceExpr="Product Code";
                Visible=FALSE;
                Style=Strong;
                StyleExpr=HighlightRow }

  }
  CODE
  {
    VAR
      NewActivationTxt@6085574 : TextConst 'ENU=<< New activation >>';
      ContiniaCompanySetup@6085579 : Record 6192773;
      ContiniaOnlineLicenseMgt@6085584 : Codeunit 6192773;
      NullGUID@6085576 : GUID;
      ActivationLabel@6085575 : Text[128];
      ContiniaProductCode@6085586 : Code[10];
      ContiniaProductName@6085573 : Text[1024];
      HighlightRow@6085577 : Boolean INDATASET;
      InstructionsTxt1@6085578 : TextConst 'ENU=This company has already been activated in Continia Online.';
      InstructionsTxt2@6085580 : TextConst 'ENU=Please select the corresponding company activation or create a new activation if this is a new or a test company.';
      CloseOK@6085583 : Boolean;
      UpdateWarningQst@1160040000 : TextConst 'ENU=This will update the existing activation in Continia Online.\\It is very important that you make sure no other company in any NAV database with your client credentials are using this activation.\\Do you want to continue?';

    PROCEDURE InitCard@6085584(NewProductCode@6085573 : Code[10];NewProductName@1160040000 : Text[1024]);
    BEGIN
      ContiniaProductCode := NewProductCode;
      ContiniaProductName := NewProductName;
    END;

    PROCEDURE IsNewCompany@6085573() : Boolean;
    BEGIN
      EXIT("Company GUID" = NullGUID);
    END;

    PROCEDURE GetGuid@6085585() : GUID;
    BEGIN
      EXIT("Company GUID");
    END;

    LOCAL PROCEDURE GetCompanyGUIDAsText@6085579(CompanyGuid@6085573 : GUID) : Text[50];
    BEGIN
      IF CompanyGuid = NullGUID THEN
        EXIT('')
      ELSE
        EXIT(FORMAT(CompanyGuid));
    END;

    BEGIN
    END.
  }
}

