OBJECT Page 1920 Container Sandbox Environment
{
  OBJECT-PROPERTIES
  {
    Date=11/27/18;
    Time=11:10:44 PM;
    Version List=;
  }
  PROPERTIES
  {
    CaptionML=ENU=Container Sandbox Environment (Preview);
    ApplicationArea=#Basic,#Suite;
    InsertAllowed=No;
    DeleteAllowed=No;
    ModifyAllowed=No;
    LinksAllowed=No;
    PageType=NavigatePage;
    UsageCategory=Lists;
    ShowFilter=No;
    OnInit=BEGIN
             LoadTopBanners;
           END;

    OnOpenPage=VAR
                 PermissionManager@1000 : Codeunit 9002;
               BEGIN
                 IF NOT PermissionManager.SoftwareAsAService OR PermissionManager.IsSandboxConfiguration THEN
                   ERROR(EnvironmentErr);
               END;

    ActionList=ACTIONS
    {
      { 7       ;    ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 8       ;1   ;Action    ;
                      Name=AzureVM;
                      CaptionML=ENU=Host in Azure;
                      ToolTipML=ENU=Create a container sandbox environment hosted on an Azure VM;
                      ApplicationArea=#Basic, #Suite;
                      InFooterBar=Yes;
                      OnAction=BEGIN
                                 HYPERLINK(GetFunctionUrl('BCSandboxAzure'));
                               END;
                                }
      { 9       ;1   ;Action    ;
                      Name=HostLocally;
                      CaptionML=ENU=Host locally;
                      ToolTipML=ENU=Create a container sandbox environment hosted on your local machine;
                      ApplicationArea=#Basic, #Suite;
                      InFooterBar=Yes;
                      OnAction=BEGIN
                                 HYPERLINK(GetFunctionUrl('BCSandboxLocal'));
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1   ;    ;Container ;
                CaptionML=ENU="""""";
                ContainerType=ContentArea }

    { 2   ;1   ;Group     ;
                Visible=TopBannerVisible;
                Editable=False;
                GroupType=Group }

    { 3   ;2   ;Field     ;
                ApplicationArea=#Basic, #Suite;
                SourceExpr=MediaResourcesStandard."Media Reference";
                Editable=FALSE }

    { 4   ;1   ;Group     ;
                GroupType=Group;
                InstructionalTextML=ENU=If your development requirements go beyond what the sandbox environment offers, you can create a container-based environment with the full product and use that for development and testing. You can host the container either locally or on an Azure VM. }

    { 5   ;1   ;Group     ;
                GroupType=Group;
                InstructionalTextML=ENU=If you decide to use Azure VM you will be asked to log into the Azure Portal and fill out an Azure Resource Management Template, and then press Purchase. The Virtual Machine will be running a container with the same version of the product as your production environment and will run in your own subscription incurring costs as determined by the selected VM size. }

    { 6   ;1   ;Group     ;
                GroupType=Group;
                InstructionalTextML=ENU=If you decide to run locally we will provide a PowerShell script that must be run on a Windows machine with PowerShell and the appropriate container hosting software installed. The PowerShell script will install and use the NavContainerHelper to run a container with the same version of the product as your production environment. }

    { 10  ;1   ;Group     ;
                GroupType=Group;
                InstructionalTextML=ENU=This Sandbox environment feature is provided as a free preview solely for testing, development and evaluation. You will not use the Sandbox in a live operating environment. Microsoft may, in its sole discretion, change the Sandbox environment or subject it to a fee for a final, commercial version, if any, or may elect not to release one. }

  }
  CODE
  {
    VAR
      MediaRepositoryDone@1009 : Record 9400;
      MediaRepositoryStandard@1000 : Record 9400;
      MediaResourcesDone@1001 : Record 2000000182;
      MediaResourcesStandard@1003 : Record 2000000182;
      ClientTypeManagement@1004 : Codeunit 4;
      TenantManagement@1006 : Codeunit 417;
      EnvironmentErr@1008 : TextConst 'ENU=This feature is only available in the online production version of the product.';
      Uri@1002 : DotNet "'System, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Uri";
      TopBannerVisible@1007 : Boolean;

    LOCAL PROCEDURE LoadTopBanners@1();
    BEGIN
      IF MediaRepositoryStandard.GET('AssistedSetup-NoText-400px.png',FORMAT(ClientTypeManagement.GetCurrentClientType)) AND
         MediaRepositoryDone.GET('AssistedSetupDone-NoText-400px.png',FORMAT(ClientTypeManagement.GetCurrentClientType))
      THEN
        IF MediaResourcesStandard.GET(MediaRepositoryStandard."Media Resources Ref") AND
           MediaResourcesDone.GET(MediaRepositoryDone."Media Resources Ref")
        THEN
          TopBannerVisible := MediaResourcesDone."Media Reference".HASVALUE;
    END;

    LOCAL PROCEDURE GetFunctionUrl@3(FunctionName@1004 : Text) : Text;
    VAR
      EnvironmentMgt@1003 : Codeunit 9005;
      UrlParameters@1001 : Text;
      FunctionUrl@1000 : Text;
      EnvironmentSuffix@1002 : Text;
    BEGIN
      UrlParameters := '?platform=' + Uri.EscapeDataString(TenantManagement.GetPlatformVersion) +
        '&application=' + Uri.EscapeDataString(TenantManagement.GetApplicationVersion) +
        '&family=' + Uri.EscapeDataString(TenantManagement.GetApplicationFamily);

      IF EnvironmentMgt.IsPROD THEN
        EnvironmentSuffix := ''
      ELSE
        EnvironmentSuffix := '-Tie';

      FunctionUrl := 'https://aka.ms/' + FunctionName + EnvironmentSuffix + UrlParameters;
      EXIT(FunctionUrl)
    END;

    BEGIN
    END.
  }
}

