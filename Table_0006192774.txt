OBJECT Table 6192774 Continia Web Portal
{
  OBJECT-PROPERTIES
  {
    Date=05/15/17;
    Time=[ 1:00:00 AM];
    Version List=DCW13.70.00.4.50;
  }
  PROPERTIES
  {
    DataPerCompany=No;
    OnInsert=VAR
               COWebServices@6085573 : Codeunit 6086006;
             BEGIN
               COWebServices.CreateWebServices;
             END;

    CaptionML=ENU=Continia Web Portal;
    LookupPageID=Page6192774;
  }
  FIELDS
  {
    { 1   ;   ;Code                ;Code20        ;CaptionML=ENU=Code;
                                                   NotBlank=Yes }
    { 8   ;   ;Windows Web Service URL;Text250    ;CaptionML=ENU=Windows Web Service URL }
    { 9   ;   ;Database Web Service URL;Text250   ;CaptionML=ENU=Database Web Service URL }
    { 10  ;   ;Local Domain Name   ;Code50        ;CaptionML=ENU=Local Domain Name }
    { 12  ;   ;Tenant              ;Text80        ;CaptionML=ENU=Tenant }
    { 14  ;   ;Web Site Url        ;Text80        ;OnValidate=VAR
                                                                ContiniaWebPortal@6085573 : Record 6192774;
                                                              BEGIN
                                                                ContiniaWebPortal.SETRANGE("Web Site Url","Web Site Url");
                                                                ContiniaWebPortal.SETRANGE("Use Continia Online Web Portal",FALSE);
                                                                ContiniaWebPortal.SETFILTER(Code,'<>%1',Code);
                                                                IF ContiniaWebPortal.FINDFIRST THEN
                                                                  ERROR(InvalidWebSiteUrlErr,FIELDCAPTION("Web Site Url"),"Web Site Url",TABLECAPTION,FIELDCAPTION(Code),ContiniaWebPortal.Code);
                                                              END;

                                                   CaptionML=ENU=Web Site Url }
    { 15  ;   ;Default Language Name;Text20       ;OnLookup=VAR
                                                              TempDisplayTable@1160040003 : TEMPORARY Record 6086007;
                                                              XmlDoc@1160040002 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlDocument";
                                                              XMLNodeList@1160040005 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNodeList";
                                                              i@1160040000 : Integer;
                                                            BEGIN
                                                              COMgt.ExecuteRequestWithoutToken("Web Site Url" + '/serverdata/languages',HttpActions::GET,TRUE,XmlDoc);
                                                              XMLNodeList := XmlDoc.DocumentElement.SelectNodes('/languages/language');

                                                              IF XMLNodeList.Count = 0 THEN
                                                                ERROR(UnableToConnectDCErr);

                                                              FOR i := 0 TO XMLNodeList.Count - 1 DO BEGIN
                                                                TempDisplayTable."Display Sort Order" := i;
                                                                COMgt.GetAttribAsText(XMLNodeList,'name',i,TempDisplayTable.Name);
                                                                COMgt.GetAttribAsText(XMLNodeList,'displayName',i,TempDisplayTable.Description);
                                                                TempDisplayTable.INSERT;
                                                              END;

                                                              TempDisplayTable.SETCURRENTKEY("Display Sort Order");
                                                              IF NOT TempDisplayTable.GET("Default Language Name") THEN
                                                                TempDisplayTable.FINDFIRST;

                                                              IF PAGE.RUNMODAL(PAGE::"Web Languages",TempDisplayTable) = ACTION::LookupOK THEN
                                                                VALIDATE("Default Language Name",TempDisplayTable.Name);
                                                            END;

                                                   CaptionML=ENU=Default User Language Name }
    { 16  ;   ;Default Units Formatting;Text20    ;OnLookup=VAR
                                                              TempDisplayTable@161024015 : TEMPORARY Record 6086007;
                                                              XmlDoc@161024012 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlDocument";
                                                              XMLNodeList@1160040001 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNodeList";
                                                              i@161024013 : Integer;
                                                            BEGIN
                                                              COMgt.ExecuteRequestWithoutToken("Web Site Url" + '/serverdata/numberanddatecultures',HttpActions::GET,TRUE,XmlDoc);
                                                              XMLNodeList := XmlDoc.DocumentElement.SelectNodes('/cultures/culture');

                                                              IF XMLNodeList.Count = 0 THEN
                                                                ERROR(UnableToConnectDCErr);

                                                              FOR i := 0 TO XMLNodeList.Count - 1 DO BEGIN
                                                                TempDisplayTable."Display Sort Order" := i;
                                                                COMgt.GetAttribAsText(XMLNodeList,'name',i,TempDisplayTable.Name);
                                                                COMgt.GetAttribAsText(XMLNodeList,'displayName',i,TempDisplayTable.Description);
                                                                TempDisplayTable.INSERT;
                                                              END;

                                                              TempDisplayTable.SETCURRENTKEY("Display Sort Order");
                                                              IF NOT TempDisplayTable.GET("Default Units Formatting") THEN
                                                                TempDisplayTable.FINDFIRST;

                                                              IF PAGE.RUNMODAL(PAGE::"User Formatting List",TempDisplayTable) = ACTION::LookupOK THEN
                                                                VALIDATE("Default Units Formatting",TempDisplayTable.Name);
                                                            END;

                                                   CaptionML=ENU=Default Units Formatting }
    { 17  ;   ;Default Time Zone   ;Text50        ;OnLookup=VAR
                                                              TempDisplayTable@161024015 : TEMPORARY Record 6086007;
                                                              XmlDoc@161024014 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlDocument";
                                                              XMLNodeList@1160040001 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNodeList";
                                                              i@161024012 : Integer;
                                                            BEGIN
                                                              COMgt.ExecuteRequestWithoutToken("Web Site Url" + '/serverdata/timezones',HttpActions::GET,TRUE,XmlDoc);
                                                              XMLNodeList := XmlDoc.DocumentElement.SelectNodes('/timezones/timezone');

                                                              IF XMLNodeList.Count = 0 THEN
                                                                ERROR(UnableToConnectDCErr);

                                                              FOR i := 0 TO XMLNodeList.Count - 1 DO BEGIN
                                                                TempDisplayTable."Display Sort Order" := i;
                                                                COMgt.GetAttribAsText(XMLNodeList,'id',i,TempDisplayTable.Name);
                                                                COMgt.GetAttribAsText(XMLNodeList,'displayName',i,TempDisplayTable.Description);
                                                                TempDisplayTable.INSERT;
                                                              END;

                                                              TempDisplayTable.SETCURRENTKEY("Display Sort Order");
                                                              IF NOT TempDisplayTable.GET("Default Time Zone") THEN
                                                                TempDisplayTable.FINDFIRST;

                                                              IF PAGE.RUNMODAL(PAGE::"Time Zone List",TempDisplayTable) = ACTION::LookupOK THEN
                                                                VALIDATE("Default Time Zone",TempDisplayTable.Name);
                                                            END;

                                                   CaptionML=ENU=Default Time Zone }
    { 21  ;   ;Use Continia Online Web Portal;Boolean;
                                                   CaptionML=ENU=Use Continia Online Web Portal }
    { 24  ;   ;Welcome E-Mails     ;Option        ;CaptionML=ENU=Welcome E-Mails;
                                                   OptionCaptionML=ENU=Send automatically,Send manually;
                                                   OptionString=Send automatically,Send manually }
  }
  KEYS
  {
    {    ;Code                                    ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      UnableToConnectDCErr@1160040002 : TextConst 'ENU=Unable to connect to the Document Capture Web Site. Please make sure that you are running the latest version of the Document Capture Web Site.';
      HttpActions@1160040004 : 'PUT,DELETE,GET';
      COMgt@6085573 : Codeunit 6192772;
      InvalidWebSiteUrlErr@6085574 : TextConst 'ENU="The %1 %2 has already been assigned to %3, %4 = %5."';

    PROCEDURE InitValues@1160040000();
    VAR
      LocalizationMgt@1160040000 : Codeunit 6192776;
    BEGIN
      CASE LocalizationMgt.NAVLocalization OF
        'AT':
          SetLocalizationValues('de-DE','de-DE','W. Europe Standard Time');
        'AU':
          SetLocalizationValues('en-AU','en-AU','AUS Eastern Standard Time');
        'BE':
          SetLocalizationValues('nl-NL','nl-NL','W. Europe Standard Time');
        'CH':
          SetLocalizationValues('de-DE','de-CH','W. Europe Standard Time');
        'DE':
          SetLocalizationValues('de-DE','de-DE','W. Europe Standard Time');
        'DK':
          SetLocalizationValues('da-DK','da-DK','Romance Standard Time');
        'ES':
          SetLocalizationValues('es-ES','es-ES','Romance Standard Time');
        'FR':
          SetLocalizationValues('fr-FR','fr-FR','Romance Standard Time');
        'GB','W1':
          SetLocalizationValues('en-GB','en-GB','UTC');
        'NA','US':
          SetLocalizationValues('en-US','en-US','Eastern Standard Time');
        'NL':
          SetLocalizationValues('nl-NL','nl-NL','W. Europe Standard Time');
        'NO':
          SetLocalizationValues('nb-NO','nb-NO','W. Europe Standard Time');
        'NZ','AP':
          SetLocalizationValues('en-NZ','en-NZ','AUS Eastern Standard Time');
        'SE':
          SetLocalizationValues('sv-SE','sv-SE','W. Europe Standard Time');
      END;
    END;

    LOCAL PROCEDURE SetLocalizationValues@1160040001(DefLangName@1160040000 : Text[20];DefUnitFormat@1160040001 : Text[20];DefTimeZone@1160040002 : Text[50]);
    BEGIN
      "Default Language Name" := DefLangName;
      "Default Units Formatting" := DefUnitFormat;
      "Default Time Zone" := DefTimeZone;
    END;

    BEGIN
    END.
  }
}

