OBJECT Page 404 Check Preview
{
  OBJECT-PROPERTIES
  {
    Date=11/27/18;
    Time=11:10:16 PM;
    Version List=;
  }
  PROPERTIES
  {
    Editable=No;
    CaptionML=ENU=Check Preview;
    LinksAllowed=No;
    SourceTable=Table81;
    DataCaptionExpr="Document No." + ' ' + CheckToAddr[1];
    PageType=Card;
    OnOpenPage=BEGIN
                 CompanyInfo.GET;
                 FormatAddr.Company(CompanyAddr,CompanyInfo);
               END;

    OnAfterGetRecord=BEGIN
                       CalcCheck;
                     END;

  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 11  ;1   ;Group     ;
                CaptionML=ENU=Payer }

    { 15  ;2   ;Field     ;
                CaptionML=ENU=Company Name;
                ToolTipML=ENU=Specifies the company name that will appear on the check.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=CompanyAddr[1] }

    { 16  ;2   ;Field     ;
                CaptionML=ENU=Company Address;
                ToolTipML=ENU=Specifies the company address that will appear on the check.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=CompanyAddr[2] }

    { 17  ;2   ;Field     ;
                CaptionML=ENU=Company Address 2;
                ToolTipML=ENU=Specifies the extended company address that will appear on the check.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=CompanyAddr[3] }

    { 18  ;2   ;Field     ;
                CaptionML=ENU=Company ZIP Code/City;
                ToolTipML=ENU=Specifies the company ZIP code and city that will appear on the check.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=CompanyAddr[4] }

    { 5   ;2   ;Field     ;
                ToolTipML=ENU=Specifies a document number for the journal line.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Document No." }

    { 19  ;2   ;Field     ;
                CaptionML=ENU=Status;
                ToolTipML=ENU=Specifies if the check is printed.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=CheckStatusText }

    { 20  ;1   ;Group     ;
                CaptionML=ENU=Amount }

    { 30  ;2   ;Group      }

    { 13  ;3   ;Field     ;
                Name=AmountText;
                CaptionML=ENU=Amount Text;
                ToolTipML=ENU=Specifies the amount in letters that will appear on the check.;
                ApplicationArea=#Basic,#Suite;
                CaptionClass=FORMAT(NumberText[1]) }

    { 14  ;3   ;Field     ;
                CaptionML=ENU=Amount Text 2;
                ToolTipML=ENU=Specifies an additional part of the amount in letters that will appear on the check.;
                ApplicationArea=#Basic,#Suite;
                CaptionClass=FORMAT(NumberText[2]) }

    { 23  ;1   ;Group     ;
                CaptionML=ENU=Payee }

    { 1902115401;2;Group  ;
                GroupType=FixedLayout }

    { 1900724701;3;Group  ;
                CaptionML=ENU=Pay to the order of }

    { 2   ;4   ;Field     ;
                CaptionML=ENU=Name;
                ToolTipML=ENU=Specifies the name of the payee that will appear on the check.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=CheckToAddr[1] }

    { 1020000;4;Field     ;
                Name=Address;
                CaptionML=ENU=Address;
                ToolTipML=ENU=Specifies the issuers address.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=ConcAddr(CheckToAddr) }

    { 1900724501;3;Group  ;
                CaptionML=ENU=Date }

    { 6   ;4   ;Field     ;
                ToolTipML=ENU=Specifies the posting date for the entry.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=CheckDateText }

    { 24  ;4   ;Field     ;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=Text002;
                Visible=FALSE }

    { 26  ;4   ;Field     ;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=Text002;
                Visible=FALSE }

    { 28  ;4   ;Field     ;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=Text002;
                Visible=FALSE }

    { 1900724401;3;Group  ;
                CaptionML=ENU=Amount }

    { 7   ;4   ;Field     ;
                ToolTipML=ENU=Specifies the amount that will appear on the check.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=CheckAmount;
                AutoFormatType=1;
                AutoFormatExpr="Currency Code";
                ShowCaption=No }

    { 25  ;4   ;Field     ;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=Text002;
                Visible=FALSE }

    { 27  ;4   ;Field     ;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=Text002;
                Visible=FALSE }

    { 29  ;4   ;Field     ;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=Text002;
                Visible=FALSE }

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Printed Check';
      Text001@1001 : TextConst 'ENU=Not Printed Check';
      GenJnlLine@1002 : Record 81;
      Cust@1003 : Record 18;
      Vend@1004 : Record 23;
      BankAcc@1005 : Record 270;
      CompanyInfo@1006 : Record 79;
      BankAcc2@1480006 : Record 270;
      Employee@1020001 : Record 5200;
      FormatAddr@1008 : Codeunit 365;
      ChkTransMgt@1480003 : Report 10400;
      CheckToAddr@1009 : ARRAY [8] OF Text[50];
      CompanyAddr@1010 : ARRAY [8] OF Text[50];
      NumberText@1011 : ARRAY [2] OF Text[80];
      CheckStatusText@1012 : Text[30];
      CheckAmount@1013 : Decimal;
      Text002@1014 : TextConst 'ENU=Placeholder';
      CheckDateFormat@1480002 : ' ,MM DD YYYY,DD MM YYYY,YYYY MM DD';
      CheckLanguage@1480001 : Integer;
      DateSeparator@1480000 : ' ,-,.,/';
      CheckStyle@1020000 : ',US,CA';
      DateIndicator@1480007 : Text[10];
      CheckDateText@1480008 : Text[30];

    LOCAL PROCEDURE CalcCheck@1();
    BEGIN
      IF ("Bal. Account Type" <> "Bal. Account Type"::"Bank Account") OR
         NOT BankAcc2.GET("Bal. Account No.")
      THEN
        CLEAR(BankAcc2);
      IF "Check Printed" THEN BEGIN
        GenJnlLine.RESET;
        GenJnlLine.SETCURRENTKEY("Journal Template Name","Journal Batch Name","Posting Date","Document No.");
        GenJnlLine.SETRANGE("Journal Template Name","Journal Template Name");
        GenJnlLine.SETRANGE("Journal Batch Name","Journal Batch Name");
        GenJnlLine.SETRANGE("Posting Date","Posting Date");
        GenJnlLine.SETRANGE("Document No.","Document No.");
        IF "Bal. Account No." = '' THEN
          GenJnlLine.SETRANGE("Bank Payment Type","Bank Payment Type"::" ")
        ELSE
          GenJnlLine.SETRANGE("Bank Payment Type","Bank Payment Type"::"Computer Check");
        GenJnlLine.SETRANGE("Check Printed",TRUE);
        CheckStatusText := Text000;
      END ELSE BEGIN
        GenJnlLine.RESET;
        GenJnlLine.SETCURRENTKEY("Journal Template Name","Journal Batch Name","Posting Date","Document No.");
        GenJnlLine.SETRANGE("Journal Template Name","Journal Template Name");
        GenJnlLine.SETRANGE("Journal Batch Name","Journal Batch Name");
        GenJnlLine.SETRANGE("Posting Date","Posting Date");
        GenJnlLine.SETRANGE("Document No.","Document No.");
        GenJnlLine.SETRANGE("Account Type","Account Type");
        GenJnlLine.SETRANGE("Account No.","Account No.");
        GenJnlLine.SETRANGE("Bal. Account Type","Bal. Account Type");
        GenJnlLine.SETRANGE("Bal. Account No.","Bal. Account No.");
        GenJnlLine.SETRANGE("Bank Payment Type","Bank Payment Type");
        CheckStatusText := Text001;
      END;

      CheckAmount := 0;
      IF GenJnlLine.FIND('-') THEN
        REPEAT
          CheckAmount := CheckAmount + GenJnlLine.Amount;
        UNTIL GenJnlLine.NEXT = 0;

      IF CheckAmount < 0 THEN
        CheckAmount := 0;

      CASE GenJnlLine."Account Type" OF
        GenJnlLine."Account Type"::"G/L Account":
          BEGIN
            CLEAR(CheckToAddr);
            CheckToAddr[1] := GenJnlLine.Description;
            ChkTransMgt.SetCheckPrintParams(
              BankAcc2."Check Date Format",
              BankAcc2."Check Date Separator",
              BankAcc2."Country/Region Code",
              BankAcc2."Bank Communication",
              CheckToAddr[1],
              CheckDateFormat,
              DateSeparator,
              CheckLanguage,
              CheckStyle);
          END;
        GenJnlLine."Account Type"::Customer:
          BEGIN
            Cust.GET(GenJnlLine."Account No.");
            Cust.Contact := '';
            FormatAddr.Customer(CheckToAddr,Cust);
            ChkTransMgt.SetCheckPrintParams(
              Cust."Check Date Format",
              Cust."Check Date Separator",
              BankAcc2."Country/Region Code",
              Cust."Bank Communication",
              CheckToAddr[1],
              CheckDateFormat,
              DateSeparator,
              CheckLanguage,
              CheckStyle);
          END;
        GenJnlLine."Account Type"::Vendor:
          BEGIN
            Vend.GET(GenJnlLine."Account No.");
            Vend.Contact := '';
            FormatAddr.Vendor(CheckToAddr,Vend);
            ChkTransMgt.SetCheckPrintParams(
              Vend."Check Date Format",
              Vend."Check Date Separator",
              BankAcc2."Country/Region Code",
              Vend."Bank Communication",
              CheckToAddr[1],
              CheckDateFormat,
              DateSeparator,
              CheckLanguage,
              CheckStyle);
          END;
        GenJnlLine."Account Type"::"Bank Account":
          BEGIN
            BankAcc.GET(GenJnlLine."Account No.");
            BankAcc.Contact := '';
            FormatAddr.BankAcc(CheckToAddr,BankAcc);
            ChkTransMgt.SetCheckPrintParams(
              BankAcc."Check Date Format",
              BankAcc."Check Date Separator",
              BankAcc2."Country/Region Code",
              BankAcc."Bank Communication",
              CheckToAddr[1],
              CheckDateFormat,
              DateSeparator,
              CheckLanguage,
              CheckStyle);
          END;
        GenJnlLine."Account Type"::"Fixed Asset":
          GenJnlLine.FIELDERROR("Account Type");
        GenJnlLine."Account Type"::Employee:
          BEGIN
            Employee.GET(GenJnlLine."Account No.");
            FormatAddr.Employee(CheckToAddr,Employee);
            ChkTransMgt.SetCheckPrintParams(
              BankAcc2."Check Date Format",
              BankAcc2."Check Date Separator",
              BankAcc2."Country/Region Code",
              BankAcc2."Bank Communication",
              CheckToAddr[1],
              CheckDateFormat,
              DateSeparator,
              CheckLanguage,
              CheckStyle);
          END
      END;

      IF NOT ChkTransMgt.FormatNoText(NumberText,CheckAmount,CheckLanguage,GenJnlLine."Currency Code") THEN
        ERROR(NumberText[1]);
      CheckDateText := ChkTransMgt.FormatDate("Document Date",CheckDateFormat,DateSeparator,CheckLanguage,DateIndicator);
    END;

    LOCAL PROCEDURE ConcAddr@1020000(Addr@1020000 : ARRAY [8] OF Text[100]) Str : Text;
    VAR
      i@1020001 : Integer;
    BEGIN
      FOR i := 2 TO ARRAYLEN(Addr) DO
        IF Addr[i] <> '' THEN
          Str := Str + Addr[i] + ', ';
      Str := DELCHR(Str,'<>',', ');
    END;

    BEGIN
    END.
  }
}

