OBJECT Page 2343 BC O365 Payment Instr. Card
{
  OBJECT-PROPERTIES
  {
    Date=11/27/18;
    Time=11:10:49 PM;
    Version List=;
  }
  PROPERTIES
  {
    CaptionML=ENU=Payment instructions;
    DataCaptionExpr=O365PaymentInstructions.Name;
    PageType=Card;
    OnOpenPage=BEGIN
                 PaymentInstructionsText := O365PaymentInstructions.GetPaymentInstructionsInCurrentLanguage;
                 NameText := O365PaymentInstructions.GetNameInCurrentLanguage;

                 UpdateDefaultLabel;
               END;

    OnQueryClosePage=BEGIN
                       IF NOT (CloseAction IN [ACTION::LookupOK,ACTION::OK]) THEN
                         EXIT;

                       IF NameText = '' THEN
                         ERROR(MustSpecifyNameErr);

                       SaveRecord;
                     END;

  }
  CONTROLS
  {
    { 1   ;0   ;Container ;
                ContainerType=ContentArea }

    { 6   ;1   ;Group     ;
                GroupType=Group }

    { 2   ;2   ;Group     ;
                GroupType=Group;
                InstructionalTextML=ENU=Provide instructions to your customers on how you want to be paid. For the best result, use at most 300 characters. }

    { 4   ;2   ;Field     ;
                Name=NameControl;
                CaptionML=ENU=Short name;
                ToolTipML=ENU=Specifies a short description of the payment instructions.;
                ApplicationArea=#Basic,#Suite,#Invoicing;
                SourceExpr=NameText }

    { 3   ;1   ;Field     ;
                Name=PaymentInstructionsControl;
                CaptionML=ENU=Payment instructions;
                ToolTipML=ENU=Specifies the payment instructions that will appear in the bottom of your invoices.;
                ApplicationArea=#Basic,#Suite,#Invoicing;
                SourceExpr=PaymentInstructionsText;
                MultiLine=Yes }

    { 7   ;1   ;Field     ;
                Name=DefaultControl;
                ApplicationArea=#Basic,#Suite,#Invoicing;
                SourceExpr=DefaultTxt;
                Enabled=NOT IsDefault;
                Editable=FALSE;
                OnDrillDown=BEGIN
                              IF NameText = '' THEN
                                ERROR(MustSpecifyNameErr);

                              SaveRecord;
                              O365PaymentInstructions.VALIDATE(Default,TRUE);
                              O365PaymentInstructions.MODIFY(TRUE);
                              SENDTRACETAG('00001SC',PaymentInstrCategoryLbl,VERBOSITY::Normal,SetAsDefaultTelemetryTxt,DATACLASSIFICATION::SystemMetadata);
                              UpdateDefaultLabel;
                            END;

                ShowCaption=No }

  }
  CODE
  {
    VAR
      O365PaymentInstructions@1002 : Record 2155;
      PaymentInstructionsText@1000 : Text;
      NameText@1001 : Text[20];
      MustSpecifyNameErr@1004 : TextConst 'ENU=You must specify a name for these payment instructions.';
      SetAsDefaultLbl@1005 : TextConst 'ENU=Set as default payment instructions';
      DefaultTxt@1006 : Text;
      InstructionsAreDefaultTxt@1007 : TextConst 'ENU=These are the default payment instructions';
      IsDefault@1008 : Boolean;
      PaymentInstrCategoryLbl@1009 : TextConst '@@@={LOCKED};ENU=AL Payment Instructions';
      SetAsDefaultTelemetryTxt@1010 : TextConst '@@@={LOCKED};ENU=Default payment instructions changed.';
      NewRecordTelemetryTxt@1011 : TextConst '@@@={LOCKED};ENU=New payment instructions inserted.';

    PROCEDURE SetPaymentInstructionsOnPage@1(NewO365PaymentInstructions@1000 : Record 2155);
    BEGIN
      O365PaymentInstructions := NewO365PaymentInstructions;
    END;

    LOCAL PROCEDURE SaveRecord@2();
    BEGIN
      WITH O365PaymentInstructions DO BEGIN
        IF NOT GET(Id) THEN BEGIN
          INSERT(TRUE);
          SENDTRACETAG('00001SD',PaymentInstrCategoryLbl,VERBOSITY::Normal,NewRecordTelemetryTxt,DATACLASSIFICATION::SystemMetadata);
        END;
        VALIDATE(Name,NameText);
        SetPaymentInstructions(PaymentInstructionsText);
        DeleteTranslationsForRecord;
        MODIFY(TRUE);
      END;
    END;

    LOCAL PROCEDURE UpdateDefaultLabel@6();
    BEGIN
      IF O365PaymentInstructions.Default THEN BEGIN
        IsDefault := TRUE;
        DefaultTxt := InstructionsAreDefaultTxt;
      END ELSE
        DefaultTxt := SetAsDefaultLbl;
    END;

    PROCEDURE GetIsDefault@3() : Boolean;
    BEGIN
      EXIT(IsDefault);
    END;

    BEGIN
    END.
  }
}

