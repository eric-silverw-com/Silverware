OBJECT Table 9510 Email Parameter
{
  OBJECT-PROPERTIES
  {
    Date=09/19/18;
    Time=12:00:00 PM;
    Version List=NAVW113.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Email Parameter;
               ESM=Par metro de correo electr¢nico;
               FRC=ParamŠtre courriel;
               ENC=Email Parameter];
  }
  FIELDS
  {
    { 1   ;   ;Document No         ;Code20        ;CaptionML=[ENU=Document No;
                                                              ESM=N§ documento;
                                                              FRC=Nø document;
                                                              ENC=Document No] }
    { 2   ;   ;Document Type       ;Option        ;TableRelation="Sales Header"."Document Type";
                                                   CaptionML=[ENU=Document Type;
                                                              ESM=Tipo de documento;
                                                              FRC=Type document;
                                                              ENC=Document Type];
                                                   OptionCaptionML=[ENU=Quote,Order,Invoice,Credit Memo,Blanket Order,Return Order;
                                                                    ESM=Cotizaci¢n,Orden,Factura,Nota de cr‚dito,Orden abierta,Devoluci¢n;
                                                                    FRC=Devis,Commande,Facture,Note de cr‚dit,Commande ouverte,Retour;
                                                                    ENC=Quote,Order,Invoice,Credit Memo,Blanket Order,Return Order];
                                                   OptionString=Quote,Order,Invoice,Credit Memo,Blanket Order,Return Order }
    { 3   ;   ;Parameter Type      ;Option        ;CaptionML=[ENU=Parameter Type;
                                                              ESM=Tipo de par metro;
                                                              FRC=Type de paramŠtre;
                                                              ENC=Parameter Type];
                                                   OptionCaptionML=[ENU=" ,Subject,Address,Body";
                                                                    ESM=" ,Asunto,Direcci¢n,Cuerpo";
                                                                    FRC=" ,Objet,Adresse,Corps";
                                                                    ENC=" ,Subject,Address,Body"];
                                                   OptionString=[ ,Subject,Address,Body] }
    { 4   ;   ;Parameter Value     ;Text250       ;CaptionML=[ENU=Parameter Value;
                                                              ESM=Valor par metro;
                                                              FRC=Valeur paramŠtre;
                                                              ENC=Parameter Value] }
    { 5   ;   ;Parameter BLOB      ;BLOB          ;CaptionML=[ENU=Parameter BLOB;
                                                              ESM=BLOB de par metro;
                                                              FRC=ParamŠtre BLOB;
                                                              ENC=Parameter BLOB] }
  }
  KEYS
  {
    {    ;Document No,Document Type,Parameter Type;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      ParameterNotSupportedErr@1001 : TextConst 'ENU=Report usage is not supported.;ESM=No se admite el uso de informes.;FRC=L''utilisation du rapport n''est pas prise en charge.;ENC=Report usage is not supported.';

    PROCEDURE GetEntryWithReportUsage@2(DocumentNo@1002 : Code[20];ReportUsage@1001 : Integer;ParameterType@1000 : Option) : Boolean;
    VAR
      ReportSelections@1004 : Record 77;
      DocumentType@1003 : Option;
    BEGIN
      IF NOT ReportSelections.ReportUsageToDocumentType(DocumentType,ReportUsage) THEN
        EXIT(FALSE);
      EXIT(GET(DocumentNo,DocumentType,ParameterType));
    END;

    PROCEDURE GetParameterValue@1() : Text;
    BEGIN
      CALCFIELDS("Parameter BLOB");
      IF "Parameter BLOB".HASVALUE THEN
        EXIT(GetTextFromBLOB);

      EXIT("Parameter Value");
    END;

    PROCEDURE SaveParameterValue@3(DocumentNo@1000 : Code[20];DocumentType@1001 : Integer;ParameterType@1002 : Option;ParameterValue@1003 : Text);
    VAR
      ParameterAlreadyExists@1004 : Boolean;
    BEGIN
      ParameterAlreadyExists := GET(DocumentNo,DocumentType,ParameterType);
      IF NOT ParameterAlreadyExists THEN BEGIN
        INIT;
        "Document No" := DocumentNo;
        "Document Type" := DocumentType;
        "Parameter Type" := ParameterType;
      END;

      CLEAR("Parameter Value");
      CLEAR("Parameter BLOB");
      IF MAXSTRLEN("Parameter Value") > STRLEN(ParameterValue) THEN
        "Parameter Value" := COPYSTR(ParameterValue,1,MAXSTRLEN("Parameter Value"))
      ELSE
        WriteToBLOB(ParameterValue);

      IF ParameterAlreadyExists THEN
        MODIFY
      ELSE
        INSERT;
    END;

    PROCEDURE SaveParameterValueWithReportUsage@5(DocumentNo@1000 : Code[20];ReportUsage@1001 : Integer;ParameterType@1002 : Option;ParameterValue@1003 : Text);
    VAR
      ReportSelections@1005 : Record 77;
      DocumentType@1004 : Option;
    BEGIN
      IF NOT ReportSelections.ReportUsageToDocumentType(DocumentType,ReportUsage) THEN
        ERROR(ParameterNotSupportedErr);
      SaveParameterValue(DocumentNo,DocumentType,ParameterType,ParameterValue);
    END;

    LOCAL PROCEDURE WriteToBLOB@15(ParameterValue@1004 : Text);
    VAR
      TempBlob@1000 : Record 99008535;
    BEGIN
      CLEAR("Parameter BLOB");
      TempBlob.WriteAsText(ParameterValue,TEXTENCODING::Windows);
      "Parameter BLOB" := TempBlob.Blob;
    END;

    LOCAL PROCEDURE GetTextFromBLOB@14() : Text;
    VAR
      TempBlob@1000 : Record 99008535;
      CR@1001 : Text[1];
    BEGIN
      CALCFIELDS("Parameter BLOB");
      CR[1] := 10;
      TempBlob.Blob := "Parameter BLOB";
      EXIT(TempBlob.ReadAsText(CR,TEXTENCODING::Windows));
    END;

    BEGIN
    END.
  }
}

