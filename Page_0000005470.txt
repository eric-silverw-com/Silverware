OBJECT Page 5470 Item Entity
{
  OBJECT-PROPERTIES
  {
    Date=09/19/18;
    Time=12:00:00 PM;
    Version List=NAVW113.00,NAVNA13.00;
  }
  PROPERTIES
  {
    CaptionML=[@@@={Locked};
               ENU=items;
               ESM=items;
               FRC=items;
               ENC=items];
    SourceTable=Table27;
    DelayedInsert=Yes;
    PageType=API;
    ChangeTrackingAllowed=Yes;
    EntitySetName=items;
    EntityName=item;
    OnAfterGetRecord=BEGIN
                       SetCalculatedFields;
                     END;

    OnNewRecord=BEGIN
                  ClearCalculatedFields;
                END;

    OnInsertRecord=VAR
                     GraphCollectionMgtItem@1002 : Codeunit 5470;
                   BEGIN
                     IF TempFieldSet.GET(DATABASE::Item,FIELDNO("Base Unit of Measure")) THEN
                       IF BaseUnitOfMeasureJSONText = '' THEN
                         BaseUnitOfMeasureJSONText := GraphCollectionMgtItem.ItemUnitOfMeasureToJSON(Rec,BaseUnitOfMeasureCode);

                     GraphCollectionMgtItem.InsertItem(Rec,TempFieldSet,BaseUnitOfMeasureJSONText);

                     SetCalculatedFields;
                     EXIT(FALSE);
                   END;

    OnModifyRecord=VAR
                     Item@1001 : Record 27;
                     GraphCollectionMgtItem@1000 : Codeunit 5470;
                   BEGIN
                     IF TempFieldSet.GET(DATABASE::Item,FIELDNO("Base Unit of Measure")) THEN
                       VALIDATE("Base Unit of Measure",BaseUnitOfMeasureCode);

                     Item.SETRANGE(Id,Id);
                     Item.FINDFIRST;

                     GraphCollectionMgtItem.ProcessComplexTypes(
                       Rec,
                       BaseUnitOfMeasureJSONText
                       );

                     IF "No." = Item."No." THEN
                       MODIFY(TRUE)
                     ELSE BEGIN
                       Item.TRANSFERFIELDS(Rec,FALSE);
                       Item.RENAME("No.");
                       TRANSFERFIELDS(Item,TRUE);
                     END;

                     SetCalculatedFields;

                     EXIT(FALSE);
                   END;

    ODataKeyFields=Id;
  }
  CONTROLS
  {
    { 18  ;0   ;Container ;
                ContainerType=ContentArea }

    { 17  ;1   ;Group     ;
                Name=Group;
                GroupType=Repeater }

    { 16  ;2   ;Field     ;
                Name=id;
                CaptionML=[@@@={Locked};
                           ENU=Id;
                           ESM=Id;
                           FRC=Id;
                           ENC=Id];
                ApplicationArea=#All;
                SourceExpr=Id;
                Editable=FALSE }

    { 15  ;2   ;Field     ;
                Name=number;
                CaptionML=[@@@={Locked};
                           ENU=Number;
                           ESM=Number;
                           FRC=Number;
                           ENC=Number];
                ApplicationArea=#All;
                SourceExpr="No." }

    { 14  ;2   ;Field     ;
                Name=displayName;
                CaptionML=[@@@={Locked};
                           ENU=DisplayName;
                           ESM=DisplayName;
                           FRC=DisplayName;
                           ENC=DisplayName];
                ToolTipML=[ENU=Specifies the Description for the Item.;
                           ESM=Especifica la descripci¢n del elemento.;
                           FRC=Sp‚cifie la description de l'‚l‚ment.;
                           ENC=Specifies the Description for the Item.];
                ApplicationArea=#All;
                SourceExpr=Description;
                OnValidate=BEGIN
                             RegisterFieldSet(FIELDNO(Description));
                           END;
                            }

    { 11  ;2   ;Field     ;
                Name=type;
                CaptionML=[@@@={Locked};
                           ENU=Type;
                           ESM=Type;
                           FRC=Type;
                           ENC=Type];
                ToolTipML=[ENU=Specifies the Type for the Item. Possible values are Inventory and Service.;
                           ESM=Especifica el Tipo para el elemento. Los valores posibles son Inventario y Servicio.;
                           FRC=Sp‚cifie le type d'article. Les valeurs possibles sont Inventaire et Service.;
                           ENC=Specifies the Type for the Item. Possible values are Inventory and Service.];
                ApplicationArea=#All;
                SourceExpr=Type;
                OnValidate=BEGIN
                             RegisterFieldSet(FIELDNO(Type));
                           END;
                            }

    { 8   ;2   ;Field     ;
                Name=itemCategoryId;
                CaptionML=[@@@={Locked};
                           ENU=ItemCategoryId;
                           ESM=ItemCategoryId;
                           FRC=ItemCategoryId;
                           ENC=ItemCategoryId];
                ApplicationArea=#All;
                SourceExpr="Item Category Id";
                OnValidate=BEGIN
                             IF "Item Category Id" = BlankGUID THEN
                               "Item Category Code" := ''
                             ELSE BEGIN
                               ItemCategory.SETRANGE(Id,"Item Category Id");
                               IF NOT ItemCategory.FINDFIRST THEN
                                 ERROR(ItemCategoryIdDoesNotMatchAnItemCategoryGroupErr);

                               "Item Category Code" := ItemCategory.Code;
                             END;

                             RegisterFieldSet(FIELDNO("Item Category Code"));
                             RegisterFieldSet(FIELDNO("Item Category Id"));
                           END;
                            }

    { 9   ;2   ;Field     ;
                Name=itemCategoryCode;
                CaptionML=[@@@={Locked};
                           ENU=ItemCategoryCode;
                           ESM=ItemCategoryCode;
                           FRC=ItemCategoryCode;
                           ENC=ItemCategoryCode];
                ApplicationArea=#All;
                SourceExpr="Item Category Code";
                Enabled=true;
                Editable=false;
                OnValidate=BEGIN
                             IF ItemCategory.Code <> '' THEN BEGIN
                               IF ItemCategory.Code <> "Item Category Code" THEN
                                 ERROR(ItemCategoriesValuesDontMatchErr);
                               EXIT;
                             END;

                             IF "Item Category Code" = '' THEN
                               "Item Category Id" := BlankGUID
                             ELSE BEGIN
                               IF NOT ItemCategory.GET("Item Category Code") THEN
                                 ERROR(ItemCategoryCodeDoesNotMatchATaxGroupErr);

                               "Item Category Id" := ItemCategory.Id;
                             END;
                           END;
                            }

    { 4   ;2   ;Field     ;
                Name=blocked;
                CaptionML=[@@@={Locked};
                           ENU=Blocked;
                           ESM=Blocked;
                           FRC=Blocked;
                           ENC=Blocked];
                ToolTipML=[ENU=Specifies whether the item is blocked.;
                           ESM=Indica si el producto est  bloqueado.;
                           FRC=Sp‚cifie si l'article est bloqu‚.;
                           ENC=Specifies whether the item is blocked.];
                ApplicationArea=#All;
                SourceExpr=Blocked;
                OnValidate=BEGIN
                             RegisterFieldSet(FIELDNO(Blocked));
                           END;
                            }

    { 1   ;2   ;Field     ;
                Name=baseUnitOfMeasureId;
                CaptionML=[@@@={Locked};
                           ENU=BaseUnitOfMeasureId;
                           ESM=BaseUnitOfMeasureId;
                           FRC=BaseUnitOfMeasureId;
                           ENC=BaseUnitOfMeasureId];
                ApplicationArea=#All;
                SourceExpr=BaseUnitOfMeasureId;
                OnValidate=BEGIN
                             IF BaseUnitOfMeasureId = BlankGUID THEN
                               BaseUnitOfMeasureCode := ''
                             ELSE BEGIN
                               ValidateUnitOfMeasure.SETRANGE(Id,BaseUnitOfMeasureId);
                               IF NOT ValidateUnitOfMeasure.FINDFIRST THEN
                                 ERROR(UnitOfMeasureIdDoesNotMatchAUnitOfMeasureErr);

                               BaseUnitOfMeasureCode := ValidateUnitOfMeasure.Code;
                             END;

                             RegisterFieldSet(FIELDNO("Unit of Measure Id"));
                             RegisterFieldSet(FIELDNO("Base Unit of Measure"));
                           END;
                            }

    { 20  ;2   ;Field     ;
                Name=baseUnitOfMeasure;
                CaptionML=[@@@={Locked};
                           ENU=BaseUnitOfMeasure;
                           ESM=BaseUnitOfMeasure;
                           FRC=BaseUnitOfMeasure;
                           ENC=BaseUnitOfMeasure];
                ToolTipML=[ENU=Specifies the Base Unit of Measure.;
                           ESM=Especifica la unidad base de medida.;
                           FRC=Sp‚cifie l'unit‚ de mesure de base.;
                           ENC=Specifies the Base Unit of Measure.];
                ApplicationArea=#All;
                SourceExpr=BaseUnitOfMeasureJSONText;
                OnValidate=VAR
                             UnitOfMeasureFromJSON@1000 : Record 204;
                           BEGIN
                             RegisterFieldSet(FIELDNO("Unit of Measure Id"));
                             RegisterFieldSet(FIELDNO("Base Unit of Measure"));

                             IF BaseUnitOfMeasureJSONText = 'null' THEN
                               EXIT;

                             GraphCollectionMgtItem.ParseJSONToUnitOfMeasure(BaseUnitOfMeasureJSONText,UnitOfMeasureFromJSON);

                             IF (ValidateUnitOfMeasure.Code <> '') AND
                                (ValidateUnitOfMeasure.Code <> UnitOfMeasureFromJSON.Code)
                             THEN
                               ERROR(UnitOfMeasureValuesDontMatchErr);
                           END;

                ODataEDMType=ITEM-UOM }

    { 6   ;2   ;Field     ;
                Name=gtin;
                CaptionML=[@@@={Locked};
                           ENU=GTIN;
                           ESM=GTIN;
                           FRC=GTIN;
                           ENC=GTIN];
                ApplicationArea=#All;
                SourceExpr=GTIN;
                OnValidate=BEGIN
                             RegisterFieldSet(FIELDNO(GTIN));
                           END;
                            }

    { 23  ;2   ;Field     ;
                Name=inventory;
                CaptionML=[@@@={Locked};
                           ENU=Inventory;
                           ESM=Inventory;
                           FRC=Inventory;
                           ENC=Inventory];
                ToolTipML=[ENU=Specifies the inventory for the item.;
                           ESM=Especifica el inventario del elemento.;
                           FRC=Sp‚cifie l'inventaire de l'article.;
                           ENC=Specifies the inventory for the item.];
                ApplicationArea=#All;
                SourceExpr=InventoryValue;
                OnValidate=BEGIN
                             VALIDATE(Inventory,InventoryValue);
                             RegisterFieldSet(FIELDNO(Inventory));
                           END;
                            }

    { 10  ;2   ;Field     ;
                Name=unitPrice;
                CaptionML=[@@@={Locked};
                           ENU=UnitPrice;
                           ESM=UnitPrice;
                           FRC=UnitPrice;
                           ENC=UnitPrice];
                ApplicationArea=#All;
                SourceExpr="Unit Price";
                OnValidate=BEGIN
                             RegisterFieldSet(FIELDNO("Unit Price"));
                           END;
                            }

    { 24  ;2   ;Field     ;
                Name=priceIncludesTax;
                CaptionML=[@@@={Locked};
                           ENU=PriceIncludesTax;
                           ESM=PriceIncludesTax;
                           FRC=PriceIncludesTax;
                           ENC=PriceIncludesTax];
                ApplicationArea=#All;
                SourceExpr="Price Includes VAT";
                OnValidate=VAR
                             GeneralLedgerSetup@1020000 : Record 98;
                           BEGIN
                             GeneralLedgerSetup.GET;
                             IF NOT GeneralLedgerSetup."VAT in Use" AND "Price Includes VAT" THEN
                               ERROR(CannotSetPriceIncludesTaxErr);

                             RegisterFieldSet(FIELDNO("Price Includes VAT"));
                           END;
                            }

    { 25  ;2   ;Field     ;
                Name=unitCost;
                CaptionML=[@@@={Locked};
                           ENU=UnitCost;
                           ESM=UnitCost;
                           FRC=UnitCost;
                           ENC=UnitCost];
                ApplicationArea=#All;
                SourceExpr="Unit Cost";
                OnValidate=BEGIN
                             RegisterFieldSet(FIELDNO("Unit Cost"));
                           END;
                            }

    { 3   ;2   ;Field     ;
                Name=taxGroupId;
                CaptionML=[@@@={Locked};
                           ENU=TaxGroupId;
                           ESM=TaxGroupId;
                           FRC=TaxGroupId;
                           ENC=TaxGroupId];
                ToolTipML=[ENU=Specifies the ID of the tax group.;
                           ESM=Especifica el id. del grupo impositivo.;
                           FRC=Sp‚cifie le code du groupe fiscal.;
                           ENC=Specifies the ID of the tax group.];
                ApplicationArea=#All;
                SourceExpr="Tax Group Id";
                OnValidate=BEGIN
                             IF "Tax Group Id" = BlankGUID THEN
                               "Tax Group Code" := ''
                             ELSE BEGIN
                               TaxGroup.SETRANGE(Id,"Tax Group Id");
                               IF NOT TaxGroup.FINDFIRST THEN
                                 ERROR(TaxGroupIdDoesNotMatchATaxGroupErr);

                               "Tax Group Code" := TaxGroup.Code;
                             END;

                             RegisterFieldSet(FIELDNO("Tax Group Code"));
                             RegisterFieldSet(FIELDNO("Tax Group Id"));
                           END;
                            }

    { 2   ;2   ;Field     ;
                Name=taxGroupCode;
                CaptionML=[@@@={Locked};
                           ENU=TaxGroupCode;
                           ESM=TaxGroupCode;
                           FRC=TaxGroupCode;
                           ENC=TaxGroupCode];
                ApplicationArea=#All;
                SourceExpr="Tax Group Code";
                OnValidate=BEGIN
                             IF TaxGroup.Code <> '' THEN BEGIN
                               IF TaxGroup.Code <> "Tax Group Code" THEN
                                 ERROR(TaxGroupValuesDontMatchErr);
                               EXIT;
                             END;

                             IF "Tax Group Code" = '' THEN
                               "Tax Group Id" := BlankGUID
                             ELSE BEGIN
                               IF NOT TaxGroup.GET("Tax Group Code") THEN
                                 ERROR(TaxGroupCodeDoesNotMatchATaxGroupErr);

                               "Tax Group Id" := TaxGroup.Id;
                             END;

                             RegisterFieldSet(FIELDNO("Tax Group Code"));
                             RegisterFieldSet(FIELDNO("Tax Group Id"));
                           END;
                            }

    { 19  ;2   ;Field     ;
                Name=lastModifiedDateTime;
                CaptionML=[@@@={Locked};
                           ENU=LastModifiedDateTime;
                           ESM=LastModifiedDateTime;
                           FRC=LastModifiedDateTime;
                           ENC=LastModifiedDateTime];
                ApplicationArea=#All;
                SourceExpr="Last DateTime Modified";
                Editable=FALSE }

    { 5   ;2   ;Part      ;
                Name=picture;
                CaptionML=[ENU=picture;
                           ESM=fotograf¡a;
                           FRC=image;
                           ENC=picture];
                SubPageLink=Id=FIELD(Id);
                PagePartID=Page5468;
                EntitySetName=picture;
                EntityName=picture;
                PartType=Page }

    { 7   ;2   ;Part      ;
                Name=defaultDimensions;
                CaptionML=[@@@={Locked};
                           ENU=Default Dimensions;
                           ESM=Default Dimensions;
                           FRC=Default Dimensions;
                           ENC=Default Dimensions];
                SubPageLink=ParentId=FIELD(Id);
                PagePartID=Page5509;
                EntitySetName=defaultDimensions;
                EntityName=defaultDimensions;
                PartType=Page }

  }
  CODE
  {
    VAR
      TempFieldSet@1011 : TEMPORARY Record 2000000041;
      ValidateUnitOfMeasure@1004 : Record 204;
      ItemCategory@1015 : Record 5722;
      TaxGroup@1008 : Record 321;
      GraphCollectionMgtItem@1001 : Codeunit 5470;
      BaseUnitOfMeasureCode@1014 : Code[10];
      BaseUnitOfMeasureJSONText@1002 : Text;
      InventoryValue@1000 : Decimal;
      CannotSetPriceIncludesTaxErr@1020000 : TextConst 'ENU=In order to set the Price Includes Tax field, Tax in Use must be enabled in General Ledger Setup.;ESM=Para configurar el campo Precio incluye impuestos, la opci¢n IVA en uso debe estar habilitada en Configuraci¢n contabilidad.;FRC=Pour d‚finir le champ Prix TTC, TVA en utilisation doit ˆtre activ‚ dans Configuration du grand livre.;ENC=In order to set the Price Includes Tax field, Tax in Use must be enabled in General Ledger Setup.';
      UnitOfMeasureValuesDontMatchErr@1007 : TextConst '@@@={Locked};ENU=The unit of measure values do not match to a specific Unit of Measure.;ESM=The unit of measure values do not match to a specific Unit of Measure.;FRC=The unit of measure values do not match to a specific Unit of Measure.;ENC=The unit of measure values do not match to a specific Unit of Measure.';
      UnitOfMeasureIdDoesNotMatchAUnitOfMeasureErr@1006 : TextConst '@@@={Locked};ENU=The "unitOfMeasureId" does not match to a Unit of Measure.;ESM=The "unitOfMeasureId" does not match to a Unit of Measure.;FRC=The "unitOfMeasureId" does not match to a Unit of Measure.;ENC=The "unitOfMeasureId" does not match to a Unit of Measure.';
      BaseUnitOfMeasureId@1013 : GUID;
      BlankGUID@1005 : GUID;
      TaxGroupValuesDontMatchErr@1012 : TextConst '@@@={Locked};ENU=The tax group values do not match to a specific Tax Group.;ESM=The tax group values do not match to a specific Tax Group.;FRC=The tax group values do not match to a specific Tax Group.;ENC=The tax group values do not match to a specific Tax Group.';
      TaxGroupIdDoesNotMatchATaxGroupErr@1010 : TextConst '@@@={Locked};ENU=The "taxGroupId" does not match to a Tax Group.;ESM=The "taxGroupId" does not match to a Tax Group.;FRC=The "taxGroupId" does not match to a Tax Group.;ENC=The "taxGroupId" does not match to a Tax Group.';
      TaxGroupCodeDoesNotMatchATaxGroupErr@1009 : TextConst '@@@={Locked};ENU=The "taxGroupCode" does not match to a Tax Group.;ESM=The "taxGroupCode" does not match to a Tax Group.;FRC=The "taxGroupCode" does not match to a Tax Group.;ENC=The "taxGroupCode" does not match to a Tax Group.';
      ItemCategoryIdDoesNotMatchAnItemCategoryGroupErr@1016 : TextConst 'ENU=The "itemCategoryId" does not match to a specific ItemCategory group.;ESM="""itemCategoryId"" no coincide con un grupo de ItemCategory espec¡fico.";FRC=L''®ÿitemCategoryIdÿ¯ ne correspond pas … un groupe d''ItemCategory sp‚cifique.;ENC=The "itemCategoryId" does not match to a specific ItemCategory group.';
      ItemCategoriesValuesDontMatchErr@1017 : TextConst 'ENU=The item categories values do not match to a specific Iteam Category.;ESM=Los valores de categor¡as de productos no coinciden con una categor¡a de producto espec¡fica.;FRC=Les valeurs de cat‚gories d''articles ne correspondent pas … une cat‚gorie d''articles sp‚cifique.;ENC=The item categories values do not match to a specific Iteam Category.';
      ItemCategoryCodeDoesNotMatchATaxGroupErr@1018 : TextConst 'ENU=The "itemCategoryCode" does not match to a Item Category.;ESM="""itemCategoryCode"" no coincide con una categor¡a de producto.";FRC=L''®ÿitemCategoryCodeÿ¯ ne correspond pas … une cat‚gorie d''articles.;ENC=The "itemCategoryCode" does not match to a Item Category.';

    LOCAL PROCEDURE SetCalculatedFields@6();
    VAR
      UnitOfMeasure@1001 : Record 204;
      GraphCollectionMgtItem@1000 : Codeunit 5470;
    BEGIN
      // UOM
      BaseUnitOfMeasureJSONText := GraphCollectionMgtItem.ItemUnitOfMeasureToJSON(Rec,"Base Unit of Measure");
      BaseUnitOfMeasureCode := "Base Unit of Measure";
      IF UnitOfMeasure.GET(BaseUnitOfMeasureCode) THEN
        BaseUnitOfMeasureId := UnitOfMeasure.Id
      ELSE
        BaseUnitOfMeasureId := BlankGUID;

      // Inventory
      CALCFIELDS(Inventory);
      InventoryValue := Inventory;
    END;

    LOCAL PROCEDURE ClearCalculatedFields@10();
    BEGIN
      CLEAR(Id);
      CLEAR(BaseUnitOfMeasureId);
      CLEAR(BaseUnitOfMeasureCode);
      CLEAR(BaseUnitOfMeasureJSONText);
      CLEAR(InventoryValue);
      TempFieldSet.DELETEALL;
    END;

    LOCAL PROCEDURE RegisterFieldSet@11(FieldNo@1000 : Integer);
    BEGIN
      IF TempFieldSet.GET(DATABASE::Item,FieldNo) THEN
        EXIT;

      TempFieldSet.INIT;
      TempFieldSet.TableNo := DATABASE::Item;
      TempFieldSet.VALIDATE("No.",FieldNo);
      TempFieldSet.INSERT(TRUE);
    END;

    BEGIN
    END.
  }
}

