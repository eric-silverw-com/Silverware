OBJECT Page 1612 Office Admin. Credentials
{
  OBJECT-PROPERTIES
  {
    Date=11/22/17;
    Time=12:00:00 PM;
    Version List=NAVW111.00;
  }
  PROPERTIES
  {
    Permissions=TableData 1612=rimd;
    CaptionML=[ENU=Office Admin. Credentials;
               ESM=Credenciales de administrador de Office;
               FRC=Informations d'identification Admin Office;
               ENC=Office Admin. Credentials];
    SaveValues=Yes;
    MultipleNewLines=No;
    InsertAllowed=Yes;
    DeleteAllowed=No;
    ModifyAllowed=Yes;
    SourceTable=Table1612;
    PageType=NavigatePage;
    OnOpenPage=BEGIN
                 ShowQuestion;
                 EmailHostedInO365 := TRUE;
               END;

    OnQueryClosePage=BEGIN
                       IF (CloseAction = ACTION::OK) OR (CloseAction = ACTION::LookupOK) THEN BEGIN
                         IF NOT GET THEN
                           INSERT;
                         SavePassword(PasswordText);
                       END;
                     END;

    ActionList=ACTIONS
    {
      { 2       ;    ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 9       ;1   ;Action    ;
                      Name=ActionBack;
                      CaptionML=[ENU=Back;
                                 ESM=Atr s;
                                 FRC=Pr‚c‚dent;
                                 ENC=Back];
                      ApplicationArea=#Basic,#Suite;
                      Enabled=BackEnabled;
                      InFooterBar=Yes;
                      Image=PreviousRecord;
                      OnAction=BEGIN
                                 NextStep(TRUE);
                               END;
                                }
      { 8       ;1   ;Action    ;
                      Name=ActionNext;
                      CaptionML=[ENU=Next;
                                 ESM=Siguiente;
                                 FRC=Suivant;
                                 ENC=Next];
                      ApplicationArea=#Basic,#Suite;
                      Enabled=NextEnabled;
                      InFooterBar=Yes;
                      Image=NextRecord;
                      OnAction=BEGIN
                                 NextStep(FALSE);
                               END;
                                }
      { 7       ;1   ;Action    ;
                      Name=ActionFinish;
                      CaptionML=[ENU=Finish;
                                 ESM=Terminar;
                                 FRC=Terminer;
                                 ENC=Finish];
                      ApplicationArea=#Basic,#Suite;
                      Enabled=FinishEnabled;
                      InFooterBar=Yes;
                      Image=Approve;
                      OnAction=BEGIN
                                 IF (Email = '') OR (PasswordText = '') OR (EmailHostedInO365 AND (Endpoint = '')) THEN
                                   ERROR(MissingCredentialErr);

                                 IF NOT INSERT(TRUE) THEN
                                   MODIFY(TRUE);

                                 CurrPage.CLOSE;
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1   ;0   ;Container ;
                ContainerType=ContentArea }

    { 10  ;1   ;Group     ;
                Name=Question;
                CaptionML=[ENU="";
                           ESM="";
                           FRC="";
                           ENC=""];
                Visible=QuestionVisible;
                GroupType=Group }

    { 12  ;2   ;Field     ;
                Name=UseO365;
                DrillDown=Yes;
                CaptionML=[ENU=Are you using an Office 365 mailbox?;
                           ESM=¨Est  usando un buz¢n de correo de Office 365?;
                           FRC=Utilisez-vous une boŒte de r‚ception Office 365ÿ?;
                           ENC=Are you using an Office 365 mailbox?];
                ApplicationArea=#Basic,#Suite;
                SourceExpr=EmailHostedInO365 }

    { 11  ;1   ;Group     ;
                Name=O365Credential;
                CaptionML=[ENU="";
                           ESM="";
                           FRC="";
                           ENC=""];
                Visible=O365CredentialVisible;
                GroupType=Group }

    { 14  ;2   ;Field     ;
                Name=O365Email;
                ExtendedDatatype=E-Mail;
                CaptionML=[ENU=Office 365 admin email address;
                           ESM=Direcci¢n de correo electr¢nico de administrador de Office 365;
                           FRC=Adresse courriel admin Office 365;
                           ENC=Office 365 admin email address];
                ApplicationArea=#Basic,#Suite;
                NotBlank=Yes;
                SourceExpr=Email }

    { 13  ;2   ;Field     ;
                Name=O365Password;
                ExtendedDatatype=Masked;
                CaptionML=[ENU=Office 365 admin password;
                           ESM=Contrase¤a de administrador de Office 365;
                           FRC=Mot de passe admin Office 365;
                           ENC=Office 365 admin password];
                ApplicationArea=#Basic,#Suite;
                NotBlank=Yes;
                SourceExpr=PasswordText;
                OnValidate=BEGIN
                             IF (PasswordText <> '') AND (NOT ENCRYPTIONENABLED) THEN
                               IF CONFIRM(EncryptionManagement.GetEncryptionIsNotActivatedQst) THEN
                                 PAGE.RUNMODAL(PAGE::"Data Encryption Management");
                           END;
                            }

    { 6   ;1   ;Group     ;
                Name=OnPremCredential;
                CaptionML=[ENU="";
                           ESM="";
                           FRC="";
                           ENC=""];
                Visible=OnPremCredentialVisible;
                GroupType=Group }

    { 3   ;2   ;Field     ;
                Name=OnPremUsername;
                ExtendedDatatype=E-Mail;
                CaptionML=[ENU=Exchange admin username;
                           ESM=Nombre de usuario de administrador de Exchange;
                           FRC=Nom utilisateur admin Exchange;
                           ENC=Exchange admin username];
                ApplicationArea=#Basic,#Suite;
                NotBlank=Yes;
                SourceExpr=Email }

    { 4   ;2   ;Field     ;
                Name=OnPremPassword;
                ExtendedDatatype=Masked;
                CaptionML=[ENU=Exchange admin password;
                           ESM=Contrase¤a de administrador de Exchange;
                           FRC=Mot de passe admin Exchange;
                           ENC=Exchange admin password];
                ApplicationArea=#Basic,#Suite;
                NotBlank=Yes;
                SourceExpr=PasswordText;
                OnValidate=BEGIN
                             IF (PasswordText <> '') AND (NOT ENCRYPTIONENABLED) THEN
                               IF CONFIRM(EncryptionManagement.GetEncryptionIsNotActivatedQst) THEN
                                 PAGE.RUNMODAL(PAGE::"Data Encryption Management");
                           END;
                            }

    { 5   ;2   ;Field     ;
                Name=Endpoint;
                CaptionML=[ENU=Exchange PowerShell Endpoint;
                           ESM=Extremo de PowerShell de Exchange;
                           FRC=Point de terminaison PowerShell Exchange;
                           ENC=Exchange PowerShell Endpoint];
                ApplicationArea=#Basic,#Suite;
                SourceExpr=Endpoint }

  }
  CODE
  {
    VAR
      EncryptionManagement@1011 : Codeunit 1266;
      Step@1005 : 'Question,O365Credential,OnPremCredential';
      EmailHostedInO365@1009 : Boolean;
      QuestionVisible@1006 : Boolean;
      O365CredentialVisible@1007 : Boolean;
      OnPremCredentialVisible@1008 : Boolean;
      BackEnabled@1002 : Boolean;
      NextEnabled@1003 : Boolean;
      FinishEnabled@1004 : Boolean;
      MissingCredentialErr@1000 : TextConst 'ENU=You must specify both an email address and a password.;ESM=Debe especificar una direcci¢n de correo electr¢nico y una contrase¤a.;FRC=Vous devez sp‚cifier une adresse de courriel et un mot de passe.;ENC=You must specify both an email address and a password.';
      PasswordText@1001 : Text;

    LOCAL PROCEDURE NextStep@1(Backwards@1000 : Boolean);
    BEGIN
      IF Backwards THEN
        Step := Step - 1
      ELSE
        Step := Step + 1;

      CASE Step OF
        Step::Question:
          ShowQuestion;
        Step::O365Credential:
          ShowO365Credential(Backwards);
        Step::OnPremCredential:
          ShowOnPremCredential;
      END;

      CurrPage.UPDATE(TRUE);
    END;

    LOCAL PROCEDURE ShowQuestion@2();
    BEGIN
      ResetControls;

      BackEnabled := FALSE;
      QuestionVisible := TRUE;
    END;

    LOCAL PROCEDURE ShowO365Credential@3(Backwards@1000 : Boolean);
    BEGIN
      ResetControls;

      // Skip to the next window if we're not using O365.
      IF NOT EmailHostedInO365 THEN BEGIN
        NextStep(Backwards);
        EXIT;
      END;

      FinishEnabled := TRUE;
      NextEnabled := FALSE;
      O365CredentialVisible := TRUE;
    END;

    LOCAL PROCEDURE ShowOnPremCredential@4();
    BEGIN
      ResetControls;

      FinishEnabled := TRUE;
      NextEnabled := FALSE;
      OnPremCredentialVisible := TRUE;
    END;

    LOCAL PROCEDURE ResetControls@9();
    BEGIN
      NextEnabled := TRUE;
      BackEnabled := TRUE;
      FinishEnabled := FALSE;

      QuestionVisible := FALSE;
      O365CredentialVisible := FALSE;
      OnPremCredentialVisible := FALSE;
    END;

    BEGIN
    END.
  }
}

