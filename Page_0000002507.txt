OBJECT Page 2507 Upload And Deploy Extension
{
  OBJECT-PROPERTIES
  {
    Date=11/27/18;
    Time=11:10:49 PM;
    Version List=;
  }
  PROPERTIES
  {
    CaptionML=ENU=Upload And Deploy Extension;
    SourceTable=Table2000000160;
    PageType=NavigatePage;
    SourceTableTemporary=Yes;
    OnOpenPage=VAR
                 WinLanguagesTable@1000 : Record 2000000045;
               BEGIN
                 LanguageID := GLOBALLANGUAGE;
                 WinLanguagesTable.SETRANGE("Language ID",LanguageID);
                 IF WinLanguagesTable.FINDFIRST THEN
                   LanguageName := WinLanguagesTable.Name;
               END;

    ActionList=ACTIONS
    {
      { 8       ;    ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 9       ;1   ;Action    ;
                      Name=Deploy;
                      CaptionML=ENU=Deploy;
                      ApplicationArea=#Basic,#Suite;
                      Promoted=Yes;
                      Enabled=Accepted;
                      InFooterBar=Yes;
                      RunPageMode=Edit;
                      OnAction=VAR
                                 NavExtensionOperationMgmt@1000 : Codeunit 2503;
                                 DotNet_ALPacDeploymentSchedule@1002 : Codeunit 3019;
                               BEGIN
                                 IF FileName = '' THEN
                                   MESSAGE(ExtensionNotUploadedMsg)
                                 ELSE BEGIN
                                   CASE DeployTo OF
                                     DeployTo::"Current version":
                                       BEGIN
                                         DotNet_ALPacDeploymentSchedule.Immediate;
                                         NavExtensionOperationMgmt.UploadNavExtension(FileStream,DotNet_ALPacDeploymentSchedule,LanguageID);
                                         MESSAGE(CurrentOperationProgressMsg);
                                       END;
                                     DeployTo::"Next minor version":
                                       BEGIN
                                         DotNet_ALPacDeploymentSchedule.StageForNextMinorUpdate;
                                         NavExtensionOperationMgmt.UploadNavExtension(FileStream,DotNet_ALPacDeploymentSchedule,LanguageID);
                                         MESSAGE(ScheduledOperationMinorProgressMsg);
                                       END;
                                     DeployTo::"Next major version":
                                       BEGIN
                                         DotNet_ALPacDeploymentSchedule.StageForNextMajorUpdate;
                                         NavExtensionOperationMgmt.UploadNavExtension(FileStream,DotNet_ALPacDeploymentSchedule,LanguageID);
                                         MESSAGE(ScheduledOperationMajorProgressMsg);
                                       END;
                                   END;
                                   CurrPage.CLOSE;
                                 END;
                               END;
                                }
      { 10      ;1   ;Action    ;
                      Name=Cancel;
                      CaptionML=ENU=Cancel;
                      ApplicationArea=#Basic,#Suite;
                      InFooterBar=Yes;
                      RunPageMode=Edit;
                      OnAction=BEGIN
                                 CurrPage.CLOSE;
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1   ;0   ;Container ;
                ContainerType=ContentArea }

    { 2   ;1   ;Field     ;
                CaptionML=ENU=Upload Extension;
                ApplicationArea=#Basic,#Suite;
                Style=StrongAccent;
                StyleExpr=TRUE }

    { 7   ;1   ;Field     ;
                CaptionML=ENU=Select .app file;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=FileName;
                Editable=FALSE;
                OnAssistEdit=BEGIN
                               UPLOADINTOSTREAM('Select .APP File','C:\','Extension Files|*.app',FileName,FileStream);
                             END;
                              }

    { 11  ;1   ;Field      }

    { 3   ;1   ;Field     ;
                CaptionML=ENU=Deploy Extension;
                ApplicationArea=#Basic,#Suite;
                Style=StrongAccent;
                StyleExpr=TRUE }

    { 6   ;1   ;Field     ;
                CaptionML=ENU=Deploy to;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=DeployTo }

    { 12  ;1   ;Field     ;
                Name=Language;
                CaptionML=ENU=Language;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=LanguageName;
                OnValidate=VAR
                             WinLanguagesTable@1000 : Record 2000000045;
                           BEGIN
                             WinLanguagesTable.SETRANGE(Name,LanguageName);
                             WinLanguagesTable.SETRANGE("Globally Enabled",TRUE);
                             WinLanguagesTable.SETRANGE("Localization Exist",TRUE);
                             IF WinLanguagesTable.FINDFIRST THEN
                               LanguageID := WinLanguagesTable."Language ID"
                             ELSE
                               ERROR(LanguageNotFoundErr,LanguageName);
                           END;

                OnLookup=VAR
                           WinLanguagesTable@1001 : Record 2000000045;
                         BEGIN
                           WinLanguagesTable.SETRANGE("Globally Enabled",TRUE);
                           WinLanguagesTable.SETRANGE("Localization Exist",TRUE);
                           IF PAGE.RUNMODAL(PAGE::"Windows Languages",WinLanguagesTable) = ACTION::LookupOK THEN BEGIN
                             LanguageID := WinLanguagesTable."Language ID";
                             LanguageName := WinLanguagesTable.Name;
                           END;
                         END;
                          }

    { 4   ;1   ;Field     ;
                Name=Disclaimer;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=DisclaimerLbl;
                Editable=False;
                Style=None;
                OnDrillDown=BEGIN
                              MESSAGE(DisclaimerMsg);
                            END;

                ShowCaption=No }

    { 5   ;1   ;Field     ;
                Name=Accepted;
                CaptionML=ENU=Accept;
                ToolTipML=ENU=Specifies that you accept Disclaimer.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=Accepted }

  }
  CODE
  {
    VAR
      FileStream@1004 : InStream;
      DeployTo@1002 : 'Current version,Next minor version,Next major version';
      FileName@1003 : Text;
      LanguageName@1006 : Text;
      LanguageID@1007 : Integer;
      LanguageNotFoundErr@1008 : TextConst '@@@="Error message to notify user that the entered language was not found. This could mean that the language doesn''t exist or that the language is not valid within the filter set for the lookup. %1=Entered value.";ENU=Cannot find the specified language, %1. Choose the lookup button to select a language.';
      ExtensionNotUploadedMsg@1010 : TextConst 'ENU=Please upload an extension file before clicking "Deploy" button.';
      CurrentOperationProgressMsg@1000 : TextConst 'ENU=Extension deployment is in progress. Please check the status page for updates.';
      ScheduledOperationMajorProgressMsg@1001 : TextConst 'ENU=Extension deployment has been scheduled for the next major version. Please check the status page for updates.';
      ScheduledOperationMinorProgressMsg@1005 : TextConst 'ENU=Extension deployment has been scheduled for the next minor version. Please check the status page for updates.';
      DisclaimerLbl@1009 : TextConst 'ENU=Disclaimer';
      DisclaimerMsg@1012 : TextConst 'ENU=The creator of this customized extension is responsible for its licensing. The customized extension is subject to the terms and conditions, privacy policy, support and billing offered by the creator, as applicable, and does not create any liability or obligation for Microsoft.';
      Accepted@1011 : Boolean;

    BEGIN
    END.
  }
}

