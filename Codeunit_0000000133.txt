OBJECT Codeunit 133 Send Incoming Document to OCR
{
  OBJECT-PROPERTIES
  {
    Date=09/19/18;
    Time=12:00:00 PM;
    Version List=NAVW113.00;
  }
  PROPERTIES
  {
    TableNo=130;
    OnRun=BEGIN
            SendDocToOCR(Rec);
          END;

  }
  CODE
  {
    VAR
      OCRWhenApprovalIsCompleteErr@1000 : TextConst 'ENU=The document can only be sent to the OCR service when the approval process is complete.;ESM=El documento solo se puede enviar al servicio OCR una vez completado el proceso de aprobaci¢n.;FRC=Le document ne peut ˆtre envoy‚ au service OCR que lorsque le processus d''approbation est termin‚.;ENC=The document can only be sent to the OCR service when the approval process is complete.';
      NoOcrAttachmentErr@1001 : TextConst 'ENU=There is no attachment of type PDF or Image.;ESM=Ning£n archivo adjunto de tipo PDF o imagen.;FRC=Il n''existe pas de piŠce jointe de type PDF ou image.;ENC=There is no attachment of type PDF or Image.';
      NoAttachmentMarkedForOcrErr@1002 : TextConst 'ENU=You must select an attachment for use for OCR.;ESM=Debe seleccionar un archivo adjunto que usar para OCR.;FRC=Vous devez s‚lectionner une piŠce jointe adapt‚e au processus OCR.;ENC=You must select an attachment for use for OCR.';
      ShowMessages@1003 : Boolean;
      CannotRemoveFromJobQueueTxt@1004 : TextConst 'ENU=The document cannot be removed from queue since it is already sent.;ESM=No se puede quitar el documento de una cola debido a que ya fue enviado.;FRC=Le document ne peut pas ˆtre supprim‚ de la file d''attente, car il a d‚j… ‚t‚ envoy‚.;ENC=The document cannot be removed from queue since it is already sent.';
      CannotSendDocumentTxt@1005 : TextConst '@@@=%1 Status of the document for example: New, Released, Posted, Created, Rejected...;ENU=The document cannot be sent to the OCR service because its status is %1.;ESM=No se puede enviar el documento al servicio OCR ya que su estado es %1.;FRC=Le document ne peut pas ˆtre envoy‚ au service OCR, car son ‚tat est %1.;ENC=The document cannot be sent to the OCR service because its status is %1.';
      CannotScheduleDocumentTxt@1007 : TextConst '@@@=%1 Status of the document for example: New, Released, Posted, Created, Rejected...;ENU=The document cannot be scheduled for sending to the OCR service because its status is %1.;ESM=No se puede programar el documento para enviarlo al servicio OCR ya que su estado es %1.;FRC=Le document ne peut pas ˆtre programm‚ pour envoi au service OCR, car son ‚tat est %1.;ENC=The document cannot be scheduled for sending to the OCR service because its status is %1.';
      RemovedFromJobQueueTxt@1006 : TextConst 'ENU=The document was successfully removed from Job Queue.;ESM=El documento se ha quitado correctamente de la cola de proyecto.;FRC=Le document a bien ‚t‚ supprim‚ de la file d''attente des travaux.;ENC=The document was successfully removed from Job Queue.';
      DocumentHasBeenScheduledTxt@1008 : TextConst 'ENU=The document has been scheduled for sending to the OCR service.;ESM=El documento se program¢ para enviarlo al servicio OCR.;FRC=Le document a ‚t‚ programm‚ pour envoi au service OCR.;ENC=The document has been scheduled for sending to the OCR service.';
      DoYouWantToSetupOCRQst@1009 : TextConst 'ENU=The OCR service is not enabled.\\Do you want to open the OCR Service Setup window?;ESM=El servicio OCR no est  habilitado.\\¨Quiere abrir la ventana de configuraci¢n del servicio OCR?;FRC=Le service OCR n''est pas activ‚.\\Voulez-vous ouvrir la fenˆtre Configuration service OCRÿ?;ENC=The OCR service is not enabled.\\Do you want to open the OCR Service Setup window?';
      OCRServiceNotEnabledErr@1010 : TextConst 'ENU=The OCR service is not enabled.;ESM=El servicio OCR no est  habilitado.;FRC=Le service OCR n''est pas activ‚.;ENC=The OCR service is not enabled.';

    PROCEDURE SendToJobQueue@55(VAR IncomingDocument@1002 : Record 130);
    BEGIN
      IF NOT VerifySendToOCR(IncomingDocument,CannotScheduleDocumentTxt) THEN
        EXIT;

      IncomingDocument.TESTFIELD("OCR Status",IncomingDocument."OCR Status"::" ");
      IncomingDocument."OCR Status" := IncomingDocument."OCR Status"::Ready;
      IncomingDocument.MODIFY;

      CODEUNIT.RUN(CODEUNIT::"Release Incoming Document",IncomingDocument);
      ShowMessage(DocumentHasBeenScheduledTxt);
      OnAfterIncomingDocReadyForOCR(IncomingDocument);
    END;

    [External]
    PROCEDURE RemoveFromJobQueue@41(VAR IncomingDocument@1000 : Record 130);
    VAR
      ReleaseIncomingDocument@1001 : Codeunit 132;
    BEGIN
      WITH IncomingDocument DO BEGIN
        IF "OCR Status" <> "OCR Status"::Ready THEN BEGIN
          ShowMessage(CannotRemoveFromJobQueueTxt);
          EXIT;
        END;

        "OCR Status" := "OCR Status"::" ";
        ReleaseIncomingDocument.Reopen(IncomingDocument);
        MODIFY;
        ShowMessage(RemovedFromJobQueueTxt);
      END;
    END;

    PROCEDURE SendDocToOCR@31(VAR IncomingDocument@1001 : Record 130);
    VAR
      IncomingDocumentAttachment@1000 : Record 133;
      OCRServiceSetup@1002 : Record 1270;
    BEGIN
      OCRServiceSetup.GET;
      IF NOT OCRServiceSetup.Enabled THEN BEGIN
        IF NOT CONFIRM(DoYouWantToSetupOCRQst) THEN
          EXIT;
        PAGE.RUNMODAL(PAGE::"OCR Service Setup",OCRServiceSetup);
        IF NOT OCRServiceSetup.Enabled THEN
          ERROR(OCRServiceNotEnabledErr);
      END;

      IF NOT VerifySendToOCR(IncomingDocument,CannotSendDocumentTxt) THEN
        EXIT;

      UpdateIncomingDocumentAttachmentForOCR(IncomingDocument);

      CASE IncomingDocument."OCR Status" OF
        IncomingDocument."OCR Status"::" ":
          IncomingDocument."OCR Status" := IncomingDocument."OCR Status"::Ready;
        IncomingDocument."OCR Status"::Ready:
          ;
        ELSE
          EXIT;
      END;

      CODEUNIT.RUN(CODEUNIT::"Release Incoming Document",IncomingDocument);

      IncomingDocument.LOCKTABLE;
      IncomingDocument.FIND;
      // Check OCR Status due to it could be changed by another user in the meantime
      IF IncomingDocument."OCR Status" = IncomingDocument."OCR Status"::Ready THEN BEGIN
        IncomingDocumentAttachment.SETRANGE("Incoming Document Entry No.",IncomingDocument."Entry No.");
        IncomingDocumentAttachment.SETRANGE("Use for OCR",TRUE);
        IF NOT IncomingDocumentAttachment.FINDFIRST THEN
          ERROR(NoAttachmentMarkedForOcrErr);
        IncomingDocumentAttachment.SendToOCR;
        IncomingDocument."OCR Status" := IncomingDocument."OCR Status"::Sent;
        IncomingDocument.MODIFY;
      END;
      COMMIT;
      OnAfterIncomingDocSentToOCR(IncomingDocument);
    END;

    [External]
    PROCEDURE ScheduleJobQueueReceive@12();
    VAR
      OCRServiceSetup@1002 : Record 1270;
    BEGIN
      OCRServiceSetup.ScheduleJobQueueReceive;
    END;

    PROCEDURE RetrieveDocFromOCR@37(VAR IncomingDocument@1002 : Record 130);
    VAR
      IncomingDocumentAttachment@1001 : Record 133;
      OCRServiceMgt@1000 : Codeunit 1294;
      OCRStatus@1003 : Integer;
    BEGIN
      WITH IncomingDocument DO BEGIN
        IF NOT ("OCR Status" IN ["OCR Status"::Sent,"OCR Status"::"Awaiting Verification"]) THEN
          TESTFIELD("OCR Status","OCR Status"::Sent);

        CheckNotCreated;
        LOCKTABLE;
        FIND;
        IncomingDocumentAttachment.SETRANGE("Incoming Document Entry No.","Entry No.");
        IncomingDocumentAttachment.SETRANGE("Use for OCR",TRUE);
        IF IncomingDocumentAttachment.FINDFIRST THEN BEGIN
          OCRStatus := OCRServiceMgt.GetDocumentForAttachment(IncomingDocumentAttachment);
          IF NOT (OCRStatus IN ["OCR Status"::Success,"OCR Status"::Error,"OCR Status"::"Awaiting Verification"]) THEN
            ERROR('');
        END;

        FIND;

        CASE OCRStatus OF
          "OCR Status"::Success:
            SetStatusToReceived(IncomingDocument);
          "OCR Status"::"Awaiting Verification":
            SetStatusToVerify(IncomingDocument);
          "OCR Status"::Error:
            SetStatusToFailed(IncomingDocument);
        END;
      END;
    END;

    [External]
    PROCEDURE SetStatusToReceived@6(VAR IncomingDocument@1000 : Record 130);
    BEGIN
      WITH IncomingDocument DO BEGIN
        FIND;
        IF ("OCR Status" = "OCR Status"::Success) AND "OCR Process Finished" THEN
          EXIT;

        "OCR Status" := "OCR Status"::Success;
        "OCR Process Finished" := TRUE;
        MODIFY;
        COMMIT;

        OnAfterIncomingDocReceivedFromOCR(IncomingDocument);
      END;
    END;

    [External]
    PROCEDURE SetStatusToFailed@8(VAR IncomingDocument@1000 : Record 130);
    BEGIN
      WITH IncomingDocument DO BEGIN
        FIND;
        "OCR Status" := "OCR Status"::Error;
        "OCR Process Finished" := TRUE;
        MODIFY;
        COMMIT;

        OnAfterIncomingDocReceivedFromOCR(IncomingDocument);
      END;
    END;

    [External]
    PROCEDURE SetStatusToVerify@9(VAR IncomingDocument@1000 : Record 130);
    BEGIN
      WITH IncomingDocument DO BEGIN
        FIND;
        "OCR Status" := "OCR Status"::"Awaiting Verification";
        MODIFY;
        COMMIT;
      END;
    END;

    [External]
    PROCEDURE TrySendToOCR@10(VAR IncomingDocument@1000 : Record 130) : Boolean;
    BEGIN
      EXIT(CODEUNIT.RUN(CODEUNIT::"Send Incoming Document to OCR",IncomingDocument));
    END;

    [External]
    PROCEDURE TryRetrieveFromOCR@13(VAR IncomingDocument@1002 : Record 130) : Boolean;
    BEGIN
      EXIT(CODEUNIT.RUN(CODEUNIT::"Retrieve Document From OCR",IncomingDocument));
    END;

    LOCAL PROCEDURE VerifySendToOCR@11(VAR IncomingDocument@1000 : Record 130;ErrorMessage@1002 : Text) : Boolean;
    VAR
      ApprovalsMgmt@1001 : Codeunit 1535;
    BEGIN
      WITH IncomingDocument DO BEGIN
        TESTFIELD(Posted,FALSE);
        CheckNotCreated;

        IF NOT (Status IN [Status::New,Status::Released,Status::"Pending Approval"]) THEN BEGIN
          ShowMessage(STRSUBSTNO(ErrorMessage,FORMAT(Status)));
          EXIT(FALSE);
        END;

        IF "OCR Status" IN ["OCR Status"::Sent,"OCR Status"::Success,"OCR Status"::"Awaiting Verification"] THEN BEGIN
          ShowMessage(STRSUBSTNO(ErrorMessage,FORMAT("OCR Status")));
          EXIT(FALSE);
        END;

        OnCheckIncomingDocSetForOCRRestrictions;

        IF ApprovalsMgmt.IsIncomingDocApprovalsWorkflowEnabled(IncomingDocument) AND (Status = Status::New) THEN
          ERROR(OCRWhenApprovalIsCompleteErr);

        UpdateIncomingDocumentAttachmentForOCR(IncomingDocument);
      END;

      EXIT(TRUE);
    END;

    LOCAL PROCEDURE UpdateIncomingDocumentAttachmentForOCR@7(VAR IncomingDocument@1001 : Record 130);
    VAR
      IncomingDocumentAttachment@1000 : Record 133;
    BEGIN
      WITH IncomingDocument DO BEGIN
        IncomingDocumentAttachment.SETRANGE("Incoming Document Entry No.","Entry No.");
        IncomingDocumentAttachment.SETFILTER(Type,'%1|%2',IncomingDocumentAttachment.Type::PDF,IncomingDocumentAttachment.Type::Image);
        IF IncomingDocumentAttachment.ISEMPTY THEN
          ERROR(NoOcrAttachmentErr);
        TESTFIELD("OCR Service Doc. Template Code");
        IncomingDocumentAttachment.SETRANGE("Use for OCR",TRUE);
        IF IncomingDocumentAttachment.ISEMPTY THEN BEGIN
          IncomingDocumentAttachment.SETRANGE("Use for OCR");
          IncomingDocumentAttachment.SETRANGE("Main Attachment",TRUE);
          IncomingDocumentAttachment.FINDFIRST;
          IncomingDocumentAttachment."Use for OCR" := TRUE;
          IncomingDocumentAttachment.MODIFY;
        END;
      END;
    END;

    [External]
    PROCEDURE SetShowMessages@3(NewShowMessages@1000 : Boolean);
    BEGIN
      ShowMessages := NewShowMessages;
    END;

    LOCAL PROCEDURE ShowMessage@5(MessageText@1000 : Text);
    BEGIN
      IF ShowMessages THEN
        MESSAGE(MessageText);
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterIncomingDocReadyForOCR@4(VAR IncomingDocument@1000 : Record 130);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterIncomingDocSentToOCR@1(VAR IncomingDocument@1000 : Record 130);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterIncomingDocReceivedFromOCR@2(VAR IncomingDocument@1000 : Record 130);
    BEGIN
    END;

    BEGIN
    END.
  }
}

