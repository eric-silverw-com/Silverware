OBJECT Codeunit 6085573 Xml Library
{
  OBJECT-PROPERTIES
  {
    Date=04/11/17;
    Time=[ 1:00:00 AM];
    Version List=DCW17.00.00.4.50;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {

    PROCEDURE CreateXmlDOM@1000000001(VAR XmlDoc@161024012 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlDocument";RootName@1000000002 : Text[30]);
    VAR
      XmlDecl@161024013 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlDeclaration";
      XmlRoot@161024014 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
    BEGIN
      XmlDoc := XmlDoc.XmlDocument;

      XmlDecl := XmlDoc.CreateXmlDeclaration('1.0','UTF-8','yes');

      XmlDoc.PreserveWhitespace := FALSE;
      XmlRoot := XmlDoc.CreateElement(RootName);

      XmlDoc.AppendChild(XmlRoot);
      XmlDoc.InsertBefore(XmlDecl,XmlRoot);
    END;

    PROCEDURE CreateUTF8XmlDOM@1000000006(VAR XmlDoc@161024013 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlDocument";RootName@161024012 : Text[30]);
    VAR
      XmlDecl@161024015 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlDeclaration";
      XmlRoot@161024014 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
    BEGIN
      XmlDoc := XmlDoc.XmlDocument;

      XmlDecl := XmlDoc.CreateXmlDeclaration('1.0','UTF-8','yes');

      XmlDoc.PreserveWhitespace := FALSE;
      XmlRoot := XmlDoc.CreateElement(RootName);

      XmlDoc.AppendChild(XmlRoot);
      XmlDoc.InsertBefore(XmlDecl,XmlRoot);
    END;

    PROCEDURE AppendXmlNode@1000000002(ParentNode@161024012 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";Name@1000000000 : Text[30];Value@1000000002 : Text[1024]);
    VAR
      Node@1000000004 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
    BEGIN
      CreateXmlNode(ParentNode,Name,Value,Node);
    END;

    PROCEDURE CreateXmlNode@1000000000(ParentNode@1000000001 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";Name@1000000000 : Text[30];Value@1000000002 : Text;VAR NewNode@1000000003 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode");
    BEGIN
      CLEAR(NewNode);
      NewNode :=  ParentNode.OwnerDocument.CreateNode('element',Name,'');
      NewNode.InnerText := Value;
      ParentNode.AppendChild(NewNode)
    END;

    PROCEDURE SetXmlNodeAttr@1000000007(VAR Node@1000000002 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";AttrName@1000000001 : Text[30];AttrValue@1000000000 : Text[1024]);
    VAR
      XmlNode@1000000005 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
      XmlAttr@1000000004 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlAttribute";
    BEGIN
      IF AttrValue = '' THEN
        EXIT;

      XmlAttr := Node.OwnerDocument.CreateAttribute(AttrName);
      XmlAttr.Value := AttrValue;
      Node.Attributes.SetNamedItem(XmlAttr);
    END;

    PROCEDURE GetNodeText@1000000021(ParentNode@1000000000 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";Name@1000000001 : Text[1024]) : Text;
    VAR
      Node@1000000002 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
    BEGIN
      IF ISNULL(ParentNode) THEN
        EXIT;

      Node := ParentNode.SelectSingleNode(Name);

      IF ISNULL(Node) THEN
        Node := ParentNode.SelectSingleNode(LOWERCASE(Name));

      IF NOT ISNULL(Node) THEN
        EXIT(Node.InnerText);
    END;

    PROCEDURE GetNodeAttrib@161024012(ParentNode@1000000000 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";NodeName@1000000001 : Text[1024];AttribName@161024012 : Text[30]) : Text[1024];
    VAR
      Node@1000000002 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
    BEGIN
      IF ISNULL(ParentNode) THEN
        EXIT;

      Node := ParentNode.SelectSingleNode(NodeName);
      IF NOT ISNULL(Node) THEN
        EXIT(Node.Attributes.GetNamedItem(AttribName).InnerText);
    END;

    PROCEDURE GetNodeGuid@1160040001(ParentNode@1000000000 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";Name@1000000001 : Text[1024]) ReturnGuid : GUID;
    VAR
      Node@1000000002 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
      GuidText@6085573 : Text;
    BEGIN
      IF ISNULL(ParentNode) THEN
        EXIT;

      GuidText := GetNodeText(ParentNode,Name);
      IF GuidText = '' THEN
        EXIT;

      IF COPYSTR(GuidText,1,1) <> '{' THEN
        GuidText := '{' + GuidText;

      IF COPYSTR(GuidText,STRLEN(GuidText)-1,1) <> '}' THEN
        GuidText := GuidText + '}';

      EVALUATE(ReturnGuid,GuidText);
    END;

    PROCEDURE SaveBase64NodeToFile@161024013(VAR Node@161024014 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";Filename@161024013 : Text[1024]) : Boolean;
    VAR
      Convert@6085573 : DotNet "'mscorlib, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Convert";
      File@6085574 : DotNet "'mscorlib, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.IO.File";
    BEGIN
      IF ISNULL(Node) THEN
        EXIT(FALSE);

      File.WriteAllBytes(Filename,Convert.FromBase64String(Node.InnerText));
      EXIT(File.Exists(Filename));
    END;

    PROCEDURE Bool2Text@1000000037(bool@1000000000 : Boolean) : Text[30];
    BEGIN
      IF bool THEN
        EXIT('true')
      ELSE
        EXIT('false');
    END;

    PROCEDURE Date2Text@1000000003(date@1000000000 : Date) : Text[30];
    BEGIN
      IF date = 0D THEN
        EXIT;
      EXIT(FORMAT(date,0,'<Day,2>-<Month,2>-<Year4>'));
    END;

    PROCEDURE Dec2Text@1000000004(dec@1000000000 : Decimal) : Text[30];
    BEGIN
      EXIT(FORMAT(dec,0,'<Sign><Integer><Decimals><Comma,.>'));
    END;

    PROCEDURE Text2Bool@1000000005(Text@1000000000 : Text[30]) : Boolean;
    BEGIN
      CASE UPPERCASE(Text) OF
        '','FALSE','NO','0':
          EXIT(FALSE);
        ELSE
          EXIT(TRUE);
      END;
    END;

    PROCEDURE Text2Date@1000000023(Text@1000000000 : Text[30]) : Date;
    VAR
      Day@1000000001 : Integer;
      Month@1000000002 : Integer;
      Year@1000000003 : Integer;
    BEGIN
      IF Text = '' THEN
        EXIT(0D);

      EVALUATE(Day,COPYSTR(Text,1,2));
      EVALUATE(Month,COPYSTR(Text,4,2));
      EVALUATE(Year,COPYSTR(Text,7,4));
      EXIT(DMY2DATE(Day,Month,Year));
    END;

    PROCEDURE Text2Time@1000000031(Text@1000000000 : Text[30]) TheTime : Time;
    VAR
      Hours@1000000001 : Integer;
      Minutes@1000000002 : Integer;
      Seconds@1000000003 : Integer;
    BEGIN
      IF Text = '' THEN
        EXIT(0T);

      EVALUATE(TheTime,
        COPYSTR(Text,1,2) + ':' +
        COPYSTR(Text,4,2) + ':'  +
        COPYSTR(Text,7,2));
    END;

    PROCEDURE Text2DateTime@1000000030(Text@1000000000 : Text[30]) : DateTime;
    BEGIN
      IF Text = '' THEN
        EXIT(0DT);

      EXIT(CREATEDATETIME(Text2Date(Text),Text2Time(COPYSTR(Text,12))));
    END;

    PROCEDURE Text2Dec@1000000022(Text@1000000000 : Text[1024]) Dec : Decimal;
    BEGIN
      IF Text = '' THEN
        EXIT(0);

      Text := CONVERTSTR(Text,'.',GetDecSep);
      EVALUATE(Dec,Text);
    END;

    PROCEDURE Text2Int@1000000028(Text@1000000000 : Text[1024]) TheInt : Integer;
    BEGIN
      IF UPPERCASE(Text) IN ['','FALSE','NO','0'] THEN
        EXIT(0);
      EVALUATE(TheInt,Text);
    END;

    PROCEDURE GetDecSep@1() : Text[1];
    BEGIN
      EXIT(COPYSTR(FORMAT(1.1),2,1));
    END;

    PROCEDURE SignDocument@1160040000(XMLFilePath@1160040001 : Text[250];XMLFileOutputPath@1160040002 : Text[250];CertificatePath@1160040003 : Text[250];CertificatePassword@1160040004 : Text[30]);
    VAR
      Cryptography@1160040000 : DotNet "'Akeyto.DocumentCapture.Dynamics.Nav.Shared, Version=4.50.0.0, Culture=neutral, PublicKeyToken=005df25b46c40152'.Akeyto.DocumentCapture.Dynamics.Nav.Shared.Cryptography";
    BEGIN
      Cryptography := Cryptography.Cryptography();
      IF NOT Cryptography.SignXml(XMLFilePath,XMLFileOutputPath,CertificatePath,CertificatePassword) THEN
        ERROR(Cryptography.GetLastError);
      CLEAR(Cryptography)
    END;

    BEGIN
    END.
  }
}

