OBJECT Codeunit 881 OCR - Receive from Service
{
  OBJECT-PROPERTIES
  {
    Date=09/19/18;
    Time=12:00:00 PM;
    Version List=NAVW113.00;
  }
  PROPERTIES
  {
    OnRun=VAR
            IncomingDocument@1000 : Record 130;
            CompanyInformation@1004 : Record 79;
            JobQueueEntry@1002 : Record 472;
            AzureADMgt@1003 : Codeunit 6300;
            JobQueueEntryFound@1001 : Boolean;
          BEGIN
            GetDocuments;
            JobQueueEntryFound :=
              JobQueueEntry.FindJobQueueEntry(JobQueueEntry."Object Type to Run"::Codeunit,CODEUNIT::"OCR - Receive from Service");
            IF JobQueueEntryFound THEN BEGIN
              CompanyInformation.GET;
              IncomingDocument.SETFILTER(
                "OCR Status",'%1|%2',
                IncomingDocument."OCR Status"::Sent,
                IncomingDocument."OCR Status"::"Awaiting Verification");
              IF IncomingDocument.ISEMPTY AND AzureADMgt.IsSaaS AND CompanyInformation."Demo Company" THEN
                JobQueueEntry.SetStatus(JobQueueEntry.Status::"On Hold")
              ELSE
                IF JobQueueEntry.Status = JobQueueEntry.Status::"On Hold" THEN
                  JobQueueEntry.Restart;
            END;
          END;

  }
  CODE
  {
    VAR
      DownloadCountMsg@1000 : TextConst '@@@="%1 = a number, e.g. 0, 1, 4.";ENU=%1 documents have been received.;ESM=Documentos recibidos: %1.;FRC=%1 documents ont ‚t‚ re‡us.;ENC=%1 documents have been received.';
      AwaitingCountMsg@1001 : TextConst '@@@="%1 = a number, e.g. 0, 1, 4.";ENU=You have %1 documents that require you to manually verify the OCR values before the documents can be received.;ESM=Tiene %1 documentos que requieren comprobar manualmente los valores de OCR para que puedan recibirse.;FRC=%1 documents n‚cessitent que vous v‚rifiez manuellement les valeurs OCR avant la r‚ception des documents.;ENC=You have %1 documents that require you to manually verify the OCR values before the documents can be received.';

    PROCEDURE GetDocuments@1();
    VAR
      IncomingDocument@1003 : Record 130;
      OCRServiceMgt@1000 : Codeunit 1294;
      ResultMsg@1004 : Text;
      DownloadedDocCount@1002 : Integer;
      AwaitingDocCount@1001 : Integer;
    BEGIN
      DownloadedDocCount := OCRServiceMgt.GetDocuments('');

      IncomingDocument.SETRANGE("OCR Status",IncomingDocument."OCR Status"::"Awaiting Verification");
      AwaitingDocCount := IncomingDocument.COUNT;

      ResultMsg := STRSUBSTNO(DownloadCountMsg,DownloadedDocCount);
      IF AwaitingDocCount > 0 THEN
        ResultMsg := STRSUBSTNO('%1\\%2',ResultMsg,STRSUBSTNO(AwaitingCountMsg,AwaitingDocCount));
      MESSAGE(ResultMsg);
    END;

    BEGIN
    END.
  }
}

