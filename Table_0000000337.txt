OBJECT Table 337 Reservation Entry
{
  OBJECT-PROPERTIES
  {
    Date=09/19/18;
    Time=12:00:00 PM;
    Version List=NAVW113.00;
  }
  PROPERTIES
  {
    OnDelete=VAR
               ActionMessageEntry@1000 : Record 99000849;
             BEGIN
               ActionMessageEntry.SETCURRENTKEY("Reservation Entry");
               ActionMessageEntry.SETRANGE("Reservation Entry","Entry No.");
               ActionMessageEntry.DELETEALL;
             END;

    CaptionML=[ENU=Reservation Entry;
               ESM=Mov. reserva;
               FRC=êcriture de rÇservation;
               ENC=Reservation Entry];
    LookupPageID=Page497;
    DrillDownPageID=Page497;
  }
  FIELDS
  {
    { 1   ;   ;Entry No.           ;Integer       ;AutoIncrement=Yes;
                                                   CaptionML=[ENU=Entry No.;
                                                              ESM=Nß mov.;
                                                              FRC=N¯ Çcriture;
                                                              ENC=Entry No.] }
    { 2   ;   ;Item No.            ;Code20        ;TableRelation=Item;
                                                   CaptionML=[ENU=Item No.;
                                                              ESM=Nß producto;
                                                              FRC=N¯ d'article;
                                                              ENC=Item No.] }
    { 3   ;   ;Location Code       ;Code10        ;TableRelation=Location;
                                                   CaptionML=[ENU=Location Code;
                                                              ESM=C¢d. almacÇn;
                                                              FRC=Code d'emplacement;
                                                              ENC=Location Code] }
    { 4   ;   ;Quantity (Base)     ;Decimal       ;OnValidate=BEGIN
                                                                Quantity := CalcReservationQuantity;
                                                                "Qty. to Handle (Base)" := "Quantity (Base)";
                                                                "Qty. to Invoice (Base)" := "Quantity (Base)";
                                                              END;

                                                   CaptionML=[ENU=Quantity (Base);
                                                              ESM=Cantidad (base);
                                                              FRC=QuantitÇ (base);
                                                              ENC=Quantity (Base)];
                                                   DecimalPlaces=0:5 }
    { 5   ;   ;Reservation Status  ;Option        ;CaptionML=[ENU=Reservation Status;
                                                              ESM=Estado reserva;
                                                              FRC=êtat de rÇservation;
                                                              ENC=Reservation Status];
                                                   OptionCaptionML=[ENU=Reservation,Tracking,Surplus,Prospect;
                                                                    ESM=Reserva,Seguimiento,Excedente,Perspectiva;
                                                                    FRC=RÇservation,Suivi,Surplus,Perspective;
                                                                    ENC=Reservation,Tracking,Surplus,Prospect];
                                                   OptionString=Reservation,Tracking,Surplus,Prospect }
    { 7   ;   ;Description         ;Text50        ;CaptionML=[ENU=Description;
                                                              ESM=Descripci¢n;
                                                              FRC=Description;
                                                              ENC=Description] }
    { 8   ;   ;Creation Date       ;Date          ;CaptionML=[ENU=Creation Date;
                                                              ESM=Fecha creaci¢n;
                                                              FRC=Date crÇation;
                                                              ENC=Creation Date] }
    { 9   ;   ;Transferred from Entry No.;Integer ;TableRelation="Reservation Entry";
                                                   CaptionML=[ENU=Transferred from Entry No.;
                                                              ESM=Transferido desde nß orden;
                                                              FRC=TransfÇrÇ de l'Çcriture N¯;
                                                              ENC=Transferred from Entry No.] }
    { 10  ;   ;Source Type         ;Integer       ;CaptionML=[ENU=Source Type;
                                                              ESM=Tipo procedencia mov.;
                                                              FRC=Type origine;
                                                              ENC=Source Type] }
    { 11  ;   ;Source Subtype      ;Option        ;CaptionML=[ENU=Source Subtype;
                                                              ESM=Subtipo origen;
                                                              FRC=Sous-type origine;
                                                              ENC=Source Subtype];
                                                   OptionCaptionML=[ENU=0,1,2,3,4,5,6,7,8,9,10;
                                                                    ESM=0,1,2,3,4,5,6,7,8,9,10;
                                                                    FRC=0,1,2,3,4,5,6,7,8,9,10;
                                                                    ENC=0,1,2,3,4,5,6,7,8,9,10];
                                                   OptionString=0,1,2,3,4,5,6,7,8,9,10 }
    { 12  ;   ;Source ID           ;Code20        ;CaptionML=[ENU=Source ID;
                                                              ESM=Id. origen;
                                                              FRC=Code origine;
                                                              ENC=Source ID] }
    { 13  ;   ;Source Batch Name   ;Code10        ;CaptionML=[ENU=Source Batch Name;
                                                              ESM=Nombre secci¢n origen;
                                                              FRC=Nom de lot source;
                                                              ENC=Source Batch Name] }
    { 14  ;   ;Source Prod. Order Line;Integer    ;CaptionML=[ENU=Source Prod. Order Line;
                                                              ESM=L°nea O.P. origen;
                                                              FRC=Ligne bon de production origine;
                                                              ENC=Source Prod. Order Line] }
    { 15  ;   ;Source Ref. No.     ;Integer       ;CaptionML=[ENU=Source Ref. No.;
                                                              ESM=Nß ref. origen;
                                                              FRC=N¯ de rÇfÇrence source;
                                                              ENC=Source Ref. No.] }
    { 16  ;   ;Item Ledger Entry No.;Integer      ;TableRelation="Item Ledger Entry";
                                                   CaptionML=[ENU=Item Ledger Entry No.;
                                                              ESM=Nß mov. producto;
                                                              FRC=N¯ Çcriture article gr. livre;
                                                              ENC=Item Ledger Entry No.];
                                                   Editable=No }
    { 22  ;   ;Expected Receipt Date;Date         ;CaptionML=[ENU=Expected Receipt Date;
                                                              ESM=Fecha recepci¢n esperada;
                                                              FRC=Date rÇception prÇvue;
                                                              ENC=Expected Receipt Date] }
    { 23  ;   ;Shipment Date       ;Date          ;CaptionML=[ENU=Shipment Date;
                                                              ESM=Fecha env°o;
                                                              FRC=Date de livraison;
                                                              ENC=Shipment Date] }
    { 24  ;   ;Serial No.          ;Code50        ;CaptionML=[ENU=Serial No.;
                                                              ESM=Nß serie;
                                                              FRC=N¯ de sÇrie;
                                                              ENC=Serial No.] }
    { 25  ;   ;Created By          ;Code50        ;TableRelation=User."User Name";
                                                   OnLookup=VAR
                                                              UserMgt@1000 : Codeunit 418;
                                                            BEGIN
                                                              UserMgt.LookupUserID("Created By");
                                                            END;

                                                   TestTableRelation=No;
                                                   DataClassification=EndUserIdentifiableInformation;
                                                   CaptionML=[ENU=Created By;
                                                              ESM=Creado por;
                                                              FRC=CrÇÇ par;
                                                              ENC=Created By] }
    { 27  ;   ;Changed By          ;Code50        ;TableRelation=User."User Name";
                                                   OnLookup=VAR
                                                              UserMgt@1000 : Codeunit 418;
                                                            BEGIN
                                                              UserMgt.LookupUserID("Changed By");
                                                            END;

                                                   TestTableRelation=No;
                                                   DataClassification=EndUserIdentifiableInformation;
                                                   CaptionML=[ENU=Changed By;
                                                              ESM=Cambiado por;
                                                              FRC=ModifiÇ par;
                                                              ENC=Changed By] }
    { 28  ;   ;Positive            ;Boolean       ;CaptionML=[ENU=Positive;
                                                              ESM=Positivo;
                                                              FRC=Positif;
                                                              ENC=Positive];
                                                   Editable=No }
    { 29  ;   ;Qty. per Unit of Measure;Decimal   ;InitValue=1;
                                                   OnValidate=BEGIN
                                                                Quantity := ROUND("Quantity (Base)" / "Qty. per Unit of Measure",0.00001);
                                                              END;

                                                   CaptionML=[ENU=Qty. per Unit of Measure;
                                                              ESM=Cdad. por unidad medida;
                                                              FRC=QuantitÇ par unitÇ de mesure;
                                                              ENC=Qty. per Unit of Measure];
                                                   DecimalPlaces=0:5;
                                                   Editable=No }
    { 30  ;   ;Quantity            ;Decimal       ;CaptionML=[ENU=Quantity;
                                                              ESM=Cantidad;
                                                              FRC=QuantitÇ;
                                                              ENC=Quantity];
                                                   DecimalPlaces=0:5 }
    { 31  ;   ;Action Message Adjustment;Decimal  ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Action Message Entry".Quantity WHERE (Reservation Entry=FIELD(Entry No.),
                                                                                                          Calculation=CONST(Sum)));
                                                   CaptionML=[ENU=Action Message Adjustment;
                                                              ESM=Ajuste mensajes acci¢n;
                                                              FRC=RÇglage de message de tÉche;
                                                              ENC=Action Message Adjustment];
                                                   DecimalPlaces=0:5;
                                                   Editable=No }
    { 32  ;   ;Binding             ;Option        ;CaptionML=[ENU=Binding;
                                                              ESM=Atado;
                                                              FRC=Consolidation;
                                                              ENC=Binding];
                                                   OptionCaptionML=[ENU=" ,Order-to-Order";
                                                                    ESM=" ,Pedido contra pedido";
                                                                    FRC=" ,Commande Ö commande";
                                                                    ENC=" ,Order-to-Order"];
                                                   OptionString=[ ,Order-to-Order];
                                                   Editable=No }
    { 33  ;   ;Suppressed Action Msg.;Boolean     ;CaptionML=[ENU=Suppressed Action Msg.;
                                                              ESM=Mensaje acci¢n suprimido;
                                                              FRC=Message d'action supprimÇ;
                                                              ENC=Suppressed Action Msg.] }
    { 34  ;   ;Planning Flexibility;Option        ;CaptionML=[ENU=Planning Flexibility;
                                                              ESM=Flexib. planificaci¢n;
                                                              FRC=FlexibilitÇ de planification;
                                                              ENC=Planning Flexibility];
                                                   OptionCaptionML=[ENU=Unlimited,None;
                                                                    ESM=Ilimitada,Ninguna;
                                                                    FRC=IllimitÇ,Aucun;
                                                                    ENC=Unlimited,None];
                                                   OptionString=Unlimited,None }
    { 38  ;   ;Appl.-to Item Entry ;Integer       ;CaptionML=[ENU=Appl.-to Item Entry;
                                                              ESM=Liq. por nß orden producto;
                                                              FRC=êcr. article Ö affecter;
                                                              ENC=Appl.-to Item Entry] }
    { 40  ;   ;Warranty Date       ;Date          ;CaptionML=[ENU=Warranty Date;
                                                              ESM=Fecha garant°a;
                                                              FRC=Date de garantie;
                                                              ENC=Warranty Date];
                                                   Editable=No }
    { 41  ;   ;Expiration Date     ;Date          ;CaptionML=[ENU=Expiration Date;
                                                              ESM=Fecha caducidad;
                                                              FRC=Date d'expiration;
                                                              ENC=Expiration Date];
                                                   Editable=No }
    { 50  ;   ;Qty. to Handle (Base);Decimal      ;CaptionML=[ENU=Qty. to Handle (Base);
                                                              ESM=Cdad. a manipular (base);
                                                              FRC=QuantitÇ Ö traiter (base);
                                                              ENC=Qty. to Handle (Base)];
                                                   DecimalPlaces=0:5 }
    { 51  ;   ;Qty. to Invoice (Base);Decimal     ;CaptionML=[ENU=Qty. to Invoice (Base);
                                                              ESM=Cdad. a facturar (base);
                                                              FRC=QuantitÇ Ö facturer (base);
                                                              ENC=Qty. to Invoice (Base)];
                                                   DecimalPlaces=0:5 }
    { 53  ;   ;Quantity Invoiced (Base);Decimal   ;CaptionML=[ENU=Quantity Invoiced (Base);
                                                              ESM=Cdad. facturada (Base);
                                                              FRC=QuantitÇ facturÇe (base);
                                                              ENC=Quantity Invoiced (Base)];
                                                   DecimalPlaces=0:5 }
    { 80  ;   ;New Serial No.      ;Code50        ;CaptionML=[ENU=New Serial No.;
                                                              ESM=Nuevo nß serie;
                                                              FRC=Nouveau n¯ de sÇrie;
                                                              ENC=New Serial No.];
                                                   Editable=No }
    { 81  ;   ;New Lot No.         ;Code50        ;CaptionML=[ENU=New Lot No.;
                                                              ESM=Nuevo nß lote;
                                                              FRC=Nouveau n¯ lot;
                                                              ENC=New Lot No.];
                                                   Editable=No }
    { 900 ;   ;Disallow Cancellation;Boolean      ;CaptionML=[ENU=Disallow Cancelation;
                                                              ESM=No permitir cancelaci¢n;
                                                              FRC=Interdire l'annulation;
                                                              ENC=Disallow Cancellation] }
    { 5400;   ;Lot No.             ;Code50        ;CaptionML=[ENU=Lot No.;
                                                              ESM=Nß lote;
                                                              FRC=N¯ de lot;
                                                              ENC=Lot No.] }
    { 5401;   ;Variant Code        ;Code10        ;TableRelation="Item Variant".Code WHERE (Item No.=FIELD(Item No.));
                                                   CaptionML=[ENU=Variant Code;
                                                              ESM=C¢d. variante;
                                                              FRC=Code variante;
                                                              ENC=Variant Code] }
    { 5811;   ;Appl.-from Item Entry;Integer      ;CaptionML=[ENU=Appl.-from Item Entry;
                                                              ESM=Liq. mov. prod.;
                                                              FRC=êcriture article Ö affecter;
                                                              ENC=Appl.-from Item Entry];
                                                   MinValue=0 }
    { 5817;   ;Correction          ;Boolean       ;CaptionML=[ENU=Correction;
                                                              ESM=Correcci¢n;
                                                              FRC=Correction;
                                                              ENC=Correction] }
    { 6505;   ;New Expiration Date ;Date          ;CaptionML=[ENU=New Expiration Date;
                                                              ESM=Nueva fecha caducidad;
                                                              FRC=Nouvelle date d'expiration;
                                                              ENC=New Expiration Date];
                                                   Editable=No }
    { 6510;   ;Item Tracking       ;Option        ;CaptionML=[ENU=Item Tracking;
                                                              ESM=Seguim. prod.;
                                                              FRC=TraáabilitÇ;
                                                              ENC=Item Tracking];
                                                   OptionCaptionML=[ENU=None,Lot No.,Lot and Serial No.,Serial No.;
                                                                    ESM=Ninguno,Nß lote,Nß serie y lote,Nß serie;
                                                                    FRC=Aucun,N¯ lot,N¯ lot et sÇrie,N¯ sÇrie;
                                                                    ENC=None,Lot No.,Lot and Serial No.,Serial No.];
                                                   OptionString=None,Lot No.,Lot and Serial No.,Serial No.;
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Entry No.,Positive                      ;Clustered=Yes }
    {    ;Source ID,Source Ref. No.,Source Type,Source Subtype,Source Batch Name,Source Prod. Order Line,Reservation Status,Shipment Date,Expected Receipt Date;
                                                   SumIndexFields=Quantity (Base),Quantity,Qty. to Handle (Base),Qty. to Invoice (Base) }
    {    ;Item No.,Variant Code,Location Code     ;MaintainSIFTIndex=No }
    {    ;Item No.,Variant Code,Location Code,Reservation Status,Shipment Date,Expected Receipt Date,Serial No.,Lot No.;
                                                   SumIndexFields=Quantity (Base);
                                                   MaintainSIFTIndex=No }
    {    ;Item No.,Source Type,Source Subtype,Reservation Status,Location Code,Variant Code,Shipment Date,Expected Receipt Date,Serial No.,Lot No.;
                                                   SumIndexFields=Quantity (Base),Quantity;
                                                   MaintainSQLIndex=No;
                                                   MaintainSIFTIndex=No }
    {    ;Item No.,Variant Code,Location Code,Item Tracking,Reservation Status,Lot No.,Serial No.;
                                                   SumIndexFields=Quantity (Base);
                                                   MaintainSQLIndex=No;
                                                   MaintainSIFTIndex=No }
    { No ;Lot No.                                  }
    { No ;Serial No.                               }
  }
  FIELDGROUPS
  {
    { 1   ;DropDown            ;Entry No.,Positive,Item No.,Description,Quantity }
  }
  CODE
  {
    VAR
      Text001@1000 : TextConst 'ENU=Line;ESM=L°nea;FRC=Ligne;ENC=Line';

    [External]
    PROCEDURE TextCaption@1() : Text[255];
    VAR
      ItemLedgEntry@1000 : Record 32;
      SalesLine@1007 : Record 37;
      ReqLine@1006 : Record 246;
      PurchLine@1005 : Record 39;
      ItemJnlLine@1004 : Record 83;
      ProdOrderLine@1003 : Record 5406;
      ProdOrderComp@1002 : Record 5407;
      AssemblyHeader@1011 : Record 900;
      AssemblyLine@1012 : Record 901;
      TransLine@1001 : Record 5741;
      ServLine@1009 : Record 5902;
      JobJnlLine@1010 : Record 210;
      ExtensionTextCaption@1008 : Text[255];
    BEGIN
      CASE "Source Type" OF
        DATABASE::"Item Ledger Entry":
          EXIT(ItemLedgEntry.TABLECAPTION);
        DATABASE::"Sales Line":
          EXIT(SalesLine.TABLECAPTION);
        DATABASE::"Requisition Line":
          EXIT(ReqLine.TABLECAPTION);
        DATABASE::"Purchase Line":
          EXIT(PurchLine.TABLECAPTION);
        DATABASE::"Item Journal Line":
          EXIT(ItemJnlLine.TABLECAPTION);
        DATABASE::"Job Journal Line":
          EXIT(JobJnlLine.TABLECAPTION);
        DATABASE::"Prod. Order Line":
          EXIT(ProdOrderLine.TABLECAPTION);
        DATABASE::"Prod. Order Component":
          EXIT(ProdOrderComp.TABLECAPTION);
        DATABASE::"Assembly Header":
          EXIT(AssemblyHeader.TABLECAPTION);
        DATABASE::"Assembly Line":
          EXIT(AssemblyLine.TABLECAPTION);
        DATABASE::"Transfer Line":
          EXIT(TransLine.TABLECAPTION);
        DATABASE::"Service Line":
          EXIT(ServLine.TABLECAPTION);
        ELSE BEGIN
          OnAfterTextCaption("Source Type",ExtensionTextCaption);
          IF ExtensionTextCaption <> '' THEN
            EXIT(ExtensionTextCaption);
          EXIT(Text001);
        END;
      END;
    END;

    [External]
    PROCEDURE SummEntryNo@3() : Integer;
    BEGIN
      CASE "Source Type" OF
        DATABASE::"Item Ledger Entry":
          EXIT(1);
        DATABASE::"Purchase Line":
          EXIT(11 + "Source Subtype");
        DATABASE::"Requisition Line":
          EXIT(21);
        DATABASE::"Sales Line":
          EXIT(31 + "Source Subtype");
        DATABASE::"Item Journal Line":
          EXIT(41 + "Source Subtype");
        DATABASE::"Job Journal Line":
          EXIT(51 + "Source Subtype");
        DATABASE::"Prod. Order Line":
          EXIT(61 + "Source Subtype");
        DATABASE::"Prod. Order Component":
          EXIT(71 + "Source Subtype");
        DATABASE::"Transfer Line":
          EXIT(101 + "Source Subtype");
        DATABASE::"Service Line":
          EXIT(110);
        DATABASE::"Assembly Header":
          EXIT(141 + "Source Subtype");
        DATABASE::"Assembly Line":
          EXIT(151 + "Source Subtype");
        ELSE
          EXIT(0);
      END;
    END;

    [External]
    PROCEDURE HasSamePointer@24(ReservEntry@1000 : Record 337) : Boolean;
    BEGIN
      EXIT(
        ("Source Type" = ReservEntry."Source Type") AND
        ("Source Subtype" = ReservEntry."Source Subtype") AND
        ("Source ID" = ReservEntry."Source ID") AND
        ("Source Batch Name" = ReservEntry."Source Batch Name") AND
        ("Source Prod. Order Line" = ReservEntry."Source Prod. Order Line") AND
        ("Source Ref. No." = ReservEntry."Source Ref. No."));
    END;

    [External]
    PROCEDURE HasSamePointerWithSpec@58(TrackingSpecification@1000 : Record 336) : Boolean;
    BEGIN
      EXIT(
        ("Source Type" = TrackingSpecification."Source Type") AND
        ("Source Subtype" = TrackingSpecification."Source Subtype") AND
        ("Source ID" = TrackingSpecification."Source ID") AND
        ("Source Batch Name" = TrackingSpecification."Source Batch Name") AND
        ("Source Prod. Order Line" = TrackingSpecification."Source Prod. Order Line") AND
        ("Source Ref. No." = TrackingSpecification."Source Ref. No."));
    END;

    [External]
    PROCEDURE SetPointer@2(RowID@1000 : Text[250]);
    VAR
      ItemTrackingMgt@1001 : Codeunit 6500;
      StrArray@1002 : ARRAY [6] OF Text[100];
    BEGIN
      ItemTrackingMgt.DecomposeRowID(RowID,StrArray);
      EVALUATE("Source Type",StrArray[1]);
      EVALUATE("Source Subtype",StrArray[2]);
      "Source ID" := StrArray[3];
      "Source Batch Name" := StrArray[4];
      EVALUATE("Source Prod. Order Line",StrArray[5]);
      EVALUATE("Source Ref. No.",StrArray[6]);
    END;

    [External]
    PROCEDURE SetPointerFilter@15();
    BEGIN
      SETCURRENTKEY(
        "Source ID","Source Ref. No.","Source Type","Source Subtype",
        "Source Batch Name","Source Prod. Order Line","Reservation Status",
        "Shipment Date","Expected Receipt Date");
      SETRANGE("Source ID","Source ID");
      SETRANGE("Source Ref. No.","Source Ref. No.");
      SETRANGE("Source Type","Source Type");
      SETRANGE("Source Subtype","Source Subtype");
      SETRANGE("Source Batch Name","Source Batch Name");
      SETRANGE("Source Prod. Order Line","Source Prod. Order Line");
    END;

    [External]
    PROCEDURE Lock@4();
    VAR
      Rec2@1000 : Record 337;
    BEGIN
      Rec2.SETCURRENTKEY("Item No.");
      IF "Item No." <> '' THEN
        Rec2.SETRANGE("Item No.","Item No.");
      Rec2.LOCKTABLE;
      IF Rec2.FINDLAST THEN;
    END;

    [External]
    PROCEDURE SetItemData@29(ItemNo@1000 : Code[20];ItemDescription@1001 : Text[50];LocationCode@1002 : Code[10];VariantCode@1003 : Code[10];QtyPerUoM@1004 : Decimal);
    BEGIN
      "Item No." := ItemNo;
      Description := ItemDescription;
      "Location Code" := LocationCode;
      "Variant Code" := VariantCode;
      "Qty. per Unit of Measure" := QtyPerUoM;
    END;

    [External]
    PROCEDURE SetSource@21(SourceType@1000 : Integer;SourceSubtype@1001 : Integer;SourceID@1002 : Code[20];SourceRefNo@1003 : Integer;SourceBatchName@1004 : Code[10];SourceProdOrderLine@1005 : Integer);
    BEGIN
      "Source Type" := SourceType;
      "Source Subtype" := SourceSubtype;
      "Source ID" := SourceID;
      "Source Ref. No." := SourceRefNo;
      "Source Batch Name" := SourceBatchName;
      "Source Prod. Order Line" := SourceProdOrderLine;
    END;

    [External]
    PROCEDURE SetSourceFilter@31(SourceType@1011 : Integer;SourceSubtype@1010 : Integer;SourceID@1009 : Code[20];SourceRefNo@1008 : Integer;SourceKey@1000 : Boolean);
    BEGIN
      IF SourceKey THEN
        SETCURRENTKEY(
          "Source ID","Source Ref. No.","Source Type","Source Subtype",
          "Source Batch Name","Source Prod. Order Line");
      SETRANGE("Source Type",SourceType);
      IF SourceSubtype >= 0 THEN
        SETRANGE("Source Subtype",SourceSubtype);
      SETRANGE("Source ID",SourceID);
      IF SourceRefNo >= 0 THEN
        SETRANGE("Source Ref. No.",SourceRefNo);
    END;

    [External]
    PROCEDURE SetSourceFilter2@9(SourceBatchName@1007 : Code[10];SourceProdOrderLine@1006 : Integer);
    BEGIN
      SETRANGE("Source Batch Name",SourceBatchName);
      SETRANGE("Source Prod. Order Line",SourceProdOrderLine);
    END;

    [External]
    PROCEDURE ClearTracking@20();
    BEGIN
      "Serial No." := '';
      "Lot No." := '';
      "Warranty Date" := 0D;
      "Expiration Date" := 0D;
    END;

    [External]
    PROCEDURE ClearTrackingFilter@14();
    BEGIN
      SETRANGE("Serial No.");
      SETRANGE("Lot No.");
    END;

    [External]
    PROCEDURE CopyTrackingFromItemLedgEntry@25(ItemLedgerEntry@1000 : Record 32);
    BEGIN
      "Serial No." := ItemLedgerEntry."Serial No.";
      "Lot No." := ItemLedgerEntry."Lot No.";

      OnAfterCopyTrackingFromItemLedgEntry(Rec,ItemLedgerEntry);
    END;

    [External]
    PROCEDURE CopyTrackingFromReservEntry@26(ReservationEntry@1000 : Record 337);
    BEGIN
      "Serial No." := ReservationEntry."Serial No.";
      "Lot No." := ReservationEntry."Lot No.";

      OnAfterCopyTrackingFromReservEntry(Rec,ReservationEntry);
    END;

    [External]
    PROCEDURE CopyTrackingFromSpec@19(TrackingSpecification@1000 : Record 336);
    BEGIN
      "Serial No." := TrackingSpecification."Serial No.";
      "Lot No." := TrackingSpecification."Lot No.";
      "Warranty Date" := TrackingSpecification."Warranty Date";
      "Expiration Date" := TrackingSpecification."Expiration Date";

      OnAfterCopyTrackingFromTrackingSpec(Rec,TrackingSpecification);
    END;

    [External]
    PROCEDURE CopyTrackingFromWhseActivLine@27(WarehouseActivityLine@1000 : Record 5767);
    BEGIN
      "Serial No." := WarehouseActivityLine."Serial No.";
      "Lot No." := WarehouseActivityLine."Lot No.";
      "Expiration Date" := WarehouseActivityLine."Expiration Date";

      OnAfterCopyTrackingFromWhseActivLine(Rec,WarehouseActivityLine);
    END;

    [External]
    PROCEDURE SetTrackingFilter@12(SerialNo@1007 : Code[50];LotNo@1006 : Code[50]);
    BEGIN
      SETRANGE("Serial No.",SerialNo);
      SETRANGE("Lot No.",LotNo);
    END;

    [External]
    PROCEDURE SetTrackingFilterBlank@23();
    BEGIN
      SETRANGE("Serial No.",'');
      SETRANGE("Lot No.",'');
    END;

    [External]
    PROCEDURE SetTrackingFilterFromItemJnlLine@8(ItemJournalLine@1000 : Record 83);
    BEGIN
      SETRANGE("Serial No.",ItemJournalLine."Serial No.");
      SETRANGE("Lot No.",ItemJournalLine."Lot No.");

      OnAfterSetTrackingFilterFromItemJnlLine(Rec,ItemJournalLine);
    END;

    [External]
    PROCEDURE SetTrackingFilterFromReservEntry@17(ReservEntry@1000 : Record 337);
    BEGIN
      SETRANGE("Serial No.",ReservEntry."Serial No.");
      SETRANGE("Lot No.",ReservEntry."Lot No.");

      OnAfterSetTrackingFilterFromReservEntry(Rec,ReservEntry);
    END;

    [External]
    PROCEDURE SetTrackingFilterFromSpec@16(TrackingSpecification@1000 : Record 336);
    BEGIN
      SETRANGE("Serial No.",TrackingSpecification."Serial No.");
      SETRANGE("Lot No.",TrackingSpecification."Lot No.");

      OnAfterSetTrackingFilterFromTrackingSpec(Rec,TrackingSpecification);
    END;

    [External]
    PROCEDURE SetTrackingFilterFromWhseSpec@18(WhseItemTrackingLine@1000 : Record 6550);
    BEGIN
      SETRANGE("Serial No.",WhseItemTrackingLine."Serial No.");
      SETRANGE("Lot No.",WhseItemTrackingLine."Lot No.");

      OnAfterSetTrackingFilterFromWhseSpec(Rec,WhseItemTrackingLine);
    END;

    [External]
    PROCEDURE UpdateItemTracking@5();
    VAR
      ItemTrackingMgt@1000 : Codeunit 6500;
    BEGIN
      "Item Tracking" := ItemTrackingMgt.ItemTrackingOption("Lot No.","Serial No.");
    END;

    [External]
    PROCEDURE UpdateActionMessageEntries@13(OldReservEntry@1000 : Record 337);
    VAR
      ActionMessageEntry@1002 : Record 99000849;
      ActionMessageEntry2@1003 : Record 99000849;
      OldReservEntry2@1004 : Record 337;
    BEGIN
      IF OldReservEntry."Reservation Status" = OldReservEntry."Reservation Status"::Surplus THEN BEGIN
        ActionMessageEntry.FilterFromReservEntry(OldReservEntry);
        IF ActionMessageEntry.FINDSET THEN
          REPEAT
            ActionMessageEntry2 := ActionMessageEntry;
            ActionMessageEntry2.TransferFromReservEntry(Rec);
            ActionMessageEntry2.MODIFY;
          UNTIL ActionMessageEntry.NEXT = 0;
        MODIFY;
      END ELSE
        IF OldReservEntry2.GET(OldReservEntry."Entry No.",NOT OldReservEntry.Positive) THEN BEGIN
          IF HasSamePointer(OldReservEntry2) THEN BEGIN
            OldReservEntry2.DELETE;
            DELETE;
          END ELSE
            MODIFY;
        END ELSE
          MODIFY;
    END;

    [External]
    PROCEDURE ClearItemTrackingFields@7();
    BEGIN
      "Lot No." := '';
      "Serial No." := '';
      UpdateItemTracking;
    END;

    LOCAL PROCEDURE CalcReservationQuantity@62() : Decimal;
    VAR
      ReservEntry@1001 : Record 337;
    BEGIN
      IF "Qty. per Unit of Measure" = 1 THEN
        EXIT("Quantity (Base)");

      ReservEntry.SETFILTER("Entry No.",'<>%1',"Entry No.");
      ReservEntry.SetSourceFilter("Source Type","Source Subtype","Source ID","Source Ref. No.",FALSE);
      ReservEntry.SetSourceFilter2("Source Batch Name","Source Prod. Order Line");
      ReservEntry.SETRANGE("Reservation Status","Reservation Status"::Reservation);
      ReservEntry.CALCSUMS("Quantity (Base)",Quantity);
      EXIT(
        ROUND((ReservEntry."Quantity (Base)" + "Quantity (Base)") / "Qty. per Unit of Measure",0.00001) -
        ReservEntry.Quantity);
    END;

    [External]
    PROCEDURE ClearApplFromToItemEntry@6();
    BEGIN
      IF Positive THEN
        "Appl.-to Item Entry" := 0
      ELSE
        "Appl.-from Item Entry" := 0;
    END;

    [External]
    PROCEDURE TestItemFields@10(ItemNo@1000 : Code[20];VariantCode@1001 : Code[10];LocationCode@1002 : Code[10]);
    BEGIN
      TESTFIELD("Item No.",ItemNo);
      TESTFIELD("Variant Code",VariantCode);
      TESTFIELD("Location Code",LocationCode);
    END;

    [External]
    PROCEDURE TrackingExists@11() : Boolean;
    BEGIN
      EXIT(("Serial No." <> '') OR ("Lot No." <> ''));
    END;

    [External]
    PROCEDURE TransferReservations@22(VAR OldReservEntry@1006 : Record 337;ItemNo@1000 : Code[20];VariantCode@1002 : Code[10];LocationCode@1003 : Code[10];TransferAll@1007 : Boolean;TransferQty@1015 : Decimal;QtyPerUOM@1008 : Decimal;SourceType@1009 : Integer;SourceSubtype@1010 : Option;SourceID@1011 : Code[20];SourceBatchName@1012 : Code[10];SourceProdOrderLine@1013 : Integer;SourceRefNo@1014 : Integer);
    VAR
      NewReservEntry@1004 : Record 337;
      CreateReservEntry@1005 : Codeunit 99000830;
      Status@1001 : 'Reservation,Tracking,Surplus,Prospect';
    BEGIN
      IF TransferAll THEN BEGIN
        OldReservEntry.FINDSET;
        OldReservEntry.TESTFIELD("Qty. per Unit of Measure",QtyPerUOM);
        REPEAT
          OldReservEntry.TestItemFields(ItemNo,VariantCode,LocationCode);

          NewReservEntry := OldReservEntry;
          NewReservEntry.SetSource(SourceType,SourceSubtype,SourceID,SourceRefNo,SourceBatchName,SourceProdOrderLine);

          NewReservEntry.UpdateActionMessageEntries(OldReservEntry);
        UNTIL OldReservEntry.NEXT = 0;
      END ELSE
        FOR Status := Status::Reservation TO Status::Prospect DO BEGIN
          IF TransferQty = 0 THEN
            EXIT;
          OldReservEntry.SETRANGE("Reservation Status",Status);
          IF OldReservEntry.FINDSET THEN
            REPEAT
              OldReservEntry.TestItemFields(ItemNo,VariantCode,LocationCode);

              TransferQty :=
                CreateReservEntry.TransferReservEntry(
                  SourceType,SourceSubtype,SourceID,SourceBatchName,SourceProdOrderLine,SourceRefNo,
                  QtyPerUOM,OldReservEntry,TransferQty);
            UNTIL (OldReservEntry.NEXT = 0) OR (TransferQty = 0);
        END;
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterCopyTrackingFromItemLedgEntry@36(VAR ReservationEntry@1000 : Record 337;ItemLedgerEntry@1001 : Record 32);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterCopyTrackingFromReservEntry@37(VAR ReservationEntry@1000 : Record 337;FromReservationEntry@1001 : Record 337);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterCopyTrackingFromTrackingSpec@38(VAR ReservationEntry@1000 : Record 337;TrackingSpecification@1001 : Record 336);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterCopyTrackingFromWhseActivLine@39(VAR ReservationEntry@1000 : Record 337;WarehouseActivityLine@1001 : Record 5767);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterSetTrackingFilterFromItemJnlLine@28(VAR ReservationEntry@1000 : Record 337;ItemJournalLine@1001 : Record 83);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterSetTrackingFilterFromReservEntry@32(VAR ReservationEntry@1000 : Record 337;FromReservationEntry@1001 : Record 337);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterSetTrackingFilterFromTrackingSpec@33(VAR ReservationEntry@1000 : Record 337;TrackingSpecification@1001 : Record 336);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterSetTrackingFilterFromWhseSpec@34(VAR ReservationEntry@1000 : Record 337;WhseItemTrackingLine@1001 : Record 6550);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterTextCaption@48(SourceType@1000 : Integer;VAR NewTextCaption@1001 : Text[255]);
    BEGIN
    END;

    BEGIN
    END.
  }
}

