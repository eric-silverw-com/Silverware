OBJECT Codeunit 92 Purch.-Post + Print
{
  OBJECT-PROPERTIES
  {
    Date=11/27/18;
    Time=11:07:51 PM;
    Version List=;
  }
  PROPERTIES
  {
    TableNo=38;
    OnRun=VAR
            PurchHeader@1000 : Record 38;
          BEGIN
            PurchHeader.COPY(Rec);
            Code(PurchHeader);
            Rec := PurchHeader;
          END;

  }
  CODE
  {
    VAR
      ReceiveInvoiceQst@1000 : TextConst 'ENU=&Receive,&Invoice,Receive &and Invoice';
      PostAndPrintQst@1001 : TextConst '@@@="%1 = Document Type";ENU=Do you want to post and print the %1?';
      ShipInvoiceQst@1002 : TextConst 'ENU=&Ship,&Invoice,Ship &and Invoice';

    LOCAL PROCEDURE Code@2(VAR PurchHeader@1002 : Record 38);
    VAR
      PurchSetup@1000 : Record 312;
      PurchasePostViaJobQueue@1001 : Codeunit 98;
      HideDialog@1003 : Boolean;
      IsHandled@1004 : Boolean;
    BEGIN
      HideDialog := FALSE;
      IsHandled := FALSE;
      OnBeforeConfirmPost(PurchHeader,HideDialog,IsHandled);
      IF IsHandled THEN
        EXIT;

      IF NOT HideDialog THEN
        IF NOT ConfirmPost(PurchHeader) THEN
          EXIT;

      OnAfterConfirmPost(PurchHeader);

      PurchSetup.GET;
      IF PurchSetup."Post & Print with Job Queue" THEN
        PurchasePostViaJobQueue.EnqueuePurchDoc(PurchHeader)
      ELSE BEGIN
        CODEUNIT.RUN(CODEUNIT::"Purch.-Post",PurchHeader);
        GetReport(PurchHeader);
      END;

      OnAfterPost(PurchHeader);
    END;

    LOCAL PROCEDURE ConfirmPost@9(VAR PurchHeader@1000 : Record 38) : Boolean;
    VAR
      Selection@1001 : Integer;
    BEGIN
      WITH PurchHeader DO BEGIN
        CASE "Document Type" OF
          "Document Type"::Order:
            BEGIN
              Selection := STRMENU(ReceiveInvoiceQst,3);
              IF Selection = 0 THEN
                EXIT(FALSE);
              Receive := Selection IN [1,3];
              Invoice := Selection IN [2,3];
            END;
          "Document Type"::"Return Order":
            BEGIN
              Selection := STRMENU(ShipInvoiceQst,3);
              IF Selection = 0 THEN
                EXIT(FALSE);
              Ship := Selection IN [1,3];
              Invoice := Selection IN [2,3];
            END
          ELSE
            IF NOT CONFIRM(PostAndPrintQst,FALSE,"Document Type") THEN
              EXIT(FALSE);
        END;
        "Print Posted Documents" := TRUE;
      END;
      EXIT(TRUE);
    END;

    [External]
    PROCEDURE GetReport@3(VAR PurchHeader@1000 : Record 38);
    BEGIN
      WITH PurchHeader DO
        CASE "Document Type" OF
          "Document Type"::Order:
            BEGIN
              IF Receive THEN
                PrintReceive(PurchHeader);
              IF Invoice THEN
                PrintInvoice(PurchHeader);
            END;
          "Document Type"::Invoice:
            PrintInvoice(PurchHeader);
          "Document Type"::"Return Order":
            BEGIN
              IF Ship THEN
                PrintShip(PurchHeader);
              IF Invoice THEN
                PrintCrMemo(PurchHeader);
            END;
          "Document Type"::"Credit Memo":
            PrintCrMemo(PurchHeader);
        END;
    END;

    LOCAL PROCEDURE PrintReceive@1(PurchHeader@1000 : Record 38);
    VAR
      PurchRcptHeader@1001 : Record 120;
    BEGIN
      PurchRcptHeader."No." := PurchHeader."Last Receiving No.";
      PurchRcptHeader.SETRECFILTER;
      PurchRcptHeader.PrintRecords(FALSE);
    END;

    LOCAL PROCEDURE PrintInvoice@4(PurchHeader@1000 : Record 38);
    VAR
      PurchInvHeader@1001 : Record 122;
    BEGIN
      IF PurchHeader."Last Posting No." = '' THEN
        PurchInvHeader."No." := PurchHeader."No."
      ELSE
        PurchInvHeader."No." := PurchHeader."Last Posting No.";
      PurchInvHeader.SETRECFILTER;
      PurchInvHeader.PrintRecords(FALSE);
    END;

    LOCAL PROCEDURE PrintShip@5(PurchHeader@1000 : Record 38);
    VAR
      ReturnShptHeader@1001 : Record 6650;
    BEGIN
      ReturnShptHeader."No." := PurchHeader."Last Return Shipment No.";
      ReturnShptHeader.SETRECFILTER;
      ReturnShptHeader.PrintRecords(FALSE);
    END;

    LOCAL PROCEDURE PrintCrMemo@6(PurchHeader@1000 : Record 38);
    VAR
      PurchCrMemoHdr@1001 : Record 124;
    BEGIN
      IF PurchHeader."Last Posting No." = '' THEN
        PurchCrMemoHdr."No." := PurchHeader."No."
      ELSE
        PurchCrMemoHdr."No." := PurchHeader."Last Posting No.";
      PurchCrMemoHdr.SETRECFILTER;
      PurchCrMemoHdr.PrintRecords(FALSE);
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterPost@8(VAR PurchaseHeader@1000 : Record 38);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterConfirmPost@10(PurchaseHeader@1000 : Record 38);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeConfirmPost@7(VAR PurchaseHeader@1000 : Record 38;VAR HideDialog@1001 : Boolean;VAR IsHandled@1002 : Boolean);
    BEGIN
    END;

    BEGIN
    END.
  }
}

