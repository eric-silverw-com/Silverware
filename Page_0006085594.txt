OBJECT Page 6085594 Document Card
{
  OBJECT-PROPERTIES
  {
    Date=08/03/18;
    Time=[ 1:00:00 AM];
    Version List=DCW17.00.00.5.00.02;
  }
  PROPERTIES
  {
    CaptionML=ENU=Document Card;
    InsertAllowed=No;
    LinksAllowed=No;
    SourceTable=Table6085590;
    DataCaptionExpr="No." + ' - ' + GetSourceName;
    PageType=Card;
    CardPageID=Document Card;
    PromotedActionCategoriesML=ENU=0,Process,2,Document,Template,Vendor,Customer;
    OnOpenPage=VAR
                 DocCat@1160040000 : Record 6085575;
                 Template@1160040001 : Record 6085579;
                 Company@1160040002 : Record 2000000006;
                 ContiniaCompanySetup@1160040003 : Record 6192773;
                 COLicenseMgt@1160040004 : Codeunit 6192773;
                 AccessMgt@6085573 : Codeunit 6085626;
                 AboutDC@6085574 : Codeunit 6085589;
                 Channel@1000000000 : Code[50];
                 ActivationState@1160040005 : 'NeverActivated,NeedReactivation,Activated,OfflineStarted';
               BEGIN
                 IsUsingPurchOrders := PurchDocMgt.GetIsUsingPurchOrders;

                 ShowMatchLines := TRUE;

                 IF "Document Category Code" <> '' THEN
                   IF DocCat.GET("Document Category Code") THEN BEGIN
                     IF DocCat."Destination Header Table No." = DATABASE::"Purchase Header" THEN
                       ShowMatchLines := IsUsingPurchOrders
                     ELSE BEGIN
                       Template.SETCURRENTKEY("Category Code",Type);
                       Template.SETRANGE("Category Code",DocCat.Code);
                       Template.SETRANGE(Type,Template.Type::Master);
                       IF Template.FINDFIRST THEN
                         ShowMatchLines := Template."Codeunit ID: Show Match" <> 0;
                     END;
                   END;

                 Company.SETFILTER(Name,'<>%1',COMPANYNAME);
                 IF Company.FINDFIRST THEN
                   REPEAT
                     IF AccessMgt.HasAccessToTable(Company.Name,DATABASE::"Continia Company Setup") THEN
                       IF COLicenseMgt.GetProductActivationState(AboutDC.ProductCode,Company.Name) = ActivationState::Activated THEN
                         ShowMoveToCompany := TRUE;
                   UNTIL (Company.NEXT = 0) OR ShowMoveToCompany;
                 ShowNavigate := Status <> Status::Open;

                 Channel := CREATEGUID;
                 IF WebClientMgt.IsWebClient THEN
                   MessageBusSocket := MessageBusSocket.MessageBusSocket(Channel)
                 ELSE
                   EventNotifierSource.ADDTEXT(Channel);

                 CurrPage.CaptureUI.PAGE.SetConfig(CurrPage.Header.PAGE.CAPTION,CurrPage.Lines.PAGE.CAPTION,Channel);

                 IsOpen := TRUE;
               END;

    OnAfterGetRecord=VAR
                       Template@161024012 : Record 6085579;
                     BEGIN
                       SourceID := GetSourceID;
                       SourceName := GetSourceName;
                       HasTemplate := "Template No." <> '';
                       HasSourceID := SourceID <> '';

                       IF IsOpen THEN BEGIN
                         EnableFields;
                         IsOpen := FALSE;
                         IF NOT Template.GET("Template No.") THEN
                           CLEAR(Template);

                         FirstLineHasCaptions := Template."First Table Line Has Captions";
                       END;

                       StatusIsOpen := Status = Status::Open;
                       StatusIsRegistered := Status = Status::Registered;
                     END;

    ActionList=ACTIONS
    {
      { 1000000032;  ;ActionContainer;
                      Name=ActionItems;
                      ActionContainerType=ActionItems }
      { 1000000031;1 ;ActionGroup;
                      Name=Functions;
                      CaptionML=ENU=F&unctions }
      { 161024025;2  ;Action    ;
                      Name=Recognize Fields;
                      ShortCutKey=Ctrl+F9;
                      CaptionML=ENU=&Recognize Fields;
                      Promoted=Yes;
                      Enabled=StatusIsOpen;
                      PromotedIsBig=Yes;
                      Image=SuggestVendorBills;
                      PromotedCategory=Category4;
                      OnAction=VAR
                                 CaptureEngine@1000000000 : Codeunit 6085575;
                                 SourceNo@1000000002 : Code[200];
                               BEGIN
                                 SourceNo := GetSourceID;
                                 CaptureEngine.CaptureDocument(Rec);

                                 IF (GetSourceID = '') THEN
                                   EXIT;

                                 IF (GetSourceID = SourceNo) AND ("Template No." = '') THEN BEGIN
                                     COMMIT;
                                     VALIDATE("Template No.",GetDefaultTemplate(TRUE));
                                     MODIFY(TRUE);
                                     CaptureEngine.CaptureDocument(Rec);
                                 END;

                                 CurrPage.Header.PAGE.UpdatePage; //RTC will drop the relationship between this page and the Header page
                                 CurrPage.CommentsPart.PAGE.UpdatePage; //RTC will drop the relationship between this page and the Comments page
                                 CurrPage.UPDATE(FALSE); // This will restore the dropped relationship

                                 EnableFields;

                                 CurrPage.CaptureUI.PAGE.RedrawValues;
                                 CurrPage.CaptureUI.PAGE.SetSendAllPendingCommands(TRUE);
                                 CurrPage.CaptureUI.PAGE.UpdatePage;

                                 CurrPage.UPDATE(FALSE);
                               END;
                                }
      { 161024023;2  ;Action    ;
                      Name=Match Lines;
                      ShortCutKey=Ctrl+M;
                      CaptionML=ENU=&Match Lines;
                      Promoted=Yes;
                      Visible=ShowMatchLines;
                      Enabled=HasTemplate AND StatusIsOpen;
                      PromotedIsBig=Yes;
                      Image=Splitlines;
                      PromotedCategory=Category4;
                      OnAction=VAR
                                 Template@1000000001 : Record 6085579;
                               BEGIN
                                 TestSourceNoAndTemplNo;

                                 TestStatus;

                                 Template.GET("Template No.");
                                 Template.TESTFIELD("Codeunit ID: Show Match");
                                 CODEUNIT.RUN(Template."Codeunit ID: Show Match",Rec);
                                 FIND('=');
                                 ValidateDocument;
                               END;
                                }
      { 161024022;2  ;Action    ;
                      Name=Validate;
                      ShortCutKey=F7;
                      CaptionML=ENU=&Validate;
                      Enabled=StatusIsOpen;
                      Image=CheckRulesSyntax;
                      PromotedCategory=Category4;
                      OnAction=BEGIN
                                 ValidateDocument;
                               END;
                                }
      { 161024021;2  ;Action    ;
                      Name=Create/Select Template;
                      Ellipsis=Yes;
                      CaptionML=ENU=Crea&te/Select Template;
                      Enabled=StatusIsOpen;
                      Image=ApplyTemplate;
                      OnAction=VAR
                                 Template@161024014 : Record 6085579;
                                 CaptureEngine@161024013 : Codeunit 6085575;
                                 NewTemplateNo@161024012 : Code[20];
                               BEGIN
                                 TestStatus;

                                 NewTemplateNo := Template.CreateNew("Document Category Code","Source Record ID Tree ID",FALSE);
                                 IF (NewTemplateNo <> '') THEN BEGIN
                                   VALIDATE("Template No.",NewTemplateNo);
                                   MODIFY(TRUE);

                                   CaptureEngine.CaptureDocument(Rec);
                                   EnableFields;

                                   CurrPage.Header.PAGE.UpdatePage; //RTC will drop the relationship between this page and the Header page
                                   CurrPage.CommentsPart.PAGE.UpdatePage; //RTC will drop the relationship between this page and the Comments page
                                   CurrPage.UPDATE(FALSE); // This will restore the dropped relationship

                                   EnableFields;

                                   CurrPage.CaptureUI.PAGE.RedrawValues;
                                   CurrPage.CaptureUI.PAGE.SetSendAllPendingCommands(TRUE);
                                 END;
                               END;
                                }
      { 6085580 ;2   ;Action    ;
                      Name=Navigate;
                      CaptionML=[ENU=&Navigate;
                                 ENC=&Navigate];
                      Promoted=Yes;
                      Visible=ShowNavigate;
                      Image=MakeOrder;
                      PromotedCategory=Category4;
                      OnAction=BEGIN
                                 ShowRegisteredDoc;
                               END;
                                }
      { 6085578 ;2   ;Action    ;
                      Name=Move to Company;
                      Ellipsis=Yes;
                      CaptionML=ENU=Move to &Company;
                      Promoted=Yes;
                      Visible=ShowMoveToCompany;
                      Enabled=StatusIsOpen;
                      Image=Export;
                      PromotedCategory=Category4;
                      OnAction=VAR
                                 Document@6085573 : Record 6085590;
                               BEGIN
                                 Document := Rec;
                                 Document.SETRECFILTER;
                                 CODEUNIT.RUN(CODEUNIT::"Doc. - Move to Company",Document);
                                 CurrPage.UPDATE(FALSE);
                               END;
                                }
      { 6085576 ;2   ;Action    ;
                      Name=Template Card;
                      ShortCutKey=Ctrl+S;
                      CaptionML=ENU=&Template Card;
                      RunObject=Page 6085584;
                      RunPageView=SORTING(No.);
                      RunPageLink=No.=FIELD(Template No.),
                                  Category Code=FIELD(Document Category Code);
                      Promoted=Yes;
                      Enabled=HasTemplate;
                      PromotedIsBig=Yes;
                      Image=Edit;
                      PromotedCategory=Category5 }
      { 6085577 ;2   ;Action    ;
                      Name=Add Template Field;
                      CaptionML=ENU=&Add Template Field;
                      Promoted=Yes;
                      Enabled=HasTemplate AND StatusIsOpen;
                      Image=Post;
                      PromotedCategory=Category5;
                      OnAction=VAR
                                 Template@161024012 : Record 6085579;
                               BEGIN
                                 IF Template.GET("Template No.") THEN
                                   IF Template.ManuallyAddTemplField THEN BEGIN
                                     CurrPage.Header.PAGE.UpdatePage; //RTC will drop the relationship between this page and the Header page
                                     CurrPage.CommentsPart.PAGE.UpdatePage; //RTC will drop the relationship between this page and the Comments page
                                     CurrPage.UPDATE(FALSE); // This will restore the dropped relationship

                                     EnableFields;

                                     CurrPage.CaptureUI.PAGE.RedrawValues;
                                     CurrPage.CaptureUI.PAGE.SetSendAllPendingCommands(TRUE);

                                     CurrPage.UPDATE(FALSE);
                                   END;
                               END;
                                }
      { 1160040200;2 ;Action    ;
                      Name=Remove Template Field;
                      CaptionML=ENU=&Remove Template Field;
                      Promoted=Yes;
                      Enabled=HasTemplate AND StatusIsOpen;
                      Image=Delete;
                      PromotedCategory=Category5;
                      OnAction=VAR
                                 Template@1160040000 : Record 6085579;
                               BEGIN
                                 IF Template.GET("Template No.") THEN
                                   IF Template.ManuallyRemoveTemplField THEN BEGIN
                                     CurrPage.Header.PAGE.UpdatePage; //RTC will drop the relationship between this page and the Header page
                                     CurrPage.CommentsPart.PAGE.UpdatePage; //RTC will drop the relationship between this page and the Comments page
                                     CurrPage.UPDATE(FALSE); // This will restore the dropped relationship

                                     EnableFields;

                                     CurrPage.CaptureUI.PAGE.RedrawValues;
                                     CurrPage.CaptureUI.PAGE.SetSendAllPendingCommands(TRUE);

                                     CurrPage.UPDATE(FALSE);
                                   END;
                               END;
                                }
      { 6085575 ;2   ;Action    ;
                      Name=Translations;
                      ShortCutKey=Ctrl+T;
                      CaptionML=ENU=&Translations;
                      Promoted=Yes;
                      Enabled=HasTemplate AND StatusIsOpen;
                      Image=Track;
                      PromotedCategory=Category5;
                      OnAction=VAR
                                 Template@1000000001 : Record 6085579;
                               BEGIN
                                 Template.GET("Template No.");
                                 Template.TESTFIELD("Codeunit ID: Transl. (Doc)");
                                 CODEUNIT.RUN(Template."Codeunit ID: Transl. (Doc)",Rec);
                               END;
                                }
      { 6085592 ;1   ;ActionGroup;
                      Name=Vendor;
                      CaptionML=[ENU=Vendor;
                                 ENC=Vendor] }
      { 6085591 ;2   ;Action    ;
                      Name=Vendor Card;
                      CaptionML=[ENU=Vendor Card;
                                 ENC=Vendor Card];
                      Promoted=Yes;
                      Visible=IsVendor;
                      PromotedIsBig=Yes;
                      Image=Vendor;
                      PromotedCategory=Category6;
                      OnAction=VAR
                                 Vendor@1160040000 : Record 23;
                               BEGIN
                                 IF HasSourceID THEN BEGIN
                                   Vendor.GET(GetSourceID);
                                   PAGE.RUN(PAGE::"Vendor Card",Vendor);
                                 END ELSE
                                   SetAndCreateNewVendor;
                               END;
                                }
      { 6085590 ;2   ;Action    ;
                      Name=Unposted Documents;
                      CaptionML=ENU=&Unposted Documents;
                      Promoted=Yes;
                      Visible=IsVendor;
                      Enabled=HasSourceID;
                      Image=Invoice;
                      PromotedCategory=Category6;
                      OnAction=VAR
                                 PurchHeader@1160040000 : Record 38;
                               BEGIN
                                 PurchHeader.SETRANGE("Pay-to Vendor No.",GetSourceID);
                                 PAGE.RUN(PAGE::"DC Purchase Documents",PurchHeader);
                               END;
                                }
      { 6085589 ;2   ;Action    ;
                      Name=Ledger Entries;
                      CaptionML=[ENU=Ledger E&ntries;
                                 ENC=Ledger E&ntries];
                      Promoted=Yes;
                      Visible=IsVendor;
                      Enabled=HasSourceID;
                      Image=VendorLedger;
                      PromotedCategory=Category6;
                      OnAction=VAR
                                 VendLedgEntry@1160040000 : Record 25;
                               BEGIN
                                 VendLedgEntry.SETCURRENTKEY("Vendor No.");
                                 VendLedgEntry.SETRANGE("Vendor No.",GetSourceID);
                                 PAGE.RUN(PAGE::"Vendor Ledger Entries",VendLedgEntry);
                               END;
                                }
      { 6085588 ;1   ;ActionGroup;
                      Name=Customer;
                      CaptionML=[ENU=Customer;
                                 ENC=Customer] }
      { 6085587 ;2   ;Action    ;
                      Name=Customer Card;
                      CaptionML=ENU=&Customer Card;
                      Promoted=Yes;
                      Visible=IsCustomer;
                      PromotedIsBig=Yes;
                      Image=Customer;
                      PromotedCategory=Category7;
                      OnAction=VAR
                                 Cust@1160040000 : Record 18;
                               BEGIN
                                 IF HasSourceID THEN BEGIN
                                   Cust.GET(GetSourceID);
                                   PAGE.RUN(PAGE::"Customer Card",Cust);
                                 END ELSE
                                   SetAndCreateNewCustomer;
                               END;
                                }
      { 6085573 ;2   ;Action    ;
                      Name=LedgerEntries;
                      CaptionML=[ENU=Ledger E&ntries;
                                 ENC=Ledger E&ntries];
                      Promoted=Yes;
                      Visible=IsCustomer;
                      Enabled=HasSourceID;
                      Image=VendorLedger;
                      PromotedCategory=Category7;
                      OnAction=VAR
                                 CustLedgEntry@1160040000 : Record 21;
                               BEGIN
                                 CustLedgEntry.SETCURRENTKEY("Customer No.");
                                 CustLedgEntry.SETRANGE("Customer No.",GetSourceID);
                                 PAGE.RUN(PAGE::"Customer Ledger Entries",CustLedgEntry);
                               END;
                                }
      { 1000000025;1 ;ActionGroup;
                      Name=Registration;
                      CaptionML=ENU=&Registration }
      { 1000000024;2 ;Action    ;
                      Name=Register;
                      ShortCutKey=F9;
                      CaptionML=ENU=&Register;
                      Promoted=Yes;
                      Enabled=StatusIsOpen;
                      PromotedIsBig=Yes;
                      Image=Approve;
                      PromotedCategory=Process;
                      OnAction=VAR
                                 Template@1000000001 : Record 6085579;
                               BEGIN
                                 Rec.RegisterYN;

                                 EnableFields;
                                 CurrPage.UPDATE(FALSE);
                               END;
                                }
      { 1000000023;2 ;Action    ;
                      Name=Reject;
                      CaptionML=ENU=Re&ject;
                      Promoted=Yes;
                      Enabled=StatusIsOpen;
                      PromotedIsBig=Yes;
                      Image=Reject;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 Reject;
                                 COMMIT;

                                 EnableFields;
                                 CurrPage.UPDATE(FALSE);
                               END;
                                }
      { 6085574 ;2   ;Action    ;
                      Name=Reopen;
                      CaptionML=ENU=Re&open;
                      Promoted=Yes;
                      Enabled=StatusIsRegistered;
                      PromotedIsBig=Yes;
                      Image=ReOpen;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 Reopen;
                                 COMMIT;

                                 EnableFields;
                                 CurrPage.UPDATE(FALSE);
                               END;
                                }
      { 1160040002;1 ;ActionGroup;
                      Name=Document;
                      CaptionML=ENU=Document;
                      ActionContainerType=NewDocumentItems }
      { 1160040004;2 ;Action    ;
                      Name=Comments;
                      CaptionML=ENU=Co&mments;
                      RunObject=Page 6085581;
                      RunPageLink=Table ID=CONST(6085590),
                                  No.=FIELD(No.);
                      Promoted=Yes;
                      Image=ViewComments;
                      PromotedCategory=Category4 }
      { 1160040001;2 ;Action    ;
                      Name=PDF File;
                      ShortCutKey=Shift+Ctrl+P;
                      CaptionML=ENU=&PDF File;
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=SendAsPDF;
                      PromotedCategory=Category4;
                      OnAction=VAR
                                 Document@1000000000 : Record 6085590;
                               BEGIN
                                 OpenPdfFile;
                               END;
                                }
      { 1160040000;2 ;Action    ;
                      Name=Incoming E-Mail;
                      CaptionML=ENU=Incoming E-Mail;
                      Promoted=Yes;
                      Visible=ShowIncomingEmail;
                      PromotedIsBig=Yes;
                      Image=Email;
                      PromotedCategory=Category4;
                      OnAction=BEGIN
                                 OpenEMailFile;
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1000000000;;Container;
                Name=ContentArea;
                ContainerType=ContentArea }

    { 1000000006;1;Group  ;
                Name=General;
                CaptionML=ENU=General;
                GroupType=Group }

    { 1000000012;2;Group  ;
                Name=Group;
                CaptionML=ENU="";
                GroupType=Group }

    { 1000000018;3;Field  ;
                Name=No.;
                SourceExpr="No.";
                Visible=FALSE }

    { 1000000007;3;Field  ;
                Name=SourceID;
                CaptionML=ENU=Source ID;
                SourceExpr=SourceID;
                CaptionClass='3,' + GetSourceNoCaption;
                Visible=FALSE;
                Editable=AllowEdit;
                OnValidate=VAR
                             CaptureEngine@6085573 : Codeunit 6085575;
                           BEGIN
                             SetManual(TRUE);
                             SetSourceID(SourceID);

                             IF "Template No." = '' THEN
                               ValidateDocument
                             ELSE BEGIN
                               COMMIT;
                               CaptureEngine.CaptureDocument(Rec);
                             END;

                             EnableFields;
                             CurrPage.UPDATE(FALSE);
                           END;

                OnLookup=VAR
                           RecIDMgt@6085573 : Codeunit 6085604;
                           CaptureEngine@6085575 : Codeunit 6085575;
                         BEGIN
                           IF NOT RecIDMgt.LookupRecordID("Document Category Code",Text) THEN
                             EXIT;

                           SetManual(TRUE);
                           SetSourceID(Text);

                           IF "Template No." = '' THEN
                             ValidateDocument
                           ELSE BEGIN
                             COMMIT;
                             CaptureEngine.CaptureDocument(Rec);
                           END;

                           EnableFields;
                           CurrPage.UPDATE(FALSE);
                         END;
                          }

    { 1000000008;3;Field  ;
                Name=Control1000000008;
                SourceExpr=SourceName;
                CaptionClass='3,' + GetSourceNoCaption;
                Editable=AllowEdit;
                OnValidate=VAR
                             CaptureEngine@6085573 : Codeunit 6085575;
                           BEGIN
                             SetManual(TRUE);
                             SetSourceID(SourceName);

                             IF "Template No." = '' THEN
                               ValidateDocument
                             ELSE BEGIN
                               COMMIT;
                               CaptureEngine.CaptureDocument(Rec);
                             END;

                             EnableFields;
                             CurrPage.UPDATE(FALSE);
                           END;

                OnLookup=VAR
                           RecIDMgt@6085574 : Codeunit 6085604;
                           CaptureEngine@6085573 : Codeunit 6085575;
                           Text2@6085575 : Text[250];
                         BEGIN
                           Text2 := GetSourceID;
                           IF NOT RecIDMgt.LookupRecordID("Document Category Code",Text2) THEN
                             EXIT;

                           SetManual(TRUE);
                           SetSourceID(Text2);

                           IF "Template No." = '' THEN
                             ValidateDocument
                           ELSE BEGIN
                             COMMIT;
                             CaptureEngine.CaptureDocument(Rec);
                           END;

                           EnableFields;
                           CurrPage.UPDATE(FALSE);
                         END;
                          }

    { 1000000013;2;Group  ;
                Name=Group2;
                CaptionML=ENU="";
                GroupType=Group }

    { 1000000014;3;Field  ;
                Name=Template No.;
                SourceExpr="Template No.";
                Importance=Promoted;
                Editable=AllowEdit;
                OnValidate=VAR
                             CaptureEngine@6085573 : Codeunit 6085575;
                           BEGIN
                             CurrPage.SAVERECORD;
                             IF "Template No." <> '' THEN BEGIN
                               COMMIT;
                               CaptureEngine.CaptureDocument(Rec);
                             END;
                             EnableFields;

                             CurrPage.UPDATE(FALSE);
                           END;
                            }

    { 1000000009;3;Field  ;
                Name=Template Description;
                SourceExpr="Template Description";
                Importance=Promoted;
                Visible=FALSE }

    { 1000000016;3;Field  ;
                Name=Status;
                SourceExpr=Status;
                Editable=FALSE }

    { 1000000017;3;Field  ;
                Name=OK;
                SourceExpr=OK }

    { 161024013;3;Field   ;
                Name=FirstLineHasCaptions;
                CaptionML=ENU=First Table Line Has Captions;
                SourceExpr=FirstLineHasCaptions;
                OnValidate=VAR
                             Template@161024012 : Record 6085579;
                           BEGIN
                             Template.GET("Template No.");
                             Template."First Table Line Has Captions" := FirstLineHasCaptions;
                             Template.MODIFY;
                           END;
                            }

    { 1000000005;3;Field  ;
                Name=EventNotifier;
                SourceExpr=EventNotifierSource;
                ControlAddIn=[Akeyto.DocumentCapture.Dynamics.Nav.EventNotifier;PublicKeyToken=0c53e51c0afe7001];
                OnControlAddIn=BEGIN
                                 HandleSimpleCommand(Index,CaptureAddinLib.BigTextToText(Data));
                               END;
                                }

    { 1000000015;1;Part   ;
                Name=Header;
                CaptionML=ENU=Header;
                SubPageView=SORTING(Document No.,Sort Order);
                SubPageLink=Document No.=FIELD(No.);
                PagePartID=Page6085595 }

    { 1000000004;1;Part   ;
                Name=Lines;
                CaptionML=ENU=Lines;
                PagePartID=Page6085597 }

    { 1000000002;1;Part   ;
                Name=CommentsPart;
                CaptionML=ENU=Comments;
                SubPageView=SORTING(Document No.);
                SubPageLink=Document No.=FIELD(No.);
                PagePartID=Page6085599;
                PartType=Page }

    { 1000000010;;Container;
                Name=FactBoxArea;
                ContainerType=FactBoxArea }

    { 1000000001;1;Part   ;
                Name=CaptureUI;
                CaptionML=ENU=Document;
                SubPageView=SORTING(No.);
                SubPageLink=No.=FIELD(No.);
                PagePartID=Page6085593 }

  }
  CODE
  {
    VAR
      DCSetup@1000000013 : Record 6085573;
      PurchDocMgt@1160040003 : Codeunit 6085709;
      CaptureMgnt@1000000011 : Codeunit 6085576;
      CaptureAddinLib@1000000001 : Codeunit 6085660;
      WebClientMgt@1000 : Codeunit 6085662;
      MessageBusSocket@1001 : DotNet "'Continia.DocumentCapture.Dynamics.Nav.MessageBus, Version=5.0.0.0, Culture=neutral, PublicKeyToken=0c53e51c0afe7001'.Continia.DocumentCapture.Dynamics.Nav.MessageBus.MessageBusSocket" WITHEVENTS;
      SourceID@6085573 : Text[250];
      SourceName@6085577 : Text[250];
      EventNotifierSource@1000000000 : BigText;
      IsOpen@1000000010 : Boolean;
      AllowEdit@1000000008 : Boolean INDATASET;
      FirstLineHasCaptions@161024012 : Boolean;
      ShowFirstLineHasCaptions@161024013 : Boolean INDATASET;
      IsVendor@6085575 : Boolean INDATASET;
      IsCustomer@6085574 : Boolean INDATASET;
      IsUsingPurchOrders@1160040002 : Boolean INDATASET;
      ShowMatchLines@1160040001 : Boolean INDATASET;
      HasTemplate@1160040000 : Boolean INDATASET;
      HasSourceID@1160040004 : Boolean INDATASET;
      SourceIDText@1160040006 : TextConst 'ENU=Source ID';
      SourceNameText@1160040005 : TextConst 'ENU=Source Name';
      ShowMoveToCompany@1160040007 : Boolean INDATASET;
      ShowNavigate@1160040008 : Boolean INDATASET;
      ShowIncomingEmail@6085576 : Boolean INDATASET;
      StatusIsOpen@6085578 : Boolean INDATASET;
      StatusIsRegistered@6085579 : Boolean INDATASET;

    LOCAL PROCEDURE EnableFields@1000000017();
    VAR
      EmptyGuid@6085573 : GUID;
    BEGIN
      AllowEdit := Status = Status::Open;

      CurrPage.Header.PAGE.UpdatePage;
      CurrPage.CommentsPart.PAGE.UpdatePage;


      CurrPage.Lines.PAGE.SetDocument(Rec);
      CurrPage.Lines.PAGE.UpdatePage;

      IsVendor := GetSourceTableNo = DATABASE::Vendor;
      IsCustomer := GetSourceTableNo = DATABASE::Customer;

      ShowNavigate := Status <> Status::Open;
      ShowIncomingEmail := "E-Mail GUID" <> EmptyGuid;
    END;

    LOCAL PROCEDURE HandleSimpleCommand@1160040000(Index@1002 : Integer;Data@1003 : Text);
    VAR
      XmlLib@1001 : Codeunit 6085573;
      Command@1000 : Code[1024];
    BEGIN
      Command := Data;
      CASE Command OF
        'UPDATEDOCUMENT':;
        'UPDATEHEADER':
          BEGIN
            FIND('=');
            ValidateDocument;
            CurrPage.Header.PAGE.UpdatePage; //RTC will drop the relationship between this page and the Header page
            CurrPage.CommentsPart.PAGE.UpdatePage; //RTC will drop the relationship between this page and the Comments page
            CurrPage.UPDATE(FALSE); // This will restore the dropped relationship
          END;
        'UPDATELINE':
          BEGIN
            CurrPage.Lines.PAGE.UpdatePage;
            IF CurrPage.Lines.PAGE.HasLines THEN
              CurrPage.UPDATE(FALSE);
          END;
      END;
    END;

    LOCAL PROCEDURE GetSourceIDCaption@1160040001() Caption : Text[250];
    VAR
      DocCat@1160040001 : Record 6085575;
      AllObjWithCaption@1160040000 : Record 2000000058;
      RecIDMgt@1160040002 : Codeunit 6085604;
    BEGIN
      Caption := SourceIDText;
      IF DocCat.GET("Document Category Code") THEN
        IF DocCat."Source Table No." <> 0 THEN
          Caption := RecIDMgt.GetObjectCaption(AllObjWithCaption."Object Type"::Table,DocCat."Source Table No.");
    END;

    LOCAL PROCEDURE GetSourceNameCaption@1160040002() Caption : Text[250];
    VAR
      DocCat@1160040002 : Record 6085575;
      AllObjWithCaption@1160040001 : Record 2000000058;
      RecIDMgt@1160040000 : Codeunit 6085604;
    BEGIN
      Caption := SourceNameText;
      IF DocCat.GET("Document Category Code") THEN
        IF (DocCat."Source Table No." <> 0) AND (DocCat."Source Field No. (Name)" <> 0) THEN
          Caption := RecIDMgt.GetFieldCaption(DocCat."Source Table No.",DocCat."Source Field No. (Name)");
    END;

    EVENT MessageBusSocket@1001::OnMessageReceived@11(type@1001 : Integer;message@1000 : Text);
    BEGIN
      HandleSimpleCommand(type,message);
    END;

    BEGIN
    END.
  }
}

