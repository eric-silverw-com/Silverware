OBJECT Table 5650 Total Value Insured
{
  OBJECT-PROPERTIES
  {
    Date=11/27/18;
    Time=11:15:02 PM;
    Version List=;
  }
  PROPERTIES
  {
    CaptionML=ENU=Total Value Insured;
  }
  FIELDS
  {
    { 1   ;   ;FA No.              ;Code20        ;TableRelation="Fixed Asset";
                                                   CaptionML=ENU=FA No. }
    { 2   ;   ;Insurance No.       ;Code20        ;TableRelation=Insurance;
                                                   CaptionML=ENU=Insurance No. }
    { 3   ;   ;Description         ;Text50        ;CaptionML=ENU=Description }
    { 4   ;   ;Total Value Insured ;Decimal       ;CaptionML=ENU=Total Value Insured;
                                                   AutoFormatType=1 }
  }
  KEYS
  {
    {    ;FA No.,Insurance No.                    ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      TempInsTotValueInsured@1000 : TEMPORARY Record 5650;

    [External]
    PROCEDURE CreateInsTotValueInsured@4(FANo@1000 : Code[20]);
    VAR
      InsCoverageLedgEntry@1001 : Record 5629;
      InsTotValueInsured2@1002 : Record 5650;
      Insurance@1003 : Record 5628;
    BEGIN
      TempInsTotValueInsured.DELETEALL;
      CLEAR(TempInsTotValueInsured);
      InsCoverageLedgEntry.SETCURRENTKEY("FA No.","Insurance No.");
      InsCoverageLedgEntry.SETRANGE("FA No.",FANo);
      IF InsCoverageLedgEntry.FIND('-') THEN
        REPEAT
          IF NOT InsCoverageLedgEntry."Disposed FA" THEN BEGIN
            InsTotValueInsured2.INIT;
            InsTotValueInsured2."FA No." := InsCoverageLedgEntry."FA No.";
            InsTotValueInsured2."Insurance No." := InsCoverageLedgEntry."Insurance No.";
            InsTotValueInsured2."Total Value Insured" := InsCoverageLedgEntry.Amount;
            TempInsTotValueInsured := InsTotValueInsured2;
            IF TempInsTotValueInsured.FIND THEN BEGIN
              TempInsTotValueInsured."Total Value Insured" :=
                TempInsTotValueInsured."Total Value Insured" + InsTotValueInsured2."Total Value Insured";
              TempInsTotValueInsured.MODIFY;
            END ELSE BEGIN
              Insurance.GET(InsCoverageLedgEntry."Insurance No.");
              TempInsTotValueInsured.Description := Insurance.Description;
              TempInsTotValueInsured.INSERT;
            END;
          END;
        UNTIL InsCoverageLedgEntry.NEXT = 0;
    END;

    [External]
    PROCEDURE FindFirst@2(SearchString@1000 : Text[3]) : Boolean;
    BEGIN
      TempInsTotValueInsured := Rec;
      IF NOT TempInsTotValueInsured.FIND(SearchString) THEN
        EXIT(FALSE);
      Rec := TempInsTotValueInsured;
      EXIT(TRUE);
    END;

    [External]
    PROCEDURE FindNext@3(NextStep@1000 : Integer) : Integer;
    BEGIN
      TempInsTotValueInsured := Rec;
      NextStep := TempInsTotValueInsured.NEXT(NextStep);
      IF NextStep <> 0 THEN
        Rec := TempInsTotValueInsured;
      EXIT(NextStep);
    END;

    BEGIN
    END.
  }
}

