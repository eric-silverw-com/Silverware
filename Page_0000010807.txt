OBJECT Page 10807 Sales Tax Setup Wizard
{
  OBJECT-PROPERTIES
  {
    Date=11/27/18;
    Time=11:12:12 PM;
    Version List=;
  }
  PROPERTIES
  {
    CaptionML=ENU=Sales Tax Setup;
    InsertAllowed=No;
    DeleteAllowed=No;
    LinksAllowed=No;
    SourceTable=Table10807;
    PageType=NavigatePage;
    ShowFilter=No;
    OnOpenPage=BEGIN
                 IF NOT GET THEN BEGIN
                   INIT;
                   Initialize;
                   INSERT;
                 END;
                 LoadTopBanners;
                 ShowIntroStep;
                 SetCityAndCountyVisible;
                 SetDefaultCountry;
               END;

    OnQueryClosePage=VAR
                       AssistedSetup@1001 : Record 1803;
                     BEGIN
                       IF CloseAction = ACTION::OK THEN
                         IF AssistedSetup.GetStatus(PAGE::"Sales Tax Setup Wizard") = AssistedSetup.Status::"Not Completed" THEN
                           IF NOT CONFIRM(NAVNotSetUpQst,FALSE) THEN
                             ERROR('');
                     END;

    ActionList=ACTIONS
    {
      { 8       ;    ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 15      ;1   ;Action    ;
                      Name=Back;
                      CaptionML=ENU=Back;
                      ApplicationArea=#Basic,#Suite;
                      Enabled=BackEnabled;
                      InFooterBar=Yes;
                      Image=PreviousRecord;
                      OnAction=BEGIN
                                 NextStep(TRUE);
                               END;
                                }
      { 14      ;1   ;Action    ;
                      Name=NextStep;
                      CaptionML=ENU=Next;
                      ApplicationArea=#Basic,#Suite;
                      Enabled=NextEnabled;
                      InFooterBar=Yes;
                      Image=NextRecord;
                      OnAction=BEGIN
                                 NextStep(FALSE);
                               END;
                                }
      { 9       ;1   ;Action    ;
                      Name=Finish;
                      CaptionML=ENU=Finish;
                      ApplicationArea=#Basic,#Suite;
                      Enabled=FinishEnabled;
                      InFooterBar=Yes;
                      Image=Approve;
                      OnAction=VAR
                                 AssistedSetup@1004 : Record 1803;
                               BEGIN
                                 StoreSalesTaxSetup;
                                 AssistedSetup.SetStatus(PAGE::"Sales Tax Setup Wizard",AssistedSetup.Status::Completed);
                                 CurrPage.CLOSE;
                                 AssignTaxAreaCode;
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1   ;0   ;Container ;
                ContainerType=ContentArea }

    { 96  ;1   ;Group     ;
                Visible=TopBannerVisible AND NOT (Step = Step::Done);
                Editable=FALSE;
                GroupType=Group }

    { 97  ;2   ;Field     ;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=MediaResourcesStandard."Media Reference";
                Editable=FALSE;
                ShowCaption=No }

    { 98  ;1   ;Group     ;
                Visible=TopBannerVisible AND (Step = Step::Done);
                Editable=FALSE;
                GroupType=Group }

    { 99  ;2   ;Field     ;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=MediaResourcesDone."Media Reference";
                Editable=FALSE;
                ShowCaption=No }

    { 11  ;1   ;Group     ;
                Name=Step1;
                CaptionML=ENU="";
                Visible=Step = Step::Intro;
                GroupType=Group }

    { 18  ;2   ;Group     ;
                Name=Para1.1;
                CaptionML=ENU=Welcome to Sales Tax Setup;
                GroupType=Group }

    { 1020000;3;Group     ;
                Name=Para1.1.1;
                CaptionML=ENU="";
                GroupType=Group;
                InstructionalTextML=ENU=You can create a default tax area code to assign to customers and vendors so that sales tax is automatically calculated in sales or purchase documents. }

    { 21  ;2   ;Group     ;
                Name=Para1.2;
                CaptionML=ENU=Let's go!;
                GroupType=Group;
                InstructionalTextML=ENU=Choose Next to create a default tax group. }

    { 2   ;1   ;Group     ;
                Name=Step2;
                CaptionML=ENU="";
                Visible=Step = Step::TaxGroupCreated;
                GroupType=Group }

    { 1020002;2;Group     ;
                Name=Para2.1;
                CaptionML=ENU=Default tax group created;
                GroupType=Group }

    { 1020001;3;Group     ;
                Name=Para2.1.1;
                CaptionML=ENU="";
                GroupType=Group;
                InstructionalTextML=ENU=Tax Group of TAXABLE has been created. You will need to assign this group to your items that are taxable. }

    { 4   ;1   ;Group     ;
                Name=Step3;
                CaptionML=ENU="";
                Visible=Step = Step::TaxAccounts;
                GroupType=Group }

    { 23  ;2   ;Group     ;
                Name=Para3.1;
                CaptionML=ENU=Select which accounts you want to use with this tax group.;
                GroupType=Group }

    { 5   ;3   ;Field     ;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Tax Account (Sales)";
                OnValidate=BEGIN
                             NextEnabled := ("Tax Account (Sales)" <> '') OR ("Tax Account (Purchases)" <> '');
                           END;
                            }

    { 1020003;3;Field     ;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Tax Account (Purchases)";
                OnValidate=BEGIN
                             NextEnabled := ("Tax Account (Sales)" <> '') OR ("Tax Account (Purchases)" <> '');
                           END;
                            }

    { 6   ;1   ;Group     ;
                Name=Step4;
                CaptionML=ENU="";
                Visible=Step = Step::TaxRates;
                GroupType=Group }

    { 1020009;2;Group     ;
                Name=Para4.1;
                CaptionML=[ENU="Enter the tax information for your area; then click next."];
                GroupType=Group }

    { 1020016;3;Group     ;
                Name=Para4.1.1;
                CaptionML=ENU="";
                Visible=CityAndCountyVisible;
                GroupType=Group;
                InstructionalTextML=ENU=Enter your city tax information }

    { 13  ;4   ;Field     ;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=City }

    { 1020004;4;Field     ;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="City Rate";
                OnValidate=BEGIN
                             VALIDATE(City);
                           END;
                            }

    { 1020010;3;Group     ;
                Name=Para4.1.2;
                CaptionML=ENU="";
                Visible=CityAndCountyVisible;
                GroupType=Group;
                InstructionalTextML=ENU=Enter your county tax information;
                Layout=Rows }

    { 1020005;4;Field     ;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=County }

    { 1020006;4;Field     ;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="County Rate";
                OnValidate=BEGIN
                             VALIDATE(County);
                           END;
                            }

    { 1020011;3;Group     ;
                Name=Para4.1.3;
                CaptionML=ENU="";
                GroupType=Group;
                InstructionalTextML=ENU=Enter your state tax information;
                Layout=Rows }

    { 1020007;4;Field     ;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=State }

    { 1020008;4;Field     ;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="State Rate";
                OnValidate=BEGIN
                             VALIDATE(State);
                           END;
                            }

    { 1030001;4;Field     ;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Country/Region" }

    { 1020013;1;Group     ;
                Name=Step5;
                CaptionML=ENU="";
                Visible=Step = Step::TaxAreaName;
                GroupType=Group }

    { 1020012;2;Group     ;
                Name=Para5.1;
                CaptionML=ENU=Enter a name for your new tax area;
                GroupType=Group }

    { 1020014;3;Field     ;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Tax Area Code";
                OnValidate=BEGIN
                             "Tax Area Code" := DELCHR("Tax Area Code",'<>',' ');
                             NextEnabled := "Tax Area Code" <> '';
                           END;
                            }

    { 1020022;1;Group     ;
                Name=Step6;
                CaptionML=ENU="";
                Visible=Step = Step::Done;
                GroupType=Group }

    { 1020021;2;Group     ;
                Name=Para6.1;
                CaptionML=ENU=That's it!;
                GroupType=Group;
                InstructionalTextML=ENU=Choose Finish to create this tax area and assign your customers to the new tax area. }

    { 1020019;3;Field     ;
                Name=AssignToCustomers;
                CaptionML=ENU=To Customers;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=AssignToCustomers }

    { 1020020;3;Field     ;
                Name=AssignToVendors;
                CaptionML=ENU=To Vendors;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=AssignToVendors }

    { 1020015;3;Field     ;
                Name=AssignToLocations;
                CaptionML=ENU=To Locations;
                SourceExpr=AssignToLocations }

    { 1020018;3;Field     ;
                Name=AssignToCompanyInfo;
                CaptionML=ENU=To Company Information;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=AssignToCompanyInfo }

  }
  CODE
  {
    VAR
      MediaRepositoryStandard@1020010 : Record 9400;
      MediaRepositoryDone@1020011 : Record 9400;
      MediaResourcesStandard@1020028 : Record 2000000182;
      MediaResourcesDone@1020029 : Record 2000000182;
      ClientTypeManagement@1020077 : Codeunit 4;
      Step@1015 : 'Intro,TaxGroupCreated,TaxAccounts,TaxRates,TaxAreaName,Done';
      GeneratedName@1020007 : Code[20];
      BackEnabled@1014 : Boolean;
      NextEnabled@1013 : Boolean;
      FinishEnabled@1010 : Boolean;
      TopBannerVisible@1020012 : Boolean;
      NAVNotSetUpQst@1020006 : TextConst 'ENU=Sales tax has not been set up.\\Are you sure that you want to exit?';
      AssignToCustomers@1020000 : Boolean;
      AssignToVendors@1020001 : Boolean;
      AssignToCompanyInfo@1020004 : Boolean;
      AssignToLocations@1020005 : Boolean;
      CityAndCountyVisible@1020013 : Boolean;

    LOCAL PROCEDURE NextStep@3(Backwards@1000 : Boolean);
    BEGIN
      IF Backwards THEN
        Step := Step - 1
      ELSE
        Step := Step + 1;

      CASE Step OF
        Step::Intro:
          ShowIntroStep;
        Step::TaxGroupCreated:
          ShowTaxGroupCreatedStep;
        Step::TaxAccounts:
          ShowTaxAccountsStep;
        Step::TaxRates:
          ShowTaxRatesStep;
        Step::TaxAreaName:
          ShowTaxAreaNameStep;
        Step::Done:
          ShowDoneStep;
      END;
      CurrPage.UPDATE(TRUE);
    END;

    LOCAL PROCEDURE ShowIntroStep@1();
    BEGIN
      ResetWizardControls;
      BackEnabled := FALSE;
    END;

    LOCAL PROCEDURE ShowTaxGroupCreatedStep@2();
    BEGIN
      ResetWizardControls;
      NextEnabled := TRUE;
    END;

    LOCAL PROCEDURE ShowTaxAccountsStep@4();
    BEGIN
      ResetWizardControls;
      NextEnabled := ("Tax Account (Purchases)" <> '') OR ("Tax Account (Sales)" <> '');
    END;

    LOCAL PROCEDURE ShowTaxRatesStep@15();
    BEGIN
      ResetWizardControls;
    END;

    LOCAL PROCEDURE ShowTaxAreaNameStep@1020005();
    BEGIN
      ResetWizardControls;
      IF "Tax Area Code" IN ['',GeneratedName] THEN BEGIN
        GeneratedName := GenerateTaxAreaCode;
        "Tax Area Code" := GeneratedName;
      END;
      NextEnabled := "Tax Area Code" <> '';
    END;

    LOCAL PROCEDURE ShowDoneStep@6();
    BEGIN
      ResetWizardControls;
      NextEnabled := FALSE;
      FinishEnabled := TRUE;
    END;

    LOCAL PROCEDURE ResetWizardControls@10();
    BEGIN
      // Buttons
      BackEnabled := TRUE;
      NextEnabled := TRUE;
      FinishEnabled := FALSE;
    END;

    LOCAL PROCEDURE AssignTaxAreaCode@1020006();
    VAR
      Customer@1020004 : Record 18;
      Vendor@1020003 : Record 23;
      Location@1020006 : Record 14;
      DummyCompanyInformation@1020005 : Record 79;
      AssignTaxAreaToCustomer@1020001 : Report 10326;
      AssignTaxAreaToVendor@1020000 : Report 10327;
      AssignTaxAreaToLocation@1020002 : Report 10328;
    BEGIN
      COMMIT;
      IF AssignToCustomers THEN BEGIN
        AssignTaxAreaToCustomer.SETTABLEVIEW(Customer);
        AssignTaxAreaToCustomer.SetDefaultAreaCode("Tax Area Code");
        AssignTaxAreaToCustomer.RUN;
        COMMIT;
      END;
      IF AssignToVendors THEN BEGIN
        AssignTaxAreaToVendor.SETTABLEVIEW(Vendor);
        AssignTaxAreaToVendor.SetDefaultAreaCode("Tax Area Code");
        AssignTaxAreaToVendor.RUN;
        COMMIT;
      END;
      IF AssignToLocations THEN BEGIN
        AssignTaxAreaToLocation.SETTABLEVIEW(Location);
        AssignTaxAreaToLocation.SetDefaultAreaCode("Tax Area Code");
        AssignTaxAreaToLocation.RUN;
        COMMIT;
      END;
      IF AssignToCompanyInfo AND DummyCompanyInformation.FINDFIRST THEN BEGIN
        DummyCompanyInformation.VALIDATE("Tax Area Code","Tax Area Code");
        DummyCompanyInformation.MODIFY;
        COMMIT;
      END;
    END;

    LOCAL PROCEDURE LoadTopBanners@1020063();
    BEGIN
      IF MediaRepositoryStandard.GET('AssistedSetup-NoText-400px.png',FORMAT(ClientTypeManagement.GetCurrentClientType)) AND
         MediaRepositoryDone.GET('AssistedSetupDone-NoText-400px.png',FORMAT(ClientTypeManagement.GetCurrentClientType))
      THEN
        IF MediaResourcesStandard.GET(MediaRepositoryStandard."Media Resources Ref") AND
           MediaResourcesDone.GET(MediaRepositoryDone."Media Resources Ref")
        THEN
          TopBannerVisible := MediaResourcesDone."Media Reference".HASVALUE;
    END;

    LOCAL PROCEDURE SetCityAndCountyVisible@1020000();
    VAR
      CompanyInformation@1020000 : Record 79;
    BEGIN
      CityAndCountyVisible := NOT CompanyInformation.IsCanada;
    END;

    LOCAL PROCEDURE SetDefaultCountry@1030001();
    VAR
      CompanyInformation@1030000 : Record 79;
    BEGIN
      IF CompanyInformation.IsCanada THEN
        "Country/Region" := "Country/Region"::CA
      ELSE
        "Country/Region" := "Country/Region"::US;
      MODIFY;
    END;

    BEGIN
    END.
  }
}

