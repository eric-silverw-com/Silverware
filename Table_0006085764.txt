OBJECT Table 6085764 Approval Sharing (Comp.)
{
  OBJECT-PROPERTIES
  {
    Date=04/06/18;
    Time=[ 1:00:00 AM];
    Version List=DCW13.70.00.4.50.05;
  }
  PROPERTIES
  {
    Permissions=TableData 6085764=rimd;
    OnInsert=VAR
               ApprovalSharing@1160040000 : Record 6085749;
             BEGIN
               TESTFIELD("Owner User ID");
               TESTFIELD("Shared to User ID");
               IF "Owner User ID" = "Shared to User ID" THEN
                 ERROR(OwnerEqualSharedToErr,FIELDCAPTION("Owner User ID"),FIELDCAPTION("Shared to User ID"),"Owner User ID");

               ApprovalSharing.CHANGECOMPANY("Company Name");
               ApprovalSharing.TRANSFERFIELDS(Rec,TRUE);
               IF NOT ApprovalSharing.INSERT THEN
                 ApprovalSharing.MODIFY;
             END;

    OnModify=VAR
               ApprovalSharing@1160040000 : Record 6085749;
             BEGIN
               IF "Copy to All Companies" THEN BEGIN
                 ApprovalSharing.GET("Owner User ID","Shared to User ID","Sharing Type","Valid From","Valid To");
                 ApprovalSharing.TRANSFERFIELDS(Rec,FALSE);
                 ApprovalSharing.MODIFY(TRUE);
               END ELSE BEGIN
                 ApprovalSharing.CHANGECOMPANY("Company Name");
                 ApprovalSharing.GET("Owner User ID","Shared to User ID","Sharing Type","Valid From","Valid To");
                 ApprovalSharing.TRANSFERFIELDS(Rec,FALSE);
                 ApprovalSharing.MODIFY;
               END;
             END;

    OnDelete=VAR
               ApprovalSharing@1160040000 : Record 6085749;
             BEGIN
               IF "Copy to All Companies" THEN BEGIN
                 ApprovalSharing.GET("Owner User ID","Shared to User ID","Sharing Type","Valid From","Valid To");
                 ApprovalSharing.DELETE(TRUE);
               END ELSE BEGIN
                 ApprovalSharing.CHANGECOMPANY("Company Name");
                 ApprovalSharing.GET("Owner User ID","Shared to User ID","Sharing Type","Valid From","Valid To");
                 ApprovalSharing.DELETE(TRUE);
               END;
             END;

    OnRename=VAR
               ApprovalSharing@1160040000 : Record 6085749;
             BEGIN
               IF "Copy to All Companies" THEN BEGIN
                 ApprovalSharing.GET(xRec."Owner User ID",xRec."Shared to User ID",xRec."Sharing Type",xRec."Valid From",xRec."Valid To");
                 ApprovalSharing.RENAME("Owner User ID","Shared to User ID","Sharing Type","Valid From","Valid To");
               END ELSE BEGIN
                 ApprovalSharing.CHANGECOMPANY("Company Name");
                 ApprovalSharing.GET(xRec."Owner User ID",xRec."Shared to User ID",xRec."Sharing Type",xRec."Valid From",xRec."Valid To");
                 ApprovalSharing.RENAME("Owner User ID","Shared to User ID","Sharing Type","Valid From","Valid To");
               END;
             END;

    CaptionML=ENU=Approval Sharing;
    LookupPageID=Page6086064;
    DrillDownPageID=Page6086064;
  }
  FIELDS
  {
    { 1   ;   ;Owner User ID       ;Code50        ;TableRelation="Continia User Setup";
                                                   OnValidate=BEGIN
                                                                IF "Owner User ID" <> '' THEN
                                                                  IF "Owner User ID" = "Shared to User ID" THEN
                                                                    ERROR(OwnerEqualSharedToErr,FIELDCAPTION("Owner User ID"),FIELDCAPTION("Shared to User ID"),"Owner User ID");

                                                                ValidateUserID("Owner User ID");
                                                              END;

                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=ENU=Owner User ID;
                                                   NotBlank=Yes }
    { 2   ;   ;Shared to User ID   ;Code50        ;TableRelation="Continia User Setup";
                                                   OnValidate=BEGIN
                                                                IF "Shared to User ID" <> '' THEN
                                                                  IF "Owner User ID" = "Shared to User ID" THEN
                                                                    ERROR(OwnerEqualSharedToErr,FIELDCAPTION("Owner User ID"),FIELDCAPTION("Shared to User ID"),"Owner User ID");

                                                                ValidateUserID("Shared to User ID");
                                                              END;

                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=ENU=Shared to User ID;
                                                   NotBlank=Yes }
    { 3   ;   ;Sharing Type        ;Option        ;CaptionML=ENU=Sharing Type;
                                                   OptionCaptionML=ENU=Normal,Out of Office;
                                                   OptionString=Normal,Out of Office }
    { 4   ;   ;Valid From          ;Date          ;CaptionML=ENU=Valid From }
    { 5   ;   ;Valid To            ;Date          ;CaptionML=ENU=Valid To }
    { 6   ;   ;Use Owners Limts & Permissions;Boolean;
                                                   CaptionML=ENU=Use Owners Limts && Permissions }
    { 7   ;   ;Display Order       ;Integer       ;CaptionML=ENU=Display Order }
    { 8   ;   ;Forward E-mails     ;Boolean       ;CaptionML=ENU=Forward E-mails }
    { 9   ;   ;Copy to All Companies;Boolean      ;CaptionML=ENU=Copy to All Companies }
    { 100 ;   ;Company Name        ;Text250       ;CaptionML=ENU=Company Name }
  }
  KEYS
  {
    {    ;Owner User ID,Shared to User ID,Sharing Type,Valid From,Valid To;
                                                   Clustered=Yes }
    {    ;Shared to User ID,Sharing Type,Valid From,Valid To }
    {    ;Display Order                            }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      OwnerEqualSharedToErr@1160040000 : TextConst 'ENU=%1 (%3) must not be equal to %2 (%3).';

    PROCEDURE LookupUserID@1160040001(VAR Text@1160040000 : Text[1024]) : Boolean;
    VAR
      ContiniaUserSetup@1160040001 : Record 6086002;
    BEGIN
      ContiniaUserSetup.CHANGECOMPANY("Company Name");
      IF ContiniaUserSetup.GET(Text) THEN;
      IF PAGE.RUNMODAL(PAGE::"Continia User List 2 (Comp.)",ContiniaUserSetup) = ACTION::LookupOK THEN BEGIN
        Text := ContiniaUserSetup."Continia User ID";
        EXIT(TRUE);
      END;
    END;

    PROCEDURE ValidateUserID@1160040002(_UserId@1160040001 : Code[50]);
    VAR
      ContiniaUserSetup@1160040000 : Record 6086002;
    BEGIN
      ContiniaUserSetup.CHANGECOMPANY("Company Name");
      ContiniaUserSetup.GET(_UserId);
    END;

    BEGIN
    {
      There is a bug in NAV 2009 RTC which prevent cross company lookup from working using CHANGECOMPANY on a record. This effect lookup
      from LookupUserID. For details see comment in Page 6086061 - Continia User Setup by Company.
    }
    END.
  }
}

