OBJECT Table 5338 Integration Synch. Job
{
  OBJECT-PROPERTIES
  {
    Date=11/27/18;
    Time=11:14:50 PM;
    Version List=;
  }
  PROPERTIES
  {
    OnDelete=VAR
               IntegrationSynchJobErrors@1000 : Record 5339;
             BEGIN
               IntegrationSynchJobErrors.SETRANGE("Integration Synch. Job ID",ID);
               IntegrationSynchJobErrors.DELETEALL;
             END;

    CaptionML=ENU=Integration Synch. Job;
  }
  FIELDS
  {
    { 1   ;   ;ID                  ;GUID          ;CaptionML=ENU=ID }
    { 2   ;   ;Start Date/Time     ;DateTime      ;CaptionML=ENU=Start Date/Time }
    { 3   ;   ;Finish Date/Time    ;DateTime      ;CaptionML=ENU=Finish Date/Time }
    { 4   ;   ;Message             ;Text250       ;CaptionML=ENU=Message }
    { 5   ;   ;Integration Table Mapping Name;Code20;
                                                   TableRelation="Integration Table Mapping".Name;
                                                   TestTableRelation=No;
                                                   CaptionML=ENU=Integration Table Mapping Name }
    { 6   ;   ;Inserted            ;Integer       ;CaptionML=ENU=Inserted }
    { 7   ;   ;Modified            ;Integer       ;CaptionML=ENU=Modified }
    { 8   ;   ;Deleted             ;Integer       ;CaptionML=ENU=Deleted }
    { 9   ;   ;Unchanged           ;Integer       ;CaptionML=ENU=Unchanged }
    { 10  ;   ;Skipped             ;Integer       ;CaptionML=ENU=Skipped }
    { 11  ;   ;Failed              ;Integer       ;CaptionML=ENU=Failed }
    { 12  ;   ;Synch. Direction    ;Option        ;CaptionML=ENU=Synch. Direction;
                                                   OptionCaptionML=ENU=Bidirectional,ToIntegrationTable,FromIntegrationTable;
                                                   OptionString=Bidirectional,ToIntegrationTable,FromIntegrationTable }
    { 13  ;   ;Job Queue Log Entry No.;Integer    ;CaptionML=ENU=Job Queue Log Entry No. }
  }
  KEYS
  {
    {    ;ID                                      ;Clustered=Yes }
    {    ;Start Date/Time,ID                       }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      DeleteEntriesQst@1000 : TextConst '@@@="%1 = Integration Synch. Job caption";ENU=Are you sure that you want to delete the %1 entries?';

    [External]
    PROCEDURE DeleteEntries@2(DaysOld@1000 : Integer);
    VAR
      IntegrationSynchJob@1002 : Record 5338;
    BEGIN
      IF NOT CONFIRM(STRSUBSTNO(DeleteEntriesQst,TABLECAPTION)) THEN
        EXIT;
      IntegrationSynchJob.COPY(Rec);
      IntegrationSynchJob.SETFILTER("Finish Date/Time",'<=%1',CREATEDATETIME(TODAY - DaysOld,TIME));
      IntegrationSynchJob.SETRANGE(Failed,0);
      IntegrationSynchJob.DELETEALL;

      IntegrationSynchJob.SETRANGE(Failed);
      IF IntegrationSynchJob.FINDSET THEN
        REPEAT
          IF IntegrationSynchJob.CanBeRemoved THEN
            IntegrationSynchJob.DELETE(TRUE);
        UNTIL IntegrationSynchJob.NEXT = 0;
    END;

    [External]
    PROCEDURE GetErrorForRecordID@1(RecID@1000 : RecordID;VAR IntegrationSynchJobErrors@1001 : Record 5339) : Boolean;
    VAR
      CRMIntegrationManagement@1002 : Codeunit 5330;
    BEGIN
      IF NOT ISNULLGUID(ID) THEN
        IntegrationSynchJobErrors.SETRANGE("Integration Synch. Job ID",ID);
      IF CRMIntegrationManagement.IsCRMTable(RecID.TABLENO) XOR ("Synch. Direction" = "Synch. Direction"::FromIntegrationTable) THEN
        IntegrationSynchJobErrors.SETRANGE("Destination Record ID",RecID)
      ELSE
        IntegrationSynchJobErrors.SETRANGE("Source Record ID",RecID);
      EXIT(IntegrationSynchJobErrors.FINDLAST);
    END;

    [External]
    PROCEDURE AreAllRecordsFailed@3() : Boolean;
    BEGIN
      EXIT(
        (Deleted = 0) AND (Inserted = 0) AND (Modified = 0) AND
        (Unchanged = 0) AND (Skipped = 0) AND (Failed <> 0));
    END;

    [External]
    PROCEDURE HaveJobsBeenIdle@4(JobQueueLogEntryNo@1000 : Integer) : Boolean;
    BEGIN
      RESET;
      SETRANGE("Job Queue Log Entry No.",JobQueueLogEntryNo);
      IF NOT ISEMPTY THEN BEGIN
        CALCSUMS(Inserted,Modified,Deleted,Failed);
        EXIT(Inserted + Modified + Deleted + Failed = 0);
      END;
    END;

    [External]
    PROCEDURE CanBeRemoved@7() AllowRemoval : Boolean;
    BEGIN
      OnCanBeRemoved(Rec,AllowRemoval);
    END;

    [Integration]
    LOCAL PROCEDURE OnCanBeRemoved@8(IntegrationSynchJob@1001 : Record 5338;VAR AllowRemoval@1000 : Boolean);
    BEGIN
    END;

    BEGIN
    END.
  }
}

