OBJECT Codeunit 6086386 Expense - Complete
{
  OBJECT-PROPERTIES
  {
    Date=05/31/18;
    Time=[ 1:00:00 AM];
    Version List=EMW16.00.10.2.60.06;
  }
  PROPERTIES
  {
    TableNo=6086320;
    OnRun=BEGIN
            ShowError := TRUE;
            Code(Rec);
          END;

  }
  CODE
  {
    VAR
      SendingDocCompleted@1160040000 : TextConst 'ENU=Reopening Expense %1...';
      ShowError@1160040001 : Boolean;
      NewerVersionExists@1160040002 : TextConst 'ENU=A newer version might exist in Continia Online. Do you want to synchronize and send again?';

    LOCAL PROCEDURE Code@1160040020(VAR Expense@1160040000 : Record 6086320);
    VAR
      EMOnlineMgt@1160040002 : Codeunit 6086305;
      EMSetup@1160040008 : Record 6086300;
      ExpValidate@1160040005 : Codeunit 6086321;
      EMApprovalsBridge@1160040001 : Codeunit 6086369;
      Window@1160040003 : Dialog;
    BEGIN
      IF Expense.Status = Expense.Status::Open THEN
        EXIT;

      Expense.CheckInboxAndThrowError;

      IF GUIALLOWED AND (Expense."Settlement No." = '') THEN
        Window.OPEN(STRSUBSTNO(SendingDocCompleted,FORMAT(Expense."Entry No.")));

      IF Expense."Settlement No." = '' THEN BEGIN
        EMApprovalsBridge.CancelApprovalRequest(DATABASE::Expense,FORMAT(Expense."Entry No."));
        Expense.GET(Expense."Entry No.");
      END;

      IF Expense.Status = Expense.Status::"Pending Expense User" THEN
        IF HasPermissionToSync AND (Expense."Settlement No." = '') AND GUIALLOWED THEN BEGIN
          IF NOT EMOnlineMgt.DeprecateExpense(Expense."Expense GUID",Expense."Continia Online Version No.",FALSE) THEN BEGIN
            IF CONFIRM(NewerVersionExists) THEN BEGIN
              CODEUNIT.RUN(CODEUNIT::"EM Online Mgt.");
              Expense.GET(Expense."Entry No.");
            END;

            EMOnlineMgt.DeprecateExpense(Expense."Expense GUID",Expense."Continia Online Version No.",TRUE);
          END;
        END ELSE
          EMOnlineMgt.DeprecateExpense(Expense."Expense GUID",Expense."Continia Online Version No.",ShowError);

      //A SHADOW EXIST FOR HISTORY PURPOSE. MAKE SURE THAT ONE IS FORGOTTEN (NO ERROR)
      IF Expense.Status > Expense.Status::"Pending Expense User" THEN
        EMOnlineMgt.DeprecateExpense(Expense."Expense GUID",'',FALSE);

      Expense.Status := Expense.Status::Open;
      Expense."Expense Completed" := TRUE;
      Expense.SetSkipSendToExpUser(TRUE);
      Expense.MODIFY(TRUE);

      ExpValidate.RUN(Expense);

      IF GUIALLOWED AND (Expense."Settlement No." = '') THEN
        Window.CLOSE;
    END;

    PROCEDURE SetShowError@1160040000(ShowErrValue@1160040000 : Boolean);
    BEGIN
      ShowError := ShowErrValue;
    END;

    PROCEDURE HasPermissionToSync@1160040001() : Boolean;
    VAR
      EMSetup@1160040001 : Record 6086300;
      ExpNavVersionMgt@1160040000 : Codeunit 6086319;
    BEGIN
      IF EMSetup.GET THEN
        IF EMSetup."Approval Administrator Role ID" <> '' THEN
          IF ExpNavVersionMgt.HasPermission(EMSetup."Approval Administrator Role ID") THEN
            EXIT(TRUE);

      IF ExpNavVersionMgt.HasPermission('EM-SUPER') THEN
        EXIT(TRUE);

      IF ExpNavVersionMgt.HasPermission('SUPER') THEN
        EXIT(TRUE);
    END;

    BEGIN
    END.
  }
}

