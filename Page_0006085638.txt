OBJECT Page 6085638 Continia User Permissions
{
  OBJECT-PROPERTIES
  {
    Date=03/15/17;
    Time=[ 1:00:00 AM];
    Version List=DCW16.00.00.4.50;
  }
  PROPERTIES
  {
    CaptionML=ENU=User Approval Permissions;
    SourceTable=Table6085634;
    DataCaptionExpr=GetDataCaptionExpr;
    PageType=List;
    OnOpenPage=BEGIN
                 CHANGECOMPANY(GetCurrCompanyName);
               END;

    OnAfterGetRecord=BEGIN
                       NewRecord := FALSE;
                       AssigningPermission := "Assigning Permission";
                       ApprovalPermission := "Approval Permission";

                       SetControlAppearance;
                     END;

    OnNewRecord=BEGIN
                  NewRecord := TRUE;

                  "Approval User Group Code" := '';
                  AssigningPermission := AssigningPermission::" ";
                  ApprovalPermission := ApprovalPermission::" ";
                END;

    OnInsertRecord=BEGIN
                     NewRecord := FALSE;

                     IF GetCurrCompanyName <> COMPANYNAME THEN
                       SetCurrCompanyNameTbl(GetCurrCompanyName);

                     CurrPage.UPDATE(FALSE);
                   END;

    OnModifyRecord=VAR
                     ContiniaUserPermission@1160040000 : Record 6085634;
                   BEGIN
                     ContiniaUserPermission.CHANGECOMPANY(GetCurrCompanyName);
                     ContiniaUserPermission.SetCurrCompanyNameTbl(GetCurrCompanyName);
                     ContiniaUserPermission.GET("Continia User ID",Type,"Dimension Code","Approval User Group Code");
                     ContiniaUserPermission."Assigning Permission" := "Assigning Permission";
                     ContiniaUserPermission."Approval Permission" := "Approval Permission";
                     ContiniaUserPermission."Assigning Filter" := "Assigning Filter";
                     ContiniaUserPermission."Approval Filter" := "Approval Filter";
                     ContiniaUserPermission.MODIFY;

                     EXIT(FALSE);
                   END;

    OnDeleteRecord=BEGIN
                     SetCurrCompanyNameTbl(GetCurrCompanyName);
                   END;

    ActionList=ACTIONS
    {
      { 6085575 ;    ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 6085574 ;1   ;Action    ;
                      Name=SelectAssigningPermission;
                      CaptionML=ENU=Select Assigning Permissions;
                      Promoted=Yes;
                      Enabled=EnableAssignPermission;
                      PromotedIsBig=Yes;
                      Image=GetLines;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 DrillDownSelection(GetCurrCompanyName,PermissionType::"Assigning Permission");
                                 CurrPage.UPDATE;
                               END;
                                }
      { 6085573 ;1   ;Action    ;
                      Name=SelectApprovalsPermission;
                      CaptionML=ENU=Select Approvals Permissions;
                      Promoted=Yes;
                      Enabled=EnableApprovalPermission;
                      PromotedIsBig=Yes;
                      Image=GetLines;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 DrillDownSelection(GetCurrCompanyName,PermissionType::"Approval Permission");
                                 CurrPage.UPDATE;
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1160040000;0;Container;
                ContainerType=ContentArea }

    { 1160040001;1;Group  ;
                Name=Group;
                GroupType=Repeater }

    { 1160040002;2;Field  ;
                SourceExpr=Type;
                OnValidate=BEGIN
                             IF NOT NewRecord THEN
                               ERROR(RenameErr,TABLECAPTION);
                           END;
                            }

    { 1160040007;2;Field  ;
                SourceExpr="Dimension Code";
                OnValidate=BEGIN
                             IF NOT NewRecord THEN
                               ERROR(RenameErr,TABLECAPTION);
                           END;

                OnLookup=BEGIN
                           EXIT(LookUpDimCode(GetCurrCompanyName,Text));
                         END;
                          }

    { 1160040011;2;Field  ;
                Lookup=Yes;
                DrillDown=Yes;
                SourceExpr="Approval User Group Code";
                Editable=FALSE;
                OnLookup=BEGIN
                           EXIT(LookUpAppUserGrpCode(GetCurrCompanyName,Text,FALSE));
                         END;
                          }

    { 1160040005;2;Field  ;
                Name=Assigning Permission;
                CaptionML=ENU=Assigning Permission;
                OptionCaptionML=ENU=" ,All,Include Selected,Exclude Selected,Filter";
                SourceExpr=AssigningPermission;
                OnValidate=VAR
                             ContiniaUserPermission@1160040000 : Record 6085634;
                           BEGIN
                             "Assigning Permission" := AssigningPermission;

                             ContiniaUserPermission.CHANGECOMPANY(GetCurrCompanyName);
                             ContiniaUserPermission.SetCurrCompanyNameTbl(GetCurrCompanyName);
                             ContiniaUserPermission.GET("Continia User ID",Type,"Dimension Code","Approval User Group Code");
                             ContiniaUserPermission."Approval Permission" := "Approval Permission";
                             ContiniaUserPermission."Assigning Filter" := "Assigning Filter";
                             ContiniaUserPermission."Approval Filter" := "Approval Filter";
                             ContiniaUserPermission.VALIDATE("Assigning Permission",AssigningPermission);
                             ContiniaUserPermission.MODIFY;
                             CurrPage.UPDATE(FALSE);

                             SetControlAppearance;
                           END;
                            }

    { 1160040012;2;Field  ;
                Name=Approval Permission;
                CaptionML=ENU=Approval Permission;
                OptionCaptionML=ENU=" ,All,Include Selected,Exclude Selected,Filter,Same as Assigning";
                SourceExpr=ApprovalPermission;
                OnValidate=VAR
                             ContiniaUserPermission@1160040000 : Record 6085634;
                           BEGIN
                             "Approval Permission" := ApprovalPermission;

                             ContiniaUserPermission.CHANGECOMPANY(GetCurrCompanyName);
                             ContiniaUserPermission.SetCurrCompanyNameTbl(GetCurrCompanyName);
                             ContiniaUserPermission.GET("Continia User ID",Type,"Dimension Code","Approval User Group Code");
                             ContiniaUserPermission."Assigning Permission" := "Assigning Permission";
                             ContiniaUserPermission."Assigning Filter" := "Assigning Filter";
                             ContiniaUserPermission."Approval Filter" := "Approval Filter";
                             ContiniaUserPermission.VALIDATE("Approval Permission",ApprovalPermission);
                             ContiniaUserPermission.MODIFY;
                             CurrPage.UPDATE(FALSE);

                             SetControlAppearance;
                           END;
                            }

    { 1160040006;2;Field  ;
                SourceExpr="Assigning Filter";
                OnLookup=BEGIN
                           EXIT(LookUpPmsFilter(GetCurrCompanyName,PermissionType::"Assigning Permission",Text));
                         END;
                          }

    { 1160040008;2;Field  ;
                SourceExpr="Approval Filter";
                OnLookup=BEGIN
                           EXIT(LookUpPmsFilter(GetCurrCompanyName,PermissionType::"Approval Permission",Text));
                         END;
                          }

    { 1160040009;2;Field  ;
                SourceExpr="No. of Assigning Selections";
                OnDrillDown=BEGIN
                              DrillDownSelection(GetCurrCompanyName,PermissionType::"Assigning Permission");
                              CurrPage.UPDATE;
                            END;
                             }

    { 1160040010;2;Field  ;
                SourceExpr="No. of Approval Selections";
                OnDrillDown=BEGIN
                              DrillDownSelection(GetCurrCompanyName,PermissionType::"Approval Permission");
                              CurrPage.UPDATE;
                            END;
                             }

  }
  CODE
  {
    VAR
      PermissionType@1160040000 : 'Assigning Permission,Approval Permission';
      CurrCompanyName@1160040001 : Text[30];
      ApprovalPermission@1160040003 : ' ,All,Include Selected,Exclude Selected,Filter,Same as Assigning';
      AssigningPermission@1160040004 : ' ,All,Include Selected,Exclude Selected,Filter';
      SkipPageModifyTrigger@1160040005 : Boolean;
      NewRecord@1160040006 : Boolean;
      RenameErr@1160040007 : TextConst 'ENU=You cannot rename a %1.;ENC=You cannot rename a %1.';
      EnableApprovalPermission@1160040002 : Boolean INDATASET;
      EnableAssignPermission@6085573 : Boolean INDATASET;

    PROCEDURE SetCurrCompanyName@1160040019(NewCompanyName@1160040000 : Text[30]);
    BEGIN
      CurrCompanyName := NewCompanyName;
    END;

    PROCEDURE GetCurrCompanyName@1160040024() : Text[30];
    BEGIN
      IF CurrCompanyName = '' THEN
        EXIT(COMPANYNAME)
      ELSE
        EXIT(CurrCompanyName);
    END;

    PROCEDURE GetDataCaptionExpr@1160040003() : Text[150];
    VAR
      PermissionSelectionMgt@1160040000 : Codeunit 6085631;
    BEGIN
      EXIT(PermissionSelectionMgt.GetUserCaptionExpr(GetCurrCompanyName,"Continia User ID"));
    END;

    LOCAL PROCEDURE SetControlAppearance@6085573();
    BEGIN
      EnableAssignPermission := FALSE;
      EnableApprovalPermission := FALSE;
      IF "Assigning Permission" IN ["Assigning Permission"::"Exclude Selected","Assigning Permission"::"Include Selected"] THEN
        EnableAssignPermission := TRUE;

      IF "Approval Permission" IN ["Approval Permission"::"Exclude Selected","Approval Permission"::"Include Selected"] THEN
        EnableApprovalPermission := TRUE;

      IF ("Approval Permission" = "Approval Permission"::"Same as Assigning") AND EnableAssignPermission THEN
        EnableApprovalPermission := TRUE;
    END;

    BEGIN
    END.
  }
}

