OBJECT Codeunit 415 Release Purchase Document
{
  OBJECT-PROPERTIES
  {
    Date=11/27/18;
    Time=11:07:55 PM;
    Version List=;
  }
  PROPERTIES
  {
    TableNo=38;
    OnRun=BEGIN
            PurchaseHeader.COPY(Rec);
            Code;
            Rec := PurchaseHeader;
          END;

  }
  CODE
  {
    VAR
      Text001@1010 : TextConst 'ENU=There is nothing to release for the document of type %1 with the number %2.';
      PurchSetup@1002 : Record 312;
      InvtSetup@1000 : Record 313;
      PurchaseHeader@1007 : Record 38;
      WhsePurchRelease@1004 : Codeunit 5772;
      Text002@1005 : TextConst 'ENU=This document can only be released when the approval process is complete.';
      Text003@1006 : TextConst 'ENU=The approval process must be canceled or completed to reopen this document.';
      Text005@1001 : TextConst 'ENU=There are unpaid prepayment invoices that are related to the document of type %1 with the number %2.';
      UnpostedPrepaymentAmountsErr@1003 : TextConst '@@@="%1 - Document Type; %2 - Document No.";ENU=There are unposted prepayment amounts on the document of type %1 with the number %2.';
      PreviewMode@1008 : Boolean;
      SkipCheckReleaseRestrictions@1009 : Boolean;

    LOCAL PROCEDURE Code@11() LinesWereModified : Boolean;
    VAR
      PurchLine@1006 : Record 39;
      PrepaymentMgt@1003 : Codeunit 441;
      NotOnlyDropShipment@1002 : Boolean;
      PostingDate@1001 : Date;
      PrintPostedDocuments@1000 : Boolean;
      IsHandled@1004 : Boolean;
    BEGIN
      WITH PurchaseHeader DO BEGIN
        IF Status = Status::Released THEN
          EXIT;

        OnBeforeReleasePurchaseDoc(PurchaseHeader,PreviewMode);
        IF NOT (PreviewMode OR SkipCheckReleaseRestrictions) THEN
          CheckPurchaseReleaseRestrictions;

        TESTFIELD("Buy-from Vendor No.");

        PurchLine.SETRANGE("Document Type","Document Type");
        PurchLine.SETRANGE("Document No.","No.");
        PurchLine.SETFILTER(Type,'>0');
        PurchLine.SETFILTER(Quantity,'<>0');
        IF NOT PurchLine.FIND('-') THEN
          ERROR(Text001,"Document Type","No.");
        InvtSetup.GET;
        IF InvtSetup."Location Mandatory" THEN BEGIN
          PurchLine.SETRANGE(Type,PurchLine.Type::Item);
          IF PurchLine.FIND('-') THEN
            REPEAT
              IF PurchLine.IsInventoriableItem THEN
                PurchLine.TESTFIELD("Location Code");
            UNTIL PurchLine.NEXT = 0;
          PurchLine.SETFILTER(Type,'>0');
        END;

        OnCodeOnAfterCheck(PurchaseHeader,PurchLine,LinesWereModified);

        PurchLine.SETRANGE("Drop Shipment",FALSE);
        NotOnlyDropShipment := PurchLine.FIND('-');
        PurchLine.RESET;

        OnBeforeCalcInvDiscount(PurchaseHeader,PreviewMode);

        PurchSetup.GET;
        IF PurchSetup."Calc. Inv. Discount" THEN BEGIN
          PostingDate := "Posting Date";
          PrintPostedDocuments := "Print Posted Documents";
          CODEUNIT.RUN(CODEUNIT::"Purch.-Calc.Discount",PurchLine);
          LinesWereModified := TRUE;
          GET("Document Type","No.");
          "Print Posted Documents" := PrintPostedDocuments;
          IF PostingDate <> "Posting Date" THEN
            VALIDATE("Posting Date",PostingDate);
        END;

        IsHandled := FALSE;
        OnBeforeModifyPurchDoc(PurchaseHeader,PreviewMode,IsHandled);
        IF IsHandled THEN
          EXIT;

        IF PrepaymentMgt.TestPurchasePrepayment(PurchaseHeader) AND ("Document Type" = "Document Type"::Order) THEN BEGIN
          Status := Status::"Pending Prepayment";
          MODIFY(TRUE);
          OnAfterReleasePurchaseDoc(PurchaseHeader,PreviewMode,LinesWereModified);
          EXIT;
        END;
        Status := Status::Released;

        LinesWereModified := LinesWereModified OR CalcAndUpdateVATOnLines(PurchaseHeader,PurchLine);

        MODIFY(TRUE);

        IF NotOnlyDropShipment THEN
          IF "Document Type" IN ["Document Type"::Order,"Document Type"::"Return Order"] THEN
            WhsePurchRelease.Release(PurchaseHeader);

        OnAfterReleasePurchaseDoc(PurchaseHeader,PreviewMode,LinesWereModified);
      END;
    END;

    [External]
    PROCEDURE Reopen@1(VAR PurchHeader@1000 : Record 38);
    BEGIN
      OnBeforeReopenPurchaseDoc(PurchHeader,PreviewMode);

      WITH PurchHeader DO BEGIN
        IF Status = Status::Open THEN
          EXIT;
        IF "Document Type" IN ["Document Type"::Order,"Document Type"::"Return Order"] THEN
          WhsePurchRelease.Reopen(PurchHeader);
        Status := Status::Open;

        MODIFY(TRUE);
      END;

      OnAfterReopenPurchaseDoc(PurchHeader,PreviewMode);
    END;

    [External]
    PROCEDURE PerformManualRelease@2(VAR PurchHeader@1002 : Record 38);
    VAR
      PrepaymentMgt@1001 : Codeunit 441;
    BEGIN
      IF PrepaymentMgt.TestPurchasePrepayment(PurchHeader) THEN
        ERROR(UnpostedPrepaymentAmountsErr,PurchHeader."Document Type",PurchHeader."No.");

      OnBeforeManualReleasePurchaseDoc(PurchHeader,PreviewMode);
      PerformManualCheckAndRelease(PurchHeader);
      OnAfterManualReleasePurchaseDoc(PurchHeader,PreviewMode);
    END;

    [External]
    PROCEDURE PerformManualCheckAndRelease@13(VAR PurchHeader@1002 : Record 38);
    VAR
      PrepaymentMgt@1001 : Codeunit 441;
      ApprovalsMgmt@1000 : Codeunit 1535;
      IsHandled@1003 : Boolean;
    BEGIN
      WITH PurchHeader DO
        IF ("Document Type" = "Document Type"::Order) AND PrepaymentMgt.TestPurchasePayment(PurchHeader) THEN BEGIN
          IF Status <> Status::"Pending Prepayment" THEN BEGIN
            Status := Status::"Pending Prepayment";
            MODIFY;
            COMMIT;
          END;
          ERROR(Text005,"Document Type","No.");
        END;

      IF ApprovalsMgmt.IsPurchaseHeaderPendingApproval(PurchHeader) THEN
        ERROR(Text002);

      IsHandled := FALSE;
      OnBeforePerformManualRelease(PurchHeader,PreviewMode,IsHandled);
      IF IsHandled THEN
        EXIT;

      CODEUNIT.RUN(CODEUNIT::"Release Purchase Document",PurchHeader);
    END;

    [External]
    PROCEDURE PerformManualReopen@3(VAR PurchHeader@1002 : Record 38);
    BEGIN
      IF PurchHeader.Status = PurchHeader.Status::"Pending Approval" THEN
        ERROR(Text003);

      OnBeforeManualReopenPurchaseDoc(PurchHeader,PreviewMode);
      Reopen(PurchHeader);
      OnAfterManualReopenPurchaseDoc(PurchHeader,PreviewMode);
    END;

    [External]
    PROCEDURE ReleasePurchaseHeader@8(VAR PurchHdr@1000 : Record 38;Preview@1001 : Boolean) LinesWereModified : Boolean;
    BEGIN
      PreviewMode := Preview;
      PurchaseHeader.COPY(PurchHdr);
      LinesWereModified := Code;
      PurchHdr := PurchaseHeader;
    END;

    [External]
    PROCEDURE CalcAndUpdateVATOnLines@9(VAR PurchaseHeader@1003 : Record 38;VAR PurchLine@1002 : Record 39) LinesWereModified : Boolean;
    VAR
      TempVATAmountLine0@1001 : TEMPORARY Record 290;
      TempVATAmountLine1@1000 : TEMPORARY Record 290;
    BEGIN
      PurchLine.SetPurchHeader(PurchaseHeader);
      IF PurchaseHeader."Tax Area Code" = '' THEN BEGIN  // VAT
        PurchLine.CalcVATAmountLines(0,PurchaseHeader,PurchLine,TempVATAmountLine0);
        PurchLine.CalcVATAmountLines(1,PurchaseHeader,PurchLine,TempVATAmountLine1);
        LinesWereModified :=
          PurchLine.UpdateVATOnLines(0,PurchaseHeader,PurchLine,TempVATAmountLine0) OR
          PurchLine.UpdateVATOnLines(1,PurchaseHeader,PurchLine,TempVATAmountLine1);
      END ELSE BEGIN
        PurchLine.CalcSalesTaxLines(PurchaseHeader,PurchLine);
        LinesWereModified := TRUE;
      END;
    END;

    PROCEDURE SetSkipCheckReleaseRestrictions@17();
    BEGIN
      SkipCheckReleaseRestrictions := TRUE;
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeCalcInvDiscount@18(VAR PurchaseHeader@1000 : Record 38;PreviewMode@1001 : Boolean);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeManualReleasePurchaseDoc@10(VAR PurchaseHeader@1000 : Record 38;PreviewMode@1001 : Boolean);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeReleasePurchaseDoc@5(VAR PurchaseHeader@1000 : Record 38;PreviewMode@1001 : Boolean);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterReleasePurchaseDoc@4(VAR PurchaseHeader@1000 : Record 38;PreviewMode@1001 : Boolean;VAR LinesWereModified@1002 : Boolean);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterManualReleasePurchaseDoc@14(VAR PurchaseHeader@1000 : Record 38;PreviewMode@1001 : Boolean);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeManualReopenPurchaseDoc@12(VAR PurchaseHeader@1000 : Record 38;PreviewMode@1001 : Boolean);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeReopenPurchaseDoc@6(VAR PurchaseHeader@1000 : Record 38;PreviewMode@1001 : Boolean);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeModifyPurchDoc@22(VAR PurchaseHeader@1000 : Record 38;PreviewMode@1001 : Boolean;VAR IsHandled@1002 : Boolean);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforePerformManualRelease@19(VAR PurchaseHeader@1000 : Record 38;PreviewMode@1002 : Boolean;VAR IsHandled@1001 : Boolean);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterReopenPurchaseDoc@7(VAR PurchaseHeader@1000 : Record 38;PreviewMode@1001 : Boolean);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterManualReopenPurchaseDoc@15(VAR PurchaseHeader@1000 : Record 38;PreviewMode@1001 : Boolean);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnCodeOnAfterCheck@16(PurchaseHeader@1000 : Record 38;VAR PurchaseLine@1001 : Record 39;VAR LinesWereModified@1002 : Boolean);
    BEGIN
    END;

    BEGIN
    END.
  }
}

