OBJECT Page 14103541 Choose New Dimension With Date
{
  OBJECT-PROPERTIES
  {
    Date=07/19/16;
    Time=[ 3:27:51 PM];
    Modified=Yes;
    Version List=VLDM3.00,VLDM4.11;
  }
  PROPERTIES
  {
    PageType=Card;
    OnInit=BEGIN
             CurrPage.LOOKUPMODE := TRUE;
           END;

    ActionList=ACTIONS
    {
      { 1900000004;0 ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 12      ;1   ;Action    ;
                      CaptionML=ENU=Help;
                      Promoted=Yes;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 DimensionMgtSetup.GET;
                                 DimensionMgtSetup.SupportWebsite(2, 14103541);
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 1   ;1   ;Group     ;
                CaptionML=ENU=General }

    { 2   ;2   ;Field     ;
                CaptionML=ENU=Dimension;
                SourceExpr=Dimensions.Code;
                TableRelation=Dimension;
                OnValidate=BEGIN
                             DimensionValue.SETRANGE("Dimension Code", Dimensions.Code);
                             GetDimValue;//DM4.07
                           END;
                            }

    { 1000000001;2;Field  ;
                CaptionML=ENU=Existing Dimension Value;
                SourceExpr=ExistingDimVal;
                Editable=FALSE }

    { 4   ;2   ;Field     ;
                CaptionML=ENU=New Dimension Value;
                SourceExpr=DimensionValue.Code;
                OnValidate=BEGIN
                             IF DimensionValue.Code <> '' THEN
                               DimensionValue.GET(Dimensions.Code, DimensionValue.Code);
                           END;

                OnLookup=BEGIN
                           CLEAR(DimensionValueList);
                           IF (DimensionValue.GETFILTER("Dimension Code") = '') OR (Dimensions.Code = '') THEN
                             ERROR('You must choose a Dimension first.');
                           DimensionValueList.SETTABLEVIEW(DimensionValue);
                           DimensionValueList.LOOKUPMODE(TRUE);
                           DimensionValueList.EDITABLE(FALSE);
                           IF DimensionValueList.RUNMODAL = ACTION::LookupOK THEN
                             DimensionValueList.GETRECORD(DimensionValue);
                         END;
                          }

    { 6   ;2   ;Field     ;
                CaptionML=ENU=Start Date;
                SourceExpr="Start Date" }

    { 8   ;2   ;Field     ;
                CaptionML=ENU=End Date;
                SourceExpr="End Date" }

  }
  CODE
  {
    VAR
      Dimensions@1000000000 : Record 348;
      DimensionValue@1000000001 : Record 349;
      DimensionMgtSetup@1000000005 : Record 14103540;
      DimensionValueList@1000000002 : Page 560;
      "Start Date"@1000000003 : Date;
      "End Date"@1000000004 : Date;
      TableID@1000000006 : Integer;
      No@1000000007 : Code[20];
      ExistingDimVal@1000000008 : Code[20];
      EntryNo@1000000009 : Integer;

    PROCEDURE ReturnValues@1000000000(VAR Dim@1000000000 : Code[20];VAR DimValue@1000000001 : Code[20];VAR StartDate@1000000002 : Date;VAR EndDate@1000000003 : Date);
    VAR
      Text001@1000000004 : TextConst 'ENU=End Date of %1 is less than Start Date of %2.\Please Re-enter.';
    BEGIN
      Dim := Dimensions.Code;
      DimValue := DimensionValue.Code;
      StartDate := "Start Date";
      IF "End Date" = 0D THEN
        EndDate := WORKDATE
      ELSE
        EndDate := "End Date";

      IF EndDate < StartDate THEN
        ERROR(Text001,FORMAT(EndDate),FORMAT(StartDate));
    END;

    PROCEDURE GetDimValue@1000000003();
    VAR
      DefaultDim@1000000000 : Record 352;
    BEGIN
      //DM4.07>>
      IF Dimensions.Code <> '' THEN BEGIN
        DefaultDim.RESET;
        DefaultDim.SETRANGE(DefaultDim."Table ID",TableID);
        DefaultDim.SETRANGE(DefaultDim."No.",No);
        DefaultDim.SETRANGE(DefaultDim."Dimension Code",Dimensions.Code);
        IF DefaultDim.FINDFIRST THEN
          ExistingDimVal := DefaultDim."Dimension Value Code"
        ELSE
         ExistingDimVal :='';
      END ELSE
      ExistingDimVal:=''
      //DM4.07<<
    END;

    PROCEDURE SetValues@1000000001(Frm_TableNo@1000000000 : Integer;From_No@1000000001 : Code[20]);
    BEGIN
      TableID :=Frm_TableNo;
      No :=From_No;
    END;

    PROCEDURE SetBudgetValues@1000000002(Frm_TableNo@1000000001 : Integer;From_EntryNo@1000000000 : Integer);
    BEGIN
      //>>VLDM4.11++
      TableID :=Frm_TableNo;
      EntryNo :=From_EntryNo;
      //<<VLDM4.11--
    END;

    BEGIN
    {
      Vertical Leap - Dimension Management

      VLDM4.11 : Created a function "SetBudgetValues" to support Budget G/L entries functionality.
    }
    END.
  }
}

