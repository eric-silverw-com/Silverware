OBJECT Page 364 Select Sending Options
{
  OBJECT-PROPERTIES
  {
    Date=11/27/18;
    Time=11:10:14 PM;
    Version List=;
  }
  PROPERTIES
  {
    CaptionML=ENU=Send Document to;
    SourceTable=Table60;
    DataCaptionExpr='';
    PageType=StandardDialog;
    SourceTableTemporary=Yes;
    OnInit=BEGIN
             SendToPrinterVisible := ClientTypeManagement.GetCurrentClientType = CLIENTTYPE::Windows;
           END;

    OnOpenPage=BEGIN
                 CurrPage.LOOKUPMODE := TRUE;
               END;

    OnAfterGetCurrRecord=VAR
                           ElectronicDocumentFormat@1000 : Record 61;
                           DocumentSendingProfile@1001 : Record 60;
                         BEGIN
                           SendElectronicallyVisible := NOT ElectronicDocumentFormat.ISEMPTY AND "One Related Party Selected";

                           IF DocumentSendingProfile.GET(Code) THEN
                             COPY(DocumentSendingProfile);
                         END;

  }
  CONTROLS
  {
    { 1   ;0   ;Container ;
                ContainerType=ContentArea }

    { 9   ;1   ;Group     ;
                CaptionML=ENU=Details;
                GroupType=Group }

    { 10  ;2   ;Group     ;
                GroupType=Group }

    { 3   ;3   ;Field     ;
                ToolTipML=ENU=Specifies if and how the document is printed when you choose the Post and Send button. If you choose the Yes (Prompt for Settings) option, the document is printed according to settings that you must make on the printer setup dialog.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=Printer;
                Visible=SendToPrinterVisible;
                OnValidate=BEGIN
                             SetSendMethodToCustom;
                           END;
                            }

    { 4   ;3   ;Field     ;
                ToolTipML=ENU=Specifies if and how the document is attached as a PDF file to an email to the involved customer when you choose the Post and Send button. If you choose the Yes (Prompt for Settings) option, the document is attached to an email according to settings that you must make in the Send Email window.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="E-Mail";
                OnValidate=BEGIN
                             SetSendMethodToCustom;
                           END;
                            }

    { 12  ;3   ;Group     ;
                Visible="E-Mail" <> "E-Mail"::No;
                GroupType=Group }

    { 13  ;4   ;Field     ;
                ToolTipML=ENU=Specifies if the document is attached to an email when you choose the Post and Send button.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="E-Mail Attachment";
                OnValidate=BEGIN
                             VerifySelectedOptionsValid;
                             "E-Mail Format" := GetFormat;
                           END;
                            }

    { 14  ;4   ;Group     ;
                Visible="E-Mail Attachment" <> "E-Mail Attachment"::PDF;
                GroupType=Group }

    { 15  ;5   ;Field     ;
                CaptionML=ENU=Format;
                ToolTipML=ENU=Specifies the format of the file that is saved to disk.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="E-Mail Format";
                OnValidate=BEGIN
                             SetSendMethodToCustom;
                             LastFormat := "E-Mail Format";
                           END;

                OnLookup=BEGIN
                           "E-Mail Format" := LookupFormat;
                         END;
                          }

    { 7   ;3   ;Field     ;
                ToolTipML=ENU=Specifies if the document is saved as a PDF file when you choose the Post and Send button.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=Disk;
                OnValidate=BEGIN
                             VerifySelectedOptionsValid;
                             SetSendMethodToCustom;
                             "Disk Format" := GetFormat;
                           END;
                            }

    { 16  ;3   ;Group     ;
                Visible=(Disk <> Disk::No) AND (Disk <> Disk::PDF);
                GroupType=Group }

    { 17  ;4   ;Field     ;
                CaptionML=ENU=Format;
                ToolTipML=ENU=Specifies the format of the electronic document.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Disk Format";
                OnValidate=BEGIN
                             SetSendMethodToCustom;
                             LastFormat := "Disk Format";
                           END;

                OnLookup=BEGIN
                           "Disk Format" := LookupFormat;
                         END;
                          }

    { 2   ;2   ;Group     ;
                Visible=SendElectronicallyVisible;
                GroupType=Group }

    { 5   ;3   ;Field     ;
                ToolTipML=ENU=Specifies if the document is sent as an electronic document that the customer can import into their system when you choose the Post and Send button. To use this option, you must also fill the Electronic Format field. Alternatively, the file can be saved to disk.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Electronic Document";
                OnValidate=BEGIN
                             VerifySelectedOptionsValid;
                             SetSendMethodToCustom;
                             "Electronic Format" := GetFormat;
                           END;
                            }

    { 18  ;3   ;Group     ;
                Visible="Electronic Document" <> "Electronic Document"::No;
                GroupType=Group }

    { 6   ;4   ;Field     ;
                CaptionML=ENU=Format;
                ToolTipML=ENU=Specifies which format to use for electronic document sending. You must fill this field if you selected the Silent option in the Electronic Document field.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Electronic Format";
                OnValidate=BEGIN
                             SetSendMethodToCustom;
                             LastFormat := "Electronic Format";
                           END;

                OnLookup=BEGIN
                           "Electronic Format" := LookupFormat;
                         END;
                          }

  }
  CODE
  {
    VAR
      ClientTypeManagement@1004 : Codeunit 4;
      SendElectronicallyVisible@1000 : Boolean INDATASET;
      CustomTxt@1003 : TextConst 'ENU=Custom';
      SendToPrinterVisible@1002 : Boolean;
      LastFormat@1001 : Code[20];

    LOCAL PROCEDURE SetSendMethodToCustom@15();
    BEGIN
      Code := CustomTxt;
    END;

    LOCAL PROCEDURE LookupFormat@16() : Code[20];
    VAR
      ElectronicDocumentFormat@1002 : Record 61;
      ElectronicDocumentFormats@1001 : Page 366;
    BEGIN
      ElectronicDocumentFormat.SETRANGE(Usage,Usage);
      ElectronicDocumentFormats.SETTABLEVIEW(ElectronicDocumentFormat);
      ElectronicDocumentFormats.LOOKUPMODE := TRUE;

      IF ElectronicDocumentFormats.RUNMODAL = ACTION::LookupOK THEN BEGIN
        ElectronicDocumentFormats.GETRECORD(ElectronicDocumentFormat);
        LastFormat := ElectronicDocumentFormat.Code;
        EXIT(ElectronicDocumentFormat.Code);
      END;

      EXIT(GetFormat);
    END;

    LOCAL PROCEDURE GetFormat@4() : Code[20];
    VAR
      ElectronicDocumentFormat@1000 : Record 61;
      FindNewFormat@1001 : Boolean;
    BEGIN
      FindNewFormat := FALSE;

      IF LastFormat = '' THEN
        FindNewFormat := TRUE
      ELSE BEGIN
        ElectronicDocumentFormat.SETRANGE(Code,LastFormat);
        ElectronicDocumentFormat.SETRANGE(Usage,Usage);
        IF NOT ElectronicDocumentFormat.FINDFIRST THEN
          FindNewFormat := TRUE;
      END;

      IF FindNewFormat THEN BEGIN
        ElectronicDocumentFormat.SETRANGE(Code);
        ElectronicDocumentFormat.SETRANGE(Usage,Usage);
        IF NOT ElectronicDocumentFormat.FINDFIRST THEN
          LastFormat := ''
        ELSE
          LastFormat := ElectronicDocumentFormat.Code;
      END;

      EXIT(LastFormat);
    END;

    BEGIN
    END.
  }
}

