OBJECT Page 2802 Native - Item Entity
{
  OBJECT-PROPERTIES
  {
    Date=09/19/18;
    Time=12:00:00 PM;
    Version List=NAVW113.00;
  }
  PROPERTIES
  {
    CaptionML=[@@@={Locked};
               ENU=invoicingItems;
               ESM=invoicingItems;
               FRC=invoicingItems;
               ENC=invoicingItems];
    SourceTable=Table27;
    DelayedInsert=Yes;
    PageType=List;
    OnOpenPage=BEGIN
                 BINDSUBSCRIPTION(NativeAPILanguageHandler);
                 SELECTLATESTVERSION;
               END;

    OnAfterGetRecord=BEGIN
                       SetCalculatedFields;
                     END;

    OnNewRecord=BEGIN
                  ClearCalculatedFields;
                END;

    OnInsertRecord=VAR
                     Item@1001 : Record 27;
                     GraphCollectionMgtItem@1002 : Codeunit 5470;
                     BaseUnitOfMeasureJSONText@1003 : Text;
                   BEGIN
                     IF "No." <> '' THEN
                       IF Item.GET("No.") THEN
                         ERROR(ItemExistsErr);

                     IF TempFieldSet.GET(DATABASE::Item,FIELDNO("Base Unit of Measure")) THEN
                       BaseUnitOfMeasureJSONText := GraphCollectionMgtItem.ItemUnitOfMeasureToJSON(Rec,BaseUnitOfMeasureCode);

                     GraphCollectionMgtItem.InsertItem(Rec,TempFieldSet,BaseUnitOfMeasureJSONText);
                     SetCalculatedFields;
                     EXIT(FALSE);
                   END;

    OnModifyRecord=VAR
                     Item@1001 : Record 27;
                   BEGIN
                     IF TempFieldSet.GET(DATABASE::Item,FIELDNO("Base Unit of Measure")) THEN
                       VALIDATE("Base Unit of Measure",BaseUnitOfMeasureCode);

                     Item.SETRANGE(Id,Id);
                     Item.FINDFIRST;

                     IF "No." = Item."No." THEN
                       MODIFY(TRUE)
                     ELSE BEGIN
                       Item.TRANSFERFIELDS(Rec,FALSE);
                       Item.RENAME("No.");
                       TRANSFERFIELDS(Item,TRUE);
                     END;

                     SetCalculatedFields;

                     EXIT(FALSE);
                   END;

    ODataKeyFields=Id;
  }
  CONTROLS
  {
    { 18  ;0   ;Container ;
                ContainerType=ContentArea }

    { 17  ;1   ;Group     ;
                Name=Group;
                GroupType=Repeater }

    { 16  ;2   ;Field     ;
                Name=id;
                CaptionML=[@@@={Locked};
                           ENU=Id;
                           ESM=Id;
                           FRC=Id;
                           ENC=Id];
                ApplicationArea=#All;
                SourceExpr=Id }

    { 15  ;2   ;Field     ;
                Name=number;
                CaptionML=[@@@={Locked};
                           ENU=Number;
                           ESM=Number;
                           FRC=Number;
                           ENC=Number];
                ApplicationArea=#All;
                SourceExpr="No." }

    { 14  ;2   ;Field     ;
                Name=displayName;
                CaptionML=[@@@={Locked};
                           ENU=DisplayName;
                           ESM=DisplayName;
                           FRC=DisplayName;
                           ENC=DisplayName];
                ToolTipML=[ENU=Specifies the Description for the Item.;
                           ESM=Especifica la descripci¢n del producto.;
                           FRC=Sp‚cifie la description de l'‚l‚ment.;
                           ENC=Specifies the Description for the Item.];
                ApplicationArea=#All;
                SourceExpr=Description;
                OnValidate=BEGIN
                             RegisterFieldSet(FIELDNO(Description));
                           END;
                            }

    { 11  ;2   ;Field     ;
                Name=type;
                CaptionML=[@@@={Locked};
                           ENU=Type;
                           ESM=Type;
                           FRC=Type;
                           ENC=Type];
                ToolTipML=[ENU=Specifies the Type for the Item. Possible values are Inventory and Service.;
                           ESM=Especifica el Tipo para el art¡culo. Los valores posibles son Inventario y Servicio.;
                           FRC=Sp‚cifie le type d'article. Les valeurs possibles sont Inventaire et Service.;
                           ENC=Specifies the Type for the Item. Possible values are Inventory and Service.];
                ApplicationArea=#All;
                SourceExpr=Type;
                OnValidate=BEGIN
                             RegisterFieldSet(FIELDNO(Type));
                           END;
                            }

    { 4   ;2   ;Field     ;
                Name=blocked;
                CaptionML=[@@@={Locked};
                           ENU=Blocked;
                           ESM=Blocked;
                           FRC=Blocked;
                           ENC=Blocked];
                ToolTipML=[ENU=Specifies whether the item is blocked.;
                           ESM=Indica si el art¡culo est  bloqueado.;
                           FRC=Sp‚cifie si l'article est bloqu‚.;
                           ENC=Specifies whether the item is blocked.];
                ApplicationArea=#All;
                SourceExpr=Blocked;
                OnValidate=BEGIN
                             RegisterFieldSet(FIELDNO(Blocked));
                           END;
                            }

    { 9   ;2   ;Field     ;
                Name=baseUnitOfMeasureId;
                CaptionML=[@@@={Locked};
                           ENU=BaseUnitOfMeasureId;
                           ESM=BaseUnitOfMeasureId;
                           FRC=BaseUnitOfMeasureId;
                           ENC=BaseUnitOfMeasureId];
                ToolTipML=[ENU=Specifies the ID of the base unit of measure.;
                           ESM=Especifica el identificador de la unidad de medida base.;
                           FRC=Sp‚cifie le code de l'unit‚ de mesure de base.;
                           ENC=Specifies the ID of the base unit of measure.];
                ApplicationArea=#All;
                SourceExpr=BaseUnitOfMeasureId;
                OnValidate=BEGIN
                             ValidateUnitOfMeasure.SETRANGE(Id,BaseUnitOfMeasureId);
                             IF NOT ValidateUnitOfMeasure.FINDFIRST THEN
                               ERROR(BaseUnitOfMeasureIdDoesNotMatchAUnitOfMeasureErr);

                             BaseUnitOfMeasureCode := ValidateUnitOfMeasure.Code;

                             RegisterFieldSet(FIELDNO("Unit of Measure Id"));
                             RegisterFieldSet(FIELDNO("Base Unit of Measure"));
                           END;
                            }

    { 8   ;2   ;Field     ;
                Name=baseUnitOfMeasureIntStdCode;
                CaptionML=[@@@={Locked};
                           ENU=BaseUnitOfMeasureInternationalStandardCode;
                           ESM=BaseUnitOfMeasureInternationalStandardCode;
                           FRC=BaseUnitOfMeasureInternationalStandardCode;
                           ENC=BaseUnitOfMeasureInternationalStandardCode];
                ToolTipML=[ENU=Specifies the international standard code of the base unit of measure.;
                           ESM=Especifica el c¢digo est ndar internacional de la unidad de medida base.;
                           FRC=Sp‚cifie le code norme internationale de l'unit‚ de mesure de base.;
                           ENC=Specifies the international standard code of the base unit of measure.];
                ApplicationArea=#All;
                SourceExpr=BaseUnitOfMeasureInternationalStandardCode;
                OnValidate=BEGIN
                             IF ValidateUnitOfMeasure."International Standard Code" <> '' THEN BEGIN
                               IF ValidateUnitOfMeasure."International Standard Code" <> BaseUnitOfMeasureInternationalStandardCode THEN
                                 ERROR(BaseUnitOfMeasureValuesDontMatchErr);
                               EXIT;
                             END;

                             ValidateUnitOfMeasure.SETRANGE("International Standard Code",BaseUnitOfMeasureInternationalStandardCode);
                             IF NOT ValidateUnitOfMeasure.FINDFIRST THEN
                               ERROR(BaseUnitOfMeasureIntStdCodeDoesNotMatchAUnitOfMeasureErr);
                             IF ValidateUnitOfMeasure.COUNT > 1 THEN
                               ERROR(BaseUnitOfMeasureIntStdCodeMatchesManyUnitsOfMeasureErr);

                             BaseUnitOfMeasureCode := ValidateUnitOfMeasure.Code;

                             RegisterFieldSet(FIELDNO("Unit of Measure Id"));
                             RegisterFieldSet(FIELDNO("Base Unit of Measure"));
                           END;
                            }

    { 22  ;2   ;Field     ;
                Name=baseUnitOfMeasureDescription;
                CaptionML=[@@@={Locked};
                           ENU=BaseUnitOfMeasureDescription;
                           ESM=BaseUnitOfMeasureDescription;
                           FRC=BaseUnitOfMeasureDescription;
                           ENC=BaseUnitOfMeasureDescription];
                ToolTipML=[ENU=Specifies the international standard code of the base unit of measure.;
                           ESM=Especifica el c¢digo est ndar internacional de la unidad de medida base.;
                           FRC=Sp‚cifie le code norme internationale de l'unit‚ de mesure de base.;
                           ENC=Specifies the international standard code of the base unit of measure.];
                ApplicationArea=#All;
                SourceExpr=BaseUnitOfMeasureDescription;
                Editable=FALSE }

    { 6   ;2   ;Field     ;
                Name=gtin;
                CaptionML=[@@@={Locked};
                           ENU=GTIN;
                           ESM=GTIN;
                           FRC=GTIN;
                           ENC=GTIN];
                ApplicationArea=#All;
                SourceExpr=GTIN;
                OnValidate=BEGIN
                             RegisterFieldSet(FIELDNO(GTIN));
                           END;
                            }

    { 23  ;2   ;Field     ;
                Name=inventory;
                CaptionML=[@@@={Locked};
                           ENU=Inventory;
                           ESM=Inventory;
                           FRC=Inventory;
                           ENC=Inventory];
                ToolTipML=[ENU=Specifies the inventory for the item.;
                           ESM=Especifica el inventario del art¡culo.;
                           FRC=Sp‚cifie l'inventaire de l'article.;
                           ENC=Specifies the inventory for the item.];
                ApplicationArea=#All;
                SourceExpr=InventoryValue;
                OnValidate=BEGIN
                             VALIDATE(Inventory,InventoryValue);
                             RegisterFieldSet(FIELDNO(Inventory));
                           END;
                            }

    { 10  ;2   ;Field     ;
                Name=unitPrice;
                CaptionML=[@@@={Locked};
                           ENU=UnitPrice;
                           ESM=UnitPrice;
                           FRC=UnitPrice;
                           ENC=UnitPrice];
                ApplicationArea=#All;
                SourceExpr="Unit Price";
                OnValidate=BEGIN
                             RegisterFieldSet(FIELDNO("Unit Price"));
                           END;
                            }

    { 24  ;2   ;Field     ;
                Name=priceIncludesTax;
                CaptionML=[@@@={Locked};
                           ENU=PriceIncludesTax;
                           ESM=PriceIncludesTax;
                           FRC=PriceIncludesTax;
                           ENC=PriceIncludesTax];
                ApplicationArea=#All;
                SourceExpr="Price Includes VAT";
                OnValidate=BEGIN
                             RegisterFieldSet(FIELDNO("Price Includes VAT"));
                           END;
                            }

    { 25  ;2   ;Field     ;
                Name=unitCost;
                CaptionML=[@@@={Locked};
                           ENU=UnitCost;
                           ESM=UnitCost;
                           FRC=UnitCost;
                           ENC=UnitCost];
                ApplicationArea=#All;
                SourceExpr="Unit Cost";
                OnValidate=BEGIN
                             RegisterFieldSet(FIELDNO("Unit Cost"));
                           END;
                            }

    { 7   ;2   ;Field     ;
                Name=taxGroupId;
                CaptionML=[@@@={Locked};
                           ENU=TaxGroupId;
                           ESM=TaxGroupId;
                           FRC=TaxGroupId;
                           ENC=TaxGroupId];
                ToolTipML=[ENU=Specifies the ID of the tax group or VAT group.;
                           ESM=Especifica el id. del grupo impositivo o grupo de IVA.;
                           FRC=Sp‚cifie le code du groupe fiscal ou du groupe TVA.;
                           ENC=Specifies the ID of the tax group or VAT group.];
                ApplicationArea=#All;
                SourceExpr=TaxGroupId;
                OnValidate=VAR
                             ValidateVATProdPostingGroup@1002 : Record 324;
                             ValidateTaxGroup@1001 : Record 321;
                           BEGIN
                             IF TaxGroupId = BlankGUID THEN BEGIN
                               TaxGroupCode := '';
                               "Tax Group Code" := '';
                               "Tax Group Id" := BlankGUID;
                               "VAT Prod. Posting Group" := '';
                               EXIT;
                             END;

                             IF GeneralLedgerSetup.UseVat THEN BEGIN
                               ValidateVATProdPostingGroup.SETRANGE(Id,TaxGroupId);
                               IF NOT ValidateVATProdPostingGroup.FINDFIRST THEN
                                 ERROR(VATGroupIdDoesNotMatchAVATGroupErr);

                               TaxGroupCode := ValidateVATProdPostingGroup.Code;
                               "VAT Prod. Posting Group" := TaxGroupCode;
                               RegisterFieldSet(FIELDNO("VAT Prod. Posting Group"));
                             END ELSE BEGIN
                               ValidateTaxGroup.SETRANGE(Id,TaxGroupId);
                               IF NOT ValidateTaxGroup.FINDFIRST THEN
                                 ERROR(TaxGroupIdDoesNotMatchATaxGroupErr);

                               TaxGroupCode := ValidateTaxGroup.Code;
                               "Tax Group Code" := TaxGroupCode;
                               "Tax Group Id" := TaxGroupId;
                               RegisterFieldSet(FIELDNO("Tax Group Code"));
                               RegisterFieldSet(FIELDNO("Tax Group Id"));
                             END;
                           END;
                            }

    { 1   ;2   ;Field     ;
                Name=taxGroupCode;
                CaptionML=[@@@={Locked};
                           ENU=TaxGroupCode;
                           ESM=TaxGroupCode;
                           FRC=TaxGroupCode;
                           ENC=TaxGroupCode];
                ToolTipML=[ENU=Specifies the code of the tax group or VAT group.;
                           ESM=Especifica el c¢digo del grupo impositivo o grupo de IVA.;
                           FRC=Sp‚cifie le code du groupe fiscal ou du groupe TVA.;
                           ENC=Specifies the code of the tax group or VAT group.];
                ApplicationArea=#All;
                SourceExpr=TaxGroupCode;
                Editable=FALSE }

    { 5   ;2   ;Field     ;
                Name=taxable;
                CaptionML=[@@@={Locked};
                           ENU=Taxable;
                           ESM=Taxable;
                           FRC=Taxable;
                           ENC=Taxable];
                ToolTipML=[ENU=Specifies the tax group code for the tax-detail entry.;
                           ESM=Especifica el c¢digo de grupo impositivo del movimiento de detalle de impuestos.;
                           FRC=Sp‚cifie le code groupe fiscal de l'‚criture sp‚cification de taxe.;
                           ENC=Specifies the tax group code for the tax-detail entry.];
                ApplicationArea=#All;
                SourceExpr=Taxable;
                OnValidate=VAR
                             GeneralLedgerSetup@1001 : Record 98;
                             TaxGroup@1002 : Record 321;
                             NativeEDMTypes@1000 : Codeunit 2801;
                           BEGIN
                             IF GeneralLedgerSetup.UseVat THEN
                               EXIT;

                             IF NOT NativeEDMTypes.GetTaxGroupFromTaxable(Taxable,TaxGroup) THEN
                               EXIT;

                             VALIDATE("Tax Group Id",TaxGroup.Id);
                             RegisterFieldSet(FIELDNO("Tax Group Id"));
                             RegisterFieldSet(FIELDNO("Tax Group Code"));
                           END;
                            }

    { 19  ;2   ;Field     ;
                Name=lastModifiedDateTime;
                CaptionML=[@@@={Locked};
                           ENU=LastModifiedDateTime;
                           ESM=LastModifiedDateTime;
                           FRC=LastModifiedDateTime;
                           ENC=LastModifiedDateTime];
                ApplicationArea=#All;
                SourceExpr="Last DateTime Modified";
                Editable=FALSE }

  }
  CODE
  {
    VAR
      TempFieldSet@1011 : TEMPORARY Record 2000000041;
      GeneralLedgerSetup@1006 : Record 98;
      ValidateUnitOfMeasure@1007 : Record 204;
      NativeAPILanguageHandler@1002 : Codeunit 2850;
      BlankGUID@1001 : GUID;
      BaseUnitOfMeasureId@1008 : GUID;
      TaxGroupId@1017 : GUID;
      TaxGroupCode@1018 : Code[20];
      BaseUnitOfMeasureInternationalStandardCode@1005 : Code[10];
      BaseUnitOfMeasureCode@1009 : Code[10];
      BaseUnitOfMeasureDescription@1012 : Text[10];
      InventoryValue@1000 : Decimal;
      Taxable@1004 : Boolean;
      BaseUnitOfMeasureValuesDontMatchErr@1015 : TextConst 'ENU=The base unit of measure values do not match to a specific Unit of Measure.;ESM=La unidad base de los valores de medida no coinciden con una unidad de medida espec¡fica.;FRC=Les valeurs unit‚ de mesure de base ne correspondent pas … une unit‚ de mesure sp‚cifique.;ENC=The base unit of measure values do not match to a specific Unit of Measure.';
      BaseUnitOfMeasureIdDoesNotMatchAUnitOfMeasureErr@1014 : TextConst '@@@={Locked};ENU=The "baseUnitOfMeasureId" does not match to a Unit of Measure.;ESM=The "baseUnitOfMeasureId" does not match to a Unit of Measure.;FRC=The "baseUnitOfMeasureId" does not match to a Unit of Measure.;ENC=The "baseUnitOfMeasureId" does not match to a Unit of Measure.';
      BaseUnitOfMeasureIntStdCodeDoesNotMatchAUnitOfMeasureErr@1013 : TextConst '@@@={Locked};ENU=The "baseUnitOfMeasureIntStdCode" does not match to a Unit of Measure.;ESM=The "baseUnitOfMeasureIntStdCode" does not match to a Unit of Measure.;FRC=The "baseUnitOfMeasureIntStdCode" does not match to a Unit of Measure.;ENC=The "baseUnitOfMeasureIntStdCode" does not match to a Unit of Measure.';
      BaseUnitOfMeasureIntStdCodeMatchesManyUnitsOfMeasureErr@1016 : TextConst '@@@={Locked};ENU=The "baseUnitOfMeasureIntStdCode" matches to many Units of Measure.;ESM=The "baseUnitOfMeasureIntStdCode" matches to many Units of Measure.;FRC=The "baseUnitOfMeasureIntStdCode" matches to many Units of Measure.;ENC=The "baseUnitOfMeasureIntStdCode" matches to many Units of Measure.';
      TaxGroupIdDoesNotMatchATaxGroupErr@1010 : TextConst '@@@={Locked};ENU=The "taxGroupId" does not match a Tax Group.;ESM=The "taxGroupId" does not match a Tax Group.;FRC=The "taxGroupId" does not match a Tax Group.;ENC=The "taxGroupId" does not match a Tax Group.';
      VATGroupIdDoesNotMatchAVATGroupErr@1022 : TextConst '@@@={Locked};ENU=The "taxGroupId" does not match a VAT Product Posting Group.;ESM=The "taxGroupId" does not match a VAT Product Posting Group.;FRC=The "taxGroupId" does not match a VAT Product Posting Group.;ENC=The "taxGroupId" does not match a VAT Product Posting Group.';
      ItemExistsErr@1003 : TextConst '@@@={Locked};ENU=The item already exists.;ESM=The item already exists.;FRC=The item already exists.;ENC=The item already exists.';

    LOCAL PROCEDURE SetCalculatedFields@6();
    BEGIN
      SetCalculatedUnitsOfMeasureFields;
      SetCalculatedTaxGroupFields;
      CALCFIELDS(Inventory);
      InventoryValue := Inventory;
    END;

    LOCAL PROCEDURE ClearCalculatedFields@10();
    BEGIN
      CLEAR(Id);
      CLEAR(BaseUnitOfMeasureId);
      CLEAR(BaseUnitOfMeasureInternationalStandardCode);
      CLEAR(BaseUnitOfMeasureCode);
      CLEAR(BaseUnitOfMeasureDescription);
      CLEAR(Taxable);
      CLEAR(TaxGroupCode);
      CLEAR(TaxGroupId);
      CLEAR(InventoryValue);
      TempFieldSet.DELETEALL;
    END;

    LOCAL PROCEDURE SetCalculatedUnitsOfMeasureFields@9();
    VAR
      UnitOfMeasure@1002 : Record 204;
      EmptyGuid@1000 : GUID;
    BEGIN
      IF UnitOfMeasure.GET("Base Unit of Measure") THEN BEGIN
        BaseUnitOfMeasureId := UnitOfMeasure.Id;
        BaseUnitOfMeasureInternationalStandardCode := UnitOfMeasure."International Standard Code";
        BaseUnitOfMeasureCode := "Base Unit of Measure";
        BaseUnitOfMeasureDescription := UnitOfMeasure.Description;
        EXIT;
      END;

      BaseUnitOfMeasureId := EmptyGuid;
      BaseUnitOfMeasureInternationalStandardCode := '';
      BaseUnitOfMeasureCode := '';
      BaseUnitOfMeasureDescription := '';
    END;

    LOCAL PROCEDURE SetCalculatedTaxGroupFields@12();
    VAR
      TaxGroup@1002 : Record 321;
      TaxSetup@1000 : Record 326;
      VATProductPostingGroup@1003 : Record 324;
      EmptyGuid@1001 : GUID;
    BEGIN
      IF GeneralLedgerSetup.UseVat AND VATProductPostingGroup.GET("VAT Prod. Posting Group") THEN BEGIN
        TaxGroupCode := VATProductPostingGroup.Code;
        TaxGroupId := VATProductPostingGroup.Id;
        Taxable := TRUE;
      END ELSE
        IF TaxGroup.GET("Tax Group Code") THEN BEGIN
          "Tax Group Id" := TaxGroup.Id;
          TaxGroupId := "Tax Group Id";
          TaxGroupCode := "Tax Group Code";
          IF TaxSetup.GET THEN
            Taxable := "Tax Group Code" <> TaxSetup."Non-Taxable Tax Group Code"
          ELSE
            Taxable := FALSE;
        END ELSE BEGIN
          TaxGroupId := EmptyGuid;
          TaxGroupCode := '';
          "Tax Group Id" := EmptyGuid;
          Taxable := FALSE;
        END;
    END;

    LOCAL PROCEDURE RegisterFieldSet@11(FieldNo@1000 : Integer);
    BEGIN
      IF TempFieldSet.GET(DATABASE::Item,FieldNo) THEN
        EXIT;

      TempFieldSet.INIT;
      TempFieldSet.TableNo := DATABASE::Item;
      TempFieldSet.VALIDATE("No.",FieldNo);
      TempFieldSet.INSERT(TRUE);
    END;

    BEGIN
    END.
  }
}

