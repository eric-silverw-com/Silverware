OBJECT Codeunit 83 Sales-Quote to Order (Yes/No)
{
  OBJECT-PROPERTIES
  {
    Date=11/27/18;
    Time=11:07:49 PM;
    Version List=;
  }
  PROPERTIES
  {
    TableNo=36;
    OnRun=VAR
            OfficeMgt@1000 : Codeunit 1630;
            SalesOrder@1002 : Page 42;
            OpenPage@1001 : Boolean;
          BEGIN
            IF IsOnRunHandled(Rec) THEN
              EXIT;

            TESTFIELD("Document Type","Document Type"::Quote);
            IF GUIALLOWED THEN
              IF NOT CONFIRM(ConfirmConvertToOrderQst,FALSE) THEN
                EXIT;

            IF CheckCustomerCreated(TRUE) THEN
              GET("Document Type"::Quote,"No.")
            ELSE
              EXIT;

            SalesQuoteToOrder.RUN(Rec);
            SalesQuoteToOrder.GetSalesOrderHeader(SalesHeader2);
            COMMIT;

            IF GUIALLOWED THEN
              IF OfficeMgt.AttachAvailable THEN
                OpenPage := TRUE
              ELSE
                OpenPage := CONFIRM(STRSUBSTNO(OpenNewInvoiceQst,SalesHeader2."No."),TRUE);
            IF OpenPage THEN BEGIN
              CLEAR(SalesOrder);
              SalesOrder.CheckNotificationsOnce;
              SalesHeader2.SETRECFILTER;
              SalesOrder.SETTABLEVIEW(SalesHeader2);
              SalesOrder.RUN;
            END;
          END;

  }
  CODE
  {
    VAR
      ConfirmConvertToOrderQst@1000 : TextConst 'ENU=Do you want to convert the quote to an order?';
      OpenNewInvoiceQst@1004 : TextConst '@@@="%1 = No. of the new sales order document.";ENU=The quote has been converted to order %1. Do you want to open the new order?';
      SalesHeader2@1002 : Record 36;
      SalesQuoteToOrder@1003 : Codeunit 86;

    LOCAL PROCEDURE IsOnRunHandled@1(VAR SalesHeader@1000 : Record 36) IsHandled : Boolean;
    BEGIN
      IsHandled := FALSE;
      OnBeforeRun(SalesHeader,IsHandled);
      EXIT(IsHandled);
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeRun@2(VAR SalesHeader@1000 : Record 36;VAR IsHandled@1001 : Boolean);
    BEGIN
    END;

    BEGIN
    END.
  }
}

