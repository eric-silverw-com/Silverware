OBJECT Table 137 Inc. Doc. Attachment Overview
{
  OBJECT-PROPERTIES
  {
    Date=09/19/18;
    Time=12:00:00 PM;
    Version List=NAVW113.00;
  }
  PROPERTIES
  {
    OnDelete=VAR
               IncomingDocumentAttachment@1000 : Record 133;
             BEGIN
               IF IncomingDocumentAttachment.GET("Incoming Document Entry No.","Line No.") THEN
                 IncomingDocumentAttachment.DELETE(TRUE);
             END;

    CaptionML=[ENU=Inc. Doc. Attachment Overview;
               ESM=Inf. general de archivos adjuntos de documentos entrantes;
               FRC=Aper‡u piŠce jointe d'un doc. entrant;
               ENC=Inc. Doc. Attachment Overview];
  }
  FIELDS
  {
    { 1   ;   ;Incoming Document Entry No.;Integer;TableRelation="Incoming Document";
                                                   CaptionML=[ENU=Incoming Document Entry No.;
                                                              ESM=N§ mov. de documentos entrantes;
                                                              FRC=Nø de s‚quence du document entrant;
                                                              ENC=Incoming Document Entry No.] }
    { 2   ;   ;Line No.            ;Integer       ;InitValue=0;
                                                   CaptionML=[ENU=Line No.;
                                                              ESM=N§ l¡nea;
                                                              FRC=Nø ligne;
                                                              ENC=Line No.] }
    { 3   ;   ;Created Date-Time   ;DateTime      ;CaptionML=[ENU=Created Date-Time;
                                                              ESM=Fecha / hora creaci¢n;
                                                              FRC=Date/heure cr‚ation;
                                                              ENC=Created Date-Time] }
    { 4   ;   ;Created By User Name;Code50        ;DataClassification=EndUserIdentifiableInformation;
                                                   CaptionML=[ENU=Created By User Name;
                                                              ESM=Creado por nombre de usuario;
                                                              FRC=Cr‚‚ par nom utilisateur;
                                                              ENC=Created By User Name] }
    { 5   ;   ;Name                ;Text250       ;CaptionML=[ENU=Name;
                                                              ESM=Nombre;
                                                              FRC=Nom;
                                                              ENC=Name];
                                                   Editable=No }
    { 6   ;   ;Type                ;Option        ;CaptionML=[ENU=Type;
                                                              ESM=Tipo;
                                                              FRC=Type;
                                                              ENC=Type];
                                                   OptionCaptionML=[ENU=" ,Image,PDF,Word,Excel,PowerPoint,Email,XML,Other";
                                                                    ESM=" ,Imagen,PDF,Word,Excel,PowerPoint,Correo electr¢nico,XML,Otros";
                                                                    FRC=" ,Image,PDF,Word,Excel,PowerPoint,Email,XML,Autre";
                                                                    ENC=" ,Image,PDF,Word,Excel,PowerPoint,Email,XML,Other"];
                                                   OptionString=[ ,Image,PDF,Word,Excel,PowerPoint,Email,XML,Other];
                                                   Editable=No }
    { 7   ;   ;File Extension      ;Text30        ;CaptionML=[ENU=File Extension;
                                                              ESM=Extensi¢n archivo;
                                                              FRC=Extension de fichier;
                                                              ENC=File Extension];
                                                   Editable=No }
    { 100 ;   ;Attachment Type     ;Option        ;CaptionML=[ENU=Attachment Type;
                                                              ESM=Tipo de archivo adjunto;
                                                              FRC=Type piŠce jointe;
                                                              ENC=Attachment Type];
                                                   OptionCaptionML=[ENU=,Group,Main Attachment,OCR Result,Supporting Attachment,Link;
                                                                    ESM=,Grupo,Archivo Adjunto Principal,Resultado de OCR,Archivo adjunto relacionado,V¡nculo;
                                                                    FRC=,Groupe,PiŠce jointe principale,R‚sultat OCR,Prise en charge piŠce jointe,Lien;
                                                                    ENC=,Group,Main Attachment,OCR Result,Supporting Attachment,Link];
                                                   OptionString=,Group,Main Attachment,OCR Result,Supporting Attachment,Link;
                                                   Editable=No }
    { 101 ;   ;Sorting Order       ;Integer       ;CaptionML=[ENU=Sorting Order;
                                                              ESM=Orden clasificaci¢n;
                                                              FRC=Ordre de tri;
                                                              ENC=Sorting Order] }
    { 102 ;   ;Indentation         ;Integer       ;CaptionML=[ENU=Indentation;
                                                              ESM=Indentar;
                                                              FRC=Indentation;
                                                              ENC=Indentation] }
  }
  KEYS
  {
    {    ;Sorting Order,Incoming Document Entry No.;
                                                   Clustered=Yes }
  }
  FIELDGROUPS
  {
    { 1   ;Brick               ;Created Date-Time,Name,File Extension    }
  }
  CODE
  {
    VAR
      SupportingAttachmentsTxt@1000 : TextConst 'ENU=Supporting Attachments;ESM=Archivos adjuntos relacionados;FRC=Prise en charge piŠces jointes;ENC=Supporting Attachments';
      NotAvailableAttachmentMsg@1001 : TextConst 'ENU=The attachment is no longer available.;ESM=El archivo adjunto ya no est  disponible.;FRC=La piŠce jointe n''est plus disponible.;ENC=The attachment is no longer available.';
      ClientTypeManagement@1077 : Codeunit 4;

    PROCEDURE NameDrillDown@13();
    VAR
      IncomingDocument@1001 : Record 130;
      IncomingDocumentAttachment@1000 : Record 133;
    BEGIN
      CASE "Attachment Type" OF
        "Attachment Type"::Group:
          EXIT;
        "Attachment Type"::Link:
          BEGIN
            IncomingDocument.GET("Incoming Document Entry No.");
            HYPERLINK(IncomingDocument.GetURL);
          END
        ELSE BEGIN
          IF NOT IncomingDocumentAttachment.GET("Incoming Document Entry No.","Line No.") THEN
            MESSAGE(NotAvailableAttachmentMsg)
          ELSE
            IF (Type = Type::Image) AND (ClientTypeManagement.GetCurrentClientType = CLIENTTYPE::Phone) THEN
              PAGE.RUN(PAGE::"O365 Incoming Doc. Att. Pict.",IncomingDocumentAttachment)
            ELSE
              IncomingDocumentAttachment.Export(Name + '.' + "File Extension",TRUE);
        END
      END;
    END;

    [External]
    PROCEDURE GetStyleTxt@4() : Text;
    BEGIN
      CASE "Attachment Type" OF
        "Attachment Type"::Group,
        "Attachment Type"::"Main Attachment",
        "Attachment Type"::Link:
          EXIT('Strong');
        ELSE
          EXIT('Standard');
      END;
    END;

    PROCEDURE InsertFromIncomingDocument@2(IncomingDocument@1000 : Record 130;VAR TempIncDocAttachmentOverview@1003 : TEMPORARY Record 137);
    VAR
      SortingOrder@1004 : Integer;
    BEGIN
      InsertMainAttachment(IncomingDocument,TempIncDocAttachmentOverview,SortingOrder);
      InsertLinkAddress(IncomingDocument,TempIncDocAttachmentOverview,SortingOrder);
      InsertSupportingAttachments(
        IncomingDocument,TempIncDocAttachmentOverview,SortingOrder,
        IncomingDocument."Document Type" <> IncomingDocument."Document Type"::"Sales Invoice");
    END;

    [External]
    PROCEDURE InsertSupportingAttachmentsFromIncomingDocument@1(IncomingDocument@1001 : Record 130;VAR TempIncDocAttachmentOverview@1000 : TEMPORARY Record 137);
    VAR
      SortingOrder@1002 : Integer;
    BEGIN
      InsertSupportingAttachments(IncomingDocument,TempIncDocAttachmentOverview,SortingOrder,FALSE);
    END;

    LOCAL PROCEDURE InsertMainAttachment@3(IncomingDocument@1000 : Record 130;VAR TempIncDocAttachmentOverview@1001 : TEMPORARY Record 137;VAR SortingOrder@1002 : Integer);
    VAR
      IncomingDocumentAttachment@1003 : Record 133;
    BEGIN
      IF NOT IncomingDocument.GetMainAttachment(IncomingDocumentAttachment) THEN
        EXIT;

      IF IncomingDocument."Document Type" = IncomingDocument."Document Type"::"Sales Invoice" THEN
        InsertFromIncomingDocumentAttachment(
          TempIncDocAttachmentOverview,IncomingDocumentAttachment,SortingOrder,
          TempIncDocAttachmentOverview."Attachment Type"::"Supporting Attachment",0)
      ELSE
        InsertFromIncomingDocumentAttachment(
          TempIncDocAttachmentOverview,IncomingDocumentAttachment,SortingOrder,
          TempIncDocAttachmentOverview."Attachment Type"::"Main Attachment",0);
    END;

    LOCAL PROCEDURE InsertSupportingAttachments@6(IncomingDocument@1000 : Record 130;VAR TempIncDocAttachmentOverview@1001 : TEMPORARY Record 137;VAR SortingOrder@1002 : Integer;IncludeGroupCaption@1004 : Boolean);
    VAR
      IncomingDocumentAttachment@1003 : Record 133;
      Indentation2@1005 : Integer;
    BEGIN
      IF NOT IncomingDocument.GetAdditionalAttachments(IncomingDocumentAttachment) THEN
        EXIT;

      IF IncludeGroupCaption THEN
        InsertGroup(TempIncDocAttachmentOverview,IncomingDocument,SortingOrder,SupportingAttachmentsTxt);
      IF IncomingDocument."Document Type" = IncomingDocument."Document Type"::"Sales Invoice" THEN
        Indentation2 := 0
      ELSE
        Indentation2 := 1;
      REPEAT
        InsertFromIncomingDocumentAttachment(
          TempIncDocAttachmentOverview,IncomingDocumentAttachment,SortingOrder,
          TempIncDocAttachmentOverview."Attachment Type"::"Supporting Attachment",Indentation2);
      UNTIL IncomingDocumentAttachment.NEXT = 0;
    END;

    LOCAL PROCEDURE InsertLinkAddress@8(IncomingDocument@1003 : Record 130;VAR TempIncDocAttachmentOverview@1000 : TEMPORARY Record 137;VAR SortingOrder@1002 : Integer);
    VAR
      URL@1001 : Text;
    BEGIN
      URL := IncomingDocument.GetURL;
      IF URL = '' THEN
        EXIT;

      CLEAR(TempIncDocAttachmentOverview);
      TempIncDocAttachmentOverview.INIT;
      TempIncDocAttachmentOverview."Incoming Document Entry No." := IncomingDocument."Entry No.";
      AssignSortingNo(TempIncDocAttachmentOverview,SortingOrder);
      TempIncDocAttachmentOverview.Name := COPYSTR(URL,1,MAXSTRLEN(TempIncDocAttachmentOverview.Name));
      TempIncDocAttachmentOverview."Attachment Type" := TempIncDocAttachmentOverview."Attachment Type"::Link;
      TempIncDocAttachmentOverview.INSERT(TRUE);
    END;

    LOCAL PROCEDURE InsertFromIncomingDocumentAttachment@7(VAR TempIncDocAttachmentOverview@1001 : TEMPORARY Record 137;IncomingDocumentAttachment@1000 : Record 133;VAR SortingOrder@1002 : Integer;AttachmentType@1003 : Option;Indentation2@1004 : Integer);
    BEGIN
      CLEAR(TempIncDocAttachmentOverview);
      TempIncDocAttachmentOverview.INIT;
      TempIncDocAttachmentOverview.TRANSFERFIELDS(IncomingDocumentAttachment);
      AssignSortingNo(TempIncDocAttachmentOverview,SortingOrder);
      TempIncDocAttachmentOverview."Attachment Type" := AttachmentType;
      TempIncDocAttachmentOverview.Indentation := Indentation2;
      TempIncDocAttachmentOverview.INSERT(TRUE);
    END;

    LOCAL PROCEDURE InsertGroup@10(VAR TempIncDocAttachmentOverview@1001 : TEMPORARY Record 137;IncomingDocument@1000 : Record 130;VAR SortingOrder@1002 : Integer;Description@1003 : Text[50]);
    BEGIN
      CLEAR(TempIncDocAttachmentOverview);
      TempIncDocAttachmentOverview.INIT;
      TempIncDocAttachmentOverview."Incoming Document Entry No." := IncomingDocument."Entry No.";
      AssignSortingNo(TempIncDocAttachmentOverview,SortingOrder);
      TempIncDocAttachmentOverview."Attachment Type" := TempIncDocAttachmentOverview."Attachment Type"::Group;
      TempIncDocAttachmentOverview.Type := Type::" ";
      TempIncDocAttachmentOverview.Name := Description;
      TempIncDocAttachmentOverview.INSERT(TRUE);
    END;

    LOCAL PROCEDURE AssignSortingNo@5(VAR TempIncDocAttachmentOverview@1000 : TEMPORARY Record 137;VAR SortingOrder@1001 : Integer);
    BEGIN
      SortingOrder += 1;
      TempIncDocAttachmentOverview."Sorting Order" := SortingOrder;
    END;

    BEGIN
    END.
  }
}

