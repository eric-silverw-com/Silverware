OBJECT Report 1498 Date Compress Bank Acc. Ledger
{
  OBJECT-PROPERTIES
  {
    Date=09/19/18;
    Time=12:00:00 PM;
    Version List=NAVW113.00;
  }
  PROPERTIES
  {
    Permissions=TableData 17=rimd,
                TableData 45=rimd,
                TableData 87=rimd,
                TableData 271=rimd,
                TableData 355=imd;
    CaptionML=[ENU=Date Compress Bank Account Ledger;
               ESM=Comprimir fechas movs. bancos;
               FRC=Compression par date du grand livre de compte bancaire;
               ENC=Date Compress Bank Account Ledger];
    ApplicationArea=#Suite;
    ProcessingOnly=Yes;
    OnPreReport=BEGIN
                  DimSelectionBuf.CompareDimText(
                    3,REPORT::"Date Compress Bank Acc. Ledger",'',RetainDimText,Text010);
                  BankAccLedgEntryFilter := COPYSTR("Bank Account Ledger Entry".GETFILTERS,1,MAXSTRLEN(DateComprReg.Filter));
                END;

    UsageCategory=Tasks;
  }
  DATASET
  {
    { 4920;    ;DataItem;                    ;
               DataItemTable=Table271;
               DataItemTableView=SORTING(Bank Account No.,Posting Date)
                                 WHERE(Open=CONST(No));
               OnPreDataItem=VAR
                               GLSetup@1000 : Record 98;
                             BEGIN
                               IF NOT CONFIRM(Text000,FALSE) THEN
                                 CurrReport.BREAK;

                               IF EntrdDateComprReg."Ending Date" = 0D THEN
                                 ERROR(Text003,EntrdDateComprReg.FIELDCAPTION("Ending Date"));

                               Window.OPEN(Text004);

                               SourceCodeSetup.GET;
                               SourceCodeSetup.TESTFIELD("Compress Bank Acc. Ledger");

                               SelectedDim.GetSelectedDim(
                                 USERID,3,REPORT::"Date Compress Bank Acc. Ledger",'',TempSelectedDim);
                               GLSetup.GET;
                               Retain[3] :=
                                 TempSelectedDim.GET(
                                   USERID,3,REPORT::"Date Compress Bank Acc. Ledger",'',GLSetup."Global Dimension 1 Code");
                               Retain[4] :=
                                 TempSelectedDim.GET(
                                   USERID,3,REPORT::"Date Compress Bank Acc. Ledger",'',GLSetup."Global Dimension 2 Code");

                               GLEntry.LOCKTABLE;
                               NewBankAccLedgEntry.LOCKTABLE;
                               GLReg.LOCKTABLE;
                               DateComprReg.LOCKTABLE;

                               IF GLEntry.FINDLAST THEN;
                               LastEntryNo := GLEntry."Entry No.";
                               NextTransactionNo := GLEntry."Transaction No." + 1;
                               SETRANGE("Entry No.",0,LastEntryNo);
                               SETRANGE("Posting Date",EntrdDateComprReg."Starting Date",EntrdDateComprReg."Ending Date");

                               InitRegisters;
                             END;

               OnAfterGetRecord=BEGIN
                                  BankAccLedgEntry2 := "Bank Account Ledger Entry";
                                  WITH BankAccLedgEntry2 DO BEGIN
                                    SETCURRENTKEY("Bank Account No.","Posting Date");
                                    COPYFILTERS("Bank Account Ledger Entry");
                                    SETRANGE("Bank Account No.","Bank Account No.");
                                    SETFILTER("Posting Date",DateComprMgt.GetDateFilter("Posting Date",EntrdDateComprReg,TRUE));
                                    SETRANGE("Bank Acc. Posting Group","Bank Acc. Posting Group");
                                    SETRANGE("Currency Code","Currency Code");
                                    SETRANGE("Document Type","Document Type");

                                    IF RetainNo(FIELDNO("Document No.")) THEN
                                      SETRANGE("Document No.","Document No.");
                                    IF RetainNo(FIELDNO("Our Contact Code")) THEN
                                      SETRANGE("Our Contact Code","Our Contact Code");
                                    IF RetainNo(FIELDNO("Global Dimension 1 Code")) THEN
                                      SETRANGE("Global Dimension 1 Code","Global Dimension 1 Code");
                                    IF RetainNo(FIELDNO("Global Dimension 2 Code")) THEN
                                      SETRANGE("Global Dimension 2 Code","Global Dimension 2 Code");
                                    IF Amount >= 0 THEN
                                      SETFILTER(Amount,'>=0')
                                    ELSE
                                      SETFILTER(Amount,'<0');

                                    InitNewEntry(NewBankAccLedgEntry);

                                    DimBufMgt.CollectDimEntryNo(
                                      TempSelectedDim,"Dimension Set ID","Entry No.",
                                      0,FALSE,DimEntryNo);
                                    ComprDimEntryNo := DimEntryNo;
                                    SummarizeEntry(NewBankAccLedgEntry,BankAccLedgEntry2);
                                    WHILE NEXT <> 0 DO BEGIN
                                      DimBufMgt.CollectDimEntryNo(
                                        TempSelectedDim,"Dimension Set ID","Entry No.",
                                        ComprDimEntryNo,TRUE,DimEntryNo);
                                      IF DimEntryNo = ComprDimEntryNo THEN
                                        SummarizeEntry(NewBankAccLedgEntry,BankAccLedgEntry2);
                                    END;

                                    InsertNewEntry(NewBankAccLedgEntry,ComprDimEntryNo);

                                    ComprCollectedEntries;
                                  END;

                                  IF DateComprReg."No. Records Deleted" >= NoOfDeleted + 10 THEN BEGIN
                                    NoOfDeleted := DateComprReg."No. Records Deleted";
                                    InsertRegisters(GLReg,DateComprReg);
                                  END;
                                END;

               OnPostDataItem=BEGIN
                                IF DateComprReg."No. Records Deleted" > NoOfDeleted THEN
                                  InsertRegisters(GLReg,DateComprReg);
                              END;

               ReqFilterFields=Bank Account No.,Bank Acc. Posting Group,Currency Code }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
      OnOpenPage=BEGIN
                   InitializeParameter;
                 END;

    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[ENU=Options;
                             ESM=Opciones;
                             FRC=Options;
                             ENC=Options] }

      { 1   ;2   ;Field     ;
                  CaptionML=[ENU=Starting Date;
                             ESM=Fecha inicial;
                             FRC=Date d‚but;
                             ENC=Starting Date];
                  ToolTipML=[ENU=Specifies the date from which the report or batch job processes information.;
                             ESM=Especifica la fecha a partir de la cual el informe o el trabajo por lotes procesa la informaci¢n.;
                             FRC=Sp‚cifie la date … partir de laquelle le rapport ou le traitement en lot traite les informations.;
                             ENC=Specifies the date from which the report or batch job processes information.];
                  ApplicationArea=#Suite;
                  SourceExpr=EntrdDateComprReg."Starting Date" }

      { 2   ;2   ;Field     ;
                  CaptionML=[ENU=Ending Date;
                             ESM=Fecha final;
                             FRC=Date fin;
                             ENC=Ending Date];
                  ToolTipML=[ENU=Specifies the date to which the report or batch job processes information.;
                             ESM=Especifica la fecha hasta la cual el informe o el trabajo por lotes procesa la informaci¢n.;
                             FRC=Sp‚cifie la date jusqu'… laquelle le rapport ou le traitement en lot traite les informations.;
                             ENC=Specifies the date to which the report or batch job processes information.];
                  ApplicationArea=#Suite;
                  SourceExpr=EntrdDateComprReg."Ending Date" }

      { 3   ;2   ;Field     ;
                  CaptionML=[ENU=Period Length;
                             ESM=Longitud periodo;
                             FRC=Dur‚e de la p‚riode;
                             ENC=Period Length];
                  ToolTipML=[ENU=Specifies the period for which data is shown in the report. For example, enter "1M" for one month, "30D" for thirty days, "3Q" for three quarters, or "5Y" for five years.;
                             ESM=Especifica el per¡odo para el que se muestran datos en el informe. Por ejemplo, indique "1M" para un mes, "30D" para 30 d¡as, "3T" para tres trimestres o "5A" para cinco a¤os.;
                             FRC=Sp‚cifie la p‚riode pour laquelle les donn‚es sont affich‚es dans le rapport. Par exemple, saisissez ®ÿ1Mÿ¯ pour un mois, ®ÿ30Dÿ¯ pour trente jours, ®ÿ3Qÿ¯ pour trois trimestres ou ®ÿ5Yÿ¯ pour cinq ann‚es.;
                             ENC=Specifies the period for which data is shown in the report. For example, enter "1M" for one month, "30D" for thirty days, "3Q" for three quarters, or "5Y" for five years.];
                  OptionCaptionML=[ENU=Day,Week,Month,Quarter,Year,Period;
                                   ESM=D¡a,Semana,Mes,Trimestre,A¤o,Periodo;
                                   FRC=Jour,Semaine,Mois,Trimestre,Ann‚e,P‚riode;
                                   ENC=Day,Week,Month,Quarter,Year,Period];
                  ApplicationArea=#Suite;
                  SourceExpr=EntrdDateComprReg."Period Length" }

      { 4   ;2   ;Field     ;
                  CaptionML=[ENU=Posting Description;
                             ESM=Texto de registro;
                             FRC=Description du report;
                             ENC=Posting Description];
                  ToolTipML=[ENU=Specifies a text that accompanies the entries that result from the compression. The default description is Date Compressed.;
                             ESM=Especifica el texto que se adjunta a los movimientos resultantes de la compresi¢n. La descripci¢n predeterminada es Comprimido por fechas.;
                             FRC=Sp‚cifie un texte qui figure … c“t‚ des ‚critures qui r‚sultent de la compression. La description par d‚faut est ®ÿCompression ‚crituresÿ¯.;
                             ENC=Specifies a text that accompanies the entries that result from the compression. The default description is Date Compressed.];
                  ApplicationArea=#Suite;
                  SourceExpr=EntrdBankAccLedgEntry.Description }

      { 25  ;2   ;Group     ;
                  CaptionML=[ENU=Retain Field Contents;
                             ESM=Guardar contenido campo;
                             FRC=Conserver champ;
                             ENC=Retain Field Contents] }

      { 5   ;3   ;Field     ;
                  CaptionML=[ENU=Document No.;
                             ESM=N§ documento;
                             FRC=Nø de document;
                             ENC=Document No.];
                  ToolTipML=[ENU=Specifies the number of the document that is processed by the report or batch job.;
                             ESM=Especifica el n£mero del documento procesado por el informe o el trabajo por lotes.;
                             FRC=Sp‚cifie le num‚ro du document trait‚ par le rapport ou le traitement en lot.;
                             ENC=Specifies the number of the document that is processed by the report or batch job.];
                  ApplicationArea=#Suite;
                  SourceExpr=Retain[1] }

      { 7   ;3   ;Field     ;
                  CaptionML=[ENU=Our Contact Code;
                             ESM=Ntro. c¢d. contacto;
                             FRC=Notre code contact;
                             ENC=Our Contact Code];
                  ToolTipML=[ENU=Specifies the employee who is responsible for this bank account.;
                             ESM=Especifica el empleado responsable de esta cuenta bancaria.;
                             FRC=Sp‚cifie le nom de l'employ‚ qui est responsable de ce compte bancaire.;
                             ENC=Specifies the employee who is responsible for this bank account.];
                  ApplicationArea=#Suite;
                  SourceExpr=Retain[2] }

      { 6   ;2   ;Field     ;
                  CaptionML=[ENU=Retain Dimensions;
                             ESM=Retener dimensiones;
                             FRC=Conserver les dimensions;
                             ENC=Retain Dimensions];
                  ToolTipML=[ENU=Specifies which dimension information you want to retain when the entries are compressed. The more dimension information that you choose to retain, the more detailed the compressed entries are.;
                             ESM=Especifica la informaci¢n de dimensi¢n que desea conservar cuando se compriman los movimientos. Cuanta m s informaci¢n de dimensi¢n elija conservar, m s detallados ser n los movimientos comprimidos.;
                             FRC=Sp‚cifie les informations analytiques … conserver une fois les ‚critures compress‚es. Plus vous choisissez de champs … conserver, plus les ‚critures compress‚es sont d‚taill‚es.;
                             ENC=Specifies which dimension information you want to retain when the entries are compressed. The more dimension information that you choose to retain, the more detailed the compressed entries are.];
                  ApplicationArea=#Dimensions;
                  SourceExpr=RetainDimText;
                  Editable=FALSE;
                  OnAssistEdit=BEGIN
                                 DimSelectionBuf.SetDimSelectionMultiple(3,REPORT::"Date Compress Bank Acc. Ledger",RetainDimText);
                               END;
                                }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=This batch job deletes entries. Therefore, it is important that you make a backup of the database before you run the batch job.\\Do you want to date compress the entries?;ESM=Este trabajo por lotes borra movimientos. Por tanto, es muy importante realizar una copia de seguridad de la base de datos antes de ejecutar el trabajo por lotes.\\¨Desea eliminar las fechas de los movimientos?;FRC=Ce traitement en lot supprime des ‚critures. Par cons‚quent, il est important d''effectuer une sauvegarde de la base de donn‚es avant d''ex‚cuter le traitement en lot.\\Voulez-vous compresser les ‚crituresÿ?;ENC=This batch job deletes entries. Therefore, it is important that you make a backup of the database before you run the batch job.\\Do you want to date compress the entries?';
      Text003@1003 : TextConst 'ENU=%1 must be specified.;ESM=Se debe indicar %1.;FRC=%1 doit ˆtre sp‚cifi‚.;ENC=%1 must be specified.';
      Text004@1004 : TextConst 'ENU=Date compressing bank account ledger entries...\\Bank Account No.       #1##########\Date                   #2######\\No. of new entries     #3######\No. of entries deleted #4######;ESM=Comprimiendo por fechas movs. banco...\\N.§ cuenta       #1##########\Fecha                   #2######\\N.§ movs. nuevos     #3######\N.§ movs. eliminados #4######;FRC=Compression des ‚critures banque...\\Nø compte bancaire       #1##########\Date                   #2######\\Nbre ‚critures cr‚‚es     #3######\Nombre d''‚critures supprim‚es #4######;ENC=Date compressing bank account ledger entries...\\Bank Account No.       #1##########\Date                   #2######\\No. of new entries     #3######\No. of entries deleted #4######';
      Text009@1009 : TextConst 'ENU=Date Compressed;ESM=Compactado por fechas;FRC=Compress‚ par date;ENC=Date Compressed';
      Text010@1010 : TextConst 'ENU=Retain Dimensions;ESM=Retener dimensiones;FRC=Conserver les dimensions;ENC=Retain Dimensions';
      SourceCodeSetup@1011 : Record 242;
      DateComprReg@1012 : Record 87;
      EntrdDateComprReg@1013 : Record 87;
      GLReg@1014 : Record 45;
      EntrdBankAccLedgEntry@1015 : Record 271;
      NewBankAccLedgEntry@1016 : Record 271;
      BankAccLedgEntry2@1017 : Record 271;
      GLEntry@1018 : Record 17;
      SelectedDim@1019 : Record 369;
      TempSelectedDim@1020 : TEMPORARY Record 369;
      DimSelectionBuf@1021 : Record 368;
      DateComprMgt@1022 : Codeunit 356;
      DimBufMgt@1023 : Codeunit 411;
      DimMgt@1024 : Codeunit 408;
      Window@1025 : Dialog;
      BankAccLedgEntryFilter@1026 : Text[250];
      NoOfFields@1027 : Integer;
      Retain@1028 : ARRAY [10] OF Boolean;
      FieldNumber@1029 : ARRAY [10] OF Integer;
      FieldNameArray@1030 : ARRAY [10] OF Text[100];
      LastEntryNo@1031 : Integer;
      NextTransactionNo@1032 : Integer;
      NoOfDeleted@1033 : Integer;
      GLRegExists@1034 : Boolean;
      i@1035 : Integer;
      ComprDimEntryNo@1036 : Integer;
      DimEntryNo@1037 : Integer;
      RetainDimText@1038 : Text[250];

    LOCAL PROCEDURE InitRegisters@4();
    VAR
      NextRegNo@1000 : Integer;
    BEGIN
      IF GLReg.FIND('+') THEN;
      GLReg.INIT;
      GLReg."No." := GLReg."No." + 1;
      GLReg."Creation Date" := TODAY;
      GLReg."Source Code" := SourceCodeSetup."Compress Bank Acc. Ledger";
      GLReg."User ID" := USERID;
      GLReg."From Entry No." := LastEntryNo + 1;

      IF DateComprReg.FINDLAST THEN
        NextRegNo := DateComprReg."No." + 1;

      DateComprReg.InitRegister(
        DATABASE::"Bank Account Ledger Entry",NextRegNo,
        EntrdDateComprReg."Starting Date",EntrdDateComprReg."Ending Date",EntrdDateComprReg."Period Length",
        BankAccLedgEntryFilter,GLReg."No.",SourceCodeSetup."Compress Bank Acc. Ledger");

      FOR i := 1 TO NoOfFields DO
        IF Retain[i] THEN
          DateComprReg."Retain Field Contents" :=
            COPYSTR(
              DateComprReg."Retain Field Contents" + ',' + FieldNameArray[i],1,
              MAXSTRLEN(DateComprReg."Retain Field Contents"));
      DateComprReg."Retain Field Contents" := COPYSTR(DateComprReg."Retain Field Contents",2);

      GLRegExists := FALSE;
      NoOfDeleted := 0;
    END;

    LOCAL PROCEDURE InsertRegisters@1(VAR GLReg@1000 : Record 45;VAR DateComprReg@1001 : Record 87);
    BEGIN
      GLEntry.INIT;
      LastEntryNo := LastEntryNo + 1;
      GLEntry."Entry No." := LastEntryNo;
      GLEntry."Posting Date" := TODAY;
      GLEntry.Description := EntrdBankAccLedgEntry.Description;
      GLEntry."Source Code" := SourceCodeSetup."Compress Bank Acc. Ledger";
      GLEntry."System-Created Entry" := TRUE;
      GLEntry."User ID" := USERID;
      GLEntry."Transaction No." := NextTransactionNo;
      GLEntry.INSERT;
      GLEntry.CONSISTENT(GLEntry.Amount = 0);
      GLReg."To Entry No." := GLEntry."Entry No.";

      IF GLRegExists THEN BEGIN
        GLReg.MODIFY;
        DateComprReg.MODIFY;
      END ELSE BEGIN
        GLReg.INSERT;
        DateComprReg.INSERT;
        GLRegExists := TRUE;
      END;
      COMMIT;

      GLEntry.LOCKTABLE;
      NewBankAccLedgEntry.LOCKTABLE;
      GLReg.LOCKTABLE;
      DateComprReg.LOCKTABLE;

      IF GLEntry.FINDLAST THEN;
      IF NewBankAccLedgEntry.FIND('+') THEN;
      IF (LastEntryNo <> GLEntry."Entry No.") OR
         (LastEntryNo <> NewBankAccLedgEntry."Entry No." + 1)
      THEN BEGIN
        LastEntryNo := GLEntry."Entry No.";
        NextTransactionNo := GLEntry."Transaction No." + 1;
        InitRegisters;
      END;
    END;

    LOCAL PROCEDURE InsertField@2(Number@1000 : Integer;Name@1001 : Text[100]);
    BEGIN
      NoOfFields := NoOfFields + 1;
      FieldNumber[NoOfFields] := Number;
      FieldNameArray[NoOfFields] := Name;
    END;

    LOCAL PROCEDURE RetainNo@3(Number@1000 : Integer) : Boolean;
    BEGIN
      EXIT(Retain[Index(Number)]);
    END;

    LOCAL PROCEDURE Index@6(Number@1000 : Integer) : Integer;
    BEGIN
      FOR i := 1 TO NoOfFields DO
        IF Number = FieldNumber[i] THEN
          EXIT(i);
    END;

    LOCAL PROCEDURE SummarizeEntry@5(VAR NewBankAccLedgEntry@1000 : Record 271;BankAccLedgEntry@1001 : Record 271);
    BEGIN
      WITH BankAccLedgEntry DO BEGIN
        NewBankAccLedgEntry.Amount := NewBankAccLedgEntry.Amount + Amount;
        NewBankAccLedgEntry."Remaining Amount" := NewBankAccLedgEntry."Remaining Amount" + "Remaining Amount";
        NewBankAccLedgEntry."Amount (LCY)" := NewBankAccLedgEntry."Amount (LCY)" + "Amount (LCY)";
        NewBankAccLedgEntry."Debit Amount" := NewBankAccLedgEntry."Debit Amount" + "Debit Amount";
        NewBankAccLedgEntry."Credit Amount" := NewBankAccLedgEntry."Credit Amount" + "Credit Amount";
        NewBankAccLedgEntry."Debit Amount (LCY)" :=
          NewBankAccLedgEntry."Debit Amount (LCY)" + "Debit Amount (LCY)";
        NewBankAccLedgEntry."Credit Amount (LCY)" :=
          NewBankAccLedgEntry."Credit Amount (LCY)" + "Credit Amount (LCY)";
        DELETE;
        DateComprReg."No. Records Deleted" := DateComprReg."No. Records Deleted" + 1;
        Window.UPDATE(4,DateComprReg."No. Records Deleted");
      END;
    END;

    LOCAL PROCEDURE ComprCollectedEntries@12();
    VAR
      BankAccLedgEntry@1000 : Record 271;
      OldDimEntryNo@1001 : Integer;
      Found@1002 : Boolean;
      BankAccLedgEntryNo@1003 : Integer;
    BEGIN
      OldDimEntryNo := 0;
      IF DimBufMgt.FindFirstDimEntryNo(DimEntryNo,BankAccLedgEntryNo) THEN BEGIN
        InitNewEntry(NewBankAccLedgEntry);
        REPEAT
          BankAccLedgEntry.GET(BankAccLedgEntryNo);
          SummarizeEntry(NewBankAccLedgEntry,BankAccLedgEntry);
          OldDimEntryNo := DimEntryNo;
          Found := DimBufMgt.NextDimEntryNo(DimEntryNo,BankAccLedgEntryNo);
          IF (OldDimEntryNo <> DimEntryNo) OR NOT Found THEN BEGIN
            InsertNewEntry(NewBankAccLedgEntry,OldDimEntryNo);
            IF Found THEN
              InitNewEntry(NewBankAccLedgEntry);
          END;
          OldDimEntryNo := DimEntryNo;
        UNTIL NOT Found;
      END;
      DimBufMgt.DeleteAllDimEntryNo;
    END;

    PROCEDURE InitNewEntry@7(VAR NewBankAccLedgEntry@1000 : Record 271);
    BEGIN
      LastEntryNo := LastEntryNo + 1;

      WITH BankAccLedgEntry2 DO BEGIN
        NewBankAccLedgEntry.INIT;
        NewBankAccLedgEntry."Entry No." := LastEntryNo;
        NewBankAccLedgEntry."Bank Account No." := "Bank Account No.";
        NewBankAccLedgEntry."Posting Date" := GETRANGEMIN("Posting Date");
        NewBankAccLedgEntry.Description := EntrdBankAccLedgEntry.Description;
        NewBankAccLedgEntry."Bank Acc. Posting Group" := "Bank Acc. Posting Group";
        NewBankAccLedgEntry."Currency Code" := "Currency Code";
        NewBankAccLedgEntry."Document Type" := "Document Type";
        NewBankAccLedgEntry."Source Code" := SourceCodeSetup."Compress Bank Acc. Ledger";
        NewBankAccLedgEntry."User ID" := USERID;
        NewBankAccLedgEntry."Transaction No." := NextTransactionNo;

        IF RetainNo(FIELDNO("Document No.")) THEN
          NewBankAccLedgEntry."Document No." := "Document No.";
        IF RetainNo(FIELDNO("Our Contact Code")) THEN
          NewBankAccLedgEntry."Our Contact Code" := "Our Contact Code";
        IF RetainNo(FIELDNO("Global Dimension 1 Code")) THEN
          NewBankAccLedgEntry."Global Dimension 1 Code" := "Global Dimension 1 Code";
        IF RetainNo(FIELDNO("Global Dimension 2 Code")) THEN
          NewBankAccLedgEntry."Global Dimension 2 Code" := "Global Dimension 2 Code";

        Window.UPDATE(1,NewBankAccLedgEntry."Bank Account No.");
        Window.UPDATE(2,NewBankAccLedgEntry."Posting Date");
        DateComprReg."No. of New Records" := DateComprReg."No. of New Records" + 1;
        Window.UPDATE(3,DateComprReg."No. of New Records");
      END;
    END;

    LOCAL PROCEDURE InsertNewEntry@23(VAR NewBankAccLedgEntry@1000 : Record 271;DimEntryNo@1001 : Integer);
    VAR
      TempDimBuf@1002 : TEMPORARY Record 360;
      TempDimSetEntry@1005 : TEMPORARY Record 480;
    BEGIN
      TempDimBuf.DELETEALL;
      DimBufMgt.GetDimensions(DimEntryNo,TempDimBuf);
      DimMgt.CopyDimBufToDimSetEntry(TempDimBuf,TempDimSetEntry);
      NewBankAccLedgEntry."Dimension Set ID" := DimMgt.GetDimensionSetID(TempDimSetEntry);
      NewBankAccLedgEntry.INSERT;
    END;

    LOCAL PROCEDURE InitializeParameter@10();
    BEGIN
      IF EntrdDateComprReg."Ending Date" = 0D THEN
        EntrdDateComprReg."Ending Date" := TODAY;
      IF EntrdBankAccLedgEntry.Description = '' THEN
        EntrdBankAccLedgEntry.Description := Text009;

      WITH "Bank Account Ledger Entry" DO BEGIN
        InsertField(FIELDNO("Document No."),FIELDCAPTION("Document No."));
        InsertField(FIELDNO("Our Contact Code"),FIELDCAPTION("Our Contact Code"));
        InsertField(FIELDNO("Global Dimension 1 Code"),FIELDCAPTION("Global Dimension 1 Code"));
        InsertField(FIELDNO("Global Dimension 2 Code"),FIELDCAPTION("Global Dimension 2 Code"));
      END;
    END;

    PROCEDURE InitializeRequest@15(StartingDate@1007 : Date;EndingDate@1006 : Date;PeriodLength@1005 : Option;Description@1004 : Text[50];RetainDocumentNo@1003 : Boolean;RetainOutContactCode@1002 : Boolean;RetainDimensionText@1000 : Text[50]);
    BEGIN
      InitializeParameter;
      EntrdDateComprReg."Starting Date" := StartingDate;
      EntrdDateComprReg."Ending Date" := EndingDate;
      EntrdDateComprReg."Period Length" := PeriodLength;
      EntrdBankAccLedgEntry.Description := Description;
      Retain[1] := RetainDocumentNo;
      Retain[2] := RetainOutContactCode;
      RetainDimText := RetainDimensionText;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

