OBJECT Table 6085634 Continia User Permission
{
  OBJECT-PROPERTIES
  {
    Date=05/09/17;
    Time=[ 1:00:00 AM];
    Version List=DCW13.70.00.4.50;
  }
  PROPERTIES
  {
    DataCaptionFields=Continia User ID,Approval User Group Code;
    OnInsert=BEGIN
               TESTFIELD("Continia User ID");
               IF Type = Type::Dimension THEN
                 TESTFIELD("Dimension Code")
               ELSE
                 TESTFIELD("Dimension Code",'');

               GetContiniaUserSetup;
             END;

    OnModify=BEGIN
               IF "Approval User Group Code" <> '' THEN
                 ERROR(ChangePermissionErr,ApprovalUserGroup.TABLECAPTION,"Approval User Group Code");
             END;

    OnDelete=VAR
               ApprovalUserGroupMgt@1160040000 : Codeunit 6085630;
             BEGIN
               IF "Approval User Group Code" <> '' THEN
                 ERROR(RemovePermissionErr,ApprovalUserGroup.TABLECAPTION,"Approval User Group Code");

               ApprovalUserGroupMgt.DeleteContUserPmsSelection(GetCurrCompanyNameTbl,2,"Approval User Group Code","Continia User ID",Type,
                 "Dimension Code");
             END;

    OnRename=BEGIN
               ERROR(RenameErr,TABLECAPTION);
             END;

    CaptionML=ENU=Continia User Permission;
    PasteIsValid=No;
    LookupPageID=Page6085662;
    DrillDownPageID=Page6085662;
  }
  FIELDS
  {
    { 1   ;   ;Continia User ID    ;Code50        ;TableRelation="Continia User Setup";
                                                   CaptionML=ENU=Continia User ID;
                                                   NotBlank=Yes }
    { 2   ;   ;Approval User Group Code;Code20    ;TableRelation="Approval User Group";
                                                   CaptionML=ENU=Approval User Group Code;
                                                   Editable=No }
    { 3   ;   ;Type                ;Option        ;OnValidate=BEGIN
                                                                IF "Approval User Group Code" <> '' THEN
                                                                  ERROR(ChangePermissionErr,ApprovalUserGroup.TABLECAPTION,"Approval User Group Code");
                                                              END;

                                                   CaptionML=ENU=Type;
                                                   OptionCaptionML=ENU=G/L Account,Item,,Fixed Asset,Charge (Item),Dimension,Job;
                                                   OptionString=G/L Account,Item,Resource,Fixed Asset,Charge (Item),Dimension,Job }
    { 4   ;   ;Dimension Code      ;Code20        ;TableRelation=IF (Type=CONST(Dimension)) Dimension.Code;
                                                   OnValidate=BEGIN
                                                                IF "Approval User Group Code" <> '' THEN
                                                                  ERROR(ChangePermissionErr,ApprovalUserGroup.TABLECAPTION,"Approval User Group Code");

                                                                IF "Dimension Code" <> '' THEN
                                                                  TESTFIELD(Type,Type::Dimension);
                                                              END;

                                                   CaptionML=ENU=Dimension Code }
    { 5   ;   ;Assigning Permission;Option        ;OnValidate=VAR
                                                                ApprovalUserGroupMgt@1160040001 : Codeunit 6085630;
                                                              BEGIN
                                                                IF "Approval User Group Code" <> '' THEN
                                                                  ERROR(ChangePermissionErr,ApprovalUserGroup.TABLECAPTION,"Approval User Group Code");

                                                                IF ("Assigning Filter" <> '') AND ("Assigning Permission" <> "Assigning Permission"::Filter) THEN BEGIN
                                                                  IF NOT CONFIRM(ChangePmsWithFilterMsg,TRUE,FIELDCAPTION("Assigning Filter"),"Assigning Filter",
                                                                    FIELDCAPTION("Assigning Permission"),"Assigning Permission")
                                                                  THEN
                                                                    ERROR('');

                                                                  "Assigning Filter" := '';
                                                                END;

                                                                IF ((xRec."Assigning Permission" = xRec."Assigning Permission"::"Include Selected") AND
                                                                  ("Assigning Permission" <> "Assigning Permission"::"Include Selected")) OR
                                                                  ((xRec."Assigning Permission" = xRec."Assigning Permission"::"Exclude Selected") AND
                                                                  ("Assigning Permission" <> "Assigning Permission"::"Exclude Selected"))
                                                                THEN BEGIN
                                                                  CALCFIELDS("No. of Assigning Selections");
                                                                  IF "No. of Assigning Selections" <> 0 THEN BEGIN
                                                                    IF NOT CONFIRM(SelectedRecordsExistMsg,TRUE,FIELDCAPTION("Assigning Permission"),xRec."Assigning Permission",
                                                                      "Assigning Permission","No. of Assigning Selections")
                                                                    THEN
                                                                      ERROR('');

                                                                    ApprovalUserGroupMgt.DeleteContUserPmsSelection(GetCurrCompanyNameTbl,0,"Approval User Group Code","Continia User ID",Type,
                                                                      "Dimension Code");
                                                                    CALCFIELDS("No. of Assigning Selections");
                                                                  END;
                                                                END;
                                                              END;

                                                   CaptionML=ENU=Assigning Permission;
                                                   OptionCaptionML=ENU=" ,All,Include Selected,Exclude Selected,Filter";
                                                   OptionString=[ ,All,Include Selected,Exclude Selected,Filter] }
    { 6   ;   ;Approval Permission ;Option        ;OnValidate=VAR
                                                                ApprovalUserGroupMgt@1160040001 : Codeunit 6085630;
                                                              BEGIN
                                                                IF "Approval User Group Code" <> '' THEN
                                                                  ERROR(ChangePermissionErr,ApprovalUserGroup.TABLECAPTION,"Approval User Group Code");

                                                                IF ("Approval Filter" <> '') AND ("Approval Permission" <> "Approval Permission"::Filter) THEN BEGIN
                                                                  IF NOT CONFIRM(ChangePmsWithFilterMsg,TRUE,FIELDCAPTION("Approval Filter"),"Approval Filter",
                                                                    FIELDCAPTION("Approval Permission"),"Approval Permission")
                                                                  THEN
                                                                    ERROR('');

                                                                  "Approval Filter" := '';
                                                                END;

                                                                IF ((xRec."Approval Permission" = xRec."Approval Permission"::"Include Selected") AND
                                                                  ("Approval Permission" <> "Approval Permission"::"Include Selected")) OR
                                                                  ((xRec."Approval Permission" = xRec."Approval Permission"::"Exclude Selected") AND
                                                                  ("Approval Permission" <> "Approval Permission"::"Exclude Selected"))
                                                                THEN BEGIN
                                                                  CALCFIELDS("No. of Approval Selections");
                                                                  IF "No. of Approval Selections" <> 0 THEN BEGIN
                                                                    IF NOT CONFIRM(SelectedRecordsExistMsg,TRUE,FIELDCAPTION("Approval Permission"),xRec."Approval Permission",
                                                                      "Approval Permission","No. of Approval Selections")
                                                                    THEN
                                                                      ERROR('');

                                                                    ApprovalUserGroupMgt.DeleteContUserPmsSelection(GetCurrCompanyNameTbl,1,"Approval User Group Code","Continia User ID",Type,
                                                                      "Dimension Code");
                                                                    CALCFIELDS("No. of Approval Selections");
                                                                  END;
                                                                END;
                                                              END;

                                                   CaptionML=ENU=Approval Permission;
                                                   OptionCaptionML=ENU=" ,All,Include Selected,Exclude Selected,Filter,Same as Assigning";
                                                   OptionString=[ ,All,Include Selected,Exclude Selected,Filter,Same as Assigning] }
    { 7   ;   ;Assigning Filter    ;Code250       ;OnValidate=BEGIN
                                                                IF "Approval User Group Code" <> '' THEN
                                                                  ERROR(ChangePermissionErr,ApprovalUserGroup.TABLECAPTION,"Approval User Group Code");

                                                                IF "Assigning Filter" <> '' THEN
                                                                  TESTFIELD("Assigning Permission","Assigning Permission"::Filter);
                                                              END;

                                                   CaptionML=ENU=Assigning Filter }
    { 8   ;   ;Approval Filter     ;Code250       ;OnValidate=BEGIN
                                                                IF "Approval User Group Code" <> '' THEN
                                                                  ERROR(ChangePermissionErr,ApprovalUserGroup.TABLECAPTION,"Approval User Group Code");

                                                                IF "Approval Filter" <> '' THEN
                                                                  TESTFIELD("Approval Permission","Approval Permission"::Filter);
                                                              END;

                                                   CaptionML=ENU=Approval Filter }
    { 9   ;   ;No. of Assigning Selections;Integer;FieldClass=FlowField;
                                                   CalcFormula=Count("Continia User Pms. Selection" WHERE (Continia User ID=FIELD(Continia User ID),
                                                                                                           Approval User Group Code=FIELD(Approval User Group Code),
                                                                                                           Type=FIELD(Type),
                                                                                                           Dimension Code=FIELD(Dimension Code),
                                                                                                           Permission Type=CONST(Assigning Permission)));
                                                   CaptionML=ENU=No. of Assigning Selections;
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 10  ;   ;No. of Approval Selections;Integer ;FieldClass=FlowField;
                                                   CalcFormula=Count("Continia User Pms. Selection" WHERE (Continia User ID=FIELD(Continia User ID),
                                                                                                           Approval User Group Code=FIELD(Approval User Group Code),
                                                                                                           Type=FIELD(Type),
                                                                                                           Dimension Code=FIELD(Dimension Code),
                                                                                                           Permission Type=CONST(Approval Permission)));
                                                   CaptionML=ENU=No. of Approval Selections;
                                                   BlankZero=Yes;
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Continia User ID,Type,Dimension Code,Approval User Group Code;
                                                   Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      RemovePermissionErr@1160040000 : TextConst 'ENU=You cannot remove this permission because it is was created from %1 %2.';
      ChangePermissionErr@1160040001 : TextConst 'ENU=You cannot change this permission because it is was created from %1 %2.';
      RenameErr@1160040002 : TextConst 'ENU=You cannot rename a %1.;ENC=You cannot rename a %1.';
      ChangePmsWithFilterMsg@1160040003 : TextConst 'ENU=The value in %1 is ''%2''. %1 will be set to blank if you change %3 to %4.\\Are you sure you want to continue?';
      ApprovalUserGroup@1160040006 : Record 6085630;
      SelectedRecordsExistMsg@1160040005 : TextConst 'ENU=You have changed %1 from %2 to %3 and there are %4 selected records. If you continue the %4 selected records will be deleted.\\Are you sure you want to continue?';
      ContiniaUserSetup@1160040007 : Record 6086002;
      ContiniaUserSetupMissingErr@1160040008 : TextConst 'ENU=%1 %2 ''%3'' does not exist in %4.';
      CurrCompanyName@1160040004 : Text[30];

    PROCEDURE SetCurrCompanyNameTbl@1160040019(NewCompanyName@1160040000 : Text[30]);
    BEGIN
      CurrCompanyName := NewCompanyName;
    END;

    PROCEDURE GetCurrCompanyNameTbl@1160040024() : Text[30];
    BEGIN
      IF CurrCompanyName = '' THEN
        EXIT(COMPANYNAME)
      ELSE
        EXIT(CurrCompanyName);
    END;

    PROCEDURE DrillDownSelection@1160040001(NewCompanyName@1160040002 : Text[30];PermissionType@1160040001 : 'Assigning Permission,Approval Permission');
    VAR
      PermissionSelectionMgt@1160040000 : Codeunit 6085631;
    BEGIN
      SetCurrCompanyNameTbl(NewCompanyName);
      PermissionSelectionMgt.DrillDownNoOfSelection(GetCurrCompanyNameTbl,PermissionType,FALSE,"Approval User Group Code",
        "Continia User ID",Type,"Dimension Code","Assigning Permission","Approval Permission",FIELDCAPTION("Assigning Permission"),
        FIELDCAPTION("Approval Permission"));
    END;

    PROCEDURE LookUpPmsFilter@57(NewCompanyName@1160040001 : Text[30];PermissionType@1160040000 : 'Assigning Permission,Approval Permission,Both';VAR Text@1001 : Text[250]) : Boolean;
    VAR
      PermissionSelectionMgt@1160040004 : Codeunit 6085631;
    BEGIN
      SetCurrCompanyNameTbl(NewCompanyName);
      EXIT(
        PermissionSelectionMgt.LookUpPmsFilter(GetCurrCompanyNameTbl,PermissionType,Type,"Dimension Code",Text));
    END;

    PROCEDURE GetContiniaUserSetup@1160040002();
    BEGIN
      TESTFIELD("Continia User ID");

      IF GetCurrCompanyNameTbl <> COMPANYNAME THEN BEGIN
        ContiniaUserSetup.CHANGECOMPANY(GetCurrCompanyNameTbl);

        IF NOT ContiniaUserSetup.GET("Continia User ID") THEN
          ERROR(ContiniaUserSetupMissingErr,ContiniaUserSetup.TABLECAPTION,ContiniaUserSetup.FIELDCAPTION("Continia User ID"),
            "Continia User ID",GetCurrCompanyNameTbl);
      END ELSE
        ContiniaUserSetup.GET("Continia User ID");
    END;

    PROCEDURE LookUpDimCode@1160040000(NewCompanyName@1160040003 : Text[30];VAR Text@1160040000 : Text[250]) : Boolean;
    VAR
      PermissionSelectionMgt@1160040001 : Codeunit 6085631;
    BEGIN
      EXIT(PermissionSelectionMgt.LookUpDimCode(NewCompanyName,Text,Type));
    END;

    PROCEDURE LookUpAppUserGrpCode@1160040003(NewCompanyName@1160040001 : Text[30];VAR Text@1160040000 : Text[250];Editable@1160040005 : Boolean) : Boolean;
    VAR
      PermissionSelectionMgt@1160040006 : Codeunit 6085631;
    BEGIN
      EXIT(PermissionSelectionMgt.LookUpAppUserGrpCode(NewCompanyName,Text,FALSE));
    END;

    PROCEDURE GetPermissionTxt@6085573(PermissionType@6085580 : 'Assigning Permission,Approval Permission';_CompanyName@6085583 : Text[30]) : Text[1024];
    VAR
      AppUserGrpMgt@6085584 : Codeunit 6085630;
    BEGIN
      EXIT(AppUserGrpMgt.GetPermissionTxt(_CompanyName,FALSE,Type,"Dimension Code",PermissionType,"Assigning Filter",
        "Assigning Permission","Approval Filter","Approval Permission","Continia User ID","Approval User Group Code"));
    END;

    PROCEDURE GetTypeTxt@6085574() : Text[1024];
    VAR
      AppUserGrpMgt@6085573 : Codeunit 6085630;
    BEGIN
      EXIT(AppUserGrpMgt.GetTypeTxt(Type,FORMAT(Type),"Dimension Code"));
    END;

    BEGIN
    END.
  }
}

