OBJECT Report 5691 Index Insurance
{
  OBJECT-PROPERTIES
  {
    Date=09/19/18;
    Time=12:00:00 PM;
    Version List=NAVW113.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Index Insurance;
               ESM=Ajustar valores seguros;
               FRC=R‚‚valuer assurance;
               ENC=Index Insurance];
    ApplicationArea=#FixedAssets;
    ProcessingOnly=Yes;
    OnPreReport=BEGIN
                  IF PostingDate = 0D THEN
                    ERROR(Text000,InsuranceJnlLine.FIELDCAPTION("Posting Date"));
                  IF PostingDate <> NORMALDATE(PostingDate) THEN
                    ERROR(Text001);
                  IF IndexFigure = 100 THEN
                    ERROR(Text002);
                  IF IndexFigure <= 0 THEN
                    ERROR(Text003);
                  FASetup.GET;
                  FASetup.TESTFIELD("Insurance Depr. Book");
                  DeprBook.GET(FASetup."Insurance Depr. Book");
                  InsuranceJnlLine.LOCKTABLE;
                  FAJnlSetup.InsuranceJnlName(DeprBook,InsuranceJnlLine,NextLineNo);
                  NoSeries := FAJnlSetup.GetInsuranceNoSeries(InsuranceJnlLine);
                  IF DocumentNo = '' THEN
                    DocumentNo := FAJnlSetup.GetInsuranceJnlDocumentNo(InsuranceJnlLine,PostingDate);
                  InsuranceJnlLine."Posting Date" := PostingDate;
                  Window.OPEN(Text004);
                END;

    UsageCategory=Tasks;
  }
  DATASET
  {
    { 3794;    ;DataItem;                    ;
               DataItemTable=Table5600;
               OnPreDataItem=BEGIN
                               InsCoverageLedgEntry.SETCURRENTKEY("FA No.","Insurance No.");
                             END;

               OnAfterGetRecord=BEGIN
                                  IF Blocked OR Inactive THEN
                                    CurrReport.SKIP;
                                  Window.UPDATE(1,"No.");
                                  InsuranceTmp.DELETEALL;
                                  InsCoverageLedgEntry.SETRANGE("FA No.","No.");
                                  InsCoverageLedgEntry.SETRANGE("Posting Date",0D,PostingDate);
                                  IF InsCoverageLedgEntry.FIND('-') THEN
                                    REPEAT
                                      InsuranceTmp."No." := InsCoverageLedgEntry."Insurance No.";
                                      IF InsuranceTmp.INSERT THEN BEGIN
                                        InsCoverageLedgEntry.SETRANGE("Insurance No.",InsCoverageLedgEntry."Insurance No.");
                                        InsCoverageLedgEntry.CALCSUMS(Amount);
                                        InsCoverageLedgEntry.SETRANGE("Insurance No.");
                                        IF InsCoverageLedgEntry.Amount <> 0 THEN BEGIN
                                          IF Insurance.GET(InsCoverageLedgEntry."Insurance No.") THEN BEGIN
                                            IF Insurance.Blocked THEN
                                              CurrReport.SKIP;
                                          END ELSE
                                            CurrReport.SKIP;
                                          InsuranceJnlLine."Line No." := 0;
                                          FAJnlSetup.SetInsuranceJnlTrailCodes(InsuranceJnlLine);
                                          InsuranceJnlLine.VALIDATE("Insurance No.",InsCoverageLedgEntry."Insurance No.");
                                          InsuranceJnlLine.VALIDATE("FA No.","No.");
                                          InsuranceJnlLine.VALIDATE(
                                            Amount,ROUND(InsCoverageLedgEntry.Amount * (IndexFigure / 100 - 1)));
                                          InsuranceJnlLine."Document No." := DocumentNo;
                                          InsuranceJnlLine."Posting No. Series" := NoSeries;
                                          InsuranceJnlLine.Description := PostingDescription;
                                          InsuranceJnlLine."Index Entry" := TRUE;
                                          NextLineNo := NextLineNo + 10000;
                                          InsuranceJnlLine."Line No." := NextLineNo;
                                          InsuranceJnlLine.INSERT(TRUE);
                                        END;
                                      END;
                                    UNTIL InsCoverageLedgEntry.NEXT = 0;
                                END;

               ReqFilterFields=No.,FA Class Code,FA Subclass Code }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[ENU=Options;
                             ESM=Opciones;
                             FRC=Options;
                             ENC=Options] }

      { 5   ;2   ;Field     ;
                  CaptionML=[ENU=Index Figure;
                             ESM=Valor ajuste;
                             FRC=Montant r‚‚valuation;
                             ENC=Index Figure];
                  ToolTipML=[ENU="Specifies an index figure that is to calculate the index amounts entered in the journal. For example, if you want to index by 2%, enter 102 in this field; if you want to index by -3% percent, enter 97 in this field.";
                             ESM="Especifica un valor de ajuste que calcula los importes de ajuste que se registran en el diario. Por ejemplo, si desea ajustar el valor en un 2ÿ%, escriba 102 en este campo; si desea ajustar el valor en un -3ÿ%, escriba 97 en este campo.";
                             FRC="Sp‚cifie un taux de r‚‚valuation permettant de calculer les montants ins‚r‚s dans le journal. Par exemple, si vous souhaitez r‚‚valuer de 2ÿ%, saisissez 102 dans ce champ; si vous souhaitez r‚‚valuer de -3ÿ%, saisissez 97 dans ce champ.";
                             ENC="Specifies an index figure that is to calculate the index amounts entered in the journal. For example, if you want to index by 2%, enter 102 in this field; if you want to index by -3% percent, enter 97 in this field."];
                  ApplicationArea=#FixedAssets;
                  DecimalPlaces=0:8;
                  SourceExpr=IndexFigure }

      { 3   ;2   ;Field     ;
                  CaptionML=[ENU=Posting Date;
                             ESM=Fecha registro;
                             FRC=Date de report;
                             ENC=Posting Date];
                  ToolTipML=[ENU=Specifies the posting date to be used by the batch job. This date appears in the Posting Date field on the insurance journal lines.;
                             ESM=Especifica la fecha de registro que se usar  en el proceso. Esta aparece en el campo Fecha registro de las l¡neas del diario de seguros.;
                             FRC=Sp‚cifie la date de report qui doit ˆtre utilis‚e par le traitement en lot. Cette date s'affiche dans le champ Date de report des lignes journal assurance.;
                             ENC=Specifies the posting date to be used by the batch job. This date appears in the Posting Date field on the insurance journal lines.];
                  ApplicationArea=#FixedAssets;
                  SourceExpr=PostingDate }

      { 8   ;2   ;Field     ;
                  CaptionML=[ENU=Document No.;
                             ESM=N§ documento;
                             FRC=Nø de document;
                             ENC=Document No.];
                  ToolTipML=[ENU=Specifies, if you leave the field empty, the next available number on the resulting journal line. If a number series is not set up, enter the document number that you want assigned to the resulting journal line.;
                             ESM=Especifica, si deja el campo vac¡o, el siguiente n£mero disponible en la l¡nea del diario resultante. Si no se configura una serie num‚rica, escriba el n£mero de documento que desea asignar a la l¡nea del diario resultante.;
                             FRC=Indique, si vous laissez le champ vide, le num‚ro suivant disponible sur la ligne journal r‚sultante. Si un num‚ro de s‚rie n'est pas configur‚, entrez le num‚ro de document que vous souhaitez affecter … la ligne journal r‚sultante.;
                             ENC=Specifies, if you leave the field empty, the next available number on the resulting journal line. If a number series is not set up, enter the document number that you want assigned to the resulting journal line.];
                  ApplicationArea=#FixedAssets;
                  SourceExpr=DocumentNo }

      { 1   ;2   ;Field     ;
                  CaptionML=[ENU=Posting Description;
                             ESM=Texto de registro;
                             FRC=Description du report;
                             ENC=Posting Description];
                  ToolTipML=[ENU=Specifies a posting description to appear on the resulting journal lines.;
                             ESM=Especifica el texto descriptivo de registro que aparecer  en las l¡neas del diario que se creen.;
                             FRC=Sp‚cifie un libell‚ ‚criture … afficher sur les lignes journal r‚sultantes.;
                             ENC=Specifies a posting description to appear on the resulting journal lines.];
                  ApplicationArea=#FixedAssets;
                  SourceExpr=PostingDescription }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=You must specify %1.;ESM=Se debe indicar %1.;FRC=Vous devez sp‚cifier %1;ENC=You must specify %1.';
      Text001@1001 : TextConst 'ENU=Posting Date must not be a closing date.;ESM=La fecha de registro no puede ser una fecha de cierre.;FRC=La date de report ne doit pas ˆtre une date de cl“ture.;ENC=Posting Date must not be a closing date.';
      Text002@1002 : TextConst 'ENU=Index Figure must not be 100.;ESM=El valor de ajuste no puede ser 100.;FRC=Valeur r‚‚valuation ne doit pas ˆtre 100;ENC=Index Figure must not be 100.';
      Text003@1003 : TextConst 'ENU=Index Figure must be positive.;ESM=El valor de ajuste debe ser positivo.;FRC=Valeur r‚‚valuation doit ˆtre positive;ENC=Index Figure must be positive.';
      Text004@1004 : TextConst 'ENU=Indexing insurance    #1##########;ESM=Ajustando seguro      #1##########;FRC=R‚‚valuation assurance    #1##########;ENC=Indexing insurance    #1##########';
      FASetup@1005 : Record 5603;
      DeprBook@1006 : Record 5611;
      Insurance@1007 : Record 5628;
      FAJnlSetup@1008 : Record 5605;
      InsuranceTmp@1009 : TEMPORARY Record 5628;
      InsCoverageLedgEntry@1010 : Record 5629;
      InsuranceJnlLine@1011 : Record 5635;
      Window@1012 : Dialog;
      PostingDate@1013 : Date;
      IndexFigure@1014 : Decimal;
      DocumentNo@1015 : Code[20];
      NoSeries@1016 : Code[20];
      PostingDescription@1017 : Text[50];
      NextLineNo@1018 : Integer;

    PROCEDURE InitializeRequest@1(DocumentNoFrom@1003 : Code[20];PostingDescriptionFrom@1002 : Text[50];PostingDateFrom@1001 : Date;IndexFigureFrom@1000 : Decimal);
    BEGIN
      DocumentNo := DocumentNoFrom;
      PostingDescription := PostingDescriptionFrom;
      PostingDate := PostingDateFrom;
      IndexFigure := IndexFigureFrom;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

