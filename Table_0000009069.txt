OBJECT Table 9069 O365 Sales Cue
{
  OBJECT-PROPERTIES
  {
    Date=09/19/18;
    Time=12:00:00 PM;
    Version List=NAVW113.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=O365 Sales Cue;
               ESM=Pila de ventas de O365;
               FRC=Pile vente O365;
               ENC=O365 Sales Cue];
  }
  FIELDS
  {
    { 1   ;   ;Primary Key         ;Code10        ;CaptionML=[ENU=Primary Key;
                                                              ESM=Clave principal;
                                                              FRC=Cl‚ primaire;
                                                              ENC=Primary Key] }
    { 2   ;   ;Overdue Sales Documents;Integer    ;FieldClass=FlowField;
                                                   CalcFormula=Count("Cust. Ledger Entry" WHERE (Document Type=FILTER(Invoice|Credit Memo),
                                                                                                 Due Date=FIELD(Overdue Date Filter),
                                                                                                 Open=CONST(Yes)));
                                                   CaptionML=[ENU=Overdue Sales Documents;
                                                              ESM=Documentos de ventas vencidos;
                                                              FRC=Documents vente ‚chus;
                                                              ENC=Overdue Sales Documents] }
    { 3   ;   ;Customers - Blocked ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count(Customer WHERE (Blocked=FILTER(<>' ')));
                                                   CaptionML=[ENU=Customers - Blocked;
                                                              ESM=Clientes - Bloqueados;
                                                              FRC=Clients - Bloqu‚s;
                                                              ENC=Customers - Blocked] }
    { 4   ;   ;CM Date Filter      ;Date          ;FieldClass=FlowFilter;
                                                   CaptionML=[ENU=CM Date Filter;
                                                              ESM=Filtro Fecha mes actual;
                                                              FRC=Filtre date FM;
                                                              ENC=CM Date Filter] }
    { 5   ;   ;YTD Date Filter     ;Date          ;FieldClass=FlowFilter;
                                                   CaptionML=[ENU=YTD Date Filter;
                                                              ESM=Filtro Fecha a¤o hasta la fecha;
                                                              FRC=Filtre de date de solde actuel;
                                                              ENC=YTD Date Filter] }
    { 6   ;   ;Due Date Filter     ;Date          ;FieldClass=FlowFilter;
                                                   CaptionML=[ENU=Due Date Filter;
                                                              ESM=Filtro Fecha vencimiento;
                                                              FRC=Filtre de date d'‚ch‚ance;
                                                              ENC=Due Date Filter] }
    { 7   ;   ;Overdue Date Filter ;Date          ;FieldClass=FlowFilter;
                                                   CaptionML=[ENU=Overdue Date Filter;
                                                              ESM=Filtro Fecha vencida;
                                                              FRC=Filtre de date avant retard;
                                                              ENC=Overdue Date Filter] }
    { 8   ;   ;User ID Filter      ;Code50        ;FieldClass=FlowFilter;
                                                   CaptionML=[ENU=User ID Filter;
                                                              ESM=Filtro Id. usuario;
                                                              FRC=Filtre code utilisateur;
                                                              ENC=User ID Filter] }
    { 9   ;   ;Non-Applied Payments;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("Bank Acc. Reconciliation" WHERE (Statement Type=CONST(Payment Application)));
                                                   CaptionML=[ENU=Non-Applied Payments;
                                                              ESM=Pagos no liquidados;
                                                              FRC=Paiements non affect‚s;
                                                              ENC=Non-Applied Payments] }
    { 10  ;   ;Invoiced YTD        ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Sales Invoice Entity Aggregate"."Amount Including VAT" WHERE (Document Date=FIELD(YTD Date Filter),
                                                                                                                                  Status=FILTER(Open|Paid)));
                                                   CaptionML=[ENU=Invoiced YTD;
                                                              ESM=A¤o hasta la fecha facturado;
                                                              FRC=Solde actuel factur‚;
                                                              ENC=Invoiced YTD] }
    { 11  ;   ;Invoiced CM         ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Sales Invoice Entity Aggregate"."Amount Including VAT" WHERE (Document Date=FIELD(CM Date Filter),
                                                                                                                                  Status=FILTER(Open|Paid)));
                                                   CaptionML=[ENU=Invoiced CM;
                                                              ESM=Mes actual facturado;
                                                              FRC=FM factur‚;
                                                              ENC=Invoiced CM] }
    { 12  ;   ;Sales Invoices Outstanding;Decimal ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Detailed Cust. Ledg. Entry"."Amount (LCY)");
                                                   CaptionML=[ENU=Sales Invoices Outstanding;
                                                              ESM=Facturas de venta pendientes;
                                                              FRC=Factures vente en attente;
                                                              ENC=Sales Invoices Outstanding] }
    { 13  ;   ;Sales Invoices Overdue;Decimal     ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Detailed Cust. Ledg. Entry"."Amount (LCY)" WHERE (Initial Entry Due Date=FIELD(Overdue Date Filter)));
                                                   CaptionML=[ENU=Sales Invoices Overdue;
                                                              ESM=Facturas de venta vencidas;
                                                              FRC=Factures vente en retard;
                                                              ENC=Sales Invoices Overdue] }
    { 14  ;   ;No. of Quotes       ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("Sales Header" WHERE (Document Type=CONST(Quote)));
                                                   CaptionML=[ENU=No. of Quotes;
                                                              ESM=N§ de cotizaciones;
                                                              FRC=Nbre de devis;
                                                              ENC=No. of Quotes] }
    { 15  ;   ;No. of Draft Invoices;Integer      ;FieldClass=FlowField;
                                                   CalcFormula=Count("Sales Header" WHERE (Document Type=CONST(Invoice)));
                                                   CaptionML=[ENU=No. of Draft Invoices;
                                                              ESM=N§ de borradores de factura;
                                                              FRC=Nbre factures provisoires;
                                                              ENC=No. of Draft Invoices] }
    { 16  ;   ;No. of Invoices YTD ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("Sales Invoice Header" WHERE (Posting Date=FIELD(YTD Date Filter)));
                                                   CaptionML=[ENU=No. of Invoices YTD;
                                                              ESM=N§ de facturas del a¤o hasta la fecha;
                                                              FRC=Nbre de factures - Solde actuel;
                                                              ENC=No. of Invoices YTD] }
    { 17  ;   ;Requested DateTime  ;DateTime      ;CaptionML=[ENU=Requested DateTime;
                                                              ESM=Valor DateTime solicitado;
                                                              FRC=Date/heure demand‚e;
                                                              ENC=Requested DateTime] }
  }
  KEYS
  {
    {    ;Primary Key                             ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      NoOutstandingMsg@1001 : TextConst 'ENU=There are no outstanding invoices.;ESM=No hay facturas pendientes.;FRC=Il n''existe aucune facture en attente.;ENC=There are no outstanding invoices.';
      NoOverdueMsg@1000 : TextConst 'ENU=There are no overdue invoices.;ESM=No hay facturas vencidas.;FRC=Il n''existe aucune facture ‚chue.;ENC=There are no overdue invoices.';
      RequestedDateMustBeInAccountingPeriodErr@1003 : TextConst '@@@="%1 = The first date of the current fiscal year, %2 = The last date of the current fiscal year.";ENU=The requested date must be in the current fiscal year ''%1..%2''.;ESM=La fecha solicitada debe pertenecer al ejercicio actual ''%1..%2''.;FRC=La date demand‚e doit figurer dans l''exercice financier en cours ®ÿ%1..%2ÿ¯.;ENC=The requested date must be in the current fiscal year ''%1..%2''.';

    PROCEDURE OnOpenActivitiesPage@1(VAR CurrencyFormatTxt@1004 : Text);
    BEGIN
      OnOpenActivitiesPageForRequestedDate(CurrencyFormatTxt,0DT);
    END;

    PROCEDURE OnOpenActivitiesPageForRequestedDate@9(VAR CurrencyFormatTxt@1004 : Text;RequestedDateTime@1005 : DateTime);
    VAR
      AccountingPeriod@1003 : Record 50;
      GLSetup@1002 : Record 98;
      O365SalesStatistics@1001 : Codeunit 2100;
      RoleCenterNotificationMgt@1000 : Codeunit 1430;
      ConfPersonalizationMgt@1007 : Codeunit 9170;
      RequestedDate@1006 : Date;
    BEGIN
      RESET;
      IF NOT GET THEN BEGIN
        INIT;
        INSERT;
      END;

      O365SalesStatistics.GetCurrentAccountingPeriod(AccountingPeriod);

      IF RequestedDateTime <> 0DT THEN BEGIN
        RequestedDate := DT2DATE(RequestedDateTime);

        IF (AccountingPeriod.GetFiscalYearStartDate(WORKDATE) > RequestedDate) OR
           (AccountingPeriod.GetFiscalYearEndDate(WORKDATE) < RequestedDate)
        THEN
          ERROR(RequestedDateMustBeInAccountingPeriodErr,
            AccountingPeriod.GetFiscalYearStartDate(WORKDATE),
            AccountingPeriod.GetFiscalYearEndDate(WORKDATE));
      END ELSE
        RequestedDate := WORKDATE;

      SETFILTER("Due Date Filter",'..%1',RequestedDate);
      SETFILTER("Overdue Date Filter",'<%1',RequestedDate);
      SETFILTER("CM Date Filter",'%1..%2',CALCDATE('<CM+1D-1M>',RequestedDate),RequestedDate);
      SETFILTER("YTD Date Filter",'%1..%2',AccountingPeriod."Starting Date",RequestedDate);

      GLSetup.GET;

      CurrencyFormatTxt := STRSUBSTNO('%1<precision, 0:0><standard format, 0>',GLSetup.GetCurrencySymbol);

      IF GUIALLOWED THEN BEGIN
        RoleCenterNotificationMgt.ShowNotifications;
        ConfPersonalizationMgt.OnRoleCenterOpen;
      END
    END;

    PROCEDURE ShowInvoices@3(OnlyOverdue@1000 : Boolean);
    VAR
      SalesInvoiceEntityAggregate@1001 : Record 5475;
      Customer@1003 : Record 18;
      O365SalesStatistics@1002 : Codeunit 2100;
    BEGIN
      SalesInvoiceEntityAggregate.SETRANGE(Status,SalesInvoiceEntityAggregate.Status::Open);
      IF OnlyOverdue THEN
        SalesInvoiceEntityAggregate.SETFILTER("Due Date",'<%1',WORKDATE);

      IF O365SalesStatistics.GetCustomersFromSalesInvoiceEntityAggregates(SalesInvoiceEntityAggregate,Customer) THEN
        PAGE.RUN(PAGE::"BC O365 Customer List",Customer)
      ELSE // no customers
        IF OnlyOverdue THEN
          MESSAGE(NoOverdueMsg)
        ELSE
          MESSAGE(NoOutstandingMsg);
    END;

    PROCEDURE ShowMonthlySalesOverview@4();
    VAR
      TempNameValueBuffer@1002 : TEMPORARY Record 823;
      Month@1000 : Integer;
    BEGIN
      Month := DATE2DMY(WORKDATE,2);
      TempNameValueBuffer.INIT;
      TempNameValueBuffer.ID := Month;
      TempNameValueBuffer.INSERT;

      PAGE.RUN(PAGE::"O365 Sales Month Summary",TempNameValueBuffer);
    END;

    PROCEDURE ShowYearlySalesOverview@5();
    VAR
      O365SalesStatistics@1000 : Codeunit 2100;
      O365SalesYearSummary@1001 : Page 2100;
    BEGIN
      IF O365SalesStatistics.GetRelativeMonthToFY <> 1 THEN BEGIN
        O365SalesYearSummary.ShowMonthlyDataPart;
        O365SalesYearSummary.RUN;
      END ELSE
        ShowMonthlySalesOverview; // the current month is the first month in the FY
    END;

    PROCEDURE ShowQuotes@6();
    BEGIN
      ShowUnpostedDocuments(TRUE);
    END;

    PROCEDURE ShowDraftInvoices@7();
    BEGIN
      ShowUnpostedDocuments(FALSE);
    END;

    PROCEDURE ShowUnpostedDocuments@11(FilterToQuotes@1001 : Boolean);
    VAR
      O365SalesDocument@1000 : Record 2103;
    BEGIN
      O365SalesDocument.SETRANGE(Posted,FALSE);
      IF FilterToQuotes THEN BEGIN
        O365SalesDocument.SETRANGE("Document Type",O365SalesDocument."Document Type"::Quote);
        PAGE.RUN(PAGE::"BC O365 Estimate List",O365SalesDocument);
      END ELSE BEGIN
        O365SalesDocument.SETRANGE("Document Type",O365SalesDocument."Document Type"::Invoice);
        PAGE.RUN(PAGE::"BC O365 Invoice List",O365SalesDocument);
      END;
    END;

    BEGIN
    END.
  }
}

