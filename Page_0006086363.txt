OBJECT Page 6086363 Expense Approval Req. Entries
{
  OBJECT-PROPERTIES
  {
    Date=01/27/17;
    Time=[ 1:00:00 AM];
    Version List=EMW16.00.10.2.50;
  }
  PROPERTIES
  {
    CaptionML=ENU=Approval Entries;
    InsertAllowed=No;
    LinksAllowed=No;
    SourceTable=Table454;
    PageType=List;
    AutoSplitKey=Yes;
    OnOpenPage=VAR
                 TableID@1160040000 : Integer;
               BEGIN
                 FILTERGROUP(4);

                 EVALUATE(TableID,GETFILTER("Table ID"));

                 IF TableID IN [DATABASE::Mileage,DATABASE::"Expense Header"] THEN
                   AmountVisibility := FALSE
                 ELSE
                   AmountVisibility := TRUE;

                 SETRANGE(Status);
                 SETRANGE("Due Date");

                 FILTERGROUP(2);
                 SETRANGE("Table ID",TableID);
                 FILTERGROUP(0);
               END;

    OnAfterGetRecord=VAR
                       ContiniaUser@1160040000 : Record 6086001;
                     BEGIN
                       IF FormatField(Rec) THEN
                         Overdue := TRUE;

                       GetRecordFromApprovalEntry;
                       CASE "Table ID" OF
                         DATABASE::Expense:
                           BEGIN
                             Expense.CALCFIELDS("Continia User Name");
                             ContiniaUserName := Expense."Continia User Name";
                             ContiniaUserID := Expense."Continia User ID";
                             OnHold := Expense."On Hold";
                           END;

                         DATABASE::Mileage:
                           BEGIN
                             Mileage.CALCFIELDS("Continia User Name");
                             ContiniaUserName := Mileage."Continia User Name";
                             ContiniaUserID := Mileage."Continia User ID";
                             OnHold := Mileage."On Hold";
                           END;

                         DATABASE::"Expense Header":
                           BEGIN
                             ContiniaUserName := ExpHeader."Continia User Name";
                             ContiniaUserID := ExpHeader."Continia User ID";
                             OnHold := ExpHeader."On Hold";
                           END;

                       END;

                       IF ContiniaUser.GET("Approver ID") THEN
                         ApproverName := ContiniaUser.Name;
                     END;

    OnModifyRecord=BEGIN
                     EXIT(FALSE);
                   END;

    OnDeleteRecord=VAR
                     EMApprovalMgnt@161024012 : Codeunit 6086312;
                   BEGIN
                     EMApprovalMgnt.ManuallyDeleleteAppvlEntry(Rec);
                     EXIT(FALSE);
                   END;

    ActionList=ACTIONS
    {
      { 1900000003;0 ;ActionContainer;
                      ActionContainerType=RelatedInformation }
      { 33      ;1   ;ActionGroup;
                      CaptionML=ENU=&Show }
      { 36      ;2   ;Action    ;
                      CaptionML=ENU=Comments;
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=ViewComments;
                      PromotedCategory=Process;
                      OnAction=VAR
                                 ApprovalCmtMgnt@1000 : Codeunit 6085600;
                               BEGIN
                                 ApprovalCmtMgnt.ShowCmtFromAppvlEntry(Rec);
                               END;
                                }
      { 40      ;2   ;Action    ;
                      CaptionML=ENU=All Entries;
                      Image=Entries;
                      OnAction=BEGIN
                                 SETRANGE(Status);
                                 SETRANGE("Due Date");
                               END;
                                }
      { 1900000004;0 ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 25      ;1   ;Action    ;
                      Name=<Action25>;
                      CaptionML=ENU=&Forward;
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=Delegate;
                      PromotedCategory=Process;
                      OnAction=VAR
                                 ApprovalEntry@1160040004 : Record 454;
                                 ApprovalEntry2@1160040003 : Record 454;
                                 EMApprovalMgnt@1160040001 : Codeunit 6086312;
                               BEGIN
                                 CurrPage.SETSELECTIONFILTER(ApprovalEntry);

                                 CurrPage.SETSELECTIONFILTER(ApprovalEntry2);
                                 IF ApprovalEntry2.FINDFIRST THEN BEGIN
                                   ApprovalEntry2.SETFILTER(Status,'<>%1',ApprovalEntry2.Status::Open);
                                   IF NOT ApprovalEntry2.ISEMPTY THEN
                                     ERROR(Text001);
                                 END;

                                 IF ApprovalEntry.FINDFIRST THEN
                                   IF (ApprovalEntry."Sender ID" = USERID) OR
                                      (EMApprovalMgnt.IsApprovalAdminUser(FALSE)) OR
                                      (ApprovalEntry."Approver ID" = USERID)
                                   THEN
                                     REPEAT
                                       EMApprovalMgnt.DelegateApprovalRequest(ApprovalEntry);
                                     UNTIL ApprovalEntry.NEXT = 0;

                                 MESSAGE(Text002);
                               END;
                                }
      { 3       ;1   ;Action    ;
                      Name=Add Approver;
                      CaptionML=ENU=&Add Approver;
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=Add;
                      PromotedCategory=Process;
                      OnAction=VAR
                                 DCApprovalMgmt@1000 : Codeunit 6085722;
                               BEGIN
                                 DCApprovalMgmt.AddApproverAfter(Rec);
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 1   ;1   ;Group     ;
                GroupType=Repeater }

    { 6   ;2   ;Field     ;
                CaptionML=ENU=Document No.;
                SourceExpr="Document No.";
                Visible=FALSE;
                Editable=FALSE }

    { 1160040000;2;Field  ;
                CaptionML=ENU=Continia User ID;
                SourceExpr=ContiniaUserID;
                CaptionClass=Expense.FIELDCAPTION("Continia User ID");
                Visible=FALSE;
                Editable=FALSE;
                OnLookup=BEGIN
                           EXIT(LookupUser(Text));
                         END;
                          }

    { 1160040009;2;Field  ;
                CaptionML=ENU=Continia User Name;
                SourceExpr=ContiniaUserName;
                CaptionClass=Expense.FIELDCAPTION("Continia User Name");
                Visible=FALSE;
                Editable=FALSE }

    { 16  ;2   ;Field     ;
                CaptionML=ENU=Approver ID;
                SourceExpr="Approver ID";
                LookupPageID=Continia User Setup List;
                OnValidate=VAR
                             EMApprovalMgnt@161024012 : Codeunit 6086312;
                           BEGIN
                             EMApprovalMgnt.ManuallyEditAppvlEntry(Rec);
                           END;

                OnLookup=BEGIN
                           EXIT(LookupUser(Text));
                         END;
                          }

    { 1160040002;2;Field  ;
                CaptionML=ENU=Approver Name;
                SourceExpr=ApproverName;
                Editable=FALSE }

    { 1160040001;2;Field  ;
                CaptionML=ENU=Original Approver ID;
                SourceExpr="Original Approver ID";
                Visible=FALSE;
                Editable=FALSE }

    { 1160040004;2;Field  ;
                SourceExpr="Last Date-Time Modified";
                Visible=False;
                Editable=false }

    { 18  ;2   ;Field     ;
                CaptionML=ENU=Status;
                SourceExpr=Status;
                Editable=FALSE }

    { 1160040010;2;Field  ;
                CaptionML=ENU=On Hold;
                SourceExpr=OnHold;
                Editable=FALSE }

    { 1160040003;2;Field  ;
                CaptionML=ENU=Currency Code;
                SourceExpr="Currency Code";
                Visible=AmountVisibility;
                Editable=FALSE }

    { 1160040008;2;Field  ;
                CaptionML=ENU=Amount;
                BlankZero=Yes;
                SourceExpr=Amount;
                Visible=AmountVisibility;
                Editable=FALSE }

    { 1160040005;2;Field  ;
                CaptionML=ENU=Amount (LCY);
                BlankZero=Yes;
                SourceExpr="Amount (LCY)";
                Editable=FALSE }

    { 1900000007;0;Container;
                ContainerType=FactBoxArea }

  }
  CODE
  {
    VAR
      Expense@161024012 : Record 6086320;
      Mileage@1160040004 : Record 6086338;
      ExpHeader@1160040005 : Record 6086339;
      ApproverName@1160040000 : Text[50];
      ContiniaUserName@1160040002 : Text[50];
      ContiniaUserID@1160040001 : Code[50];
      OnHold@1160040003 : Code[3];
      Overdue@1005 : Boolean;
      Text001@1003 : TextConst 'ENU=You can only forward open Approvals Entries.';
      Text002@1004 : TextConst 'ENU=The selected Approvals have been forwarded.';
      AmountVisibility@1160040006 : Boolean INDATASET;

    LOCAL PROCEDURE FormatField@1(Rec@1000 : Record 454) : Boolean;
    BEGIN
      IF Status IN [Status::Created,Status::Open] THEN
        IF Rec."Due Date" < TODAY THEN
          EXIT(TRUE)
        ELSE
          EXIT(FALSE);
    END;

    PROCEDURE LookupUser@1160040002(VAR Text@1160040000 : Text[1024]) : Boolean;
    VAR
      ContiniaUserSetup@1160040001 : Record 6086002;
    BEGIN
      IF ContiniaUserSetup.GET(Text) THEN;

      IF PAGE.RUNMODAL(0,ContiniaUserSetup) = ACTION::LookupOK THEN BEGIN
        Text := ContiniaUserSetup."Continia User ID";
        EXIT(TRUE);
      END;
    END;

    PROCEDURE GetRecordFromApprovalEntry@1160040000();
    VAR
      EMApprovalMgnt@1160040000 : Codeunit 6086312;
    BEGIN
      CASE "Table ID" OF
        DATABASE::Expense:
          IF NOT Expense.GET(EMApprovalMgnt.Code2Int("Document No.")) THEN
            CLEAR(Expense);

        DATABASE::Mileage:
          IF NOT Mileage.GET(EMApprovalMgnt.Code2Int("Document No.")) THEN
            CLEAR(Mileage);

        DATABASE::"Expense Header":
          IF NOT ExpHeader.GET(ExpHeader."Document Type"::Settlement,"Document No.") THEN
            CLEAR(ExpHeader);

      END;
    END;

    BEGIN
    END.
  }
}

