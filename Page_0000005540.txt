OBJECT Page 5540 Item Availability by Timeline
{
  OBJECT-PROPERTIES
  {
    Date=09/19/18;
    Time=12:00:00 PM;
    Version List=NAVW113.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Item Availability by Timeline;
               ESM=Disponib. prod. por escala de tiempo;
               FRC=Disponibilit‚ article par chronologie;
               ENC=Item Availability by Timeline];
    ApplicationArea=#Advanced;
    InsertAllowed=No;
    DeleteAllowed=No;
    LinksAllowed=No;
    SourceTable=Table5541;
    DataCaptionExpr=PageCaption;
    SourceTableView=SORTING(Due Date)
                    ORDER(Ascending);
    PageType=Worksheet;
    UsageCategory=Tasks;
    SourceTableTemporary=Yes;
    OnInit=BEGIN
             BlockCurrRecNotifications := FALSE;
             IgnoreChanges := FALSE;
             TemplateNameNewSupply := '';
             WorksheetNameNewSupply := '';
             State := State::" ";
           END;

    OnOpenPage=BEGIN
                 ItemSelected := Item."No." <> '';
               END;

    OnAfterGetRecord=BEGIN
                       Description := TimelineEventDescription;
                       ActionMsg := ActionMessage;
                     END;

    OnQueryClosePage=BEGIN
                       IF NOT (State = State::"Unsaved Changes") THEN
                         EXIT(TRUE);

                       EXIT(CONFIRM(Text003));
                     END;

    OnAfterGetCurrRecord=BEGIN
                           IF (NOT BlockCurrRecNotifications) AND (CurrReferenceNo <> "Reference No.") THEN BEGIN
                             CurrReferenceNo := "Reference No.";
                             CurrPage.Visualization.SelectTransaction("Reference No.");
                           END;
                         END;

    ActionList=ACTIONS
    {
      { 3       ;0   ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 21      ;1   ;Action    ;
                      Name=Delete;
                      ShortCutKey=Ctrl+Delete;
                      CaptionML=[ENU=Delete;
                                 ESM=Eliminar;
                                 FRC=Supprimer;
                                 ENC=Delete];
                      ToolTipML=[ENU=Delete the record.;
                                 ESM=Elimina el registro.;
                                 FRC=Supprimez l'enregistrement.;
                                 ENC=Delete the record.];
                      ApplicationArea=#Basic,#Suite;
                      Promoted=Yes;
                      Image=Delete;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 IF NOT CONFIRM(Text009,TRUE) THEN
                                   EXIT;

                                 DeleteSelectedRecords;
                               END;
                                }
      { 6       ;1   ;Action    ;
                      Name=<Reload>;
                      CaptionML=[ENU=Reload;
                                 ESM=Volver a cargar;
                                 FRC=Recharger;
                                 ENC=Reload];
                      ToolTipML=[ENU=Update the window with values created by other users since you opened the window.;
                                 ESM=Permite actualizar la ventana con los valores creados por otros usuarios desde que abri¢ la ventana.;
                                 FRC=Mettez … jour la fenˆtre … l'aide de valeurs cr‚‚es par d'autres utilisateurs depuis que vous l'avez ouverte.;
                                 ENC=Update the window with values created by other users since you opened the window.];
                      ApplicationArea=#Basic,#Suite;
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=Refresh;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 InitAndCreateTimelineEvents;
                               END;
                                }
      { 7       ;1   ;Action    ;
                      Name=<TransferChanges>;
                      CaptionML=[ENU=Save Changes;
                                 ESM=Guardar cambios;
                                 FRC=Enregistrer les modifications;
                                 ENC=Save Changes];
                      ToolTipML=[ENU=Transfer the changes to the planning worksheet lines.;
                                 ESM=Transfiere los cambios a las l¡neas de la hoja de planificaci¢n.;
                                 FRC=Transf‚rez les modifications vers les lignes feuille planification.;
                                 ENC=Transfer the changes to the planning worksheet lines.];
                      ApplicationArea=#Basic,#Suite;
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=SuggestLines;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 TransferChangesToPlanningWksh;
                                 InitAndCreateTimelineEvents;
                                 CurrPage.UPDATE(FALSE);
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 1   ;1   ;Group     ;
                CaptionML=[ENU=Options;
                           ESM=Opciones;
                           FRC=Options;
                           ENC=Options];
                GroupType=Group }

    { 30  ;2   ;Field     ;
                Name=<ItemNo>;
                CaptionML=[ENU=Item No.;
                           ESM=N§ producto;
                           FRC=Nø d'article;
                           ENC=Item No.];
                ToolTipML=[ENU=Specifies the number of the item you want to view item availability for.;
                           ESM=Especifica el n£mero del producto para el que desea consultar la disponibilidad de producto.;
                           FRC=Sp‚cifie le num‚ro de l'article pour lequel vous voulez afficher la disponibilit‚ d'un article.;
                           ENC=Specifies the number of the item you want to view item availability for.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr=ItemNo;
                TableRelation=Item;
                Importance=Promoted;
                OnValidate=VAR
                             ItemNo2@1001 : Code[20];
                           BEGIN
                             IF ItemNo <> Item."No." THEN BEGIN
                               ItemNo2 := ItemNo;
                               Item.RESET;
                               Item.GET(ItemNo);
                               IF LocationFilter <> '' THEN
                                 Item.SETFILTER("Location Filter",LocationFilter);
                               ItemSelected := TRUE;
                               InitAndCreateTimelineEvents;
                               VariantFilter := Item."Variant Filter";
                               ItemNo := ItemNo2;
                             END;
                           END;
                            }

    { 35  ;2   ;Field     ;
                Name=<VariantFilter>;
                CaptionML=[ENU=Variant Filter;
                           ESM=Filtro variante;
                           FRC=Filtre variante;
                           ENC=Variant Filter];
                ToolTipML=[ENU=Specifies a variant code to view only the projected available balance for that variant of the item.;
                           ESM=Especifica un c¢digo de variante si solo desea ver el saldo disponible proyectado para dicha variante del producto.;
                           FRC=Sp‚cifie un code variante pour visualiser uniquement le stock pr‚visionnel de cette variante de l'article.;
                           ENC=Specifies a variant code to view only the projected available balance for that variant of the item.];
                ApplicationArea=#Planning;
                SourceExpr=VariantFilter;
                Importance=Promoted;
                OnValidate=VAR
                             VariantFilter2@1001 : Text;
                           BEGIN
                             IF VariantFilter <> Item.GETFILTER("Variant Filter") THEN BEGIN
                               CalcItemAvailTimeline.FindVariantWithinFilter(ItemNo,VariantFilter);

                               VariantFilter2 := VariantFilter;
                               Item.SETRANGE("Variant Filter");
                               IF VariantFilter <> '' THEN
                                 Item.SETFILTER("Variant Filter",VariantFilter);
                               InitAndCreateTimelineEvents;
                               VariantFilter := VariantFilter2;
                             END;
                           END;

                OnLookup=VAR
                           ItemVariant@1001 : Record 5401;
                           ItemVariants@1000 : Page 5401;
                         BEGIN
                           ItemVariant.SETRANGE("Item No.",ItemNo);
                           ItemVariants.SETTABLEVIEW(ItemVariant);
                           ItemVariants.LOOKUPMODE := TRUE;
                           IF ItemVariants.RUNMODAL = ACTION::LookupOK THEN BEGIN
                             ItemVariants.GETRECORD(ItemVariant);
                             Text := ItemVariant.Code;
                             EXIT(TRUE);
                           END;
                           EXIT(FALSE);
                         END;
                          }

    { 4   ;2   ;Field     ;
                Name=<LocationFilter>;
                CaptionML=[ENU=Location Filter;
                           ESM=Filtro almac‚n;
                           FRC=Filtre emplacement;
                           ENC=Location Filter];
                ToolTipML=[ENU=Specifies a location code to view only the projected available balance for the item at that location.;
                           ESM=Especifica un c¢digo de almac‚n si solo desea ver el saldo disponible proyectado para el producto en dicho almac‚n.;
                           FRC=Sp‚cifie un code emplacement pour visualiser uniquement le solde disponible pr‚visionnel de l'article dans cet emplacement.;
                           ENC=Specifies a location code to view only the projected available balance for the item at that location.];
                ApplicationArea=#Location;
                SourceExpr=LocationFilter;
                Importance=Promoted;
                OnValidate=VAR
                             LocationFilter2@1001 : Text;
                           BEGIN
                             IF LocationFilter <> Item.GETFILTER("Location Filter") THEN BEGIN
                               CalcItemAvailTimeline.FindLocationWithinFilter(LocationFilter);

                               LocationFilter2 := LocationFilter;
                               Item.SETRANGE("Location Filter");
                               IF LocationFilter <> '' THEN
                                 Item.SETFILTER("Location Filter",LocationFilter);
                               InitAndCreateTimelineEvents;
                               LocationFilter := LocationFilter2;
                             END;
                           END;

                OnLookup=VAR
                           Location@1001 : Record 14;
                           LocationList@1000 : Page 15;
                         BEGIN
                           LocationList.SETTABLEVIEW(Location);
                           LocationList.LOOKUPMODE := TRUE;
                           IF LocationList.RUNMODAL = ACTION::LookupOK THEN BEGIN
                             LocationList.GETRECORD(Location);
                             Text := Location.Code;
                             EXIT(TRUE);
                           END;
                           EXIT(FALSE);
                         END;
                          }

    { 13  ;2   ;Field     ;
                Name=<LastUpdateTime>;
                CaptionML=[ENU=Last Updated;
                           ESM=éltima actualizaci¢n;
                           FRC=DerniŠre mise … jour;
                           ENC=Last Updated];
                ToolTipML=[ENU=Specifies when the availability figures in the Item Availability by Timeline window were last updated.;
                           ESM=Especifica cu ndo se actualizaron por £ltima vez las cifras de disponibilidad de la ventana Disponib. prod. por escala de tiempo.;
                           FRC=Sp‚cifie la date de la derniŠre mise … jour des chiffres de disponibilit‚ dans la fenˆtre Disponibilit‚ article par chronologie.;
                           ENC=Specifies when the availability figures in the Item Availability by Timeline window were last updated.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr=LastUpdateTime;
                Importance=Promoted;
                Editable=FALSE }

    { 9   ;2   ;Field     ;
                Name=<ForecastName>;
                CaptionML=[ENU=Forecast Name;
                           ESM=Nombre de previsi¢n;
                           FRC=Nom pr‚vision;
                           ENC=Forecast Name];
                ToolTipML=[ENU=Specifies a production forecast you want to include as anticipated demand in the graphical view of projected available balance.;
                           ESM=Especifica la previsi¢n de producci¢n que desea incluir como demanda prevista en la vista gr fica de saldo disponible proyectado.;
                           FRC=Sp‚cifie une pr‚vision de production … inclure en tant que demande anticip‚e dans la vue graphique du stock pr‚visionnel.;
                           ENC=Specifies a production forecast you want to include as anticipated demand in the graphical view of projected available balance.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr=ForecastName;
                TableRelation="Production Forecast Name";
                Importance=Promoted;
                OnValidate=VAR
                             ForecastName2@1001 : Code[10];
                           BEGIN
                             ForecastName2 := ForecastName;
                             InitAndCreateTimelineEvents;
                             ForecastName := ForecastName2;
                           END;
                            }

    { 16  ;2   ;Field     ;
                Name=<IncludeBlanketOrders>;
                CaptionML=[ENU=Include Blanket Sales Orders;
                           ESM=Incluir pedidos abiertos de ventas;
                           FRC=Inclure commandes permanentes ventes;
                           ENC=Include Blanket Sales Orders];
                ToolTipML=[ENU=Specifies if you want to include anticipated demand from blanket sales orders in the graphical view of projected available balance.;
                           ESM=Especifica si desea incluir la demanda prevista de los pedidos abiertos de venta en la vista gr fica de saldo disponible estimado.;
                           FRC=Sp‚cifie si vous voulez inclure une demande anticip‚e … partir des commandes permanentes ventes dans la vue graphique du solde disponible pr‚visionnel.;
                           ENC=Specifies if you want to include anticipated demand from blanket sales orders in the graphical view of projected available balance.];
                ApplicationArea=#Suite;
                SourceExpr=IncludeBlanketOrders;
                Importance=Promoted;
                OnValidate=VAR
                             IncludeBlanketOrders2@1001 : Boolean;
                           BEGIN
                             IncludeBlanketOrders2 := IncludeBlanketOrders;
                             InitAndCreateTimelineEvents;
                             IncludeBlanketOrders := IncludeBlanketOrders2;
                           END;
                            }

    { 8   ;1   ;Group     ;
                CaptionML=[ENU=Timeline;
                           ESM=Escala de tiempo;
                           FRC=Chronologie;
                           ENC=Timeline];
                GroupType=Group }

    { 15  ;2   ;Field     ;
                Name=Visualization;
                ApplicationArea=#Basic,#Suite;
                Editable=FALSE;
                ControlAddIn=[Microsoft.Dynamics.Nav.Client.TimelineVisualization;PublicKeyToken=31bf3856ad364e35] }

    { 10  ;1   ;Group     ;
                Name=changeListGroup;
                CaptionML=[ENU=Event Changes;
                           ESM=Cambios de eventos;
                           FRC=Modification des ‚v‚nements;
                           ENC=Event Changes];
                GroupType=GridLayout }

    { 20  ;2   ;Group     ;
                Name=changeList;
                CaptionML=[ENU=Change List;
                           ESM=Lista de cambios;
                           FRC=Liste des modifications;
                           ENC=Change List];
                GroupType=Repeater }

    { 5   ;3   ;Field     ;
                Name=ActionMessage;
                CaptionML=[ENU=Action Message;
                           ESM=Mensaje acci¢n;
                           FRC=Message d'action;
                           ENC=Action Message];
                ToolTipML=[ENU=Specifies the action to take to rebalance the demand-supply situation shown in the graph on the Timeline FastTab.;
                           ESM=Especifica la acci¢n que se tomar  para volver a equilibrar la situaci¢n de oferta y demanda que se muestra en la ficha desplegable Escala de tiempo.;
                           FRC=Sp‚cifie l'action … effectuer pour r‚‚quilibrer la situation offre/demande affich‚e dans le graphique du raccourci Chronologie.;
                           ENC=Specifies the action to take to rebalance the demand-supply situation shown in the graph on the Timeline FastTab.];
                OptionCaptionML=[ENU=" ,New,Change Qty.,Reschedule,Resched. & Chg. Qty.,Cancel";
                                 ESM=" ,Nuevo,Cambiar cdad.,Reprogramar,Reprog. y cambiar cdad.,Cancelar";
                                 FRC=" ,Nouveau,Modifier qt‚,R‚‚chelonner,R‚‚chelonner & modifier qt‚,Annuler";
                                 ENC=" ,New,Change Qty.,Reschedule,Resched. & Chg. Qty.,Cancel"];
                ApplicationArea=#Manufacturing;
                SourceExpr=ActionMsg;
                Editable=FALSE }

    { 2   ;3   ;Field     ;
                Name=Description;
                CaptionML=[ENU=Description;
                           ESM=Descripci¢n;
                           FRC=Description;
                           ENC=Description];
                ToolTipML=[ENU=Specifies the description of the planning worksheet lines that are represented on the Timeline FastTab.;
                           ESM=Especifica la descripci¢n de las l¡neas de la hoja de planificaci¢n que se representan en la ficha desplegable Escala de tiempo.;
                           FRC=Sp‚cifie la description des lignes feuille planification qui sont repr‚sent‚es sur le raccourci Chronologie.;
                           ENC=Specifies the description of the planning worksheet lines that are represented on the Timeline FastTab.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr=Description;
                Editable=FALSE;
                OnDrillDown=BEGIN
                              ShowDocument("Reference No.");
                            END;
                             }

    { 17  ;3   ;Field     ;
                ToolTipML=[ENU=Specifies the due date that is stated on the existing supply order, when an action message proposes to reschedule the order.;
                           ESM=Especifica la fecha de vencimiento indicada en el pedido de suministro existente, si el mensaje de acci¢n propone reprogramar el pedido.;
                           FRC=Sp‚cifie la date d'‚ch‚ance indiqu‚e sur la commande d'approvisionnement existante lorsqu'un message d'action propose de replanifier la commande.;
                           ENC=Specifies the due date that is stated on the existing supply order, when an action message proposes to reschedule the order.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Original Due Date" }

    { 14  ;3   ;Field     ;
                ToolTipML=[ENU=Specifies the due date of the suggested supply order that the line on the Event Changes FastTab represents.;
                           ESM=Especifica la fecha de vencimiento del pedido de suministro sugerido que representa la l¡nea en la ficha desplegable Cambios de eventos.;
                           FRC=Indique la date d'‚ch‚ance de la commande d'approvisionnement sugg‚r‚e que la ligne sur le raccourci Modification des ‚v‚nements repr‚sente.;
                           ENC=Specifies the due date of the suggested supply order that the line on the Event Changes FastTab represents.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Due Date";
                OnValidate=BEGIN
                             IF "Due Date" <> xRec."Due Date" THEN BEGIN
                               TESTFIELD("Due Date");
                               CurrPage.Visualization.RescheduleTransaction("Reference No.",CREATEDATETIME("Due Date",TempTimelineEvent.DefaultTime));
                               State := State::"Unsaved Changes";
                             END;
                           END;
                            }

    { 12  ;3   ;Field     ;
                ToolTipML=[ENU=Specifies the quantity on the supply order, when an action message suggests a change to the quantity on the order.;
                           ESM=Especifica la cantidad indicada en el pedido de suministro, cuando el mensaje de acci¢n sugiere un cambio en la cantidad del pedido.;
                           FRC=Sp‚cifie la quantit‚ indiqu‚e sur la commande d'approvisionnement lorsqu'un message d'action propose de modifier la quantit‚ d'une commande.;
                           ENC=Specifies the quantity on the supply order, when an action message suggests a change to the quantity on the order.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Original Quantity" }

    { 11  ;3   ;Field     ;
                ToolTipML=[ENU=Specifies the quantity of the suggested supply order that the line on the Event Changes FastTab represents.;
                           ESM=Especifica la cantidad del pedido de suministro sugerido que representa la l¡nea en la ficha desplegable Cambios de eventos.;
                           FRC=Indique la quantit‚ de la commande d'approvisionnement sugg‚r‚e repr‚sent‚e par la ligne sur le raccourci Modification des ‚v‚nements.;
                           ENC=Specifies the quantity of the suggested supply order that the line on the Event Changes FastTab represents.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr=Quantity;
                OnValidate=BEGIN
                             IF Quantity <> xRec.Quantity THEN BEGIN
                               CurrPage.Visualization.ChangeTransactionQuantity("Reference No.",Quantity);
                               State := State::"Unsaved Changes";
                             END;
                           END;
                            }

    { 18  ;3   ;Field     ;
                ToolTipML=[ENU=Specifies the reference number for the item in the timeline event change table.;
                           ESM=Especifica el n£mero de referencia del producto en la tabla Cambio evento escala de tiempo.;
                           FRC=Sp‚cifie le num‚ro de r‚f‚rence de l'article dans la table Changement ‚v‚nement chronologie.;
                           ENC=Specifies the reference number for the item in the timeline event change table.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Reference No.";
                Visible=false }

  }
  CODE
  {
    VAR
      Item@1004 : Record 27;
      TempTimelineEvent@1015 : TEMPORARY Record 5540;
      CalcItemAvailTimeline@1023 : Codeunit 5540;
      ItemNo@1003 : Code[20];
      LocationFilter@1009 : Text;
      VariantFilter@1008 : Text;
      ForecastName@1005 : Code[10] INDATASET;
      TemplateNameNewSupply@1018 : Code[10];
      WorksheetNameNewSupply@1017 : Code[10];
      Description@1006 : Text[250];
      ActionMsg@1013 : ' ,New,Change Qty.,Reschedule,Resched. & Chg. Qty.,Cancel';
      State@1024 : ' ,Initialized,Unsaved Changes';
      LastUpdateTime@1016 : DateTime;
      IncludeBlanketOrders@1014 : Boolean INDATASET;
      Text001@1022 : TextConst 'ENU=The changes have been saved to the planning or requisition worksheet.;ESM=Los cambios se han guardado en la hoja de horas de planificaci¢n o demanda.;FRC=Les modifications ont ‚t‚ enregistr‚es dans la feuille planification ou la feuille de r‚quisition.;ENC=The changes have been saved to the planning or requisition worksheet.';
      CurrReferenceNo@1057 : Text[200];
      BlockCurrRecNotifications@1007 : Boolean;
      Text002@1012 : TextConst 'ENU=The changes have been saved to the planning or requisition worksheet.\\New supply is saved to worksheet Template Name %1, Batch Name %2, Location Code %3, and Variant Code %4.;ESM=Los cambios se han guardado en la hoja de horas de planificaci¢n o demanda.\\El nuevo suministro se guarda en la hoja de trabajo Nombre libro %1, Nombre secci¢n %2, C¢d. almac‚n %3 y c¢d.variante %4.;FRC=Les modifications ont ‚t‚ enregistr‚es vers la feuille planification ou la feuille de r‚quisition.\\Le nouvel approvisionnement est enregistr‚ dans le nom modŠle feuille %1, nom de la feuille %2, code d''emplacement %3 et code variante %4.;ENC=The changes have been saved to the planning or requisition worksheet.\\New supply is saved to worksheet Template Name %1, Batch Name %2, Location Code %3, and Variant Code %4.';
      Text003@1019 : TextConst 'ENU=All unsaved changes will not be saved. Are you sure that you want to close the window?;ESM=Los cambios no guardados no se guardar n. ¨Est  seguro de que desea cerrar la ventana?;FRC=Toutes les modifications non enregistr‚es seront perdues. Voulez-vous vraiment fermer la fenˆtre?;ENC=All unsaved changes will not be saved. Are you sure that you want to close the window?';
      Text004@1020 : TextConst 'ENU=Show Projected Available Balance;ESM=Mostrar saldo disponible estimado;FRC=Afficher le stock pr‚visionnel;ENC=Show Projected Available Balance';
      Text005@1021 : TextConst 'ENU=Include Planning Suggestions;ESM=Incluir sugerencias de planificaci¢n;FRC=Inclure propositions planification;ENC=Include Planning Suggestions';
      Text006@1000 : TextConst 'ENU=Workdate;ESM=Trabajo;FRC=Date de travail;ENC=Workdate';
      Text007@1001 : TextConst 'ENU=Today;ESM=Hoy;FRC=Aujourd''hui;ENC=Today';
      IgnoreChanges@1025 : Boolean;
      ItemSelected@1002 : Boolean INDATASET;
      Text008@1010 : TextConst 'ENU=New Supply %1;ESM=Nuevo suministro %1;FRC=Nouvel approvisionnement %1;ENC=New Supply %1';
      Text009@1011 : TextConst 'ENU=Do you want to delete?;ESM=¨Confirma que desea eliminar?;FRC=Voulez-vous supprimer?;ENC=Do you want to delete?';

    PROCEDURE InitAndCreateTimelineEvents@14();
    BEGIN
      InitTimeline;
      CreateTimelineEvents;
      SetScenarioTitles;
      State := State::Initialized;
    END;

    LOCAL PROCEDURE InitTimeline@1();
    VAR
      timeSpan@1000 : DotNet "'mscorlib, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.TimeSpan";
    BEGIN
      CurrPage.Visualization.RemoveSpecialDates;
      CurrPage.Visualization.AddSpecialDate(CREATEDATETIME(WORKDATE,TempTimelineEvent.DefaultTime),Text006);
      CurrPage.Visualization.AddSpecialDate(CREATEDATETIME(TODAY,TempTimelineEvent.DefaultTime),Text007);
      CurrPage.Visualization.TransactionPrecision := Decimals(Item."Rounding Precision");
      CurrPage.Visualization.TransactionUnitOfMeasure := Item."Base Unit of Measure";
      CurrPage.Visualization.InitialLevelDateMargin := timeSpan.FromDays(CalcItemAvailTimeline.InitialTimespanDays);
      CurrPage.Visualization.FinalLevelDateMargin := timeSpan.FromDays(CalcItemAvailTimeline.FinalTimespanDays);
    END;

    LOCAL PROCEDURE UpdateItemRequestFields@11(VAR Item@1000 : Record 27);
    BEGIN
      ItemNo := Item."No.";
      LocationFilter := '';
      IF Item.GETFILTER("Location Filter") <> '' THEN
        LocationFilter := Item.GETFILTER("Location Filter");
      VariantFilter := '';
      IF Item.GETFILTER("Variant Filter") <> '' THEN
        VariantFilter := Item.GETFILTER("Variant Filter");
    END;

    LOCAL PROCEDURE PageCaption@5() : Text[250];
    BEGIN
      EXIT(STRSUBSTNO('%1 %2',Item."No.",Item.Description));
    END;

    [External]
    PROCEDURE SetItem@3(VAR NewItem@1000 : Record 27);
    BEGIN
      Item.COPY(NewItem);
      UpdateItemRequestFields(Item);
    END;

    [External]
    PROCEDURE SetForecastName@2(NewForcastName@1000 : Code[10]);
    BEGIN
      ForecastName := NewForcastName;
    END;

    [External]
    PROCEDURE SetWorksheet@8(NewTemplateName@1001 : Code[10];NewWorksheetName@1000 : Code[10]);
    BEGIN
      TemplateNameNewSupply := NewTemplateName;
      WorksheetNameNewSupply := NewWorksheetName;
    END;

    LOCAL PROCEDURE ExportDataToTimeline@19(VAR TimelineEvent@1003 : Record 5540);
    VAR
      transactionTable@1002 : DotNet "'Microsoft.Dynamics.Framework.UI.WinForms.DataVisualization.Timeline, Version=13.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Framework.UI.WinForms.DataVisualization.TimelineVisualization.DataModel+TransactionDataTable";
      visualizationScenarios@1000 : DotNet "'Microsoft.Dynamics.Nav.Client.TimelineVisualization, Version=13.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Client.TimelineVisualization.VisualizationScenarios";
      WithForecast@1001 : Integer;
      WithForecastAndAdjustments@1004 : Integer;
    BEGIN
      TimelineEvent.TransferToTransactionTable(TimelineEvent,transactionTable);

      WithForecast := 2; // Enum value for CoreTransactionsWithForecast check box
      WithForecastAndAdjustments := 4; // Enum value for CoreTransactionsWithForecastAndAdjustments check box
      visualizationScenarios := WithForecast + WithForecastAndAdjustments; // Show the specified check boxes
      CurrPage.Visualization.SetTransactions(transactionTable,visualizationScenarios);
    END;

    LOCAL PROCEDURE ImportChangesFromTimeline@18();
    VAR
      changeTable@1003 : DotNet "'Microsoft.Dynamics.Framework.UI.WinForms.DataVisualization.Timeline, Version=13.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Framework.UI.WinForms.DataVisualization.TimelineVisualization.DataModel+TransactionChangesDataTable";
    BEGIN
      BlockCurrRecNotifications := TRUE;

      changeTable := CurrPage.Visualization.GetTransactionChanges;
      TransferFromTransactionChangeTable(Rec,changeTable);
      SetCurrentChangeRec(CurrReferenceNo);

      BlockCurrRecNotifications := FALSE;
    END;

    LOCAL PROCEDURE CreateTimelineEvents@17();
    BEGIN
      RESET;
      DELETEALL;
      TempTimelineEvent.DELETEALL;
      Item.SETRANGE("Drop Shipment Filter",FALSE);
      CalcItemAvailTimeline.Initialize(Item,ForecastName,IncludeBlanketOrders,0D,TRUE);
      CalcItemAvailTimeline.CreateTimelineEvents(TempTimelineEvent);
      ExportDataToTimeline(TempTimelineEvent);
      LastUpdateTime := CURRENTDATETIME;
    END;

    LOCAL PROCEDURE DeleteSelectedRecords@10();
    VAR
      TempTimelineEventChange@1001 : TEMPORARY Record 5541;
    BEGIN
      TempTimelineEventChange.COPY(Rec,TRUE);
      CurrPage.SETSELECTIONFILTER(TempTimelineEventChange);
      IF TempTimelineEventChange.FINDSET THEN BEGIN
        IgnoreChanges := TRUE;
        REPEAT
          CurrPage.Visualization.DeleteTransaction(TempTimelineEventChange."Reference No.");
        UNTIL TempTimelineEventChange.NEXT = 0;
        ImportChangesFromTimeline;
        IgnoreChanges := FALSE;
        IF (State = State::"Unsaved Changes") AND (COUNT = 0) THEN
          State := State::Initialized;
      END;
    END;

    LOCAL PROCEDURE ShowDocument@4(RefNo@1001 : Text[255]);
    VAR
      ReferenceNo@1000 : Integer;
    BEGIN
      IF EVALUATE(ReferenceNo,RefNo) THEN BEGIN
        TempTimelineEvent.GET(ReferenceNo);
        CalcItemAvailTimeline.ShowDocument(TempTimelineEvent."Source Document ID");
      END;
    END;

    LOCAL PROCEDURE SetCurrentChangeRec@20(RefNo@1000 : Text[255]);
    BEGIN
      IF ISEMPTY OR (RefNo = '') THEN
        EXIT;

      SETRANGE("Reference No.",RefNo);
      IF FINDFIRST THEN BEGIN
        CurrPage.SETRECORD(Rec);
        CurrPage.UPDATE(FALSE);
      END;
      SETRANGE("Reference No.");
    END;

    LOCAL PROCEDURE TimelineEventDescription@12() : Text[250];
    VAR
      ReferenceNo@1000 : Integer;
    BEGIN
      IF NewSupply THEN
        EXIT(STRSUBSTNO(Text008,FORMAT("Due Date")));

      IF EVALUATE(ReferenceNo,"Reference No.") THEN
        IF TempTimelineEvent.GET(ReferenceNo) THEN
          EXIT(TempTimelineEvent.Description);

      EXIT("Reference No.");
    END;

    LOCAL PROCEDURE SetScenarioTitles@6();
    VAR
      visualizationScenarios@1000 : DotNet "'Microsoft.Dynamics.Nav.Client.TimelineVisualization, Version=13.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Client.TimelineVisualization.VisualizationScenarios";
    BEGIN
      CurrPage.Visualization.SetScenarioTitle(visualizationScenarios.CoreTransactionsWithForecast,Text004);
      CurrPage.Visualization.SetScenarioTitle(visualizationScenarios.CoreTransactionsWithForecastAndAdjustments,Text005);
    END;

    LOCAL PROCEDURE TransferChangesToPlanningWksh@7();
    VAR
      LocationCodeNewSupply@1002 : Code[10];
      VariantCodeNewSupply@1001 : Code[10];
      NewSupplyTransfer@1000 : Boolean;
    BEGIN
      NewSupplyTransfer :=
        CalcItemAvailTimeline.TransferChangesToPlanningWksh(
          Rec,ItemNo,LocationFilter,VariantFilter,TemplateNameNewSupply,WorksheetNameNewSupply);

      IF NOT NewSupplyTransfer THEN
        MESSAGE(Text001)
      ELSE BEGIN
        IF LocationCodeNewSupply = '' THEN
          LocationCodeNewSupply := CalcItemAvailTimeline.BlankValue;
        IF VariantCodeNewSupply = '' THEN
          VariantCodeNewSupply := CalcItemAvailTimeline.BlankValue;
        MESSAGE(Text002,TemplateNameNewSupply,WorksheetNameNewSupply,LocationCodeNewSupply,VariantCodeNewSupply)
      END;
    END;

    LOCAL PROCEDURE Decimals@29(Precision@1000 : Decimal) Decimals : Integer;
    BEGIN
      IF (Precision <= 0) OR (Precision > 1) THEN
        EXIT(DefaultPrecision);

      Decimals := 0;

      WHILE (Decimals <= MaxPrecision) AND (ROUND(Precision,1,'>') > Precision) DO BEGIN
        Decimals += 1;
        Precision *= 10;
      END;

      EXIT(Decimals);
    END;

    LOCAL PROCEDURE DefaultPrecision@35() : Integer;
    BEGIN
      EXIT(5);
    END;

    LOCAL PROCEDURE MaxPrecision@39() : Integer;
    BEGIN
      EXIT(5);
    END;

    EVENT Visualization@-5::TransactionActivated@30(refNo@1000 : Text);
    BEGIN
      ShowDocument(refNo);
    END;

    EVENT Visualization@-5::SelectedTransactionChanged@31(refNo@1000 : Text);
    BEGIN
      SetCurrentChangeRec(refNo);
    END;

    EVENT Visualization@-5::TransactionValueChanged@32(refNo@1000 : Text);
    BEGIN
      IF IgnoreChanges THEN
        EXIT;

      IF refNo <> '' THEN BEGIN // Specific Transaction Value Changed
        State := State::"Unsaved Changes";
        IF NewSupply THEN
          CurrReferenceNo := COPYSTR(refNo,1,MAXSTRLEN("Reference No."));
      END;

      ImportChangesFromTimeline;
    END;

    EVENT Visualization@-5::AddInReady@33();
    BEGIN
      IF ItemSelected THEN
        InitAndCreateTimelineEvents;
    END;

    BEGIN
    END.
  }
}

