OBJECT Codeunit 6085662 Web Client Management
{
  OBJECT-PROPERTIES
  {
    Date=06/04/18;
    Time=[ 1:00:00 AM];
    Version List=DCW19.00.00.4.50.06;
  }
  PROPERTIES
  {
    OnRun=BEGIN
            DownloadAndImportWebClientAddins;
          END;

  }
  CODE
  {

    PROCEDURE IsWebClient@1() : Boolean;
    VAR
      ActiveSession@6085573 : Record 2000000110;
    BEGIN
      IF ActiveSession.GET(SERVICEINSTANCEID,SESSIONID) THEN
        EXIT(ActiveSession."Client Type" = ActiveSession."Client Type"::"Web Client");
    END;

    LOCAL PROCEDURE DownloadAndImportWebClientAddins@6();
    VAR
      Addin@1000 : Record 2000000069;
      COMgt@1001 : Codeunit 6192772;
      FileSysMgt@1002 : Codeunit 6085584;
      FilePath@1003 : Text;
    BEGIN
      Addin.SETFILTER("Add-in Name",'Continia.DocumentCapture.Dynamics.Nav.Web.*');
      Addin.SETRANGE(Category,Addin.Category::"JavaScript Control Add-in");
      IF Addin.FINDSET(TRUE,FALSE) THEN
        REPEAT
          FilePath := FileSysMgt.GetTempFilename(Addin."Add-in Name",'zip');
          COMgt.DownloadFile(GetAddInResourceUrl(Addin),FilePath,FALSE);
          Addin.Resource.IMPORT(FilePath);
          Addin.MODIFY;
          FileSysMgt.DeleteFile(FilePath,TRUE);
        UNTIL Addin.NEXT = 0;
    END;

    PROCEDURE DownloadPDFDocument@6085573(Document@6085573 : Record 6085590);
    VAR
      FileSysMgt@6085578 : Codeunit 6085584;
      FileExtension@6085577 : Text[10];
      ToFileName@6085576 : Text[1024];
      FromFileName@6085575 : Text[1024];
      FileFilter@6085574 : Text[1024];
    BEGIN
      WITH Document DO BEGIN
        IF "File Type" = "File Type"::OCR THEN BEGIN
          FileExtension := '.pdf';
          ToFileName := "No." + FileExtension;
          FromFileName := GetPdfServerFileName;
        END ELSE BEGIN
          FileExtension := "File Extension";
          ToFileName := "No." + '.' + FileExtension;
          FromFileName := GetMiscServerFileName;
        END;
        FileFilter := STRSUBSTNO('File (*.%1)|*.%1',FileExtension);
        DOWNLOAD(FromFileName,'Download','C:\',FileFilter,ToFileName);
      END;
    END;

    PROCEDURE DownloadEmail@3(Document@6085573 : Record 6085590);
    VAR
      FileSysMgt@6085578 : Codeunit 6085584;
      FileExtension@6085577 : Text[10];
      ToFileName@6085576 : Text[1024];
      FromFileName@6085575 : Text[1024];
      FileFilter@6085574 : Text[1024];
    BEGIN
      WITH Document DO BEGIN
        FileExtension := 'eml';
        ToFileName := "No." + '.' + FileExtension;
        FromFileName := GetEMailServerFileName;

        FileFilter := STRSUBSTNO('E-Mail File (*.%1)|*.%1',FileExtension);
        DOWNLOAD(FromFileName,'Download','C:\',FileFilter,ToFileName);
      END;
    END;

    LOCAL PROCEDURE GetAddInResourceUrl@2(Addin@1002 : Record 2000000069) : Text;
    VAR
      CODC@1000 : Codeunit 6085617;
      AboutDC@1001 : Codeunit 6085589;
    BEGIN
      EXIT(STRSUBSTNO('%1/addins/%2/%3.zip',CODC.GetResourceUrl,AboutDC.ControlAddInVersion,LOWERCASE(Addin."Add-in Name")));
    END;

    BEGIN
    END.
  }
}

