OBJECT Codeunit 143 ECSL Report Validate
{
  OBJECT-PROPERTIES
  {
    Date=11/22/17;
    Time=12:00:00 PM;
    Version List=NAVW111.00;
  }
  PROPERTIES
  {
    TableNo=740;
    OnRun=BEGIN
            ErrorMessage.SetContext(RECORDID);
            ErrorMessage.ClearLog;

            IF NOT IsPeriodValid THEN BEGIN
              ErrorMessage.LogMessage(Rec,FIELDNO("No."),ErrorMessage."Message Type"::Error,InvalidPeriodErr);
              EXIT;
            END;

            ValidateVATReportHasLine(Rec);
            CheckVATRegNoOnLines(Rec);
            CheckZeroAmountOnLines(Rec);
          END;

  }
  CODE
  {
    VAR
      NoLineFoundErr@1000 : TextConst 'ENU=You cannot release a blank tax report.;ESM=No puede lanzar un informe de IVA en blanco.;FRC=Vous ne pouvez pas publier de rapport de TVA vide.;ENC=You cannot release a blank tax report.';
      ErrorMessage@1042 : Record 700;
      ZeroTotalValueErr@1040 : TextConst '@@@=Placeholder 1 holds the line no that cause the error;ENU=You cannot release the report because line No. %1 has zero as the Total Value Of Supplies.;ESM=No puede lanzar el informe porque la l¡nea n.§ %1 tiene cero como Valor total de suministros.;FRC=Vous ne pouvez pas publier le rapport, car la Valeur totale des fournitures, indiqu‚e en ligne nø %1, est d‚finie sur z‚ro.;ENC=You cannot release the report because line No. %1 has zero as the Total Value Of Supplies.';
      NoVatRegNoErr@1041 : TextConst '@@@=Placeholder 1 holds the line no that cause the error;ENU=You cannot release the report because line No. %1 is missing a tax Registration Number. You need to correct this on the sales document and post it again.;ESM=No puede lanzar el informe porque en la l¡nea n.§ %1 falta un CIF/NIF. Debe corregir este problema en el documento de venta y volver a registrarlo.;FRC=Vous ne pouvez pas publier le rapport, car la ligne nø %1 ne contient pas de num‚ro d''identification intracommunautaire. Vous devez corriger cela sur le document vente et le publier … nouveau.;ENC=You cannot release the report because line No. %1 is missing a GST/HST Registration Number. You need to correct this on the sales document and post it again.';
      InvalidPeriodErr@1043 : TextConst 'ENU=The period is not valid.;ESM=El per¡odo no es v lido.;FRC=La p‚riode n''est pas valide.;ENC=The period is not valid.';

    LOCAL PROCEDURE ValidateVATReportHasLine@7(VATReportHeader@1040 : Record 740);
    VAR
      ECSLVATReportLine@1041 : Record 362;
    BEGIN
      ECSLVATReportLine.SETRANGE("Report No.",VATReportHeader."No.");
      IF ECSLVATReportLine.COUNT = 0 THEN
        ErrorMessage.LogMessage(VATReportHeader,VATReportHeader.FIELDNO("No."),ErrorMessage."Message Type"::Error,NoLineFoundErr);
    END;

    LOCAL PROCEDURE CheckVATRegNoOnLines@1040(VATReportHeader@1040 : Record 740);
    VAR
      ECSLVATReportLine@1041 : Record 362;
    BEGIN
      ECSLVATReportLine.SETRANGE("Report No.",VATReportHeader."No.");
      ECSLVATReportLine.SETRANGE("Customer VAT Reg. No.",'');
      IF ECSLVATReportLine.FINDSET THEN
        REPEAT
          ErrorMessage.LogMessage(ECSLVATReportLine,ECSLVATReportLine.FIELDNO("Customer VAT Reg. No."),
            ErrorMessage."Message Type"::Error,STRSUBSTNO(NoVatRegNoErr,ECSLVATReportLine."Line No."));
        UNTIL ECSLVATReportLine.NEXT = 0;
    END;

    LOCAL PROCEDURE CheckZeroAmountOnLines@1041(VATReportHeader@1040 : Record 740);
    VAR
      ECSLVATReportLine@1041 : Record 362;
    BEGIN
      ECSLVATReportLine.SETRANGE("Report No.",VATReportHeader."No.");
      ECSLVATReportLine.SETRANGE("Total Value Of Supplies",0);

      IF ECSLVATReportLine.FINDSET THEN
        REPEAT
          ErrorMessage.LogMessage(ECSLVATReportLine,ECSLVATReportLine.FIELDNO("Total Value Of Supplies"),
            ErrorMessage."Message Type"::Error,STRSUBSTNO(ZeroTotalValueErr,ECSLVATReportLine."Line No."));
        UNTIL ECSLVATReportLine.NEXT = 0;
    END;

    BEGIN
    END.
  }
}

