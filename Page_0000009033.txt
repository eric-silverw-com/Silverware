OBJECT Page 9033 Invite External Accountant
{
  OBJECT-PROPERTIES
  {
    Date=09/19/18;
    Time=12:00:00 PM;
    Version List=NAVW113.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Invite External Accountant;
               ESM=Invitar a contable externo;
               FRC=Inviter un comptable externe;
               ENC=Invite External Accountant];
    PageType=NavigatePage;
    OnInit=BEGIN
             DefineInitialEmailBody;
           END;

    OnOpenPage=VAR
                 PermissionManager@1000 : Codeunit 9002;
                 InviteExternalAccountant@1001 : Codeunit 9033;
                 NavUserAccountHelper@1004 : DotNet "'Microsoft.Dynamics.Nav.NavUserAccount, Version=13.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.NavUserAccount.NavUserAccountHelper";
                 ProgressWindow@1002 : Dialog;
                 ErrorMessage@1003 : Text;
               BEGIN
                 OnInvitationStart;
                 IF NOT PermissionManager.SoftwareAsAService THEN
                   ERROR(SaaSOnlyErrorErr);

                 ProgressWindow.OPEN(WizardOpenValidationMsg);

                 IF NOT InviteExternalAccountant.VerifySMTPIsEnabledAndSetup THEN
                   ERROR(SMTPMustBeSetupErrorErr);

                 IF NOT InviteExternalAccountant.InvokeIsExternalAccountantLicenseAvailable(ErrorMessage) THEN BEGIN
                   OnInvitationNoExternalAccountantLicenseFail;
                   ERROR(NoExternalAccountantLicenseAvailableErr);
                 END;

                 IF NOT InviteExternalAccountant.InvokeIsUserAdministrator THEN BEGIN
                   OnInvitationNoAADPermissionsFail;
                   ERROR(NoAADPermissionsErr);
                 END;

                 IF NOT (NavUserAccountHelper.IsSessionAdminSession OR NavUserAccountHelper.IsUserSuperInAllCompanies) THEN BEGIN
                   OnInvitationNoUserTablePermissionsFail;
                   ERROR(NoUserTableWritePermissionErr);
                 END;

                 ProgressWindow.CLOSE;
                 Step := Step::Start;
                 EnableControls;
               END;

    OnQueryClosePage=BEGIN
                       IF CloseAction = ACTION::OK THEN
                         IF NOT UserInvited THEN
                           IF NOT CONFIRM(SetupNotCompletedQst,FALSE) THEN
                             ERROR('');
                     END;

    ActionList=ACTIONS
    {
      { 2       ;    ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 3       ;1   ;Action    ;
                      Name=ActionBack;
                      CaptionML=[ENU=Back;
                                 ESM=Atr†s;
                                 FRC=PrÇcÇdent;
                                 ENC=Back];
                      ApplicationArea=#Basic,#Suite;
                      Enabled=BackActionEnabled;
                      InFooterBar=Yes;
                      Image=PreviousRecord;
                      OnAction=BEGIN
                                 NextStep(TRUE);
                               END;
                                }
      { 4       ;1   ;Action    ;
                      Name=ActionNext;
                      CaptionML=[ENU=Next;
                                 ESM=Siguiente;
                                 FRC=Suivant;
                                 ENC=Next];
                      ApplicationArea=#Basic,#Suite;
                      Enabled=NextActionEnabled;
                      InFooterBar=Yes;
                      Image=NextRecord;
                      OnAction=VAR
                                 ErrorMessage@1010 : Text;
                               BEGIN
                                 IF Step = Step::DefineInformation THEN BEGIN
                                   IF (NewUserEmailAddress <> '') AND (NewFirstName <> '') AND (NewLastName <> '') AND (NewUserWelcomeEmail <> '') THEN BEGIN
                                     IF InviteExternalAccountant.InvokeEmailAddressIsAADAccount(NewUserEmailAddress,ErrorMessage) THEN BEGIN
                                       Invite;
                                       OnInvitationEnd(WasInvitationSuccessful,InvitationResult,InviteProgress);
                                       NextStep(FALSE);
                                     END ELSE
                                       ERROR(ErrorMessage);
                                   END ELSE
                                     ERROR(NotAllFieldsEnteredErrorErr);
                                 END ELSE
                                   NextStep(FALSE);
                               END;
                                }
      { 46      ;1   ;Action    ;
                      Name=ActionClose;
                      CaptionML=[ENU=Close;
                                 ESM=Cerrar;
                                 FRC=Fermer;
                                 ENC=Close];
                      ApplicationArea=#Basic,#Suite;
                      Visible=CloseActionVisible;
                      Enabled=TRUE;
                      InFooterBar=Yes;
                      Image=NextRecord;
                      OnAction=BEGIN
                                 CurrPage.CLOSE;
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1   ;0   ;Container ;
                ContainerType=ContentArea }

    { 10  ;1   ;Group     ;
                Visible=FirstStepVisible;
                GroupType=Group }

    { 11  ;2   ;Group     ;
                CaptionML=[ENU=Welcome to assisted setup for inviting an external accountant.;
                           ESM=Bienvenido a la configuraci¢n asistida para invitar a un contable externo.;
                           FRC=Bienvenue dans la configuration assistÇe concernant l'invitation d'un comptable externe.;
                           ENC=Welcome to assisted setup for inviting an external accountant.];
                Visible=FirstStepVisible;
                GroupType=Group }

    { 12  ;3   ;Group     ;
                Visible=FirstStepVisible;
                GroupType=Group;
                InstructionalTextML=[ENU=This guide will help you invite an external accountant to login to your company.;
                                     ESM=Esta gu°a le ayudar† a invitar a contables externos para que inicien sesi¢n en su empresa.;
                                     FRC=Ce guide vous aide Ö inviter un comptable externe Ö se connecter Ö votre compagnie.;
                                     ENC=This guide will help you invite an external accountant to login to your company.] }

    { 24  ;4   ;Group     ;
                CaptionML=[ENU=This Invite External Accountant feature allows your organization to share its data with an external party either through the use of a separate portal or through the external party's access to your organization's online services account. Microsoft has no control over the third-party's use of your data. You are responsible for ensuring that you have separate agreements in place with any such external user governing such external user's access to and use of your data.;
                           ESM=La caracter°stica Invitar a contador externo permite que la organizaci¢n comparta sus datos con una parte externa mediante el uso de un portal independiente o a travÇs del acceso de la parte externa a la cuenta de servicios en l°nea de la organizaci¢n. Microsoft no tiene control sobre el uso que hagan los terceros de los datos de la organizaci¢n. Usted es responsable de garantizar la existencia de contratos independientes con tales usuarios externos que rijan el acceso a los datos de la organizaci¢n y su uso por parte de dichos usuarios externos.;
                           FRC=Cette fonctionnalitÇ Inviter un comptable externe permet Ö votre organisation de partager ses donnÇes avec un tiers externe soit Ö l'aide d'un portail distinct ou de l'accäs du tiers externe au compte de services en ligne de votre organisation. Microsoft n'a aucun contrìle sur l'utilisation que fera le tiers de vos donnÇes. Vous avez la responsabilitÇ de veiller Ö disposer d'ententes distinctes en place avec des utilisateurs externes de ce type rÇgissant ce type d'accäs et d'utilisation externes de vos donnÇes.;
                           ENC=This Invite External Accountant feature allows your organization to share its data with an external party either through the use of a separate portal or through the external party's access to your organization's online services account. Microsoft has no control over the third-party's use of your data. You are responsible for ensuring that you have separate agreements in place with any such external user governing such external user's access to and use of your data.];
                Visible=FirstStepVisible;
                GroupType=Group }

    { 8   ;5   ;Group     ;
                CaptionML=[ENU=By clicking 'I Accept', you consent to share your organization's data with external parties you designate.;
                           ESM=Al hacer clic en "Acepto", proporciona su consentimiento para compartir datos de su organizaci¢n con las partes externas que designe.;
                           FRC=En cliquant sur ÆˇJ'accepteˇØ, vous consentez Ö actionager les donnÇes de votre organisation avec des tiers externes que vous dÇsignez.;
                           ENC=By clicking 'I Accept', you consent to share your organization's data with external parties you designate.];
                Visible=FirstStepVisible;
                GroupType=Group }

    { 6   ;6   ;Field     ;
                Name=DataPrivacy;
                CaptionML=[ENU=I Accept;
                           ESM=Acepto;
                           FRC=J'accepte;
                           ENC=I Accept];
                ToolTipML=[ENU=Specifies your consent to share your organization's data with external parties you designate.;
                           ESM=Indica su consentimiento para compartir datos de su organizaci¢n con las partes externas que designe.;
                           FRC=SpÇcifie que vous consentez Ö actionager les donnÇes de votre organisation avec des tiers externes que vous dÇsignez.;
                           ENC=Specifies your consent to share your organization's data with external parties you designate.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr=DataPrivacyAccepted;
                OnValidate=BEGIN
                             NextActionEnabled := TRUE;
                           END;
                            }

    { 7   ;6   ;Group     ;
                Visible=FirstStepVisible;
                GroupType=Group;
                InstructionalTextML=[ENU=Choose Next to get started.;
                                     ESM=Para comenzar, elija Siguiente.;
                                     FRC=SÇlectionnez Suivant pour commencer.;
                                     ENC=Choose Next to get started.] }

    { 13  ;1   ;Group     ;
                Visible=DefineInformationStepVisible;
                GroupType=Group }

    { 20  ;2   ;Group     ;
                Visible=DefineInformationStepVisible;
                GroupType=Group;
                InstructionalTextML=[ENU=Please enter the email address of the accountant.;
                                     ESM=Escriba la direcci¢n de correo electr¢nico del contable.;
                                     FRC=Veuillez entrer l'adresse Çlectronique du comptable.;
                                     ENC=Please enter the email address of the accountant.] }

    { 21  ;3   ;Field     ;
                Name=NewUserEmailAddress;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=NewUserEmailAddress;
                ShowMandatory=TRUE;
                ShowCaption=No }

    { 25  ;2   ;Group     ;
                Visible=DefineInformationStepVisible;
                GroupType=Group;
                InstructionalTextML=[ENU=Please enter the first name.;
                                     ESM=Introduzca el nombre.;
                                     FRC=Veuillez entrer un prÇnom.;
                                     ENC=Please enter the first name.] }

    { 26  ;3   ;Field     ;
                Name=NewFirstName;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=NewFirstName;
                OnValidate=BEGIN
                             DefineInitialEmailBody;
                           END;

                ShowMandatory=TRUE;
                ShowCaption=No }

    { 15  ;2   ;Group     ;
                Visible=DefineInformationStepVisible;
                GroupType=Group;
                InstructionalTextML=[ENU=Please enter the last name.;
                                     ESM=Introduzca los apellidos.;
                                     FRC=Veuillez entrer un nom.;
                                     ENC=Please enter the last name.] }

    { 5   ;3   ;Field     ;
                Name=NewLastName;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=NewLastName;
                ShowMandatory=TRUE;
                ShowCaption=No }

    { 16  ;2   ;Group     ;
                CaptionML=[ENU=Welcome Email;
                           ESM=Correo electr¢nico de bienvenida;
                           FRC=Courriel de bienvenue;
                           ENC=Welcome Email];
                Visible=DefineInformationStepVisible;
                GroupType=Group }

    { 23  ;3   ;Field     ;
                Name=NewUserWelcomeEmail;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=NewUserWelcomeEmail;
                MultiLine=Yes;
                ShowCaption=No }

    { 17  ;1   ;Group     ;
                Visible=CloseActionVisible;
                GroupType=Group }

    { 18  ;2   ;Field     ;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=InvitationResult;
                Enabled=FALSE;
                Editable=FALSE;
                Style=Strong;
                StyleExpr=TRUE;
                ShowCaption=No }

    { 19  ;2   ;Field     ;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=InviteProgress;
                Enabled=FALSE;
                Editable=FALSE;
                MultiLine=Yes;
                ShowCaption=No }

  }
  CODE
  {
    VAR
      InviteExternalAccountant@1018 : Codeunit 9033;
      AzureADMgt@1001 : Codeunit 6300;
      Step@1002 : 'Start,DefineInformation,Finish';
      FirstStepVisible@1004 : Boolean;
      DefineInformationStepVisible@1005 : Boolean;
      BackActionEnabled@1008 : Boolean;
      NextActionEnabled@1009 : Boolean;
      SetupNotCompletedQst@1013 : TextConst 'ENU=The user was not yet invited. Are you sure that you want to exit?;ESM=Todav°a no se ha invitado al usuario. ®Est† seguro de que desea salir?;FRC=L''utilisateur n''a pas encore ÇtÇ invitÇ. “tes-vous sñr de vouloir quitterˇ?;ENC=The user was not yet invited. Are you sure that you want to exit?';
      DataPrivacyAccepted@1000 : Boolean;
      CloseActionVisible@1021 : Boolean;
      NewUserEmailAddress@1010 : Text;
      NewFirstName@1053 : Text;
      NewLastName@1020 : Text;
      NewUserWelcomeEmail@1012 : Text;
      UserInvited@1015 : Boolean;
      EmailGreetingTxt@1024 : TextConst 'ENU="Hello ";ESM="Hola ";FRC="Bonjour ";ENC="Hello "';
      EmailBodyTxt@1025 : TextConst '@@@=%1 - product name;ENU=Please accept this invitation to get access to my %1.;ESM=Acepte esta invitaci¢n para obtener acceso a mi %1.;FRC=Veuillez accepter cette invitation pour accÇder Ö mon %1.;ENC=Please accept this invitation to get access to my %1.';
      EmailClosingTxt@1026 : TextConst 'ENU=Best Regards,;ESM=Atentamente,;FRC=Meilleures salutations,;ENC=Best Regards,';
      SaaSOnlyErrorErr@1014 : TextConst 'ENU=This functionality is not intended for on premises.;ESM=Esta funcionalidad no est† dise§ada para su uso local.;FRC=Cette fonctionnalitÇ n''est pas destinÇe pour une utilisation sur site.;ENC=This functionality is not intended for on premises.';
      InviteProgress@1022 : Text;
      InvitationErrorTxt@1027 : TextConst '@@@="%1=part of the invite process, e.g. invite, profile update, license assignment.  %2 =the error message.";ENU=Inviting external accountant failed while doing the %1.  %2;ESM=La invitaci¢n al contable externo ha fallado al hacer %1.  %2;FRC=êchec de l''invitation du comptable externe lors du processus de %1.  %2;ENC=Inviting external accountant failed while doing the %1.  %2';
      InviteTxt@1028 : TextConst 'ENU=invite;ESM=invitar;FRC=inviter;ENC=invite';
      ProfileUpdateTxt@1029 : TextConst 'ENU=profile update;ESM=actualizaci¢n de perfil;FRC=mise Ö jour du profil;ENC=profile update';
      LicenseAssignmentTxt@1030 : TextConst 'ENU=license assignment;ESM=asignaci¢n de licencia;FRC=affectation de licence;ENC=licence assignment';
      EmailTxt@1031 : TextConst 'ENU=email;ESM=correo electr¢nico;FRC=Courriel;ENC=email';
      InvitationSuccessTxt@1032 : TextConst '@@@="%1=first name.  %2 =last name.";ENU=%1 %2 was successfully invited!;ESM=Se ha invitado correctamente a %1 %2;FRC=%1 %2 a ÇtÇ invitÇ avec succäsˇ!;ENC=%1 %2 was successfully invited!';
      NoExternalAccountantLicenseAvailableErr@1033 : TextConst 'ENU=No External Accountant license available. Contact your administrator.;ESM=No hay licencias de contador externo disponibles. P¢ngase en contacto con el administrador.;FRC=Aucune licence de comptable externe disponible. Contactez votre administrateur.;ENC=No External Accountant licence available. Contact your administrator.';
      NoAADPermissionsErr@1034 : TextConst 'ENU=You do not have permission to invite the user. You must either be a global administrator or a user administrator in Azure AD. Please contact your administrator.;ESM=No tiene permiso para invitar al usuario. Debe ser un administrador global o un administrador de usuarios en AzureˇAD. P¢ngase en contacto con el administrador.;FRC=Vous n''àtes pas autorisÇ Ö inviter l''utilisateur. Vous devez àtre un administrateur global ou un administrateur d''utilisateur dans Azure AD. Contactez votre administrateur.;ENC=You do not have permission to invite the user. You must either be a global administrator or a user administrator in Azure AD. Please contact your administrator.';
      WizardOpenValidationMsg@1035 : TextConst 'ENU=Verifying permissions and license availability.;ESM=Verificando los permisos y la disponibilidad de la licencia.;FRC=VÇrification des autorisations et de la disponibilitÇ du contrat de licence.;ENC=Verifying permissions and licence availability.';
      InviteProgressWindowMsg@1036 : TextConst 'ENU=Inviting external accountant.  This process could take a little while.;ESM=Invitando a contable externo. Este proceso puede tardar unos instantes.;FRC=Invitation du comptable externe en cours. Ce processus peut prendre quelques instants.;ENC=Inviting external accountant.  This process could take a little while.';
      EmailSubjectTxt@1037 : TextConst '@@@=%1 - product name;ENU=You have been invited to %1;ESM=Ha recibido una invitaci¢n a %1;FRC=Vous avez ÇtÇ invitÇ Ö %1;ENC=You have been invited to %1';
      EmailUseThisLinkWhenAddingClientTxt@1039 : TextConst 'ENU=Navigate to the link below to verify you can log in. Then you can use the same link when adding this client in your Accountant Hub.;ESM=Vaya al v°nculo siguiente para comprobar que puede iniciar sesi¢n. Podr† utilizar este mismo v°nculo al agregar este cliente en su Accountant Hub.;FRC=Cliquez sur le lien ci-dessous pour vÇrifier que vous pouvez vous connecter. Vous pouvez ensuite utiliser le màme lien lorsque vous ajoutez ce client dans votre Accountant Hub.;ENC=Navigate to the link below to verify you can log in. Then you can use the same link when adding this client in your Accountant Hub.';
      EmailNotUsingAccountantPortalTxt@1040 : TextConst '@@@={Do not translate html portion.};ENU="Not using the Accountant Hub?  Click <a href=""https://dynamics.microsoft.com/en-us/business-central/accountants"">here</a> to learn more.";ESM="®No se utiliza Accountant Hub? Haga clic <a href=""https://dynamics.microsoft.com/en-us/business-central/accountants"">aqu°</a> para obtener m†s informaci¢n.";FRC="Vous n''utilisez pas Accountant Hubˇ? Cliquez <a href=""https://dynamics.microsoft.com/en-us/business-central/accountants"">ici</a> pour en savoir plus.";ENC="Not using the Accountant Hub?  Click <a href=""https://dynamics.microsoft.com/en-us/business-central/accountants"">here</a> to learn more."';
      InvitationResult@1023 : Text;
      FailureTxt@1041 : TextConst 'ENU=Failure;ESM=Error;FRC=êchec;ENC=Failure';
      SuccessTxt@1042 : TextConst 'ENU=Success;ESM=Correcto;FRC=RÇussite;ENC=Success';
      EmailErrorTxt@1043 : TextConst 'ENU=Error occurred while sending email.;ESM=Error al enviar el correo electr¢nico.;FRC=Une erreur s''est produite lors de l''envoi du courriel.;ENC=Error occurred while sending email.';
      NotAllFieldsEnteredErrorErr@1044 : TextConst 'ENU=To continue, enter all required fields.;ESM=Para continuar, especifique todos los campos obligatorios.;FRC=Pour continuer, renseignez tous les champs obligatoires.;ENC=To continue, enter all required fields.';
      WasInvitationSuccessful@1060 : Boolean;
      SMTPMustBeSetupErrorErr@1045 : TextConst 'ENU=SMTP must be configured prior to inviting external accountant. Contact your administrator.;ESM=Se debe configurar SMTP antes de invitar a un contador externo. P¢ngase en contacto con el administrador.;FRC=SMTP doit àtre configurÇ avant l''invitation d''un comptable externe. Contactez votre administrateur.;ENC=SMTP must be configured prior to inviting external accountant. Contact your administrator.';
      NoUserTableWritePermissionErr@1046 : TextConst 'ENU=This step adds a user to your company, and only your administrator can do that. Please contact your administrator.;ESM=Este paso agrega un usuario a su empresa y solo lo puede hacer el administrador. P¢ngase en contacto con el administrador.;FRC=Cette Çtape ajoute un utilisateur Ö votre compagnie, et seul votre administrateur peut l''effectuer. Contactez votre administrateur.;ENC=This step adds a user to your company, and only your administrator can do that. Please contact your administrator.';

    LOCAL PROCEDURE EnableControls@1();
    BEGIN
      ResetControls;

      CASE Step OF
        Step::Start:
          ShowStartStep;
        Step::DefineInformation:
          ShowDefineInformationStep;
        Step::Finish:
          ShowFinishStep;
      END;
    END;

    LOCAL PROCEDURE NextStep@3(Backwards@1000 : Boolean);
    BEGIN
      IF Backwards THEN
        Step := Step - 1
      ELSE
        Step := Step + 1;

      EnableControls;
    END;

    LOCAL PROCEDURE ShowStartStep@4();
    BEGIN
      FirstStepVisible := TRUE;

      BackActionEnabled := FALSE;
    END;

    LOCAL PROCEDURE ShowDefineInformationStep@5();
    BEGIN
      DefineInformationStepVisible := TRUE;
    END;

    LOCAL PROCEDURE ShowFinishStep@6();
    BEGIN
      NextActionEnabled := FALSE;
      CloseActionVisible := TRUE;
    END;

    LOCAL PROCEDURE Invite@1045();
    VAR
      SMTPMail@1011 : Codeunit 400;
      MailManagement@1012 : Codeunit 9520;
      GuestGraphUser@1013 : DotNet "'Microsoft.Dynamics.Nav.LicensingService.Model, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.LicensingService.Model.UserInfo";
      Graph@1014 : DotNet "'Microsoft.Dynamics.Nav.AzureADGraphClient, Version=13.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.AzureADGraphClient.GraphQuery";
      ProgressWindow@1015 : Dialog;
      InvitedUserId@1016 : GUID;
      InviteRedeemUrl@1017 : Text;
      ErrorMessage@1018 : Text;
    BEGIN
      UserInvited := TRUE;
      ProgressWindow.OPEN(InviteProgressWindowMsg);

      IF NOT InviteExternalAccountant.InvokeInvitationsRequest(NewFirstName + NewLastName,
           NewUserEmailAddress,GetWebClientUrl,InvitedUserId,InviteRedeemUrl,ErrorMessage)
      THEN BEGIN
        InvitationResult := FailureTxt;
        InviteProgress := STRSUBSTNO(InvitationErrorTxt,InviteTxt,ErrorMessage);
        ProgressWindow.CLOSE;
        EXIT;
      END;
      Graph := Graph.GraphQuery;

      IF NOT InviteExternalAccountant.TryGetGuestGraphUser(InvitedUserId,GuestGraphUser) THEN BEGIN
        InvitationResult := FailureTxt;
        InviteProgress := STRSUBSTNO(InvitationErrorTxt,InviteTxt,ErrorMessage);
        ProgressWindow.CLOSE;
        EXIT;
      END;

      IF NOT InviteExternalAccountant.InvokeUserProfileUpdateRequest(GuestGraphUser,
           Graph.GetTenantDetail().CountryLetterCode,ErrorMessage)
      THEN BEGIN
        InvitationResult := FailureTxt;
        InviteProgress := STRSUBSTNO(InvitationErrorTxt,ProfileUpdateTxt,ErrorMessage);
        ProgressWindow.CLOSE;
        EXIT;
      END;

      IF NOT InviteExternalAccountant.InvokeUserAssignLicenseRequest(GuestGraphUser,ErrorMessage) THEN BEGIN
        InvitationResult := FailureTxt;
        InviteProgress := STRSUBSTNO(InvitationErrorTxt,LicenseAssignmentTxt,ErrorMessage);
        ProgressWindow.CLOSE;
        EXIT;
      END;

      SMTPMail.CreateMessage('',MailManagement.GetSenderEmailAddress,NewUserEmailAddress,
        STRSUBSTNO(EmailSubjectTxt,PRODUCTNAME.MARKETING),DefineFullEmailBody(NewUserWelcomeEmail),TRUE);
      IF NOT SMTPMail.TrySend THEN BEGIN
        InvitationResult := FailureTxt;
        InviteProgress := STRSUBSTNO(InvitationErrorTxt,EmailTxt,EmailErrorTxt);
        ProgressWindow.CLOSE;
        EXIT;
      END;

      ProgressWindow.CLOSE;

      InvitationResult := SuccessTxt;
      WasInvitationSuccessful := TRUE;
      InviteProgress := STRSUBSTNO(InvitationSuccessTxt,NewFirstName,NewLastName);
      CurrPage.UPDATE(FALSE);
      InviteExternalAccountant.UpdateAssistedSetup;
      InviteExternalAccountant.CreateNewUser(InvitedUserId);
    END;

    LOCAL PROCEDURE ResetControls@7();
    BEGIN
      FirstStepVisible := FALSE;
      DefineInformationStepVisible := FALSE;
      NextActionEnabled := FALSE;

      BackActionEnabled := TRUE;

      IF DataPrivacyAccepted THEN
        NextActionEnabled := TRUE;

      CloseActionVisible := FALSE;
    END;

    LOCAL PROCEDURE DefineInitialEmailBody@1046();
    VAR
      User@1010 : Record 2000000120;
      EmailGreeting@1011 : Text;
      EmailBody@1012 : Text;
      EmailClosing@1013 : Text;
    BEGIN
      User.GET(USERSECURITYID);
      EmailGreeting := EmailGreetingTxt + NewFirstName + ',' + NewLineForTextControl;
      EmailBody := STRSUBSTNO(EmailBodyTxt,PRODUCTNAME.MARKETING) + NewLineForTextControl + NewLineForTextControl;
      EmailClosing := EmailClosingTxt + NewLineForTextControl + User."User Name" + NewLineForTextControl + COMPANYNAME;
      NewUserWelcomeEmail := EmailGreeting + EmailBody + EmailClosing;
      CurrPage.UPDATE;
    END;

    LOCAL PROCEDURE DefineFullEmailBody@1047(InitialEmailMessage@1010 : Text) : Text;
    VAR
      EmailBody@1012 : Text;
    BEGIN
      EmailBody := ReplaceNewLinesWithHtmlLineBreak(InitialEmailMessage);
      EmailBody := EmailBody + LineBreakForEmail + LineBreakForEmail + EmailUseThisLinkWhenAddingClientTxt;
      EmailBody := EmailBody + LineBreakForEmail + GetWebClientUrl + LineBreakForEmail;
      EmailBody := EmailBody + LineBreakForEmail + EmailNotUsingAccountantPortalTxt + LineBreakForEmail;
      EXIT(EmailBody)
    END;

    LOCAL PROCEDURE NewLineForTextControl@1048() Newline : Text;
    BEGIN
      Newline[1] := 13;
      Newline[2] := 10;
    END;

    LOCAL PROCEDURE LineBreakForEmail@1050() : Text;
    BEGIN
      EXIT('</br>');
    END;

    LOCAL PROCEDURE ReplaceNewLinesWithHtmlLineBreak@1051(InputText@1010 : Text) : Text;
    VAR
      String@1011 : DotNet "'mscorlib, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.String";
      TextToReplace@1012 : Text;
    BEGIN
      String := InputText;
      TextToReplace[1] := 10;
      EXIT(String.Replace(TextToReplace,LineBreakForEmail));
    END;

    LOCAL PROCEDURE GetWebClientUrl@1052() : Text;
    VAR
      Graph@1004 : DotNet "'Microsoft.Dynamics.Nav.AzureADGraphClient, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.AzureADGraphClient.GraphQuery";
      ClientUrl@1010 : Text;
      TenantDomainName@1000 : Text;
      TenantObjectId@1001 : Text;
    BEGIN
      IF InviteExternalAccountant.IsPPE THEN
        ClientUrl := 'https://businesscentral.dynamics-tie.com/'
      ELSE
        ClientUrl := 'https://businesscentral.dynamics.com/';

      TenantDomainName := AzureADMgt.GetInitialTenantDomainName;
      Graph := Graph.GraphQuery;
      TenantObjectId := Graph.GetTenantDetail.ObjectId;

      IF STRLEN(TenantDomainName) > 0 THEN
        ClientUrl := ClientUrl + TenantDomainName
      ELSE
        ClientUrl := ClientUrl + TenantObjectId;

      ClientUrl := ClientUrl + '?redirectedfromsignup=1';

      EXIT(ClientUrl);
    END;

    [Integration]
    LOCAL PROCEDURE OnInvitationStart@20();
    BEGIN
      // This event is called the invitation process is started.
    END;

    [Integration]
    LOCAL PROCEDURE OnInvitationNoExternalAccountantLicenseFail@21();
    BEGIN
      // This event is called when the invitation process can not proceed due to a lack of external accountant licenses.
    END;

    [Integration]
    LOCAL PROCEDURE OnInvitationNoAADPermissionsFail@22();
    BEGIN
      // This event is called when the invitation process can not proceed due to a lack of user AAD permissions.
    END;

    [Integration]
    LOCAL PROCEDURE OnInvitationEnd@23(WasInvitationSuccessful@1001 : Boolean;Result@1002 : Text;Progress@1003 : Text);
    BEGIN
      // This event is called when the invitation process is finished.
    END;

    [Integration]
    LOCAL PROCEDURE OnInvitationNoUserTablePermissionsFail@24();
    BEGIN
      // This event is called when the invitation process can not proceed because session is not admin or user is not super in all companies.
    END;

    BEGIN
    END.
  }
}

