OBJECT Page 2145 O365 VAT Posting Setup Card
{
  OBJECT-PROPERTIES
  {
    Date=11/27/18;
    Time=11:10:46 PM;
    Version List=;
  }
  PROPERTIES
  {
    CaptionML=ENU=Tax Rate;
    InsertAllowed=No;
    DeleteAllowed=No;
    SourceTable=Table324;
    PageType=Card;
    OnClosePage=BEGIN
                  UpdateVATClause;
                  UpdateVATPercentage;
                END;

    OnAfterGetCurrRecord=BEGIN
                           IF NOT VATPostingSetup.GET(O365TemplateManagement.GetDefaultVATBusinessPostingGroup,Code) THEN
                             EXIT;

                           VATPercentage := VATPostingSetup."VAT %";
                           DefaultGroup := Code = O365TemplateManagement.GetDefaultVATProdPostingGroup;

                           // VAT Regulation Reference = Vat clause
                           IF NOT VATClause.GET(VATPostingSetup."VAT Clause Code") THEN BEGIN
                             VATClause.INIT;
                             VATClause.Code := Code;
                             VATClause.INSERT;
                             VATPostingSetup.VALIDATE("VAT Clause Code",Code);
                             VATPostingSetup.MODIFY(TRUE);
                           END;
                           VATRegulationReference := VATClause.Description;
                           IF DefaultGroup THEN
                             DefaultVATGroupTxt := DefaultGroupTxt
                           ELSE
                             DefaultVATGroupTxt := SetAsDefaultTxt;
                         END;

    ActionList=ACTIONS
    {
      { 7       ;    ;ActionContainer;
                      ActionContainerType=ActionItems }
    }
  }
  CONTROLS
  {
    { 1   ;0   ;Container ;
                ContainerType=ContentArea }

    { 3   ;1   ;Field     ;
                ApplicationArea=#Basic,#Suite,#Invoicing;
                NotBlank=Yes;
                SourceExpr=Description }

    { 6   ;1   ;Field     ;
                Name=VAT Percentage;
                CaptionML=ENU=Tax Percentage;
                ToolTipML=ENU=Specifies the relevant tax rate as a percentage (%). For example, if the tax rate is 25%, enter 25 in this field.;
                ApplicationArea=#Basic,#Suite,#Invoicing;
                SourceExpr=VATPercentage;
                MinValue=0 }

    { 5   ;1   ;Field     ;
                Name=VAT Regulation Reference;
                CaptionML=ENU=Tax Regulation Reference;
                ToolTipML=ENU=Specifies the Tax Regulation Reference for the tax rate. Tax Regulation Reference describe the tax that is being reported on a sales document, and are displayed on printed documents alongside the tax identifier and tax rate.;
                ApplicationArea=#Basic,#Suite,#Invoicing;
                SourceExpr=VATRegulationReference }

    { 8   ;1   ;Field     ;
                ApplicationArea=#Basic,#Suite,#Invoicing;
                SourceExpr=DefaultVATGroupTxt;
                Enabled=NOT DefaultGroup;
                Editable=FALSE;
                OnDrillDown=BEGIN
                              O365TemplateManagement.SetDefaultVATProdPostingGroup(Code);
                              DefaultVATGroupTxt := DefaultGroupTxt;
                              DefaultGroup := TRUE;
                            END;

                ShowCaption=No }

  }
  CODE
  {
    VAR
      VATPostingSetup@1002 : Record 325;
      VATClause@1005 : Record 560;
      O365TemplateManagement@1004 : Codeunit 2142;
      VATPercentage@1003 : Decimal;
      DefaultGroup@1010 : Boolean;
      VATRegulationReference@1001 : Text[250];
      DefaultVATGroupTxt@1011 : Text;
      DefaultGroupTxt@1012 : TextConst 'ENU=This is the default Tax Rate';
      SetAsDefaultTxt@1013 : TextConst 'ENU=Set as default Tax Rate';

    LOCAL PROCEDURE UpdateVATClause@8();
    BEGIN
      IF Description = VATRegulationReference THEN
        EXIT;

      VATClause.VALIDATE(Description,VATRegulationReference);
      VATClause.MODIFY(TRUE);
    END;

    LOCAL PROCEDURE UpdateVATPercentage@4();
    VAR
      SalesLine@1000 : Record 37;
    BEGIN
      IF VATPercentage = VATPostingSetup."VAT %" THEN
        EXIT;
      VATPostingSetup.VALIDATE("VAT %",VATPercentage);
      VATPostingSetup.MODIFY(TRUE);
      SalesLine.SETRANGE("VAT Prod. Posting Group",Code);
      IF SalesLine.FINDSET THEN
        REPEAT
          SalesLine.VALIDATE("VAT Prod. Posting Group");
          SalesLine.MODIFY(TRUE);
        UNTIL SalesLine.NEXT = 0;
    END;

    BEGIN
    END.
  }
}

