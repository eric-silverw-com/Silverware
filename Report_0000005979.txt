OBJECT Report 5979 Copy Service Document
{
  OBJECT-PROPERTIES
  {
    Date=11/22/17;
    Time=12:00:00 PM;
    Version List=NAVW111.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Copy Service Document;
               ESM=Copiar documento servicio;
               FRC=Copier le document de service;
               ENC=Copy Service Document];
    ProcessingOnly=Yes;
    OnPreReport=BEGIN
                  IF DocNo = '' THEN
                    ERROR(Text004);
                  ValidateDocNo;
                  IF FromServContractHeader."Ship-to Code" <> ServContractHeader."Ship-to Code" THEN
                    IF NOT CONFIRM(Text003,FALSE) THEN
                      CurrReport.QUIT;
                  AllLinesCopied := CopyDocMgt.CopyServContractLines(ServContractHeader,DocType,DocNo,OutServContractLine);
                END;

    OnPostReport=BEGIN
                   COMMIT;
                   IF NOT AllLinesCopied THEN
                     IF CONFIRM(
                          Text000,
                          TRUE)
                     THEN BEGIN
                       OutServContractLine.MARKEDONLY := TRUE;
                       PAGE.RUNMODAL(PAGE::"Service Contract Line List",OutServContractLine);
                     END;
                 END;

  }
  DATASET
  {
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
      OnOpenPage=BEGIN
                   IF DocNo <> '' THEN BEGIN
                     CASE DocType OF
                       DocType::Quote:
                         IF FromServContractHeader.GET(FromServContractHeader."Contract Type"::Quote,DocNo) THEN;
                       DocType::Contract:
                         IF FromServContractHeader.GET(FromServContractHeader."Contract Type"::Contract,DocNo) THEN;
                     END;
                     IF FromServContractHeader."Contract No." = '' THEN
                       DocNo := ''
                     ELSE
                       FromServContractHeader.CALCFIELDS(Name);
                   END;
                 END;

    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[ENU=Options;
                             ESM=Opciones;
                             FRC=Options;
                             ENC=Options] }

      { 3   ;2   ;Field     ;
                  CaptionML=[ENU=Document Type;
                             ESM=Tipo documento;
                             FRC=Type de document;
                             ENC=Document Type];
                  ToolTipML=[ENU=Specifies the type of service document that you want to copy.;
                             ESM=Especifica el tipo de documento de servicio que desea copiar.;
                             FRC=Sp‚cifie le type de document service que vous souhaitez copier.;
                             ENC=Specifies the type of service document that you want to copy.];
                  OptionCaptionML=[ENU=Quote,Contract;
                                   ESM=Cotizaci¢n,Contrato;
                                   FRC=Devis,Contrat;
                                   ENC=Quote,Contract];
                  ApplicationArea=#Service;
                  SourceExpr=DocType;
                  OnValidate=BEGIN
                               DocNo := '';
                               ValidateDocNo;
                             END;
                              }

      { 8   ;2   ;Field     ;
                  CaptionML=[ENU=Document No.;
                             ESM=N§ documento;
                             FRC=Nø de document;
                             ENC=Document No.];
                  ToolTipML=[ENU=Specifies the document number that you want to copy from by choosing the field.;
                             ESM=Especifica el n£mero de documento del cual desea copiar al elegir el campo.;
                             FRC=Sp‚cifie le num‚ro document … partir duquel vous souhaitez effectuer une copie en s‚lectionnant le champ.;
                             ENC=Specifies the document number that you want to copy from by choosing the field.];
                  ApplicationArea=#Service;
                  SourceExpr=DocNo;
                  OnValidate=BEGIN
                               ValidateDocNo;
                             END;

                  OnLookup=BEGIN
                             LookupDocNo;
                           END;
                            }

      { 2   ;2   ;Field     ;
                  CaptionML=[ENU=Customer No.;
                             ESM=N§ cliente;
                             FRC=Nø client;
                             ENC=Customer No.];
                  ToolTipML=[ENU=Specifies the number of the customer.;
                             ESM=Especifica el n£mero del cliente.;
                             FRC=Sp‚cifie le num‚ro du client.;
                             ENC=Specifies the number of the customer.];
                  ApplicationArea=#Service;
                  SourceExpr=FromServContractHeader."Customer No.";
                  Editable=FALSE }

      { 5   ;2   ;Field     ;
                  CaptionML=[ENU=Customer Name;
                             ESM=Nombre cliente;
                             FRC=Nom client;
                             ENC=Customer Name];
                  ToolTipML=[ENU=Specifies the customer name from a document that you have selected to copy the information from.;
                             ESM=Especifica el nombre de cliente de un documento que ha seleccionado desde el que hay que copiar la informaci¢n.;
                             FRC=Sp‚cifie le nom du client … partir d'un document que vous avez s‚lectionn‚ pour en copier les informations.;
                             ENC=Specifies the customer name from a document that you have selected to copy the information from.];
                  ApplicationArea=#Service;
                  SourceExpr=FromServContractHeader.Name;
                  Editable=FALSE }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      ServContractHeader@1007 : Record 5965;
      FromServContractHeader@1010 : Record 5965;
      OutServContractLine@1004 : Record 5964;
      CopyDocMgt@1009 : Codeunit 6620;
      DocType@1026 : 'Quote,Contract';
      DocNo@1027 : Code[20];
      AllLinesCopied@1003 : Boolean;
      Text000@1005 : TextConst 'ENU=It was not possible to copy all of the service contract lines.\\Do you want to see these lines?;ESM=No se pudieron copiar todas las l¡neas del contrato de servicio.\\¨Desea ver estas l¡neas?;FRC=Il n''a pas ‚t‚ possible de copier toutes les lignes contrat service.\\Voulez-vous afficher ces lignes ?;ENC=It was not possible to copy all of the service contract lines.\\Do you want to see these lines?';
      Text002@1002 : TextConst 'ENU=You can only copy the document with the same %1.;ESM=Solo es posible copiar el documento con el mismo %1.;FRC=Vous pouvez uniquement copier un document avec la mˆme valeur %1.;ENC=You can only copy the document with the same %1.';
      Text003@1006 : TextConst 'ENU=The document has a different ship-to code.\\Do you want to continue?;ESM=El documento tiene un C¢d. direcci¢n env¡o diferente.\\¨Desea continuar?;FRC=Le document a un code de livraison diff‚rent.\\Voulez-vous continuer?;ENC=The document has a different ship-to code.\\Do you want to continue?';
      Text004@1008 : TextConst 'ENU=You must fill in the Document No. field.;ESM=Debe rellenar el campo N§ documento.;FRC=Vous devez remplir le champ Nø de document.;ENC=You must fill in the Document No. field.';

    PROCEDURE SetServContractHeader@2(VAR NewServContractHeader@1000 : Record 5965);
    BEGIN
      ServContractHeader := NewServContractHeader;
    END;

    LOCAL PROCEDURE ValidateDocNo@10();
    BEGIN
      IF DocNo = '' THEN
        FromServContractHeader.INIT
      ELSE BEGIN
        FromServContractHeader.INIT;
        FromServContractHeader.GET(DocType,DocNo);
        IF FromServContractHeader."Customer No." <> ServContractHeader."Customer No." THEN
          ERROR(Text002,ServContractHeader.FIELDCAPTION("Customer No."));
        IF FromServContractHeader."Currency Code" <> ServContractHeader."Currency Code" THEN
          ERROR(Text002,ServContractHeader.FIELDCAPTION("Currency Code"));
        FromServContractHeader.CALCFIELDS(Name);
      END;
    END;

    LOCAL PROCEDURE LookupDocNo@3();
    BEGIN
      FromServContractHeader.FILTERGROUP := 2;
      FromServContractHeader.SETRANGE("Contract Type",CopyDocMgt.ServContractHeaderDocType(DocType));
      IF ServContractHeader."Contract Type" = CopyDocMgt.ServContractHeaderDocType(DocType) THEN
        FromServContractHeader.SETFILTER("Contract No.",'<>%1',ServContractHeader."Contract No.");
      FromServContractHeader."Contract Type" := CopyDocMgt.ServContractHeaderDocType(DocType);
      FromServContractHeader."Contract No." := DocNo;
      FromServContractHeader.SETCURRENTKEY("Customer No.","Currency Code","Ship-to Code");
      FromServContractHeader.SETRANGE("Customer No.",ServContractHeader."Customer No.");
      FromServContractHeader.SETRANGE("Currency Code",ServContractHeader."Currency Code");
      FromServContractHeader.FILTERGROUP := 0;
      FromServContractHeader.SETRANGE("Ship-to Code",ServContractHeader."Ship-to Code");
      IF PAGE.RUNMODAL(0,FromServContractHeader) = ACTION::LookupOK THEN
        DocNo := FromServContractHeader."Contract No.";
      ValidateDocNo;
    END;

    PROCEDURE InitializeRequest@1(DocumentType@1000 : Option;DocumentNo@1001 : Code[20]);
    BEGIN
      DocType := DocumentType;
      DocNo := DocumentNo;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

