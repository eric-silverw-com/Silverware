OBJECT Page 2317 BC O365 Item Card
{
  OBJECT-PROPERTIES
  {
    Date=11/27/18;
    Time=11:10:48 PM;
    Version List=;
  }
  PROPERTIES
  {
    CaptionML=ENU=Price;
    SourceTable=Table27;
    DataCaptionExpr=Description;
    PageType=Card;
    RefreshOnActivate=Yes;
    OnInit=VAR
             O365SalesInitialSetup@6115 : Record 2110;
           BEGIN
             IsUsingVAT := O365SalesInitialSetup.IsUsingVAT;
           END;

    OnNewRecord=VAR
                  DocumentNoVisibility@1001 : Codeunit 1400;
                BEGIN
                  IF GUIALLOWED AND DocumentNoVisibility.ItemNoSeriesIsDefault THEN
                    NewMode := TRUE;
                END;

    OnQueryClosePage=BEGIN
                       EXIT(CanExitAfterProcessingItem);
                     END;

    OnAfterGetCurrRecord=VAR
                           VATProductPostingGroup@1000 : Record 324;
                           UnitOfMeasure@1001 : Record 204;
                         BEGIN
                           CreateItemFromTemplate;
                           IF VATProductPostingGroup.GET("VAT Prod. Posting Group") THEN
                             VATProductPostingGroupDescription := VATProductPostingGroup.Description;
                           IF UnitOfMeasure.GET("Base Unit of Measure") THEN
                             UnitOfMeasureDescription := UnitOfMeasure.GetDescriptionInCurrentLanguage;
                           IsPageEditable := CurrPage.EDITABLE;
                         END;

  }
  CONTROLS
  {
    { 3   ;0   ;Container ;
                ContainerType=ContentArea }

    { 1   ;1   ;Group     ;
                Name=Item;
                CaptionML=ENU=Product/Service;
                GroupType=Group }

    { 4   ;2   ;Field     ;
                Name=Description;
                ToolTipML=ENU=Specifies what you are selling.;
                ApplicationArea=#Basic,#Suite,#Invoicing;
                SourceExpr=Description;
                Importance=Promoted }

    { 38  ;2   ;Field     ;
                CaptionML=ENU=Price;
                ToolTipML=ENU=Specifies the price for one unit.;
                ApplicationArea=#Basic,#Suite,#Invoicing;
                SourceExpr="Unit Price";
                Importance=Promoted }

    { 6   ;2   ;Field     ;
                CaptionML=ENU=Price per;
                ToolTipML=ENU=Specifies the price for one unit.;
                ApplicationArea=#Basic,#Suite,#Invoicing;
                SourceExpr=UnitOfMeasureDescription;
                Editable=FALSE;
                OnAssistEdit=VAR
                               TempUnitOfMeasure@1000 : TEMPORARY Record 204;
                               O365UnitsOfMeasureList@1001 : Page 2194;
                             BEGIN
                               TempUnitOfMeasure.CreateListInCurrentLanguage(TempUnitOfMeasure);
                               IF TempUnitOfMeasure.GET("Base Unit of Measure") THEN;

                               O365UnitsOfMeasureList.SETRECORD(TempUnitOfMeasure);
                               O365UnitsOfMeasureList.LOOKUPMODE(TRUE);
                               IF O365UnitsOfMeasureList.RUNMODAL = ACTION::LookupOK THEN BEGIN
                                 O365UnitsOfMeasureList.GETRECORD(TempUnitOfMeasure);
                                 VALIDATE("Base Unit of Measure",TempUnitOfMeasure.Code);
                                 UnitOfMeasureDescription := TempUnitOfMeasure.Description;
                               END;

                               CurrPage.UPDATE(TRUE);
                             END;

                QuickEntry=FALSE }

    { 1020000;2;Field     ;
                Name=Taxable;
                CaptionML=ENU=Add sales tax;
                ToolTipML=ENU=Specifies the tax group code for the tax-detail entry.;
                ApplicationArea=#Basic,#Suite,#Invoicing;
                NotBlank=Yes;
                SourceExpr=Taxable;
                Visible=NOT IsUsingVAT;
                Editable=IsPageEditable;
                OnValidate=VAR
                             TaxSetup@1020000 : Record 326;
                             TaxGroup@1020001 : Record 321;
                           BEGIN
                             IF TaxSetup.GET AND (TaxSetup."Non-Taxable Tax Group Code" <> '') THEN BEGIN
                               IF Taxable THEN
                                 TaxGroup.SETFILTER(Code,'<>%1',TaxSetup."Non-Taxable Tax Group Code")
                               ELSE
                                 TaxGroup.SETFILTER(Code,'%1',TaxSetup."Non-Taxable Tax Group Code")
                             END;
                             IF TaxGroup.FINDFIRST THEN
                               VALIDATE("Tax Group Code",TaxGroup.Code);
                           END;

                ShowMandatory=TRUE }

    { 13  ;2   ;Field     ;
                CaptionML=ENU=Tax;
                ToolTipML=ENU=Specifies the Tax rate for this price.;
                ApplicationArea=#Basic,#Suite,#Invoicing;
                NotBlank=Yes;
                SourceExpr=VATProductPostingGroupDescription;
                Visible=IsUsingVAT;
                Editable=FALSE;
                OnAssistEdit=VAR
                               VATProductPostingGroup@1000 : Record 324;
                             BEGIN
                               IF PAGE.RUNMODAL(PAGE::"O365 VAT Product Posting Gr.",VATProductPostingGroup) = ACTION::LookupOK THEN BEGIN
                                 VALIDATE("VAT Prod. Posting Group",VATProductPostingGroup.Code);
                                 VATProductPostingGroupDescription := VATProductPostingGroup.Description;
                               END;
                             END;

                QuickEntry=FALSE }

    { 9   ;0   ;Container ;
                ContainerType=FactBoxArea }

    { 2   ;1   ;Part      ;
                Name=ItemPicture;
                CaptionML=ENU=Picture;
                ApplicationArea=#Basic,#Suite,#Invoicing;
                SubPageLink=No.=FIELD(No.),
                            Date Filter=FIELD(Date Filter),
                            Global Dimension 1 Filter=FIELD(Global Dimension 1 Filter),
                            Global Dimension 2 Filter=FIELD(Global Dimension 2 Filter),
                            Location Filter=FIELD(Location Filter),
                            Drop Shipment Filter=FIELD(Drop Shipment Filter),
                            Variant Filter=FIELD(Variant Filter);
                PagePartID=Page346;
                PartType=Page }

  }
  CODE
  {
    VAR
      ProcessNewItemOptionQst@1002 : TextConst 'ENU=Keep editing,Discard';
      ProcessNewItemInstructionTxt@1001 : TextConst 'ENU=Description is missing. Keep the price?';
      VATProductPostingGroupDescription@1027 : Text[50];
      NewMode@1006 : Boolean;
      IsUsingVAT@6115 : Boolean;
      Taxable@1020000 : Boolean;
      IsPageEditable@1003 : Boolean;
      Created@1000 : Boolean;
      UnitOfMeasureDescription@1004 : Text[10];

    LOCAL PROCEDURE CanExitAfterProcessingItem@2() : Boolean;
    VAR
      Response@1000 : ',KeepEditing,Discard';
    BEGIN
      IF "No." = '' THEN
        EXIT(TRUE);

      IF NewMode AND (Description = '') THEN BEGIN
        IF DELETE(TRUE) THEN;
        EXIT(TRUE);
      END;

      IF GUIALLOWED AND (Description = '') THEN
        CASE STRMENU(ProcessNewItemOptionQst,Response::KeepEditing,ProcessNewItemInstructionTxt) OF
          Response::Discard:
            EXIT(DELETE(TRUE));
          ELSE
            EXIT(FALSE);
        END;

      EXIT(TRUE);
    END;

    LOCAL PROCEDURE CreateItemFromTemplate@3();
    VAR
      ItemTemplate@1001 : Record 1301;
      Item@1000 : Record 27;
      TaxSetup@1020000 : Record 326;
      O365SalesManagement@1002 : Codeunit 2107;
    BEGIN
      IF NewMode AND (NOT Created) THEN
        IF ItemTemplate.NewItemFromTemplate(Item) THEN BEGIN
          O365SalesManagement.SetItemDefaultValues(Item);
          COPY(Item);
          Created := TRUE;
          CurrPage.UPDATE(TRUE);
        END;

      IF TaxSetup.GET THEN
        Taxable := "Tax Group Code" <> TaxSetup."Non-Taxable Tax Group Code";
    END;

    BEGIN
    END.
  }
}

