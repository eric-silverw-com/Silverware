OBJECT Report 5754 Create Pick
{
  OBJECT-PROPERTIES
  {
    Date=09/19/18;
    Time=12:00:00 PM;
    Version List=NAVW113.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Create Pick;
               ESM=Crear picking;
               FRC=Cr‚er un article … pr‚lever;
               ENC=Create Pick];
    ProcessingOnly=Yes;
  }
  DATASET
  {
    { 5444;    ;DataItem;                    ;
               DataItemTable=Table2000000026;
               DataItemTableView=SORTING(Number)
                                 WHERE(Number=CONST(1));
               OnPreDataItem=BEGIN
                               CLEAR(CreatePick);
                               CreatePick.SetValues(
                                 AssignedID,0,SortPick,1,MaxNoOfSourceDoc,MaxNoOfLines,PerZone,
                                 DoNotFillQtytoHandle,BreakbulkFilter,PerBin);
                             END;

               OnAfterGetRecord=BEGIN
                                  PickWkshLine.SETFILTER("Qty. to Handle (Base)",'>%1',0);
                                  PickWkshLineFilter.COPYFILTERS(PickWkshLine);

                                  IF PickWkshLine.FIND('-') THEN BEGIN
                                    IF PickWkshLine."Location Code" = '' THEN
                                      Location.INIT
                                    ELSE
                                      Location.GET(PickWkshLine."Location Code");
                                    REPEAT
                                      PickWkshLine.CheckBin(PickWkshLine."Location Code",PickWkshLine."To Bin Code",TRUE);
                                      TempNo := TempNo + 1;

                                      IF PerWhseDoc THEN BEGIN
                                        PickWkshLine.SETRANGE("Whse. Document Type",PickWkshLine."Whse. Document Type");
                                        PickWkshLine.SETRANGE("Whse. Document No.",PickWkshLine."Whse. Document No.");
                                      END;
                                      IF PerDestination THEN BEGIN
                                        PickWkshLine.SETRANGE("Destination Type",PickWkshLine."Destination Type");
                                        PickWkshLine.SETRANGE("Destination No.",PickWkshLine."Destination No.");
                                        SetPickFilters;
                                        PickWkshLineFilter.COPYFILTER("Destination Type",PickWkshLine."Destination Type");
                                        PickWkshLineFilter.COPYFILTER("Destination No.",PickWkshLine."Destination No.");
                                      END ELSE BEGIN
                                        PickWkshLineFilter.COPYFILTER("Destination Type",PickWkshLine."Destination Type");
                                        PickWkshLineFilter.COPYFILTER("Destination No.",PickWkshLine."Destination No.");
                                        SetPickFilters;
                                      END;
                                      PickWkshLineFilter.COPYFILTER("Whse. Document Type",PickWkshLine."Whse. Document Type");
                                      PickWkshLineFilter.COPYFILTER("Whse. Document No.",PickWkshLine."Whse. Document No.");
                                    UNTIL NOT PickWkshLine.FIND('-');
                                    CheckPickActivity;
                                  END ELSE
                                    ERROR(Text000);
                                END;
                                 }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
      OnOpenPage=BEGIN
                   IF LocationCode <> '' THEN BEGIN
                     Location.GET(LocationCode);
                     IF Location."Use ADCS" THEN
                       DoNotFillQtytoHandle := TRUE;
                   END;
                 END;

    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[ENU=Options;
                             ESM=Opciones;
                             FRC=Options;
                             ENC=Options] }

      { 13  ;2   ;Group     ;
                  CaptionML=[ENU=Create Pick;
                             ESM=Crear picking;
                             FRC=Cr‚er un article … pr‚lever;
                             ENC=Create Pick] }

      { 21  ;3   ;Field     ;
                  CaptionML=[ENU=Per Whse. Document;
                             ESM=Por documento almac‚n;
                             FRC=Par document magasin;
                             ENC=Per Whse. Document];
                  ToolTipML=[ENU=Specifies if you want to create separate pick documents for worksheet lines with the same warehouse source document.;
                             ESM=Especifica si desea crear documentos de picking independientes para las l¡neas de la hoja de c lculo con el mismo documento de origen de almac‚n.;
                             FRC=Sp‚cifie si vous souhaitez cr‚er des documents de pr‚lŠvement distincts pour les lignes feuille avec le mˆme document source entrep“t.;
                             ENC=Specifies if you want to create separate pick documents for worksheet lines with the same warehouse source document.];
                  ApplicationArea=#Warehouse;
                  SourceExpr=PerWhseDoc }

      { 3   ;3   ;Field     ;
                  CaptionML=[ENU=Per Cust./Vend./Loc.;
                             ESM=Por Cli./Prov./Alm.;
                             FRC=Par client/fourn./mag.;
                             ENC=Per Cust./Vend./Loc.];
                  ToolTipML=[ENU=Specifies if you want to create separate pick documents for each customer (sale orders), vendor (purchase return orders), and location (transfer orders).;
                             ESM=Especifica si desea crear documentos de picking independientes para cada cliente (pedidos de venta), el proveedor (devoluciones de compra), y la ubicaci¢n (pedidos de transferencia).;
                             FRC=Sp‚cifie si vous souhaitez cr‚er des documents de pr‚lŠvement distincts pour chaque client (documents de vente), fournisseur (retours achat) et emplacement (ordres de transfert).;
                             ENC=Specifies if you want to create separate pick documents for each customer (sale orders), vendor (purchase return orders), and location (transfer orders).];
                  ApplicationArea=#Warehouse;
                  SourceExpr=PerDestination }

      { 6   ;3   ;Field     ;
                  CaptionML=[ENU=Per Item;
                             ESM=Por prod.;
                             FRC=Par article;
                             ENC=Per Item];
                  ToolTipML=[ENU=Specifies if you want to create separate pick documents for each item in the pick worksheet.;
                             ESM=Especifica si desea crear los documentos de picking independientes para cada art¡culo en la hoja de c lculo de picking.;
                             FRC=Sp‚cifie si vous souhaitez cr‚er des documents de pr‚lŠvement distincts pour chaque article dans la feuille de pr‚lŠvement.;
                             ENC=Specifies if you want to create separate pick documents for each item in the pick worksheet.];
                  ApplicationArea=#Warehouse;
                  SourceExpr=PerItem }

      { 23  ;3   ;Field     ;
                  CaptionML=[ENU=Per From Zone;
                             ESM=Por Desde zona;
                             FRC=Par zone origine;
                             ENC=Per From Zone];
                  ToolTipML=[ENU=Specifies if you want to create separate pick documents for each zone that you will take the items from.;
                             ESM=Especifica si desea crear documentos de picking independientes para cada zona de la que traer  los art¡culos.;
                             FRC=Sp‚cifie si vous souhaitez cr‚er des documents de pr‚lŠvement distincts pour chaque zone dans laquelle vous allez pr‚lever des articles.;
                             ENC=Specifies if you want to create separate pick documents for each zone that you will take the items from.];
                  ApplicationArea=#Warehouse;
                  SourceExpr=PerZone }

      { 8   ;3   ;Field     ;
                  CaptionML=[ENU=Per Bin;
                             ESM=Por ubicaci¢n;
                             FRC=Par zone;
                             ENC=Per Bin];
                  ToolTipML=[ENU=Specifies if you want to create separate pick documents for each bin that you will take the items from.;
                             ESM=Especifica si desea crear documentos de picking independientes para cada ubicaci¢n de la que traer  los art¡culos.;
                             FRC=Sp‚cifie si vous souhaitez cr‚er des documents de pr‚lŠvement distincts pour chaque zone dans laquelle vous allez pr‚lever des articles.;
                             ENC=Specifies if you want to create separate pick documents for each bin that you will take the items from.];
                  ApplicationArea=#Warehouse;
                  SourceExpr=PerBin }

      { 10  ;3   ;Field     ;
                  CaptionML=[ENU=Per Due Date;
                             ESM=Por fecha vto.;
                             FRC=Par date d'‚ch‚ance;
                             ENC=Per Due Date];
                  ToolTipML=[ENU=Specifies if you want to create separate pick documents for source documents that have the same due date.;
                             ESM=Especifica si desea crear documentos de picking independientes para los documentos de origen con la misma fecha de vencimiento.;
                             FRC=Sp‚cifie si vous souhaitez cr‚er des documents de pr‚lŠvement distincts pour les documents sources ayant la mˆme date d'‚ch‚ance.;
                             ENC=Specifies if you want to create separate pick documents for source documents that have the same due date.];
                  ApplicationArea=#Warehouse;
                  SourceExpr=PerDate }

      { 1   ;2   ;Field     ;
                  CaptionML=[ENU=Max. No. of Pick Lines;
                             ESM=N§ m x. l¡ns. picking;
                             FRC=Nombre max. de lignes;
                             ENC=Max. No. of Pick Lines];
                  ToolTipML=[ENU=Specifies if you want to create pick documents that have no more than the specified number of lines in each document.;
                             ESM=Especifica si desea crear documentos de picking que no superen el n£mero de l¡neas especificado en cada documento.;
                             FRC=Sp‚cifie si vous souhaitez cr‚er des documents de pr‚lŠvement qui n'ont pas plus que le nombre de lignes sp‚cifi‚ dans chaque document.;
                             ENC=Specifies if you want to create pick documents that have no more than the specified number of lines in each document.];
                  ApplicationArea=#Warehouse;
                  BlankZero=Yes;
                  SourceExpr=MaxNoOfLines;
                  MultiLine=Yes }

      { 12  ;2   ;Field     ;
                  CaptionML=[ENU=Max. No. of Pick Source Docs.;
                             ESM=N§ m x. de doc. origen picking;
                             FRC=Nombre max. de doc. origine;
                             ENC=Max. No. of Pick Source Docs.];
                  ToolTipML=[ENU=Specifies if you want to create pick documents that each cover no more than the specified number of source documents.;
                             ESM=Especifica si desea crear documentos de picking que no abarquen m s que el n£mero de l¡neas especificado en los documentos de origen.;
                             FRC=Sp‚cifie si vous souhaitez cr‚er des documents de pr‚lŠvement ne couvrant chacun pas plus que le nombre de documents sources sp‚cifi‚.;
                             ENC=Specifies if you want to create pick documents that each cover no more than the specified number of source documents.];
                  ApplicationArea=#Warehouse;
                  BlankZero=Yes;
                  SourceExpr=MaxNoOfSourceDoc;
                  MultiLine=Yes }

      { 16  ;2   ;Field     ;
                  CaptionML=[ENU=Assigned User ID;
                             ESM=Id. usuario asignado;
                             FRC=Code utilisateur affect‚;
                             ENC=Assigned User ID];
                  ToolTipML=[ENU=Specifies the ID of the assigned user to perform the pick instruction.;
                             ESM=Especifica el id. del usuario asignado para realizar la instrucci¢n de picking.;
                             FRC=Sp‚cifie le code de l'utilisateur affect‚ pour ex‚cuter l'instruction pr‚lŠvement.;
                             ENC=Specifies the ID of the assigned user to perform the pick instruction.];
                  ApplicationArea=#Warehouse;
                  SourceExpr=AssignedID;
                  TableRelation="Warehouse Employee";
                  OnValidate=VAR
                               WhseEmployee@1000 : Record 7301;
                             BEGIN
                               IF AssignedID <> '' THEN
                                 WhseEmployee.GET(AssignedID,LocationCode);
                             END;

                  OnLookup=VAR
                             WhseEmployee@1000 : Record 7301;
                             LookupWhseEmployee@1001 : Page 7348;
                           BEGIN
                             WhseEmployee.SETCURRENTKEY("Location Code");
                             WhseEmployee.SETRANGE("Location Code",LocationCode);
                             LookupWhseEmployee.LOOKUPMODE(TRUE);
                             LookupWhseEmployee.SETTABLEVIEW(WhseEmployee);
                             IF LookupWhseEmployee.RUNMODAL = ACTION::LookupOK THEN BEGIN
                               LookupWhseEmployee.GETRECORD(WhseEmployee);
                               AssignedID := WhseEmployee."User ID";
                             END;
                           END;
                            }

      { 18  ;2   ;Field     ;
                  CaptionML=[ENU=Sorting Method for Pick Lines;
                             ESM=M‚t. clasif. para l¡ns. picking;
                             FRC=M‚thode de tri;
                             ENC=Sorting Method for Pick Lines];
                  ToolTipML=[ENU=Specifies that you want to select from the available options to sort lines in the created pick document.;
                             ESM=Especifica si desea seleccionar entre las opciones disponibles para ordenar las l¡neas en el documento de picking creado.;
                             FRC=Sp‚cifie que vous souhaitez s‚lectionner parmi les options disponibles pour trier les lignes du document de pr‚lŠvement cr‚‚.;
                             ENC=Specifies that you want to select from the available options to sort lines in the created pick document.];
                  OptionCaptionML=[ENU=" ,Item,Document,Shelf/Bin No.,Due Date,Destination,Bin Ranking,Action Type";
                                   ESM=" ,Prod.,Doc.,Estant./N§ ubic.,Fecha vto.,Destino,Ranking ubic.,Tipo acci¢n";
                                   FRC=" ,Article,Document,Nø de tablette/zone.,Date d'‚ch‚ance,Destination,Classement de zone,Type de tƒche";
                                   ENC=" ,Item,Document,Shelf/Bin No.,Due Date,Destination,Bin Ranking,Action Type"];
                  ApplicationArea=#Warehouse;
                  SourceExpr=SortPick;
                  MultiLine=Yes }

      { 26  ;2   ;Field     ;
                  CaptionML=[ENU=Set Breakbulk Filter;
                             ESM=Define filtro div. bulto;
                             FRC=R‚gler le filtre de rupture de charge;
                             ENC=Set Breakbulk Filter];
                  ToolTipML=[ENU=Specifies if you do not want to view the intermediate breakbulk pick lines, when a larger unit of measure is converted to a smaller unit of measure and completely picked.;
                             ESM=Especifica si desea crear si desea ocultar l¡neas de picking de divisi¢n de bulto intermedias cuando una unidad de medida grande se convierte en una m s peque¤a y con el picking realizado completamente.;
                             FRC=Sp‚cifie si vous ne souhaitez pas afficher les lignes pr‚lŠvement de d‚conditionnement interm‚diaires lorsqu'une unit‚ de mesure plus grande est convertie dans une unit‚ de mesure plus petite, et pr‚lev‚e dans son int‚gralit‚.;
                             ENC=Specifies if you do not want to view the intermediate breakbulk pick lines, when a larger unit of measure is converted to a smaller unit of measure and completely picked.];
                  ApplicationArea=#Warehouse;
                  SourceExpr=BreakbulkFilter }

      { 24  ;2   ;Field     ;
                  CaptionML=[ENU=Do Not Fill Qty. to Handle;
                             ESM=No rellene cdad. a manipular;
                             FRC=Ne pas remplir qt‚ … traiter;
                             ENC=Do Not Fill Qty. to Handle];
                  ToolTipML=[ENU=Specifies if you want to leave the Quantity to Handle field in the created pick lines empty.;
                             ESM=Especifica si desea dejar en blanco el campo Cantidad a manipular en las l¡neas de picking creadas.;
                             FRC=Sp‚cifie si vous souhaitez laisser le champ Quantit‚ … traiter vide dans les lignes pr‚lŠvement cr‚‚es.;
                             ENC=Specifies if you want to leave the Quantity to Handle field in the created pick lines empty.];
                  ApplicationArea=#Warehouse;
                  SourceExpr=DoNotFillQtytoHandle }

      { 14  ;2   ;Field     ;
                  CaptionML=[ENU=Print Pick;
                             ESM=Imprim. picking;
                             FRC=Imprimer pr‚lŠvement;
                             ENC=Print Pick];
                  ToolTipML=[ENU=Specifies that you want to print the pick documents when they are created.;
                             ESM=Especifica que desea imprimir los documentos de picking cuando se creen.;
                             FRC=Sp‚cifie que vous souhaitez imprimer les documents de pr‚lŠvement lors de leur cr‚ation.;
                             ENC=Specifies that you want to print the pick documents when they are created.];
                  ApplicationArea=#Warehouse;
                  SourceExpr=PrintPick }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=There is nothing to handle.;ESM=No hay nada que manipular.;FRC=Il n''y a rien … traiter.;ENC=There is nothing to handle.';
      Text001@1001 : TextConst 'ENU=Pick activity no. %1 has been created.;ESM=N§ activ. picking %1 ha sido creada.;FRC=Le pr‚lŠvement nø %1 a ‚t‚ cr‚‚.;ENC=Pick activity no. %1 has been created.';
      Text002@1002 : TextConst 'ENU=Pick activities no. %1 to %2 have been created.;ESM=N§ activ. picking %1 a %2 han sido creadas.;FRC=Les pr‚lŠvements du nø %1 au nø %2 ont ‚t‚ cr‚‚s.;ENC=Pick activities no. %1 to %2 have been created.';
      Location@1006 : Record 14;
      PickWkshLine@1004 : Record 7326;
      PickWkshLineFilter@1005 : Record 7326;
      Cust@1011 : Record 18;
      CreatePick@1038 : Codeunit 7312;
      LocationCode@1009 : Code[10];
      AssignedID@1017 : Code[50];
      FirstPickNo@1018 : Code[20];
      FirstSetPickNo@1010 : Code[20];
      LastPickNo@1019 : Code[20];
      MaxNoOfLines@1020 : Integer;
      MaxNoOfSourceDoc@1021 : Integer;
      TempNo@1024 : Integer;
      SortPick@1029 : ' ,Item,Document,Shelf No.,Due Date,Destination,Bin Ranking,Action Type';
      PerDestination@1030 : Boolean;
      PerItem@1031 : Boolean;
      PerZone@1037 : Boolean;
      PerBin@1032 : Boolean;
      PerWhseDoc@1027 : Boolean;
      PerDate@1033 : Boolean;
      PrintPick@1034 : Boolean;
      DoNotFillQtytoHandle@1003 : Boolean;
      Text003@1008 : TextConst 'ENU="You can create a Pick only for the available quantity in %1 %2 = %3,%4 = %5,%6 = %7,%8 = %9.";ESM="Puede crear un picking s¢lo por la cdad. disponible en %1 %2 = %3,%4 = %5,%6 = %7,%8 = %9.";FRC="Vous pouvez uniquement cr‚er un pr‚lŠvement pour %1 %2 = %3,%4 = %5,%6 = %7,%8 = %9.";ENC="You can create a Pick only for the available quantity in %1 %2 = %3,%4 = %5,%6 = %7,%8 = %9."';
      BreakbulkFilter@1007 : Boolean;
      NothingToHandleErr@1012 : TextConst 'ENU=There is nothing to handle. %1.;ESM=No hay nada que manipular. %1.;FRC=Il n''y a rien … traiter. %1.;ENC=There is nothing to handle. %1.';

    LOCAL PROCEDURE CreateTempLine@1();
    VAR
      PickWhseActivHeader@1001 : Record 5766;
      TempWhseItemTrkgLine@1002 : TEMPORARY Record 6550;
      ItemTrackingMgt@1003 : Codeunit 6500;
      PickQty@1000 : Decimal;
      PickQtyBase@1005 : Decimal;
      TempMaxNoOfSourceDoc@1004 : Integer;
      OldFirstSetPickNo@1006 : Code[20];
      TotalQtyPickedBase@1007 : Decimal;
    BEGIN
      PickWkshLine.LOCKTABLE;
      REPEAT
        IF Location."Bin Mandatory" AND
           (NOT Location."Always Create Pick Line")
        THEN
          IF PickWkshLine.CalcAvailableQtyBase < PickWkshLine."Qty. to Handle" THEN
            ERROR(
              Text003,
              PickWkshLine.TABLECAPTION,PickWkshLine.FIELDCAPTION("Worksheet Template Name"),
              PickWkshLine."Worksheet Template Name",PickWkshLine.FIELDCAPTION(Name),
              PickWkshLine.Name,PickWkshLine.FIELDCAPTION("Location Code"),
              PickWkshLine."Location Code",PickWkshLine.FIELDCAPTION("Line No."),
              PickWkshLine."Line No.");

        PickWkshLine.TESTFIELD("Qty. per Unit of Measure");
        CreatePick.SetWhseWkshLine(PickWkshLine,TempNo);
        CASE PickWkshLine."Whse. Document Type" OF
          PickWkshLine."Whse. Document Type"::Shipment:
            CreatePick.SetTempWhseItemTrkgLine(
              PickWkshLine."Whse. Document No.",DATABASE::"Warehouse Shipment Line",'',0,
              PickWkshLine."Whse. Document Line No.",PickWkshLine."Location Code");
          PickWkshLine."Whse. Document Type"::Assembly:
            CreatePick.SetTempWhseItemTrkgLine(
              PickWkshLine."Whse. Document No.",DATABASE::"Assembly Line",'',0,
              PickWkshLine."Whse. Document Line No.",PickWkshLine."Location Code");
          PickWkshLine."Whse. Document Type"::"Internal Pick":
            CreatePick.SetTempWhseItemTrkgLine(
              PickWkshLine."Whse. Document No.",DATABASE::"Whse. Internal Pick Line",'',0,
              PickWkshLine."Whse. Document Line No.",PickWkshLine."Location Code");
          PickWkshLine."Whse. Document Type"::Production:
            CreatePick.SetTempWhseItemTrkgLine(
              PickWkshLine."Source No.",PickWkshLine."Source Type",'',PickWkshLine."Source Line No.",
              PickWkshLine."Source Subline No.",PickWkshLine."Location Code");
          ELSE // Movement Worksheet Line
            CreatePick.SetTempWhseItemTrkgLine(
              PickWkshLine.Name,DATABASE::"Prod. Order Component",PickWkshLine."Worksheet Template Name",
              0,PickWkshLine."Line No.",PickWkshLine."Location Code");
        END;

        PickQty := PickWkshLine."Qty. to Handle";
        PickQtyBase := PickWkshLine."Qty. to Handle (Base)";
        IF (PickQty > 0) AND
           (PickWkshLine."Destination Type" = PickWkshLine."Destination Type"::Customer)
        THEN BEGIN
          PickWkshLine.TESTFIELD("Destination No.");
          Cust.GET(PickWkshLine."Destination No.");
          Cust.CheckBlockedCustOnDocs(Cust,PickWkshLine."Source Document",FALSE,FALSE);
        END;

        CreatePick.SetCalledFromWksh(TRUE);

        WITH PickWkshLine DO
          CreatePick.CreateTempLine("Location Code","Item No.","Variant Code",
            "Unit of Measure Code",'',"To Bin Code","Qty. per Unit of Measure",PickQty,PickQtyBase);

        TotalQtyPickedBase := CreatePick.GetActualQtyPickedBase;

        // Update/delete lines
        PickWkshLine."Qty. to Handle (Base)" := PickWkshLine.CalcBaseQty(PickWkshLine."Qty. to Handle");
        IF PickWkshLine."Qty. (Base)" =
           PickWkshLine."Qty. Handled (Base)" + TotalQtyPickedBase
        THEN
          PickWkshLine.DELETE(TRUE)
        ELSE BEGIN
          PickWkshLine."Qty. Handled" := PickWkshLine."Qty. Handled" + PickWkshLine.CalcQty(TotalQtyPickedBase);
          PickWkshLine."Qty. Handled (Base)" := PickWkshLine.CalcBaseQty(PickWkshLine."Qty. Handled");
          PickWkshLine."Qty. Outstanding" := PickWkshLine.Quantity - PickWkshLine."Qty. Handled";
          PickWkshLine."Qty. Outstanding (Base)" := PickWkshLine.CalcBaseQty(PickWkshLine."Qty. Outstanding");
          PickWkshLine."Qty. to Handle" := 0;
          PickWkshLine."Qty. to Handle (Base)" := 0;
          PickWkshLine.MODIFY;
        END;
      UNTIL PickWkshLine.NEXT = 0;

      OldFirstSetPickNo := FirstSetPickNo;
      CreatePick.CreateWhseDocument(FirstSetPickNo,LastPickNo,FALSE);
      IF FirstSetPickNo = OldFirstSetPickNo THEN
        EXIT;

      IF FirstPickNo = '' THEN
        FirstPickNo := FirstSetPickNo;
      CreatePick.ReturnTempItemTrkgLines(TempWhseItemTrkgLine);
      ItemTrackingMgt.UpdateWhseItemTrkgLines(TempWhseItemTrkgLine);
      COMMIT;

      TempMaxNoOfSourceDoc := MaxNoOfSourceDoc;
      PickWhseActivHeader.SETRANGE(Type,PickWhseActivHeader.Type::Pick);
      PickWhseActivHeader.SETRANGE("No.",FirstSetPickNo,LastPickNo);
      PickWhseActivHeader.FIND('-');
      REPEAT
        IF SortPick > 0 THEN
          PickWhseActivHeader.SortWhseDoc;
        COMMIT;
        IF PrintPick THEN BEGIN
          REPORT.RUN(REPORT::"Picking List",FALSE,FALSE,PickWhseActivHeader);
          TempMaxNoOfSourceDoc -= 1;
        END;
      UNTIL ((PickWhseActivHeader.NEXT = 0) OR (TempMaxNoOfSourceDoc = 0));
    END;

    PROCEDURE SetWkshPickLine@4(VAR PickWkshLine2@1000 : Record 7326);
    BEGIN
      PickWkshLine.COPYFILTERS(PickWkshLine2);
      LocationCode := PickWkshLine2."Location Code";
    END;

    PROCEDURE GetResultMessage@2() : Boolean;
    BEGIN
      IF FirstPickNo <> '' THEN
        IF FirstPickNo = LastPickNo THEN
          MESSAGE(Text001,FirstPickNo)
        ELSE
          MESSAGE(Text002,FirstPickNo,LastPickNo);
      EXIT(FirstPickNo <> '');
    END;

    PROCEDURE InitializeReport@3(AssignedID2@1013 : Code[50];MaxNoOfLines2@1010 : Integer;MaxNoOfSourceDoc2@1009 : Integer;SortPick2@1007 : ' ,Item,Document,Shelf/Bin No.,Due Date,Ship-To,Bin Ranking,Action Type';PerDestination2@1006 : Boolean;PerItem2@1005 : Boolean;PerZone2@1004 : Boolean;PerBin2@1003 : Boolean;PerWhseDoc2@1002 : Boolean;PerDate2@1001 : Boolean;PrintPick2@1000 : Boolean;DoNotFillQtytoHandle2@1008 : Boolean;BreakbulkFilter2@1011 : Boolean);
    BEGIN
      AssignedID := AssignedID2;
      MaxNoOfLines := MaxNoOfLines2;
      MaxNoOfSourceDoc := MaxNoOfSourceDoc2;
      SortPick := SortPick2;
      PerDestination := PerDestination2;
      PerItem := PerItem2;
      PerZone := PerZone2;
      PerBin := PerBin2;
      PerWhseDoc := PerWhseDoc2;
      PerDate := PerDate2;
      PrintPick := PrintPick2;
      DoNotFillQtytoHandle := DoNotFillQtytoHandle2;
      BreakbulkFilter := BreakbulkFilter2;
    END;

    LOCAL PROCEDURE CheckPickActivity@5();
    BEGIN
      IF FirstPickNo = '' THEN
        ERROR(NothingToHandleErr,CreatePick.GetExpiredItemMessage);
    END;

    LOCAL PROCEDURE SetPickFilters@6();
    BEGIN
      IF PerItem THEN BEGIN
        PickWkshLine.SETRANGE("Item No.",PickWkshLine."Item No.");
        IF PerBin THEN
          SetPerBinFilters
        ELSE BEGIN
          IF NOT Location."Bin Mandatory" THEN
            PickWkshLineFilter.COPYFILTER("Shelf No.",PickWkshLine."Shelf No.");
          SetPerDateFilters;
        END;
        PickWkshLineFilter.COPYFILTER("Item No.",PickWkshLine."Item No.");
      END ELSE BEGIN
        PickWkshLineFilter.COPYFILTER("Item No.",PickWkshLine."Item No.");
        IF PerBin THEN
          SetPerBinFilters
        ELSE BEGIN
          IF NOT Location."Bin Mandatory" THEN
            PickWkshLineFilter.COPYFILTER("Shelf No.",PickWkshLine."Shelf No.");
          SetPerDateFilters;
        END;
      END;
    END;

    LOCAL PROCEDURE SetPerBinFilters@7();
    BEGIN
      IF NOT Location."Bin Mandatory" THEN
        PickWkshLine.SETRANGE("Shelf No.",PickWkshLine."Shelf No.");
      IF PerDate THEN BEGIN
        PickWkshLine.SETRANGE("Due Date",PickWkshLine."Due Date");
        CreateTempLine;
        PickWkshLineFilter.COPYFILTER("Due Date",PickWkshLine."Due Date");
      END ELSE BEGIN
        PickWkshLineFilter.COPYFILTER("Due Date",PickWkshLine."Due Date");
        CreateTempLine;
      END;
      IF NOT Location."Bin Mandatory" THEN
        PickWkshLineFilter.COPYFILTER("Shelf No.",PickWkshLine."Shelf No.");
    END;

    LOCAL PROCEDURE SetPerDateFilters@10();
    BEGIN
      IF PerDate THEN BEGIN
        PickWkshLine.SETRANGE("Due Date",PickWkshLine."Due Date");
        CreateTempLine;
        PickWkshLineFilter.COPYFILTER("Due Date",PickWkshLine."Due Date");
      END ELSE BEGIN
        PickWkshLineFilter.COPYFILTER("Due Date",PickWkshLine."Due Date");
        CreateTempLine;
      END;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

