OBJECT Report 99003803 Copy Production Forecast
{
  OBJECT-PROPERTIES
  {
    Date=11/27/18;
    Time=11:13:46 PM;
    Version List=;
  }
  PROPERTIES
  {
    CaptionML=ENU=Copy Production Forecast;
    ProcessingOnly=Yes;
  }
  DATASET
  {
    { 2936;    ;DataItem;                    ;
               DataItemTable=Table99000852;
               DataItemTableView=SORTING(Entry No.);
               OnPreDataItem=BEGIN
                               IF NOT CONFIRM(Text000,FALSE) THEN
                                 EXIT;

                               LOCKTABLE;

                               IF ProdForecastEntry2.FINDLAST THEN
                                 LastEntryNo := ProdForecastEntry2."Entry No.";

                               NextEntryNo := LastEntryNo + 1;

                               ProdForecastName.SETRANGE(Name,ToProdForecastEntry."Production Forecast Name");
                               IF NOT ProdForecastName.FINDFIRST THEN BEGIN
                                 ProdForecastName.Name := ToProdForecastEntry."Production Forecast Name";
                                 ProdForecastName.INSERT;
                               END;
                             END;

               OnAfterGetRecord=BEGIN
                                  IF "Entry No." > LastEntryNo THEN
                                    CurrReport.BREAK;
                                  ProdForecastEntry2 := "Production Forecast Entry";

                                  IF ToProdForecastEntry."Production Forecast Name" <> '' THEN
                                    ProdForecastEntry2."Production Forecast Name" := ToProdForecastEntry."Production Forecast Name";
                                  IF ToProdForecastEntry."Item No." <> '' THEN
                                    ProdForecastEntry2."Item No." := ToProdForecastEntry."Item No.";
                                  IF ToProdForecastEntry."Location Code" <> '' THEN
                                    ProdForecastEntry2."Location Code" := ToProdForecastEntry."Location Code";
                                  ProdForecastEntry2."Component Forecast" := ToProdForecastEntry."Component Forecast";
                                  IF FORMAT(ChangeDateExpression) <> '' THEN
                                    ProdForecastEntry2."Forecast Date" := CALCDATE(ChangeDateExpression,"Forecast Date");

                                  ProdForecastEntry2."Entry No." := NextEntryNo;
                                  ProdForecastEntry2.INSERT;
                                  NextEntryNo := NextEntryNo + 1;
                                END;

               ReqFilterFields=Production Forecast Name,Item No. }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=ENU=Options }

      { 21  ;2   ;Group     ;
                  CaptionML=ENU=Copy to }

      { 1   ;3   ;Field     ;
                  Name=ProductionForecastName;
                  CaptionML=ENU=Production Forecast Name;
                  ToolTipML=ENU=Specifies the name of the production forecast to which you want to copy the entries. Before you can select a production forecast name, it must be set up in the Production Forecast Names window, which you open by clicking the field.;
                  ApplicationArea=#Manufacturing;
                  SourceExpr=ToProdForecastEntry."Production Forecast Name";
                  TableRelation="Production Forecast Name" }

      { 2   ;3   ;Field     ;
                  Name=ItemNo;
                  CaptionML=ENU=Item No.;
                  ToolTipML=ENU=Specifies the number of the item to which you want to copy the entries. To see the existing item numbers, click the field.;
                  ApplicationArea=#Manufacturing;
                  SourceExpr=ToProdForecastEntry."Item No.";
                  TableRelation=Item }

      { 4   ;3   ;Field     ;
                  Name=LocationCode;
                  CaptionML=ENU=Location Code;
                  ToolTipML=ENU=Specifies a location for the production forecast to which you are copying entries.;
                  ApplicationArea=#Location;
                  SourceExpr=ToProdForecastEntry."Location Code";
                  TableRelation=Location }

      { 7   ;2   ;Field     ;
                  Name=ComponentForecast;
                  CaptionML=ENU=Component Forecast;
                  ToolTipML=ENU=Specifies whether the entry is for a component item. Leave the field blank if the entry is for a sales item.;
                  ApplicationArea=#Manufacturing;
                  SourceExpr=ToProdForecastEntry."Component Forecast" }

      { 6   ;2   ;Field     ;
                  Name=DateChangeFormula;
                  CaptionML=ENU=Date Change Formula;
                  ToolTipML=[ENU="Specifies how the dates on the entries that are copied will be changed. Use a date formula; for example, to copy last week's forecast to this week, use the formula 1W (one week)."];
                  ApplicationArea=#Manufacturing;
                  SourceExpr=ChangeDateExpression }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Do you want to copy the production forecast?';
      ToProdForecastEntry@1001 : Record 99000852;
      ProdForecastEntry2@1002 : Record 99000852;
      ProdForecastName@1003 : Record 99000851;
      ChangeDateExpression@1004 : DateFormula;
      LastEntryNo@1005 : Integer;
      NextEntryNo@1006 : Integer;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

