OBJECT Page 6061 Contract Trend Lines
{
  OBJECT-PROPERTIES
  {
    Date=11/27/18;
    Time=11:11:25 PM;
    Version List=;
  }
  PROPERTIES
  {
    CaptionML=ENU=Lines;
    LinksAllowed=No;
    SourceTable=Table2000000007;
    PageType=ListPart;
    OnFindRecord=BEGIN
                   EXIT(PeriodFormMgt.FindDate(Which,Rec,PeriodType));
                 END;

    OnNextRecord=BEGIN
                   EXIT(PeriodFormMgt.NextDate(Steps,Rec,PeriodType));
                 END;

    OnAfterGetRecord=BEGIN
                       SetDateFilter;
                       ServContract.CALCFIELDS(
                         "Contract Invoice Amount",
                         "Contract Discount Amount",
                         "Contract Cost Amount",
                         "Contract Prepaid Amount");
                       ProfitAmount := ServContract."Contract Invoice Amount" - ServContract."Contract Cost Amount";
                       IF ServContract."Contract Invoice Amount" <> 0 THEN
                         ProfitPct := ROUND((ProfitAmount / ServContract."Contract Invoice Amount") * 100,0.01)
                       ELSE
                         ProfitPct := 0;
                     END;

  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 1   ;1   ;Group     ;
                Editable=FALSE;
                GroupType=Repeater }

    { 2   ;2   ;Field     ;
                CaptionML=ENU=Period Start;
                ToolTipML=ENU=Specifies the starting date of the period that you want to view.;
                ApplicationArea=#Service;
                SourceExpr="Period Start" }

    { 8   ;2   ;Field     ;
                CaptionML=ENU=Period Name;
                ToolTipML=ENU=Specifies the name of the period shown in the line.;
                ApplicationArea=#Service;
                SourceExpr="Period Name" }

    { 18  ;2   ;Field     ;
                CaptionML=ENU=Prepaid Income;
                ToolTipML=ENU=Specifies the total income (in $) that has been posted to the prepaid account for the service contract in the periods specified in the Period Start field.;
                ApplicationArea=#Prepayments;
                SourceExpr=ServContract."Contract Prepaid Amount";
                OnDrillDown=BEGIN
                              SetDateFilter;
                              ServLedgEntry.RESET;
                              ServLedgEntry.SETCURRENTKEY(Type,"No.","Entry Type","Moved from Prepaid Acc.","Posting Date");
                              ServLedgEntry.SETRANGE("Service Contract No.",ServContract."Contract No.");
                              ServLedgEntry.SETRANGE("Entry Type",ServLedgEntry."Entry Type"::Sale);
                              ServLedgEntry.SETRANGE("Moved from Prepaid Acc.",FALSE);
                              ServLedgEntry.SETRANGE(Type,ServLedgEntry.Type::"Service Contract");
                              ServLedgEntry.SETRANGE(Open,FALSE);
                              ServLedgEntry.SETRANGE(Prepaid,TRUE);
                              ServLedgEntry.SETFILTER("Posting Date",ServContract.GETFILTER("Date Filter"));
                              PAGE.RUNMODAL(0,ServLedgEntry);
                            END;
                             }

    { 4   ;2   ;Field     ;
                DrillDown=Yes;
                CaptionML=ENU=Posted Income;
                ToolTipML=ENU=Specifies the total income (in $) that has been posted to the general ledger for the service contract in the periods specified in the Period Start field.;
                ApplicationArea=#Service;
                SourceExpr=ServContract."Contract Invoice Amount";
                OnDrillDown=BEGIN
                              SetDateFilter;
                              ServLedgEntry.RESET;
                              ServLedgEntry.SETCURRENTKEY(Type,"No.","Entry Type","Moved from Prepaid Acc.","Posting Date");
                              ServLedgEntry.SETRANGE("Service Contract No.",ServContract."Contract No.");
                              ServLedgEntry.SETRANGE("Entry Type",ServLedgEntry."Entry Type"::Sale);
                              ServLedgEntry.SETRANGE("Moved from Prepaid Acc.",TRUE);
                              ServLedgEntry.SETRANGE(Open,FALSE);
                              ServLedgEntry.SETFILTER("Posting Date",ServContract.GETFILTER("Date Filter"));
                              PAGE.RUNMODAL(0,ServLedgEntry);
                            END;
                             }

    { 6   ;2   ;Field     ;
                CaptionML=ENU=Posted Cost;
                ToolTipML=ENU=Specifies the cost of the service contract based on its service usage in the periods specified in the Period Start field.;
                ApplicationArea=#Service;
                SourceExpr=ServContract."Contract Cost Amount";
                OnDrillDown=BEGIN
                              SetDateFilter;
                              CLEAR(ServLedgEntry);
                              ServLedgEntry.SETCURRENTKEY(Type,"No.","Entry Type","Moved from Prepaid Acc.","Posting Date");
                              ServLedgEntry.SETRANGE("Entry Type",ServLedgEntry."Entry Type"::Usage);
                              ServLedgEntry.SETRANGE("Service Contract No.",ServContract."Contract No.");
                              ServLedgEntry.SETRANGE("Moved from Prepaid Acc.",TRUE);
                              ServLedgEntry.SETRANGE(Open,FALSE);
                              ServLedgEntry.SETFILTER("Posting Date",ServContract.GETFILTER("Date Filter"));
                              PAGE.RUNMODAL(0,ServLedgEntry);
                            END;
                             }

    { 12  ;2   ;Field     ;
                CaptionML=ENU=Discount Amount;
                ToolTipML=ENU=Specifies the amount of discount being applied for the line.;
                ApplicationArea=#Service;
                SourceExpr=ServContract."Contract Discount Amount";
                OnDrillDown=BEGIN
                              SetDateFilter;
                              CLEAR(ServLedgEntry);
                              ServLedgEntry.SETCURRENTKEY("Service Contract No.");
                              ServLedgEntry.SETRANGE("Service Contract No.",ServContract."Contract No.");
                              ServLedgEntry.SETRANGE("Entry Type",ServLedgEntry."Entry Type"::Usage);
                              ServLedgEntry.SETRANGE("Moved from Prepaid Acc.",TRUE);
                              ServLedgEntry.SETRANGE(Open,FALSE);
                              ServLedgEntry.SETFILTER("Posting Date",ServContract.GETFILTER("Date Filter"));
                              PAGE.RUNMODAL(0,ServLedgEntry);
                            END;
                             }

    { 14  ;2   ;Field     ;
                CaptionML=ENU=Profit;
                ToolTipML=ENU=Specifies the profit (posted incom0e minus posted cost in $) for the service contract in the periods specified in the Period Start field.;
                ApplicationArea=#Service;
                SourceExpr=ProfitAmount;
                AutoFormatType=1 }

    { 16  ;2   ;Field     ;
                CaptionML=ENU=Profit %;
                ToolTipML=ENU="Specifies the profit percentage for the service contract in the periods specified in the Period Start field. ";
                ApplicationArea=#Service;
                SourceExpr=ProfitPct }

  }
  CODE
  {
    VAR
      ServContract@1000 : Record 5965;
      ServLedgEntry@1001 : Record 5907;
      PeriodFormMgt@1002 : Codeunit 359;
      PeriodType@1003 : 'Day,Week,Month,Quarter,Year,Accounting Period';
      AmountType@1004 : 'Net Change,Balance at Date';
      ProfitAmount@1005 : Decimal;
      ProfitPct@1006 : Decimal;

    [External]
    PROCEDURE Set@1(VAR NewServContract@1000 : Record 5965;NewPeriodType@1001 : Integer;NewAmountType@1002 : 'Net Change,Balance at Date');
    BEGIN
      ServContract.COPY(NewServContract);
      PeriodType := NewPeriodType;
      AmountType := NewAmountType;
      CurrPage.UPDATE(FALSE);
    END;

    LOCAL PROCEDURE SetDateFilter@4();
    BEGIN
      IF AmountType = AmountType::"Net Change" THEN
        ServContract.SETRANGE("Date Filter","Period Start","Period End")
      ELSE
        ServContract.SETRANGE("Date Filter",0D,"Period End");
    END;

    BEGIN
    END.
  }
}

