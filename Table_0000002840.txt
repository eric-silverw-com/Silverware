OBJECT Table 2840 Native - Gen. Settings Buffer
{
  OBJECT-PROPERTIES
  {
    Date=09/19/18;
    Time=12:00:00 PM;
    Version List=NAVW113.00;
  }
  PROPERTIES
  {
    ReplicateData=No;
    CaptionML=[ENU=Native - Gen. Settings Buffer;
               ESM=Nativo - b£fer de configuraci¢n general;
               FRC=Native - Tampon paramŠtres g‚n‚raux;
               ENC=Native - Gen. Settings Buffer];
  }
  FIELDS
  {
    { 1   ;   ;Primary Key         ;Code10        ;DataClassification=SystemMetadata;
                                                   CaptionML=[ENU=Primary Key;
                                                              ESM=Clave principal;
                                                              FRC=Cl‚ primaire;
                                                              ENC=Primary Key] }
    { 2   ;   ;Currency Symbol     ;Text10        ;DataClassification=SystemMetadata;
                                                   CaptionML=[ENU=Currency Symbol;
                                                              ESM=S¡mbolo de divisa;
                                                              FRC=Symbole devise;
                                                              ENC=Currency Symbol] }
    { 3   ;   ;Paypal Email Address;Text250       ;DataClassification=SystemMetadata;
                                                   CaptionML=[ENU=Paypal Email Address;
                                                              ESM=Direcci¢n de correo electr¢nico de Paypal;
                                                              FRC=Adresse ‚lectronique PayPal;
                                                              ENC=Paypal Email Address] }
    { 4   ;   ;Country/Region Code ;Code10        ;DataClassification=SystemMetadata;
                                                   CaptionML=[ENU=Country/Region Code;
                                                              ESM=C¢digo pa¡s/regi¢n;
                                                              FRC=Code pays/r‚gion;
                                                              ENC=Country/Region Code] }
    { 5   ;   ;Language Locale ID  ;Integer       ;DataClassification=SystemMetadata;
                                                   CaptionML=[ENU=Language Locale ID;
                                                              ESM=Id. configuraci¢n regional de idioma;
                                                              FRC=Code paramŠtres r‚gionaux de langue;
                                                              ENC=Language Locale ID] }
    { 6   ;   ;Language Code       ;Text50        ;DataClassification=SystemMetadata;
                                                   CaptionML=[ENU=Language Code;
                                                              ESM=C¢digo idioma;
                                                              FRC=Code langue;
                                                              ENC=Language Code] }
    { 7   ;   ;Language Display Name;Text80       ;DataClassification=SystemMetadata;
                                                   CaptionML=[ENU=Language Display Name;
                                                              ESM=Nombre para mostrar de idioma;
                                                              FRC=Nom langue d'affichage;
                                                              ENC=Language Display Name] }
    { 50  ;   ;Default Tax ID      ;GUID          ;DataClassification=SystemMetadata;
                                                   CaptionML=[ENU=Default Tax ID;
                                                              ESM=ID de impuesto predeterminado;
                                                              FRC=Code taxe par d‚faut;
                                                              ENC=Default Tax ID];
                                                   Editable=No }
    { 51  ;   ;Defauilt Tax Description;Text50    ;DataClassification=SystemMetadata;
                                                   CaptionML=[ENU=Defauilt Tax Description;
                                                              ESM=Descripci¢n de impuesto predeterminado;
                                                              FRC=Description de la taxe par d‚faut;
                                                              ENC=Defauilt Tax Description];
                                                   Editable=No }
    { 52  ;   ;Default Payment Terms ID;GUID      ;DataClassification=SystemMetadata;
                                                   CaptionML=[ENU=Default Payment Terms ID;
                                                              ESM=Id. de condiciones pago predeterminadas;
                                                              FRC=Code modalit‚s de paiement par d‚faut;
                                                              ENC=Default Payment Terms ID];
                                                   Editable=No }
    { 53  ;   ;Def. Pmt. Term Description;Text50  ;DataClassification=SystemMetadata;
                                                   CaptionML=[ENU=Def. Pmt. Term Description;
                                                              ESM=Descripci¢n de condiciones de pago predeter.;
                                                              FRC=Description des conditions de paiement par d‚faut;
                                                              ENC=Def. Pmt. Term Description];
                                                   Editable=No }
    { 54  ;   ;Default Payment Method ID;GUID     ;DataClassification=SystemMetadata;
                                                   CaptionML=[ENU=Default Payment Method ID;
                                                              ESM=Id. de m‚todo de pago predeterminado;
                                                              FRC=Code mode de rŠglement par d‚faut;
                                                              ENC=Default Payment Method ID];
                                                   Editable=No }
    { 55  ;   ;Def. Pmt. Method Description;Text50;DataClassification=SystemMetadata;
                                                   CaptionML=[ENU=Def. Pmt. Method Description;
                                                              ESM=Descripci¢n de m‚todo de pago predeter.;
                                                              FRC=Description du mode de rŠglement par d‚faut;
                                                              ENC=Def. Pmt. Method Description];
                                                   Editable=No }
    { 56  ;   ;Amount Rounding Precision;Decimal  ;DataClassification=SystemMetadata;
                                                   CaptionML=[ENU=Amount Rounding Precision;
                                                              ESM=Prec. redondeo importe;
                                                              FRC=Pr‚cision arrondissement montant;
                                                              ENC=Amount Rounding Precision];
                                                   Editable=No }
    { 57  ;   ;Unit-Amount Rounding Precision;Decimal;
                                                   DataClassification=SystemMetadata;
                                                   CaptionML=[ENU=Unit-Amount Rounding Precision;
                                                              ESM=Precisi¢n de redondeo precio unitario;
                                                              FRC=Pr‚cis. arrondissement montant unit‚;
                                                              ENC=Unit-Amount Rounding Precision];
                                                   Editable=No }
    { 58  ;   ;VAT/Tax Rounding Precision;Decimal ;DataClassification=SystemMetadata;
                                                   CaptionML=[ENU=VAT/Tax Rounding Precision;
                                                              ESM=Precisi¢n redondeo IVA/impuesto;
                                                              FRC=Pr‚cision arrondissement TVA/Taxe;
                                                              ENC=VAT/Tax Rounding Precision];
                                                   Editable=No }
    { 59  ;   ;Quantity Rounding Precision;Decimal;DataClassification=SystemMetadata;
                                                   CaptionML=[ENU=Quantity Rounding Precision;
                                                              ESM=Precisi¢n de redondeo de cantidad;
                                                              FRC=Pr‚cision arrondissement quantit‚;
                                                              ENC=Quantity Rounding Precision];
                                                   Editable=No }
    { 60  ;   ;EnableSync          ;Boolean       ;DataClassification=SystemMetadata;
                                                   CaptionML=[ENU=EnableSync;
                                                              ESM=EnableSync;
                                                              FRC=EnableSync;
                                                              ENC=EnableSync] }
    { 61  ;   ;EnableSyncCoupons   ;Boolean       ;DataClassification=SystemMetadata;
                                                   CaptionML=[ENU=EnableSyncCoupons;
                                                              ESM=EnableSyncCoupons;
                                                              FRC=EnableSyncCoupons;
                                                              ENC=EnableSyncCoupons] }
  }
  KEYS
  {
    {    ;Primary Key                             ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      RecordMustBeTemporaryErr@1000 : TextConst 'ENU=General Settings Buffer must be used as a temporary record.;ESM=El b£fer de configuraci¢n general debe usarse como registro temporal.;FRC=Le tampon des paramŠtres g‚n‚raux doit ˆtre utilis‚ comme enregistrement temporaire.;ENC=General Settings Buffer must be used as a temporary record.';
      CannotEnableCouponsSyncErr@1001 : TextConst 'ENU=Cannot enable coupons synchronization while Microsoft Graph synchronization is turned off.;ESM=No se puede habilitar la sincronizaci¢n de cupones si est  desactivada la sincronizaci¢n de MicrosoftÿGraph.;FRC=Impossible d''activer la synchronisation des coupons lorsque la synchronisation de Microsoft Graph est d‚sactiv‚e.;ENC=Cannot enable coupons synchronization while Microsoft Graph synchronization is turned off.';
      CannotSetWebhookSubscriptionUserErr@1007 : TextConst 'ENU=Cannot set the webhook subscription user.;ESM=No se puede establecer el usuario de suscripci¢n de webhook.;FRC=Impossible de d‚finir l''utilisateur d''un abonnement webhook.;ENC=Cannot set the webhook subscription user.';
      CannotGetCompanyInformationErr@1003 : TextConst 'ENU=Cannot get the company information.;ESM=No se puede obtener la informaci¢n de la empresa.;FRC=Impossible d''obtenir les informations sur la compagnie.;ENC=Cannot get the company information.';
      SyncOnlyAllowedInSaasErr@1004 : TextConst 'ENU=Microsoft Graph synchronization is only allowed in SaaS.;ESM=La sincronizaci¢n de Microsoft Graph solo se permite en SaaS.;FRC=La synchronisation de Microsoft Graph est uniquement autoris‚e dans SaaS.;ENC=Microsoft Graph synchronization is only allowed in SaaS.';
      SyncNotAllowedInDemoCompanyErr@1005 : TextConst 'ENU=Microsoft Graph synchronization is not allowed in a demo company.;ESM=La sincronizaci¢n de Microsoft Graph no se permite en una empresa de demostraci¢n.;FRC=La synchronisation de Microsoft Graph n''est pas autoris‚e dans une compagnie de d‚monstration.;ENC=Microsoft Graph synchronization is not allowed in a demo company.';
      SyncNotAllowedErr@1006 : TextConst 'ENU=Microsoft Graph synchronization is not allowed.;ESM=No se permite la sincronizaci¢n de Microsoft Graph.;FRC=La synchronisation de Microsoft Graph n''est pas autoris‚e.;ENC=Microsoft Graph synchronization is not allowed.';

    PROCEDURE LoadRecord@2();
    VAR
      CompanyInformation@1001 : Record 79;
      O365SalesInitialSetup@1003 : Record 2110;
      GeneralLedgerSetup@1004 : Record 98;
      TempNativeAPITaxSetup@1007 : TEMPORARY Record 2850;
      MarketingSetup@1008 : Record 5079;
      PaypalAccountProxy@1002 : Codeunit 1060;
    BEGIN
      IF NOT ISTEMPORARY THEN
        ERROR(RecordMustBeTemporaryErr);

      DELETEALL;
      CLEAR(Rec);

      CompanyInformation.GET;
      "Country/Region Code" := CompanyInformation."Country/Region Code";

      GeneralLedgerSetup.GET;
      "Currency Symbol" := GeneralLedgerSetup.GetCurrencySymbol;
      "Amount Rounding Precision" := GetNumberOfDecimals(GeneralLedgerSetup."Amount Rounding Precision");
      "Unit-Amount Rounding Precision" := GetNumberOfDecimals(GeneralLedgerSetup."Unit-Amount Rounding Precision");
      "Quantity Rounding Precision" := 5;
      // We hardcode these to 2/3 as they are like that in business center.
      IF GeneralLedgerSetup.UseVat THEN
        "VAT/Tax Rounding Precision" := 2
      ELSE
        "VAT/Tax Rounding Precision" := 3;

      PaypalAccountProxy.GetPaypalAccount("Paypal Email Address");

      TempNativeAPITaxSetup.LoadSetupRecords;
      TempNativeAPITaxSetup.SETRANGE(Default,TRUE);
      IF TempNativeAPITaxSetup.FINDFIRST THEN BEGIN
        "Default Tax ID" := TempNativeAPITaxSetup.Id;
        "Defauilt Tax Description" := TempNativeAPITaxSetup.Description;
      END;

      GetLanguageInfo;
      GetPaymentInfo;

      IF MarketingSetup.GET THEN BEGIN
        EnableSync := MarketingSetup."Sync with Microsoft Graph";
        IF EnableSync THEN BEGIN
          O365SalesInitialSetup.GET;
          EnableSyncCoupons := O365SalesInitialSetup."Coupons Integration Enabled";
        END;
      END;

      INSERT(TRUE);
    END;

    PROCEDURE SaveRecord@1();
    VAR
      PaypalAccountProxy@1000 : Codeunit 1060;
    BEGIN
      IF xRec."Currency Symbol" <> "Currency Symbol" THEN
        UpdateCurrencySymbol;

      IF xRec."Paypal Email Address" <> "Paypal Email Address" THEN
        PaypalAccountProxy.SetPaypalAccount("Paypal Email Address",TRUE);

      IF xRec."Country/Region Code" <> "Country/Region Code" THEN
        UpdateCountryRegionCode;

      IF xRec."Language Locale ID" <> "Language Locale ID" THEN
        UpdateLanguageId;

      IF xRec.EnableSync <> EnableSync THEN
        UpdateSync;

      IF xRec.EnableSyncCoupons <> EnableSyncCoupons THEN
        UpdateCouponsSync;
    END;

    LOCAL PROCEDURE UpdateCurrencySymbol@3();
    VAR
      GeneralLedgerSetup@1000 : Record 98;
    BEGIN
      GeneralLedgerSetup.GET;
      IF "Currency Symbol" <> GeneralLedgerSetup."Local Currency Symbol" THEN BEGIN
        GeneralLedgerSetup.VALIDATE("Local Currency Symbol","Currency Symbol");
        GeneralLedgerSetup.MODIFY(TRUE);
      END;
    END;

    LOCAL PROCEDURE UpdateCountryRegionCode@6();
    VAR
      CompanyInformation@1000 : Record 79;
    BEGIN
      CompanyInformation.GET;
      IF CompanyInformation."Country/Region Code" <> "Country/Region Code" THEN BEGIN
        CompanyInformation.VALIDATE("Country/Region Code","Country/Region Code");
        CompanyInformation.MODIFY(TRUE);
      END;
    END;

    LOCAL PROCEDURE UpdateLanguageId@9();
    VAR
      UserPersonalization@1000 : Record 2000000073;
    BEGIN
      UserPersonalization.GET(USERSECURITYID);
      IF "Language Locale ID" <> UserPersonalization."Language ID" THEN BEGIN
        UserPersonalization.VALIDATE("Language ID","Language Locale ID");
        UserPersonalization.MODIFY(TRUE);
        GetLanguageInfo;
        GetPaymentInfo;
      END;
    END;

    LOCAL PROCEDURE UpdateSync@7();
    VAR
      MarketingSetup@1001 : Record 5079;
      O365SalesBackgroundSetup@1000 : Codeunit 2111;
    BEGIN
      CheckSyncAllowed;

      O365SalesBackgroundSetup.InitializeGraphSync(EnableSync,FALSE);

      MarketingSetup.GET;
      EnableSync := MarketingSetup."Sync with Microsoft Graph";

      IF NOT EnableSync THEN
        EnableSyncCoupons := FALSE;
    END;

    LOCAL PROCEDURE UpdateCouponsSync@10();
    VAR
      MarketingSetup@1000 : Record 5079;
      O365SalesInitialSetup@1001 : Record 2110;
    BEGIN
      IF EnableSyncCoupons THEN BEGIN
        CheckSyncAllowed;
        MarketingSetup.GET;
        IF NOT MarketingSetup."Sync with Microsoft Graph" THEN
          ERROR(CannotEnableCouponsSyncErr);
        IF MarketingSetup.TrySetWebhookSubscriptionUser(USERSECURITYID) THEN
          ERROR(CannotSetWebhookSubscriptionUserErr);
      END;
      O365SalesInitialSetup.GET;
      O365SalesInitialSetup."Coupons Integration Enabled" := EnableSyncCoupons;
      O365SalesInitialSetup.MODIFY(TRUE);
    END;

    LOCAL PROCEDURE GetNumberOfDecimals@4(AmountRoundingPrecision@1000 : Decimal) : Integer;
    VAR
      Count@1001 : Integer;
    BEGIN
      Count := 0;

      IF AmountRoundingPrecision >= 1 THEN
        EXIT(Count);

      REPEAT
        Count += 1;
        AmountRoundingPrecision := AmountRoundingPrecision * 10;
      UNTIL AmountRoundingPrecision >= 1;

      EXIT(Count);
    END;

    PROCEDURE GetPaymentInfo@12();
    VAR
      O365SalesInitialSetup@1001 : Record 2110;
      PaymentMethod@1005 : Record 289;
      PaymentTerms@1006 : Record 3;
    BEGIN
      O365SalesInitialSetup.GET;

      IF PaymentTerms.GET(O365SalesInitialSetup."Default Payment Terms Code") THEN BEGIN
        "Default Payment Terms ID" := PaymentTerms.Id;
        "Def. Pmt. Term Description" := PaymentTerms.GetDescriptionInCurrentLanguage;
      END;

      IF PaymentMethod.GET(O365SalesInitialSetup."Default Payment Method Code") THEN BEGIN
        "Default Payment Method ID" := PaymentMethod.Id;
        "Def. Pmt. Method Description" :=
          COPYSTR(PaymentMethod.GetDescriptionInCurrentLanguage,1,MAXSTRLEN("Def. Pmt. Method Description"));
      END;
    END;

    LOCAL PROCEDURE GetLanguageInfo@5();
    VAR
      UserPersonalization@1001 : Record 2000000073;
      WindowsLanguage@1000 : Record 2000000045;
      CultureInfo@1002 : DotNet "'mscorlib, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Globalization.CultureInfo";
    BEGIN
      UserPersonalization.GET(USERSECURITYID);
      "Language Locale ID" := UserPersonalization."Language ID";
      CultureInfo := CultureInfo.CultureInfo("Language Locale ID");
      "Language Code" := CultureInfo.Name;
      IF WindowsLanguage.GET("Language Locale ID") THEN
        "Language Display Name" := WindowsLanguage.Name;
    END;

    PROCEDURE CheckSyncAllowed@8();
    VAR
      CompanyInformation@1003 : Record 79;
      CompanyInformationMgt@1002 : Codeunit 1306;
      PermissionManager@1001 : Codeunit 9002;
      WebhookManagement@1000 : Codeunit 5377;
    BEGIN
      IF WebhookManagement.IsSyncAllowed THEN
        EXIT;

      IF NOT CompanyInformation.GET THEN
        ERROR(CannotGetCompanyInformationErr);

      IF NOT PermissionManager.SoftwareAsAService THEN
        ERROR(SyncOnlyAllowedInSaasErr);

      IF CompanyInformationMgt.IsDemoCompany THEN
        ERROR(SyncNotAllowedInDemoCompanyErr);

      ERROR(SyncNotAllowedErr);
    END;

    BEGIN
    END.
  }
}

