OBJECT Page 1797 Data Migration Error
{
  OBJECT-PROPERTIES
  {
    Date=09/19/18;
    Time=12:00:00 PM;
    Version List=NAVW113.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Data Migration Errors;
               ESM=Errores de migraci¢n de datos;
               FRC=Erreurs de migration des donn‚es;
               ENC=Data Migration Errors];
    InsertAllowed=No;
    DeleteAllowed=No;
    ModifyAllowed=No;
    SourceTable=Table1797;
    PageType=List;
    OnOpenPage=VAR
                 Notification@1000 : Notification;
               BEGIN
                 Notification.MESSAGE := SkipEditNotificationMsg;
                 Notification.SEND;
               END;

    OnAfterGetCurrRecord=VAR
                           DataMigrationFacade@1002 : Codeunit 6100;
                           StagingTableRecId@1001 : RecordID;
                           DummyRecordId@1000 : RecordID;
                         BEGIN
                           StagingTableRecId := "Source Staging Table Record ID";
                           StagingTableRecIdSpecified := StagingTableRecId <> DummyRecordId;

                           DataMigrationFacade.OnInitDataMigrationError("Migration Type",BulkFixErrorsButtonEnabled);
                         END;

    ActionList=ACTIONS
    {
      { 5       ;    ;ActionContainer;
                      ActionContainerType=NewDocumentItems }
      { 6       ;1   ;Action    ;
                      Name=SkipSelection;
                      CaptionML=[ENU=Skip Selections;
                                 ESM=Omitir selecciones;
                                 FRC=Ignorer les s‚lections;
                                 ENC=Skip Selections];
                      ToolTipML=[ENU=Exclude the selected errors from the migration.;
                                 ESM=Excluir los errores seleccionados de la migraci¢n.;
                                 FRC=Excluez les erreurs s‚lectionn‚es de la migration.;
                                 ENC=Exclude the selected errors from the migration.];
                      ApplicationArea=#All;
                      Promoted=Yes;
                      Enabled=StagingTableRecIdSpecified;
                      PromotedIsBig=Yes;
                      Image=Delete;
                      PromotedCategory=Process;
                      PromotedOnly=Yes;
                      OnAction=VAR
                                 DataMigrationError@1001 : Record 1797;
                               BEGIN
                                 CheckAtLeastOneSelected;
                                 IF NOT CONFIRM(SkipSelectionConfirmQst,FALSE) THEN
                                   EXIT;
                                 CurrPage.SETSELECTIONFILTER(DataMigrationError);
                                 DataMigrationError.FINDSET;
                                 REPEAT
                                   DataMigrationError.Ignore;
                                 UNTIL DataMigrationError.NEXT = 0;
                                 CurrPage.UPDATE;
                               END;
                                }
      { 7       ;1   ;Action    ;
                      Name=Edit;
                      CaptionML=[ENU=Edit Record;
                                 ESM=Editar registro;
                                 FRC=Modifier l'enregistrement;
                                 ENC=Edit Record];
                      ToolTipML=[ENU=Edit the record that caused the error.;
                                 ESM=Editar el registro que caus¢ el error.;
                                 FRC=Modifiez l'enregistrement … l'origine de l'erreur.;
                                 ENC=Edit the record that caused the error.];
                      ApplicationArea=#All;
                      Promoted=Yes;
                      Enabled=StagingTableRecIdSpecified;
                      PromotedIsBig=Yes;
                      Image=Edit;
                      PromotedCategory=Process;
                      PromotedOnly=Yes;
                      OnAction=BEGIN
                                 EditRecord;
                               END;
                                }
      { 8       ;1   ;Action    ;
                      Name=Migrate;
                      CaptionML=[ENU=Migrate;
                                 ESM=Migrar;
                                 FRC=Migrer;
                                 ENC=Migrate];
                      ToolTipML=[ENU=Migrate the selected errors.;
                                 ESM=Migrar los errores seleccionados.;
                                 FRC=Migrez les erreurs s‚lectionn‚es.;
                                 ENC=Migrate the selected errors.];
                      ApplicationArea=#All;
                      Promoted=Yes;
                      Enabled=StagingTableRecIdSpecified;
                      PromotedIsBig=Yes;
                      Image=Refresh;
                      PromotedCategory=Process;
                      PromotedOnly=Yes;
                      OnAction=VAR
                                 DataMigrationError@1000 : Record 1797;
                               BEGIN
                                 CheckAtLeastOneSelected;
                                 CurrPage.SETSELECTIONFILTER(DataMigrationError);
                                 StartMigration(DataMigrationError);
                               END;
                                }
      { 4       ;1   ;Action    ;
                      Name=BulkFixErrors;
                      CaptionML=[ENU=Bulk-Fix Errors;
                                 ESM=Correcci¢n masiva de errores;
                                 FRC=Erreurs de correction en bloc;
                                 ENC=Bulk-Fix Errors];
                      ToolTipML=[ENU=Open a list of all entities that contained an error. You can fix some or all of the errors and migrate the updated data.;
                                 ESM=Abre una lista de todas las entidades en las que hay un error. Puede corregir algunos o todos los errores, y migrar los datos actualizados.;
                                 FRC=Ouvrez une liste de toutes les entit‚s qui contenaient une erreur. Vous pouvez corriger toutes les erreurs ou certaines d'entre elles et migrer les donn‚es mises … jour.;
                                 ENC=Open a list of all entities that contained an error. You can fix some or all of the errors and migrate the updated data.];
                      ApplicationArea=#All;
                      Promoted=Yes;
                      Enabled=BulkFixErrorsButtonEnabled;
                      PromotedIsBig=Yes;
                      Image=List;
                      PromotedCategory=Process;
                      PromotedOnly=Yes;
                      OnAction=VAR
                                 DataMigrationFacade@1000 : Codeunit 6100;
                                 DataMigrationOverview@1001 : Page 1799;
                               BEGIN
                                 DataMigrationFacade.OnBatchEditFromErrorView("Migration Type","Destination Table ID");

                                 IF NOT CONFIRM(STRSUBSTNO(MigrateEntitiesAgainQst,DataMigrationOverview.CAPTION),TRUE) THEN
                                   EXIT;

                                 StartMigration(Rec);
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1   ;0   ;Container ;
                ContainerType=ContentArea }

    { 2   ;1   ;Group     ;
                Name=Group;
                GroupType=Repeater }

    { 3   ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the error message that relates to the data migration.;
                           ESM=Especifica el mensaje de error relacionado con la migraci¢n de datos.;
                           FRC=Sp‚cifie le message d'erreur qui est associ‚ … la migration des donn‚es.;
                           ENC=Specifies the error message that relates to the data migration.];
                ApplicationArea=#All;
                SourceExpr="Error Message";
                OnDrillDown=BEGIN
                              IF StagingTableRecIdSpecified THEN
                                EditRecord;
                            END;
                             }

  }
  CODE
  {
    VAR
      MultipleRecordsSelectedErr@1000 : TextConst 'ENU=You can view the content of one record at a time.;ESM=Puede ver el contenido de un registro a la vez.;FRC=Vous pouvez afficher le contenu d''un enregistrement … la fois.;ENC=You can view the content of one record at a time.';
      MigrationStartedMsg@1001 : TextConst '@@@="%1 = Caption for the page Data Migration Overview";ENU=The selected records are scheduled for data migration. To check the status of the migration, go to the %1 page.;ESM=Los registros seleccionados est n programados para la migraci¢n de datos. Para comprobar el estado de la migraci¢n, vaya a la p gina %1.;FRC=Les enregistrements s‚lectionn‚s sont programm‚s pour la migration des donn‚es. Pour v‚rifier l''‚tat de la migration, acc‚dez … la page %1.;ENC=The selected records are scheduled for data migration. To check the status of the migration, go to the %1 page.';
      NoSelectionsMadeErr@1002 : TextConst 'ENU=No records have been selected.;ESM=No se ha seleccionado ning£n registro.;FRC=Aucun enregistrement n''a ‚t‚ s‚lectionn‚.;ENC=No records have been selected.';
      StagingTableRecIdSpecified@1003 : Boolean;
      SkipSelectionConfirmQst@1004 : TextConst 'ENU=The selected errors will be deleted and the corresponding entities will not be migrated. Do you want to continue?;ESM=Los errores seleccionados se eliminar n y las entidades correspondientes no se migrar n. ¨Desea continuar?;FRC=Les erreurs s‚lectionn‚es seront supprim‚es et les entit‚s correspondantes ne seront pas migr‚es. Voulez-vous continuerÿ?;ENC=The selected errors will be deleted and the corresponding entities will not be migrated. Do you want to continue?';
      ExtensionNotInstalledErr@1005 : TextConst 'ENU=Sorry, but it looks like someone uninstalled the data migration extension you are trying to use. When that happens, we remove all data that was not fully migrated.;ESM=Parece que alguien ha desinstalado la extensi¢n de migraci¢n de datos que intenta utilizar. Cuando esto ocurre, es necesario quitar todos los datos que no se migraron completamente.;FRC=D‚sol‚, mais il semblerait que quelqu''un ait d‚sinstall‚ l''extension de migration des donn‚es que vous essayez d''utiliser. Lors de cette d‚sinstallation, nous avons supprim‚ toutes les donn‚es dont la migration n''‚tait pas complŠte.;ENC=Sorry, but it looks like someone uninstalled the data migration extension you are trying to use. When that happens, we remove all data that was not fully migrated.';
      SkipEditNotificationMsg@1006 : TextConst 'ENU=Skip errors, or edit the entity to fix them, and then migrate again.;ESM=Omita los errores o edite la entidad para corregirlos y, a continuaci¢n, migre de nuevo.;FRC=Ignorez les erreurs ou modifiez l''entit‚ pour les corriger, puis recommencez la migration.;ENC=Skip errors, or edit the entity to fix them, and then migrate again.';
      MigrateEntitiesAgainQst@1008 : TextConst '@@@="%1 = caption of the Data Migration Overview page";ENU=Do you want to migrate the updated entities?\\If you do, remember to refresh the %1 page so you can follow the progress.;ESM=¨Desea migrar las entidades actualizadas?\\Si lo hace, no olvide actualizar la p gina %1 para poder hacer un seguimiento del progreso.;FRC=Voulez-vous migrer les entit‚s mises … jourÿ? \\Si vous le faites, n''oubliez pas d''actualiser la page %1 afin de pouvoir suivre la progression.;ENC=Do you want to migrate the updated entities?\\If you do, remember to refresh the %1 page so you can follow the progress.';
      BulkFixErrorsButtonEnabled@1009 : Boolean;

    LOCAL PROCEDURE CheckAtLeastOneSelected@1();
    VAR
      DataMigrationError@1000 : Record 1797;
    BEGIN
      CurrPage.SETSELECTIONFILTER(DataMigrationError);
      IF DataMigrationError.COUNT = 0 THEN
        ERROR(NoSelectionsMadeErr);
    END;

    LOCAL PROCEDURE EditRecord@2();
    VAR
      DataMigrationError@1002 : Record 1797;
      DataMigrationStatus@1001 : Record 1799;
      RecordRef@1000 : RecordRef;
    BEGIN
      CheckAtLeastOneSelected;
      CurrPage.SETSELECTIONFILTER(DataMigrationError);
      IF DataMigrationError.COUNT > 1 THEN
        ERROR(MultipleRecordsSelectedErr);

      DataMigrationError.FINDFIRST;
      DataMigrationStatus.SETRANGE("Migration Type",DataMigrationError."Migration Type");
      DataMigrationStatus.SETRANGE("Destination Table ID",DataMigrationError."Destination Table ID");
      DataMigrationStatus.FINDFIRST;

      IF NOT RecordRef.GET(DataMigrationError."Source Staging Table Record ID") THEN
        ERROR(ExtensionNotInstalledErr);

      OpenStagingTablePage(DataMigrationStatus."Source Staging Table ID",RecordRef);
    END;

    LOCAL PROCEDURE OpenStagingTablePage@5(PageId@1000 : Integer;StagingTableRecord@1001 : Variant);
    BEGIN
      PAGE.RUNMODAL(PageId,StagingTableRecord);
    END;

    LOCAL PROCEDURE StartMigration@4(VAR DataMigrationError@1000 : Record 1797);
    VAR
      DataMigrationFacade@1001 : Codeunit 6100;
      DataMigrationOverview@1002 : Page 1799;
    BEGIN
      DataMigrationError.MODIFYALL("Scheduled For Retry",TRUE,TRUE);

      DataMigrationFacade.StartMigration("Migration Type",TRUE);

      MESSAGE(MigrationStartedMsg,DataMigrationOverview.CAPTION);
      CurrPage.CLOSE;
    END;

    BEGIN
    END.
  }
}

