OBJECT Report 1086 Job Calculate WIP
{
  OBJECT-PROPERTIES
  {
    Date=09/19/18;
    Time=12:00:00 PM;
    Version List=NAVW113.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Job Calculate WIP;
               ESM=Calcular WIP proyecto;
               FRC=Projet - Calculer TEC;
               ENC=Job Calculate WIP];
    ApplicationArea=#Jobs;
    ProcessingOnly=Yes;
    OnPreReport=VAR
                  NewNoSeriesCode@1000 : Code[20];
                BEGIN
                  JobsSetup.GET;

                  IF DocNo = '' THEN BEGIN
                    JobsSetup.TESTFIELD("Job Nos.");
                    NoSeriesMgt.InitSeries(JobsSetup."Job WIP Nos.",JobsSetup."Job WIP Nos.",0D,DocNo,NewNoSeriesCode);
                  END;

                  IF PostingDate = 0D THEN
                    PostingDate := WORKDATE;

                  JobCalculateBatches.BatchError(PostingDate,DocNo);
                END;

    OnPostReport=VAR
                   WIPPosted@1001 : Boolean;
                   WIPQst@1002 : Text;
                 BEGIN
                   JobWIPEntry.SETCURRENTKEY("Job No.");
                   JobWIPEntry.SETFILTER("Job No.",Job.GETFILTER("No."));
                   WIPPosted := JobWIPEntry.FINDFIRST;
                   COMMIT;

                   IF WIPPosted THEN BEGIN
                     IF WIPPostedWithWarnings THEN
                       MESSAGE(Text002)
                     ELSE
                       MESSAGE(Text000);
                     IF DIALOG.CONFIRM(PreviewQst) THEN BEGIN
                       JobWIPEntry.SETRANGE("Job No.",Job."No.");
                       PAGE.RUNMODAL(PAGE::"Job WIP Entries",JobWIPEntry);

                       WIPQst := STRSUBSTNO(RunWIPFunctionsQst,'Job Post WIP to G/L');
                       IF DIALOG.CONFIRM(WIPQst) THEN
                         REPORT.RUN(REPORT::"Job Post WIP to G/L",FALSE,FALSE,Job)
                       ELSE
                         REPORT.RUNMODAL(REPORT::"Job Post WIP to G/L",TRUE,FALSE,Job);
                     END;
                   END ELSE
                     MESSAGE(Text001);
                 END;

    UsageCategory=Tasks;
  }
  DATASET
  {
    { 8019;    ;DataItem;                    ;
               DataItemTable=Table167;
               DataItemTableView=SORTING(No.);
               OnAfterGetRecord=VAR
                                  JobCalculateWIP@1000 : Codeunit 1000;
                                BEGIN
                                  JobCalculateWIP.JobCalcWIP(Job,PostingDate,DocNo);
                                  CALCFIELDS("WIP Warnings");
                                  WIPPostedWithWarnings := WIPPostedWithWarnings OR "WIP Warnings";
                                END;

               ReqFilterFields=No.,Planning Date Filter,Posting Date Filter }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      OnOpenPage=VAR
                   NewNoSeriesCode@1000 : Code[20];
                 BEGIN
                   IF PostingDate = 0D THEN
                     PostingDate := WORKDATE;

                   JobsSetup.GET;

                   JobsSetup.TESTFIELD("Job Nos.");
                   NoSeriesMgt.InitSeries(JobsSetup."Job WIP Nos.",JobsSetup."Job WIP Nos.",0D,DocNo,NewNoSeriesCode);
                 END;

    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[ENU=Options;
                             ESM=Opciones;
                             FRC=Options;
                             ENC=Options] }

      { 1   ;2   ;Field     ;
                  Name=PostingDate;
                  CaptionML=[ENU=Posting Date;
                             ESM=Fecha registro;
                             FRC=Date de report;
                             ENC=Posting Date];
                  ToolTipML=[ENU=Specifies the posting date for the document.;
                             ESM=Especifica la fecha de registro del documento.;
                             FRC=Sp‚cifie la date de report du document.;
                             ENC=Specifies the posting date for the document.];
                  ApplicationArea=#Jobs;
                  SourceExpr=PostingDate }

      { 5   ;2   ;Field     ;
                  Name=DocumentNo;
                  CaptionML=[ENU=Document No.;
                             ESM=N§ documento;
                             FRC=Nø de document;
                             ENC=Document No.];
                  ToolTipML=[ENU=Specifies the number of a document that the calculation will apply to.;
                             ESM=Especifica el n£mero de un documento en el que se aplicar  el c lculo.;
                             FRC=Sp‚cifie le num‚ro de document auquel s'applique le calcul.;
                             ENC=Specifies the number of a document that the calculation will apply to.];
                  ApplicationArea=#Jobs;
                  SourceExpr=DocNo }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      JobWIPEntry@1004 : Record 1004;
      JobsSetup@1008 : Record 315;
      JobCalculateBatches@1000 : Codeunit 1005;
      NoSeriesMgt@1009 : Codeunit 396;
      PostingDate@1001 : Date;
      DocNo@1003 : Code[20];
      Text000@1006 : TextConst 'ENU=WIP was successfully calculated.;ESM=WIP calculado correctamente.;FRC=Les TEC ont bien ‚t‚ calcul‚s.;ENC=WIP was successfully calculated.';
      Text001@1007 : TextConst 'ENU=There were no new WIP entries created.;ESM=No hab¡a nuevos movimientos WIP creados.;FRC=Aucune nouvelle ‚criture TEC cr‚‚e.;ENC=There were no new WIP entries created.';
      Text002@1002 : TextConst 'ENU=WIP was calculated with warnings.;ESM=WIP se ha calculado con advertencias.;FRC=TEC calcul‚ avec des avertissements.;ENC=WIP was calculated with warnings.';
      WIPPostedWithWarnings@1005 : Boolean;
      PreviewQst@1010 : TextConst 'ENU=Do you want to preview the posting accounts?;ESM=¨Desea obtener una vista previa de las cuentas de registro?;FRC=Souhaitez-vous afficher un aper‡u des comptes de report?;ENC=Do you want to preview the posting accounts?';
      RunWIPFunctionsQst@1011 : TextConst '@@@="%1 = The name of the Job Post WIP to G/L report";ENU=You must run the %1 function to post the completion entries for this job. \Do you want to run this function now?;ESM=Debe ejecutar la funci¢n %1 para registrar los movimientos de finalizaci¢n de este proyecto. \¨Desea ejecutar esta funci¢n ahora?;FRC=Vous devez ex‚cuter la fonction %1 pour reporter les ‚critures d''achŠvement pour ce projet. \Souhaitez-vous ex‚cuter cette fonction maintenant?;ENC=You must run the %1 function to post the completion entries for this job. \Do you want to run this function now?';

    PROCEDURE InitializeRequest@1();
    BEGIN
      PostingDate := WORKDATE;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

