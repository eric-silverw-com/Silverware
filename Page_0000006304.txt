OBJECT Page 6304 Power BI Report Selection
{
  OBJECT-PROPERTIES
  {
    Date=09/19/18;
    Time=12:00:00 PM;
    Version List=NAVW113.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Power BI Reports Selection;
               ESM=Selecci¢n de informes de Power BI;
               FRC=S‚lection de rapports Power BI;
               ENC=Power BI Reports Selection];
    InsertAllowed=No;
    DeleteAllowed=No;
    LinksAllowed=No;
    SourceTable=Table6302;
    PageType=List;
    SourceTableTemporary=Yes;
    PromotedActionCategoriesML=[ENU=New,Process,Report,Manage,Get Reports;
                                ESM=Nuevo,Procesar,Informe,Administrar,Obtener informes;
                                FRC=Nouveau,Traiter,Rapport,G‚rer,Obtenir les rapports;
                                ENC=New,Process,Report,Manage,Get Reports];
    OnOpenPage=BEGIN
                 IF NOT TryLoadReportsList THEN
                   ShowErrorMessage(GETLASTERRORTEXT);

                 IsSaaS := AzureADMgt.IsSaaS;
               END;

    OnQueryClosePage=BEGIN
                       SaveAndClose;
                     END;

    ActionList=ACTIONS
    {
      { 5       ;    ;ActionContainer;
                      ActionContainerType=Reports }
      { 7       ;1   ;Action    ;
                      Name=EnableReport;
                      CaptionML=[ENU=Enable;
                                 ESM=Habilitar;
                                 FRC=Activer;
                                 ENC=Enable];
                      ToolTipML=[ENU=Enables the report selection.;
                                 ESM=Habilita la selecci¢n de informes.;
                                 FRC=Active la s‚lection du rapport.;
                                 ENC=Enables the report selection.];
                      ApplicationArea=#All;
                      Promoted=Yes;
                      Enabled=NOT Enabled;
                      PromotedIsBig=Yes;
                      Image=Report;
                      PromotedCategory=Category4;
                      PromotedOnly=Yes;
                      Scope=Repeater;
                      OnAction=BEGIN
                                 Enabled := TRUE;
                                 CurrPage.UPDATE;
                               END;

                      Gesture=LeftSwipe }
      { 8       ;1   ;Action    ;
                      Name=DisableReport;
                      CaptionML=[ENU=Disable;
                                 ESM=Deshabilitar;
                                 FRC=D‚sactiver;
                                 ENC=Disable];
                      ToolTipML=[ENU=Disables the report selection.;
                                 ESM=Deshabilita la selecci¢n de informes.;
                                 FRC=D‚sactive la s‚lection du rapport.;
                                 ENC=Disables the report selection.];
                      ApplicationArea=#All;
                      Promoted=Yes;
                      Enabled=Enabled;
                      PromotedIsBig=Yes;
                      Image=Report;
                      PromotedCategory=Category4;
                      PromotedOnly=Yes;
                      Scope=Repeater;
                      OnAction=BEGIN
                                 Enabled := FALSE;
                                 CurrPage.UPDATE;
                               END;

                      Gesture=RightSwipe }
      { 9       ;1   ;Action    ;
                      Name=Refresh;
                      CaptionML=[ENU=Refresh List;
                                 ESM=Actualizar lista;
                                 FRC=Actualiser liste;
                                 ENC=Refresh List];
                      ToolTipML=[ENU=Update the report list with any newly added reports.;
                                 ESM=Permite actualizar la lista de informes con todos los informes reci‚n agregados.;
                                 FRC=Mettez … jour la liste des rapports avec les rapports ajout‚s r‚cemment.;
                                 ENC=Update the report list with any newly added reports.];
                      ApplicationArea=#All;
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=Refresh;
                      PromotedCategory=Category5;
                      PromotedOnly=Yes;
                      OnAction=BEGIN
                                 IsErrorMessageVisible := FALSE;
                                 IsUrlFieldVisible := FALSE;

                                 IF NOT TryLoadReportsList THEN
                                   ShowErrorMessage(GETLASTERRORTEXT);
                               END;
                                }
      { 10      ;1   ;Action    ;
                      Name=MyOrganization;
                      CaptionML=[ENU=My Organization;
                                 ESM=Mi organizaci¢n;
                                 FRC=Mon organisation;
                                 ENC=My Organization];
                      ToolTipML=[ENU=Browse content packs that other people in your organization have published.;
                                 ESM=Explorar paquetes de contenidos publicados por otras personas de su organizaci¢n.;
                                 FRC=Parcourir les packs de contenu publi‚s par d'autres personnes de votre organisation.;
                                 ENC=Browse content packs that other people in your organization have published.];
                      ApplicationArea=#All;
                      Promoted=Yes;
                      Image=PowerBI;
                      PromotedCategory=Category5;
                      PromotedOnly=Yes;
                      OnAction=BEGIN
                                 // Opens a new browser tab to Power BI's content pack list, set to the My Organization tab.
                                 HYPERLINK(PowerBIServiceMgt.GetContentPacksMyOrganizationUrl);
                               END;
                                }
      { 11      ;1   ;Action    ;
                      Name=Services;
                      CaptionML=[ENU=Services;
                                 ESM=Servicios;
                                 FRC=Services;
                                 ENC=Services];
                      ToolTipML=[ENU=Choose content packs from online services that you use.;
                                 ESM=Seleccionar paquetes de contenidos de servicios en l¡nea que est  usando.;
                                 FRC=Choisir des packs de contenu … partir des services en ligne que vous utilisez.;
                                 ENC=Choose content packs from online services that you use.];
                      ApplicationArea=#All;
                      Promoted=Yes;
                      Image=PowerBI;
                      PromotedCategory=Category5;
                      PromotedOnly=Yes;
                      OnAction=BEGIN
                                 // Opens a new browser tab to AppSource's content pack list, filtered to NAV reports.
                                 HYPERLINK(PowerBIServiceMgt.GetContentPacksServicesUrl);
                               END;
                                }
      { 12      ;1   ;Action    ;
                      Name=ConnectionInfo;
                      CaptionML=[ENU=Connection Information;
                                 ESM=Informaci¢n de la conexi¢n;
                                 FRC=Informations connexion;
                                 ENC=Connection Information];
                      ToolTipML=[ENU=Show information for connecting to Power BI content packs.;
                                 ESM=Muestra informaci¢n para conectarse con paquetes contenidos de Power BI.;
                                 FRC=Afficher les informations de connexion pour connecter des packs contenus PowerÿBI.;
                                 ENC=Show information for connecting to Power BI content packs.];
                      ApplicationArea=#All;
                      RunObject=Page 6316;
                      Promoted=Yes;
                      Visible=NOT IsSaaS;
                      Image=Setup;
                      PromotedCategory=Category5;
                      PromotedOnly=Yes }
      { 18      ;1   ;Action    ;
                      Name=CleanDeployments;
                      CaptionML=[ENU=Default Reports;
                                 ESM=Informes predeterminados;
                                 FRC=Rapports par d‚faut;
                                 ENC=Default Reports];
                      ToolTipML=[ENU=Manage your deployed default reports.;
                                 ESM=Administrar sus informes predeterminados implementados.;
                                 FRC=G‚rez vos rapports par d‚faut d‚ploy‚s.;
                                 ENC=Manage your deployed default reports.];
                      ApplicationArea=#All;
                      Promoted=Yes;
                      Image=Setup;
                      PromotedCategory=Category4;
                      PromotedOnly=Yes;
                      OnAction=BEGIN
                                 PAGE.RUNMODAL(PAGE::"Power BI Deployments");
                                 // TODO: Set this button's visibility to equal "IsSaaS" so it's not visible for on-prem, where we won't have OOB uploads anyways.
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1   ;0   ;Container ;
                ContainerType=ContentArea }

    { 2   ;1   ;Group     ;
                Name=Group;
                Visible=HasReports AND NOT IsErrorMessageVisible AND NOT IsUrlFieldVisible;
                GroupType=Repeater }

    { 3   ;2   ;Field     ;
                Name=ReportName;
                CaptionML=[ENU=Report Name;
                           ESM=Nombre informe;
                           FRC=Nom du rapport;
                           ENC=Report Name];
                ToolTipML=[ENU=Specifies the name of the report.;
                           ESM=Especifica el nombre del informe.;
                           FRC=Sp‚cifie le nom du rapport.;
                           ENC=Specifies the name of the report.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr=ReportName;
                Editable=FALSE;
                OnDrillDown=BEGIN
                              // Event handler for when the user clicks the report name hyperlink. This automatically selects
                              // and enables the report and closes the window, so the user immediately sees the clicked report
                              // on the parent page.
                              Enabled := TRUE;

                              // OnDrillDown returns a LookupCancel Action when the page closes.
                              // IsPgclosedOkay is used to tell the caller of this page that inspite of the LookupCancel
                              // the action should be treated like a LookupOk
                              IsPgClosedOkay := TRUE;
                              SaveAndClose;
                            END;
                             }

    { 4   ;2   ;Field     ;
                Name=Enabled;
                ToolTipML=[ENU=Specifies that the report selection is enabled.;
                           ESM=Especifica que la selecci¢n de informes est  habilitada.;
                           FRC=Indique que la s‚lection du rapport est activ‚e.;
                           ENC=Specifies that the report selection is enabled.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr=Enabled }

    { 6   ;1   ;Group     ;
                Visible=NOT HasReports AND NOT IsErrorMessageVisible AND NOT IsUrlFieldVisible;
                GroupType=Group }

    { 13  ;2   ;Field     ;
                Name=NoReportsError;
                CaptionML=[ENU=There are no reports available from Power BI.;
                           ESM=No hay informes disponibles en Power BI.;
                           FRC=Aucun ‚tat n'est disponible dans Power BI.;
                           ENC=There are no reports available from Power BI.];
                ToolTipML=[ENU=Specifies that the user needs to select Power BI reports.;
                           ESM=Especifica que el usuario debe seleccionar los informes de Power BI.;
                           FRC=Sp‚cifie que l'utilisateur doit s‚lectionner les rapports Power BI.;
                           ENC=Specifies that the user needs to select Power BI reports.];
                ApplicationArea=#Basic,#Suite }

    { 14  ;1   ;Group     ;
                Visible=IsErrorMessageVisible AND NOT IsUrlFieldVisible;
                GroupType=Group }

    { 15  ;2   ;Field     ;
                Name=ErrorMessage;
                ToolTipML=[ENU=Specifies an error that occurred.;
                           ESM=Especifica un error que se ha producido.;
                           FRC=Sp‚cifie une erreur qui s'est produite.;
                           ENC=Specifies an error that occurred.];
                ApplicationArea=#All;
                SourceExpr=ErrorMessageText;
                Editable=FALSE;
                ShowCaption=No }

    { 17  ;1   ;Group     ;
                Visible=IsUrlFieldVisible;
                GroupType=Group }

    { 16  ;2   ;Field     ;
                Name=ErrorLink;
                ToolTipML=[ENU=Specifies an error that occurred and how to resolve it.;
                           ESM=Especifica un error que se ha producido y c¢mo resolverlo.;
                           FRC=Sp‚cifie une erreur qui s'est produite et indique comment la r‚soudre.;
                           ENC=Specifies an error that occurred and how to resolve it.];
                ApplicationArea=#All;
                SourceExpr=ErrorUrlText;
                Editable=FALSE;
                Style=StrongAccent;
                StyleExpr=TRUE;
                OnDrillDown=BEGIN
                              HYPERLINK(ErrorUrlPath);
                            END;

                ShowCaption=No }

  }
  CODE
  {
    VAR
      AzureADMgt@1002 : Codeunit 6300;
      PowerBIServiceMgt@1005 : Codeunit 6301;
      Context@1001 : Text[30];
      NameFilter@1004 : Text;
      IsPgClosedOkay@1003 : Boolean;
      IsSaaS@1006 : Boolean;
      HasReports@1007 : Boolean;
      IsErrorMessageVisible@1000 : Boolean;
      IsUrlFieldVisible@1009 : Boolean;
      ErrorMessageText@1008 : Text;
      ErrorUrlText@1010 : Text;
      ErrorUrlPath@1011 : Text;

    [External]
    PROCEDURE SetContext@2(ParentContext@1000 : Text[30]);
    BEGIN
      // Sets the ID of the parent page that reports are being selected for.
      Context := ParentContext;
    END;

    [External]
    PROCEDURE SetNameFilter@10(ParentFilter@1000 : Text);
    BEGIN
      // Sets the value to filter report names by.
      // This should be called by the parent page before opening this page.
      NameFilter := ParentFilter;
    END;

    LOCAL PROCEDURE FilterReports@21();
    BEGIN
      // Filters the report collection by name, if the parent page has provided a value to filter by.
      // This filter will display any report that has the value anywhere in the name, case insensitive.
      IF NameFilter <> '' THEN
        SETFILTER(ReportName,'%1','@*' + NameFilter + '*');
    END;

    [TryFunction]
    LOCAL PROCEDURE TryLoadReportsList@1();
    BEGIN
      LoadReportsList;
    END;

    LOCAL PROCEDURE LoadReportsList@6();
    VAR
      ExceptionMessage@1000 : Text;
      ExceptionDetails@1001 : Text;
    BEGIN
      // Clears and retrieves a list of all reports in the user's Power BI account.
      RESET;
      DELETEALL;
      PowerBIServiceMgt.GetReports(Rec,ExceptionMessage,ExceptionDetails,Context);

      HasReports := NOT ISEMPTY;
      IF ISEMPTY THEN
        INSERT; // Hack to prevent empty list error.

      // Set sort order, scrollbar position, and filters.
      SETCURRENTKEY(ReportName);
      FINDFIRST;
      FilterReports;
    END;

    LOCAL PROCEDURE SaveAndClose@5();
    VAR
      PowerBiReportConfiguration@1001 : Record 6301;
      TempPowerBiReportBuffer@1000 : TEMPORARY Record 6302;
    BEGIN
      // use a temp buffer record for saving to not disturb the position, filters, etc. of the source table
      // ShareTable = TRUE makes a shallow copy of the record, which is OK since no modifications are made to the records themselves
      TempPowerBiReportBuffer.COPY(Rec,TRUE);

      // Clear out all old records before re-adding (easiest way to remove invalid rows, e.g. deleted reports).
      PowerBiReportConfiguration.SETFILTER("User Security ID",USERSECURITYID);
      PowerBiReportConfiguration.SETFILTER(Context,Context);
      PowerBiReportConfiguration.DELETEALL;
      PowerBiReportConfiguration.RESET;

      TempPowerBiReportBuffer.SETRANGE(Enabled,TRUE);
      IF TempPowerBiReportBuffer.FIND('-') THEN
        REPEAT
          PowerBiReportConfiguration.INIT;
          PowerBiReportConfiguration."User Security ID" := USERSECURITYID;
          PowerBiReportConfiguration."Report ID" := TempPowerBiReportBuffer.ReportID;
          PowerBiReportConfiguration.Context := Context;
          PowerBiReportConfiguration.EmbedUrl := TempPowerBiReportBuffer.EmbedUrl;
          PowerBiReportConfiguration.INSERT;
        UNTIL TempPowerBiReportBuffer.NEXT = 0;

      IsPgClosedOkay := TRUE;
      CurrPage.CLOSE;
    END;

    [External]
    PROCEDURE IsPageClosedOkay@3() : Boolean;
    BEGIN
      EXIT(IsPgClosedOkay);
    END;

    LOCAL PROCEDURE ShowErrorMessage@7(TextToShow@1000 : Text);
    BEGIN
      // Dealing with 401 Unauthorized error URL, per page 6303.
      IF TextToShow = PowerBIServiceMgt.GetUnauthorizedErrorText THEN BEGIN
        ErrorUrlText := TextToShow;
        ErrorUrlPath := PowerBIServiceMgt.GetPowerBIUrl;
        IsUrlFieldVisible := TRUE;
      END ELSE BEGIN
        IF TextToShow = '' THEN
          TextToShow := PowerBIServiceMgt.GetGenericError;

        ErrorMessageText := TextToShow;
        IsErrorMessageVisible := TRUE;
      END;
    END;

    BEGIN
    END.
  }
}

