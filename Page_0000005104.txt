OBJECT Page 5104 Contact Picture
{
  OBJECT-PROPERTIES
  {
    Date=09/19/18;
    Time=12:00:00 PM;
    Version List=NAVW113.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Contact Picture;
               ESM=Imagen contacto;
               FRC=Image contact;
               ENC=Contact Picture];
    InsertAllowed=No;
    DeleteAllowed=No;
    LinksAllowed=No;
    SourceTable=Table5050;
    PageType=CardPart;
    OnOpenPage=BEGIN
                 CameraAvailable := CameraProvider.IsAvailable;
                 IF CameraAvailable THEN
                   CameraProvider := CameraProvider.Create;
               END;

    OnAfterGetCurrRecord=BEGIN
                           SetEditableOnPictureActions;
                         END;

    ActionList=ACTIONS
    {
      { 2       ;    ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 3       ;1   ;Action    ;
                      Name=TakePicture;
                      CaptionML=[ENU=Take;
                                 ESM=Hacer;
                                 FRC=Pr‚lever;
                                 ENC=Take];
                      ToolTipML=[ENU=Activate the camera on the device.;
                                 ESM=Activa la c mara en el dispositivo.;
                                 FRC=Activez la cam‚ra sur l'appareil.;
                                 ENC=Activate the camera on the device.];
                      ApplicationArea=#Basic,#Suite;
                      Promoted=Yes;
                      Visible=CameraAvailable;
                      PromotedIsBig=Yes;
                      Image=Camera;
                      PromotedCategory=Process;
                      OnAction=VAR
                                 CameraOptions@1000 : DotNet "'Microsoft.Dynamics.Nav.ClientExtensions, Version=13.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Client.Capabilities.CameraOptions";
                               BEGIN
                                 TESTFIELD("No.");
                                 TESTFIELD(Name);

                                 IF NOT CameraAvailable THEN
                                   EXIT;

                                 CameraOptions := CameraOptions.CameraOptions;
                                 CameraOptions.Quality := 100;
                                 CameraProvider.RequestPictureAsync(CameraOptions);
                               END;
                                }
      { 4       ;1   ;Action    ;
                      Name=ImportPicture;
                      CaptionML=[ENU=Import;
                                 ESM=Importar;
                                 FRC=Importer;
                                 ENC=Import];
                      ToolTipML=[ENU=Import a picture file.;
                                 ESM=Importa un archivo de imagen.;
                                 FRC=Importez un fichier image.;
                                 ENC=Import a picture file.];
                      ApplicationArea=#Basic,#Suite;
                      Image=Import;
                      OnAction=VAR
                                 FileManagement@1001 : Codeunit 419;
                                 FileName@1000 : Text;
                                 ClientFileName@1002 : Text;
                               BEGIN
                                 TESTFIELD("No.");
                                 TESTFIELD(Name);

                                 IF Image.HASVALUE THEN
                                   IF NOT CONFIRM(OverrideImageQst) THEN
                                     EXIT;

                                 FileName := FileManagement.UploadFile(SelectPictureTxt,ClientFileName);
                                 IF FileName = '' THEN
                                   EXIT;

                                 CLEAR(Image);
                                 Image.IMPORTFILE(FileName,ClientFileName);
                                 IF NOT INSERT(TRUE) THEN
                                   MODIFY(TRUE);

                                 IF FileManagement.DeleteServerFile(FileName) THEN;
                               END;
                                }
      { 6       ;1   ;Action    ;
                      Name=ExportFile;
                      CaptionML=[ENU=Export;
                                 ESM=Exportar;
                                 FRC=Exporter;
                                 ENC=Export];
                      ToolTipML=[ENU=Export the picture to a file.;
                                 ESM=Exporta la imagen a un archivo.;
                                 FRC=Exportez l'image vers un fichier.;
                                 ENC=Export the picture to a file.];
                      ApplicationArea=#Basic,#Suite;
                      Enabled=DeleteExportEnabled;
                      Image=Export;
                      OnAction=VAR
                                 FileManagement@1001 : Codeunit 419;
                                 ToFile@1003 : Text;
                                 ExportPath@1005 : Text;
                               BEGIN
                                 TESTFIELD("No.");
                                 TESTFIELD(Name);

                                 ToFile := STRSUBSTNO('%1 %2.jpg',"No.",Name);
                                 ExportPath := TEMPORARYPATH + "No." + FORMAT(Image.MEDIAID);
                                 Image.EXPORTFILE(ExportPath);

                                 FileManagement.ExportImage(ExportPath,ToFile);
                               END;
                                }
      { 7       ;1   ;Action    ;
                      Name=DeletePicture;
                      CaptionML=[ENU=Delete;
                                 ESM=Eliminar;
                                 FRC=Supprimer;
                                 ENC=Delete];
                      ToolTipML=[ENU=Delete the record.;
                                 ESM=Elimina el registro.;
                                 FRC=Supprimez l'enregistrement.;
                                 ENC=Delete the record.];
                      ApplicationArea=#Basic,#Suite;
                      Enabled=DeleteExportEnabled;
                      Image=Delete;
                      OnAction=BEGIN
                                 TESTFIELD("No.");

                                 IF NOT CONFIRM(DeleteImageQst) THEN
                                   EXIT;

                                 CLEAR(Image);
                                 MODIFY(TRUE);
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 1   ;1   ;Field     ;
                ToolTipML=[ENU=Specifies the picture of the contact, for example, a photograph if the contact is a person, or a logo if the contact is a company.;
                           ESM=Especifica la imagen del contacto, por ejemplo, una fotograf¡a si el contacto es una persona o un logotipo si el contacto es una empresa.;
                           FRC=Sp‚cifie l'image du contact, par exemple une photographie si le contact est une personne ou un logo s'il s'agit d'une compagnie.;
                           ENC=Specifies the picture of the contact, for example, a photograph if the contact is a person, or a logo if the contact is a company.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr=Image;
                ShowCaption=No }

  }
  CODE
  {
    VAR
      CameraProvider@1000 : DotNet "'Microsoft.Dynamics.Nav.ClientExtensions, Version=13.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Client.Capabilities.CameraProvider" WITHEVENTS RUNONCLIENT;
      CameraAvailable@1002 : Boolean;
      OverrideImageQst@1007 : TextConst 'ENU=The existing picture will be replaced. Do you want to continue?;ESM=Se sustituir  la imagen existente. ¨Quiere continuar?;FRC=L''image existante sera remplac‚e. Voulez-vous continuerÿ?;ENC=The existing picture will be replaced. Do you want to continue?';
      DeleteImageQst@1003 : TextConst 'ENU=Are you sure you want to delete the picture?;ESM=¨Est  seguro de que desea eliminar la imagen?;FRC=Voulez-vous vraiment supprimer l''imageÿ?;ENC=Are you sure you want to delete the picture?';
      SelectPictureTxt@1001 : TextConst 'ENU=Select a picture to upload;ESM=Seleccionar una imagen para cargar;FRC=S‚lectionner une image … charger;ENC=Select a picture to upload';
      DeleteExportEnabled@1005 : Boolean;

    LOCAL PROCEDURE SetEditableOnPictureActions@1();
    BEGIN
      DeleteExportEnabled := Image.HASVALUE;
    END;

    EVENT CameraProvider@1000::PictureAvailable@10(PictureName@1001 : Text;PictureFilePath@1000 : Text);
    VAR
      File@1003 : File;
      Instream@1004 : InStream;
    BEGIN
      IF (PictureName = '') OR (PictureFilePath = '') THEN
        EXIT;

      IF Image.HASVALUE THEN
        IF NOT CONFIRM(OverrideImageQst) THEN BEGIN
          IF ERASE(PictureFilePath) THEN;
          EXIT;
        END;

      File.OPEN(PictureFilePath);
      File.CREATEINSTREAM(Instream);

      CLEAR(Image);
      Image.IMPORTSTREAM(Instream,PictureName);
      IF NOT INSERT(TRUE) THEN
        MODIFY(TRUE);

      File.CLOSE;
      IF ERASE(PictureFilePath) THEN;
    END;

    BEGIN
    END.
  }
}

