OBJECT Report 1201 Post Direct Debit Collection
{
  OBJECT-PROPERTIES
  {
    Date=11/22/17;
    Time=12:00:00 PM;
    Version List=NAVW111.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Post Direct Debit Collection;
               ESM=Registrar cobro por adeudo directo;
               FRC=Valider recouvrement prÇlävement;
               ENC=Post Direct Debit Collection];
    ProcessingOnly=Yes;
    OnPreReport=BEGIN
                  GenJnlBatch.GET(GeneralJournalTemplateName,GeneralJournalBatchName);
                  GenJnlLine.SETRANGE("Journal Template Name",GeneralJournalTemplateName);
                  GenJnlLine.SETRANGE("Journal Batch Name",GeneralJournalBatchName);
                  IF GenJnlLine.FINDLAST THEN;
                  LastLineNo := GenJnlLine."Line No.";
                END;

    OnPostReport=BEGIN
                   Window.CLOSE;
                   IF CreateJnlOnly THEN
                     MESSAGE(JnlCreatedMsg,TotalCount - SkippedCount,SkippedCount)
                   ELSE
                     MESSAGE(PostedMsg,TotalCount - SkippedCount,SkippedCount);

                   IF SkippedCount = 0 THEN BEGIN
                     DirectDebitCollection.GET(DirectDebitCollectionNo);
                     DirectDebitCollection.Status := DirectDebitCollection.Status::Posted;
                     DirectDebitCollection.MODIFY;
                   END;
                 END;

  }
  DATASET
  {
    { 1   ;    ;DataItem;                    ;
               DataItemTable=Table1208;
               DataItemTableView=SORTING(Direct Debit Collection No.,Entry No.);
               OnPreDataItem=BEGIN
                               SETRANGE("Direct Debit Collection No.",DirectDebitCollectionNo);
                               SETRANGE(Status,Status::"File Created");
                               TotalCount := COUNT;
                               Window.OPEN(ProgressMsg);
                             END;

               OnAfterGetRecord=BEGIN
                                  CurrCount += 1;
                                  Window.UPDATE(1,CurrCount * 10000 DIV TotalCount);
                                  IF CreateJnlLine("Direct Debit Collection Entry") THEN BEGIN
                                    Status := Status::Posted;
                                    MODIFY;
                                  END ELSE
                                    SkippedCount += 1;
                                END;
                                 }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
      OnOpenPage=BEGIN
                   IF DirectDebitCollectionNo <> 0 THEN
                     IF DirectDebitCollection.GET(DirectDebitCollectionNo) THEN;
                 END;

    }
    CONTROLS
    {
      { 1   ;    ;Container ;
                  CaptionML=[ENU=Options;
                             ESM=Opciones;
                             FRC=Options;
                             ENC=Options];
                  ContainerType=ContentArea }

      { 3   ;1   ;Field     ;
                  CaptionML=[ENU=Direct Debit Collection No.;
                             ESM=Nß cobro por adeudo directo;
                             FRC=N¯ de recouvrement de prÇlävement;
                             ENC=Direct Debit Collection No.];
                  ToolTipML=[ENU=Specifies the direct debit collection that you want to post payment receipt for.;
                             ESM=Especifica el cobro por adeudo directo para el que desea registrar la recepci¢n de pago.;
                             FRC=SpÇcifie le recouvrement de prÇlävement pour lequel vous souhaitez reporter la rÇception paiement.;
                             ENC=Specifies the direct debit collection that you want to post payment receipt for.];
                  ApplicationArea=#Suite;
                  SourceExpr=DirectDebitCollectionNo;
                  TableRelation="Direct Debit Collection";
                  OnValidate=BEGIN
                               DirectDebitCollection.GET(DirectDebitCollectionNo);
                               DirectDebitCollection.TESTFIELD(Status,DirectDebitCollection.Status::"File Created");
                             END;

                  OnLookup=VAR
                             DirectDebitCollections@1000 : Page 1207;
                           BEGIN
                             DirectDebitCollection.SETRANGE(Status,DirectDebitCollection.Status::"File Created");
                             IF DirectDebitCollectionNo = 0 THEN
                               DirectDebitCollection.FINDLAST
                             ELSE
                               IF DirectDebitCollection.GET(DirectDebitCollectionNo) THEN;
                             DirectDebitCollections.LOOKUPMODE := TRUE;
                             DirectDebitCollections.SETRECORD(DirectDebitCollection);
                             DirectDebitCollections.SETTABLEVIEW(DirectDebitCollection);
                             IF DirectDebitCollections.RUNMODAL = ACTION::LookupOK THEN BEGIN
                               DirectDebitCollections.GETRECORD(DirectDebitCollection);
                               DirectDebitCollectionNo := DirectDebitCollection."No.";
                             END;
                           END;
                            }

      { 5   ;1   ;Field     ;
                  CaptionML=[ENU=Identifier;
                             ESM=Identificador;
                             FRC=Identificateur;
                             ENC=Identifier];
                  ToolTipML=[ENU=Specifies the collection.;
                             ESM=Especifica el cobro.;
                             FRC=SpÇcifie le prÇlävement.;
                             ENC=Specifies the collection.];
                  ApplicationArea=#Suite;
                  SourceExpr=DirectDebitCollection.Identifier;
                  Editable=FALSE }

      { 6   ;1   ;Field     ;
                  CaptionML=[ENU=Status;
                             ESM=Estado;
                             FRC=êtat;
                             ENC=Status];
                  ToolTipML=[ENU=Specifies the status of the collection.;
                             ESM=Especifica el estado del cobro.;
                             FRC=SpÇcifie l'Çtat du prÇlävement.;
                             ENC=Specifies the status of the collection.];
                  ApplicationArea=#Suite;
                  SourceExpr=DirectDebitCollection.Status;
                  Editable=FALSE }

      { 7   ;1   ;Field     ;
                  CaptionML=[ENU=Bank Account No.;
                             ESM=C¢d. cuenta banco;
                             FRC=N¯ compte bancaire;
                             ENC=Bank Account No.];
                  ToolTipML=[ENU=Specifies which of your company's bank accounts the collected payment will be transferred to from the customer's bank account.;
                             ESM=Especifica a cu†l de las cuentas bancarias de la empresa se transferir† el pago cobrado desde la cuenta bancaria del cliente.;
                             FRC=SpÇcifie les comptes bancaires de votre compagnie sur lesquels le paiement collectÇ va àtre transfÇrÇ Ö partir du compte bancaire du client.;
                             ENC=Specifies which of your company's bank accounts the collected payment will be transferred to from the customer's bank account.];
                  ApplicationArea=#Suite;
                  SourceExpr=DirectDebitCollection."To Bank Account No.";
                  Editable=FALSE }

      { 4   ;1   ;Field     ;
                  CaptionML=[ENU=General Journal Template;
                             ESM=Plantilla de libros diario general;
                             FRC=Modäle journal gÇnÇral;
                             ENC=General Journal Template];
                  ToolTipML=[ENU=Specifies the general journal template that the entries are placed in.;
                             ESM=Especifica el libro diario general en el que se colocan los movimientos.;
                             FRC=SpÇcifie le modäle journal gÇnÇral dans lequel les Çcritures sont saisies.;
                             ENC=Specifies the general journal template that the entries are placed in.];
                  ApplicationArea=#Suite;
                  SourceExpr=GeneralJournalTemplateName;
                  TableRelation="Gen. Journal Template";
                  OnValidate=VAR
                               GenJournalTemplate@1001 : Record 80;
                             BEGIN
                               IF GeneralJournalTemplateName = '' THEN BEGIN
                                 GeneralJournalBatchName := '';
                                 EXIT;
                               END;
                               GenJournalTemplate.GET(GeneralJournalTemplateName);
                               IF NOT (GenJournalTemplate.Type IN
                                       [GenJournalTemplate.Type::General,GenJournalTemplate.Type::Purchases,GenJournalTemplate.Type::Payments,
                                        GenJournalTemplate.Type::Sales,GenJournalTemplate.Type::"Cash Receipts"])
                               THEN
                                 ERROR(
                                   TemplateTypeErr,
                                   GenJournalTemplate.Type::General,GenJournalTemplate.Type::Purchases,GenJournalTemplate.Type::Payments,
                                   GenJournalTemplate.Type::Sales,GenJournalTemplate.Type::"Cash Receipts");

                               GenJournalTemplate.TESTFIELD("No. Series");
                             END;
                              }

      { 2   ;1   ;Field     ;
                  CaptionML=[ENU=General Journal Batch;
                             ESM=Lote diario general;
                             FRC=Nom journal gÇnÇral;
                             ENC=General Journal Batch];
                  ToolTipML=[ENU=Specifies the general journal batch that the entries are placed in.;
                             ESM=Especifica la secci¢n del diario general en la que se colocan los movimientos.;
                             FRC=SpÇcifie le lot journal gÇnÇral dans lequel les Çcritures sont saisies.;
                             ENC=Specifies the general journal batch that the entries are placed in.];
                  ApplicationArea=#Suite;
                  SourceExpr=GeneralJournalBatchName;
                  TableRelation="Gen. Journal Batch";
                  OnValidate=VAR
                               GenJournalBatch@1000 : Record 232;
                             BEGIN
                               GenJournalBatch.GET(GeneralJournalTemplateName,GeneralJournalBatchName);
                               GenJournalBatch.TESTFIELD(Recurring,FALSE);
                               GenJournalBatch.TESTFIELD("No. Series");
                             END;

                  OnLookup=VAR
                             GenJournalBatch@1000 : Record 232;
                             GeneralJournalBatches@1001 : Page 251;
                           BEGIN
                             GenJournalBatch.SETRANGE("Journal Template Name",GeneralJournalTemplateName);
                             GeneralJournalBatches.SETTABLEVIEW(GenJournalBatch);
                             IF GeneralJournalBatches.RUNMODAL = ACTION::OK THEN BEGIN
                               GeneralJournalBatches.GETRECORD(GenJournalBatch);
                               GeneralJournalBatchName := GenJournalBatch.Name;
                             END;
                           END;
                            }

      { 8   ;1   ;Field     ;
                  CaptionML=[ENU=Create Journal Only;
                             ESM=Crear solo diario;
                             FRC=CrÇer journal uniquement;
                             ENC=Create Journal Only];
                  ToolTipML=[ENU=Specifies if you want to post the payment receipt when you choose the OK button. The payment receipt will be prepared in the specified journal and will not be posted until someone posts the journal lines in question.;
                             ESM=Especifica si desea registrar la recepci¢n de pago al elegir el bot¢n Aceptar. La recepci¢n de pago se preparar† en el diario especificado y no se registrar† hasta que alguien registre las l°neas del diario en cuesti¢n.;
                             FRC=Indique si vous souhaitez reporter le reáu de paiement lorsque vous sÇlectionnez le bouton OK. Le reáu de paiement est prÇparÇ dans le journal spÇcifiÇ et n'est pas reportÇ tant qu'une autre personne ne reporte pas les lignes journal en question.;
                             ENC=Specifies if you want to post the payment receipt when you choose the OK button. The payment receipt will be prepared in the specified journal and will not be posted until someone posts the journal lines in question.];
                  ApplicationArea=#Suite;
                  SourceExpr=CreateJnlOnly }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      TemplateTypeErr@1000 : TextConst '@@@="%1..5 lists Type=General,Purchases,Payments,Sales,Cash Receipts";ENU=Only General Journal templates of type %1, %2, %3, %4, or %5 are allowed.;ESM=Solo se permiten plantillas de diario general de tipo %1, %2, %3, %4 o %5.;FRC=Seuls les modäles journal gÇnÇral de type %1, %2, %3, %4 ou %5 sont autorisÇs.;ENC=Only General Journal templates of type %1, %2, %3, %4, or %5 are allowed.';
      DirectDebitCollection@1004 : Record 1207;
      GenJnlLine@1006 : Record 81;
      GenJnlBatch@1017 : Record 232;
      GenJnlPostLine@1018 : Codeunit 12;
      DimMgt@1009 : Codeunit 408;
      Window@1013 : Dialog;
      DirectDebitCollectionNo@1003 : Integer;
      GeneralJournalTemplateName@1001 : Code[10];
      GeneralJournalBatchName@1002 : Code[10];
      LastLineNo@1005 : Integer;
      PostingTxt@1007 : TextConst '@@@="%1=Doc. type, %2=Doc. No. E.g. Invoice 234 Direct Debit";ENU=%1 %2 Direct Debit;ESM=Adeudo directo de %1 %2;FRC=%1 %2 PrÇlävement;ENC=%1 %2 Direct Debit';
      CreateJnlOnly@1008 : Boolean;
      TotalCount@1010 : Integer;
      CurrCount@1011 : Integer;
      ProgressMsg@1012 : TextConst 'ENU=#1##################;ESM=#1##################;FRC=#1##################;ENC=#1##################';
      JnlCreatedMsg@1014 : TextConst '@@@=%1 and %2 are both numbers / count.;ENU=%1 journal lines were created. %2 lines were skipped.;ESM=Se crearon %1 l°neas de diario. Se omitieron %2 l°neas.;FRC=%1 lignes journal ont ÇtÇ crÇÇes. %2 lignes ont ÇtÇ ignorÇes.;ENC=%1 journal lines were created. %2 lines were skipped.';
      PostedMsg@1015 : TextConst '@@@=%1 and %2 are both numbers / count.;ENU=%1 payments were posted. %2 lines were skipped.;ESM=Se registraron %1 pagos. Se omitieron %2 l°neas.;FRC=%1 paiements ont ÇtÇ reportÇs. %2 lignes ont ÇtÇ ignorÇes.;ENC=%1 payments were posted. %2 lines were skipped.';
      SkippedCount@1016 : Integer;

    PROCEDURE SetCollectionEntry@4(NewCollectionEntry@1000 : Integer);
    BEGIN
      DirectDebitCollectionNo := NewCollectionEntry;
      DirectDebitCollection.GET(DirectDebitCollectionNo);
    END;

    PROCEDURE SetJnlBatch@1(NewGenJnlTemplateName@1000 : Code[10];NewGenJnlBachName@1001 : Code[10]);
    BEGIN
      GeneralJournalTemplateName := NewGenJnlTemplateName;
      GeneralJournalBatchName := NewGenJnlBachName;
      CurrReport.USEREQUESTPAGE := FALSE;
    END;

    PROCEDURE SetCreateJnlOnly@2(NewCreateJnlOnly@1000 : Boolean);
    BEGIN
      CreateJnlOnly := NewCreateJnlOnly;
    END;

    LOCAL PROCEDURE CreateJnlLine@3(VAR DirectDebitCollectionEntry@1000 : Record 1208) : Boolean;
    VAR
      CustLedgEntry@1001 : Record 21;
      GenJournalTemplate@1004 : Record 80;
      NoSeriesMgt@1003 : Codeunit 396;
      DimensionSetIDArr@1002 : ARRAY [10] OF Integer;
    BEGIN
      CustLedgEntry.GET(DirectDebitCollectionEntry."Applies-to Entry No.");
      CustLedgEntry.CALCFIELDS("Remaining Amount");
      IF NOT CustLedgEntry.Open OR (CustLedgEntry."Remaining Amount" < DirectDebitCollectionEntry."Transfer Amount") THEN
        EXIT(FALSE);

      LastLineNo += 10000;
      GenJnlLine."Journal Template Name" := GeneralJournalTemplateName;
      GenJnlLine."Journal Batch Name" := GeneralJournalBatchName;
      GenJnlLine."Line No." := LastLineNo;
      IF GenJnlBatch."No. Series" <> '' THEN
        IF CreateJnlOnly THEN
          GenJnlLine.SetUpNewLine(GenJnlLine,GenJnlLine."Balance (LCY)",TRUE)
        ELSE BEGIN
          GenJnlLine."Document No." := NoSeriesMgt.GetNextNo(GenJnlBatch."No. Series",GenJnlLine."Posting Date",TRUE);
          GenJournalTemplate.GET(GeneralJournalTemplateName);
          GenJnlLine."Source Code" := GenJournalTemplate."Source Code";
        END;

      GenJnlLine.VALIDATE("Document Type",GenJnlLine."Document Type"::Payment);
      GenJnlLine.VALIDATE("Account Type",GenJnlLine."Account Type"::Customer);
      GenJnlLine.VALIDATE("Account No.",DirectDebitCollectionEntry."Customer No.");
      GenJnlLine.VALIDATE("Bal. Account Type",GenJnlLine."Bal. Account Type"::"Bank Account");
      GenJnlLine.VALIDATE("Bal. Account No.",DirectDebitCollection."To Bank Account No.");
      DimensionSetIDArr[1] := GenJnlLine."Dimension Set ID";
      DimensionSetIDArr[2] := CustLedgEntry."Dimension Set ID";
      GenJnlLine."Dimension Set ID" :=
        DimMgt.GetCombinedDimensionSetID(
          DimensionSetIDArr,GenJnlLine."Shortcut Dimension 1 Code",GenJnlLine."Shortcut Dimension 2 Code");

      GenJnlLine.VALIDATE("Posting Date",DirectDebitCollectionEntry."Transfer Date");
      GenJnlLine.Description :=
        COPYSTR(
          STRSUBSTNO(
            PostingTxt,CustLedgEntry."Document Type",CustLedgEntry."Document No."),1,MAXSTRLEN(GenJnlLine.Description));
      GenJnlLine.VALIDATE("Currency Code",CustLedgEntry."Currency Code");
      GenJnlLine.VALIDATE(Amount,-DirectDebitCollectionEntry."Transfer Amount");
      GenJnlLine."Applies-to Doc. Type" := CustLedgEntry."Document Type";
      GenJnlLine.VALIDATE("Applies-to Doc. No.",CustLedgEntry."Document No.");
      IF CreateJnlOnly THEN
        GenJnlLine.INSERT(TRUE)
      ELSE
        GenJnlPostLine.RunWithCheck(GenJnlLine);
      EXIT(TRUE);
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

