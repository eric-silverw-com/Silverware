OBJECT Page 594 Change Log Setup (Field) List
{
  OBJECT-PROPERTIES
  {
    Date=11/27/18;
    Time=11:10:25 PM;
    Version List=;
  }
  PROPERTIES
  {
    CaptionML=ENU=Change Log Setup (Field) List;
    InsertAllowed=No;
    DeleteAllowed=No;
    SourceTable=Table2000000041;
    DataCaptionExpr=PageCaption;
    PageType=List;
    OnInit=BEGIN
             LogDeletionVisible := TRUE;
             LogModificationVisible := TRUE;
             LogInsertionVisible := TRUE;
           END;

    OnOpenPage=BEGIN
                 FILTERGROUP(2);
                 SETRANGE(Class,Class::Normal);
                 FILTERGROUP(0);
                 PageCaption := FORMAT(TableNo) + ' ' + TableName;
               END;

    OnAfterGetRecord=BEGIN
                       GetRec;
                       TransFromRec;
                     END;

    OnAfterGetCurrRecord=BEGIN
                           GetRec;
                           TransFromRec;
                         END;

  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 1   ;1   ;Group     ;
                GroupType=Repeater }

    { 2   ;2   ;Field     ;
                Lookup=No;
                CaptionML=ENU=No.;
                ToolTipML=ENU=Specifies the number of the field.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="No.";
                Editable=FALSE }

    { 4   ;2   ;Field     ;
                DrillDown=No;
                CaptionML=ENU=Field Caption;
                ToolTipML=ENU=Specifies the caption of the field, that is, the name that will be shown in the user interface.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Field Caption";
                Editable=FALSE }

    { 6   ;2   ;Field     ;
                Name=Log Insertion;
                CaptionML=ENU=Log Insertion;
                ToolTipML=ENU=Specifies whether to log the insertion for the selected line on the change log.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=LogIns;
                Visible=LogInsertionVisible;
                OnValidate=BEGIN
                             IF NOT InsVisible THEN BEGIN
                               LogInsertionVisible := FALSE;
                               ERROR(CannotChangeColumnErr);
                             END;
                             UpdateRec;
                           END;
                            }

    { 8   ;2   ;Field     ;
                Name=Log Modification;
                CaptionML=ENU=Log Modification;
                ToolTipML=ENU=Specifies whether to log the modification for the selected line on the change log.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=LogMod;
                Visible=LogModificationVisible;
                OnValidate=BEGIN
                             IF NOT ModVisible THEN BEGIN
                               LogModificationVisible := FALSE;
                               ERROR(CannotChangeColumnErr);
                             END;
                             UpdateRec;
                           END;
                            }

    { 10  ;2   ;Field     ;
                Name=Log Deletion;
                CaptionML=ENU=Log Deletion;
                ToolTipML=ENU=Specifies whether to log the deletion for the selected line on the change log.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=LogDel;
                Visible=LogDeletionVisible;
                OnValidate=BEGIN
                             IF NOT DelVisible THEN BEGIN
                               LogDeletionVisible := FALSE;
                               ERROR(CannotChangeColumnErr);
                             END;
                             UpdateRec;
                           END;
                            }

    { 1900000007;0;Container;
                ContainerType=FactBoxArea }

    { 1900383207;1;Part   ;
                Visible=FALSE;
                PartType=System;
                SystemPartID=RecordLinks }

    { 1905767507;1;Part   ;
                Visible=FALSE;
                PartType=System;
                SystemPartID=Notes }

  }
  CODE
  {
    VAR
      ChangeLogSetupField@1000 : Record 404;
      CannotChangeColumnErr@1004 : TextConst 'ENU=You cannot change this column.';
      LogIns@1001 : Boolean;
      LogMod@1008 : Boolean;
      LogDel@1009 : Boolean;
      InsVisible@1006 : Boolean;
      ModVisible@1005 : Boolean;
      DelVisible@1003 : Boolean;
      LogInsertionVisible@19000347 : Boolean INDATASET;
      LogModificationVisible@19032389 : Boolean INDATASET;
      LogDeletionVisible@19056880 : Boolean INDATASET;
      PageCaption@1002 : Text[250];

    [External]
    PROCEDURE SelectColumn@1(NewInsVisible@1000 : Boolean;NewModVisible@1001 : Boolean;NewDelVisible@1002 : Boolean);
    BEGIN
      InsVisible := NewInsVisible;
      ModVisible := NewModVisible;
      DelVisible := NewDelVisible;

      LogInsertionVisible := InsVisible;
      LogModificationVisible := ModVisible;
      LogDeletionVisible := DelVisible;
    END;

    LOCAL PROCEDURE UpdateRec@18();
    BEGIN
      GetRec;
      TransToRec;
      WITH ChangeLogSetupField DO
        IF NOT ("Log Insertion" OR "Log Modification" OR "Log Deletion") THEN BEGIN
          IF DELETE THEN;
        END ELSE
          IF NOT MODIFY THEN
            INSERT;
    END;

    LOCAL PROCEDURE GetRec@2();
    BEGIN
      IF NOT ChangeLogSetupField.GET(TableNo,"No.") THEN BEGIN
        ChangeLogSetupField.INIT;
        ChangeLogSetupField."Table No." := TableNo;
        ChangeLogSetupField."Field No." := "No.";
      END;
    END;

    LOCAL PROCEDURE TransFromRec@5();
    BEGIN
      LogIns := ChangeLogSetupField."Log Insertion";
      LogMod := ChangeLogSetupField."Log Modification";
      LogDel := ChangeLogSetupField."Log Deletion";
    END;

    LOCAL PROCEDURE TransToRec@6();
    BEGIN
      ChangeLogSetupField."Log Insertion" := LogIns;
      ChangeLogSetupField."Log Modification" := LogMod;
      ChangeLogSetupField."Log Deletion" := LogDel;
    END;

    BEGIN
    END.
  }
}

