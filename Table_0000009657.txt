OBJECT Table 9657 Custom Report Selection
{
  OBJECT-PROPERTIES
  {
    Date=11/27/18;
    Time=11:15:49 PM;
    Version List=;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               TESTFIELD("Report ID");
               CheckEmailBodyUsage;
             END;

    OnModify=BEGIN
               TESTFIELD("Report ID");
               CheckEmailBodyUsage;
             END;

    CaptionML=ENU=Custom Report Selection;
  }
  FIELDS
  {
    { 1   ;   ;Source Type         ;Integer       ;CaptionML=ENU=Source Type }
    { 2   ;   ;Source No.          ;Code20        ;TableRelation=IF (Source Type=CONST(18)) Customer.No.
                                                                 ELSE IF (Source Type=CONST(23)) Vendor.No.;
                                                   CaptionML=ENU=Source No. }
    { 3   ;   ;Usage               ;Option        ;CaptionML=ENU=Usage;
                                                   OptionCaptionML=ENU=S.Quote,S.Order,S.Invoice,S.Cr.Memo,S.Test,P.Quote,P.Order,P.Invoice,P.Cr.Memo,P.Receipt,P.Ret.Shpt.,P.Test,B.Stmt,B.Recon.Test,B.Check,Reminder,Fin.Charge,Rem.Test,F.C.Test,Prod. Order,S.Blanket,P.Blanket,M1,M2,M3,M4,Inv1,Inv2,Inv3,SM.Quote,SM.Order,SM.Invoice,SM.Credit Memo,SM.Contract Quote,SM.Contract,SM.Test,S.Return,P.Return,S.Shipment,S.Ret.Rcpt.,S.Work Order,Invt. Period Test,SM.Shipment,S.Test Prepmt.,P.Test Prepmt.,S.Arch. Quote,S.Arch. Order,P.Arch. Quote,P.Arch. Order,S. Arch. Return Order,P. Arch. Return Order,Asm. Order,P.Assembly Order,S.Order Pick Instruction,S. Arch. Blanket Order,P. Arch. Blanket Order,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,C.Statement,V.Remittance,JQ,S.Invoice Draft,Pro Forma S. Invoice;
                                                   OptionString=S.Quote,S.Order,S.Invoice,S.Cr.Memo,S.Test,P.Quote,P.Order,P.Invoice,P.Cr.Memo,P.Receipt,P.Ret.Shpt.,P.Test,B.Stmt,B.Recon.Test,B.Check,Reminder,Fin.Charge,Rem.Test,F.C.Test,Prod. Order,S.Blanket,P.Blanket,M1,M2,M3,M4,Inv1,Inv2,Inv3,SM.Quote,SM.Order,SM.Invoice,SM.Credit Memo,SM.Contract Quote,SM.Contract,SM.Test,S.Return,P.Return,S.Shipment,S.Ret.Rcpt.,S.Work Order,Invt. Period Test,SM.Shipment,S.Test Prepmt.,P.Test Prepmt.,S.Arch. Quote,S.Arch. Order,P.Arch. Quote,P.Arch. Order,S. Arch. Return Order,P. Arch. Return Order,Asm. Order,P.Assembly Order,S.Order Pick Instruction,S. Arch. Blanket Order,P. Arch. Blanket Order,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,C.Statement,V.Remittance,JQ,S.Invoice Draft,Pro Forma S. Invoice }
    { 4   ;   ;Sequence            ;Integer       ;AutoIncrement=Yes;
                                                   CaptionML=ENU=Sequence }
    { 5   ;   ;Report ID           ;Integer       ;TableRelation=AllObjWithCaption."Object ID" WHERE (Object Type=CONST(Report));
                                                   OnValidate=BEGIN
                                                                CALCFIELDS("Report Caption");
                                                              END;

                                                   CaptionML=ENU=Report ID }
    { 6   ;   ;Report Caption      ;Text250       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObjWithCaption."Object Caption" WHERE (Object Type=CONST(Report),
                                                                                                                Object ID=FIELD(Report ID)));
                                                   CaptionML=ENU=Report Caption;
                                                   Editable=No }
    { 7   ;   ;Custom Report Layout Code;Code20   ;TableRelation="Custom Report Layout" WHERE (Code=FIELD(Custom Report Layout Code));
                                                   OnValidate=BEGIN
                                                                CALCFIELDS("Custom Report Description");
                                                              END;

                                                   CaptionML=ENU=Custom Report Layout Code }
    { 8   ;   ;Custom Report Description;Text250  ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Custom Report Layout".Description WHERE (Code=FIELD(Custom Report Layout Code)));
                                                   CaptionML=ENU=Custom Report Description;
                                                   Editable=No }
    { 9   ;   ;Send To Email       ;Text200       ;CaptionML=ENU=Send To Email }
    { 19  ;   ;Use for Email Attachment;Boolean   ;InitValue=Yes;
                                                   OnValidate=BEGIN
                                                                IF NOT "Use for Email Body" THEN
                                                                  VALIDATE("Email Body Layout Code",'');
                                                              END;

                                                   CaptionML=ENU=Use for Email Attachment }
    { 20  ;   ;Use for Email Body  ;Boolean       ;OnValidate=BEGIN
                                                                IF NOT "Use for Email Body" THEN
                                                                  VALIDATE("Email Body Layout Code",'');
                                                              END;

                                                   CaptionML=ENU=Use for Email Body }
    { 21  ;   ;Email Body Layout Code;Code20      ;TableRelation="Custom Report Layout" WHERE (Code=FIELD(Email Body Layout Code),
                                                                                               Report ID=FIELD(Report ID));
                                                   OnValidate=BEGIN
                                                                IF "Email Body Layout Code" <> '' THEN
                                                                  TESTFIELD("Use for Email Body",TRUE);
                                                                CALCFIELDS("Email Body Layout Description");
                                                              END;

                                                   CaptionML=ENU=Email Body Layout Code }
    { 22  ;   ;Email Body Layout Description;Text250;
                                                   FieldClass=FlowField;
                                                   CalcFormula=Lookup("Custom Report Layout".Description WHERE (Code=FIELD(Email Body Layout Code)));
                                                   CaptionML=ENU=Email Body Layout Description;
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Source Type,Source No.,Usage,Sequence   ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      EmailBodyIsAlreadyDefinedErr@1000 : TextConst '@@@="%1 = Usage, for example Sales Invoice";ENU=An email body is already defined for %1.';
      CannotBeUsedAsAnEmailBodyErr@1001 : TextConst '@@@="%1 = Report ID,%2 = Type";ENU=Report %1 uses the %2, which cannot be used as an email body.';

    [External]
    PROCEDURE InitUsage@3();
    BEGIN
      Usage := xRec.Usage;
    END;

    [External]
    PROCEDURE FilterReportUsage@1(NewSourceType@1002 : Integer;NewSourceNo@1001 : Code[20];NewUsage@1000 : Option);
    BEGIN
      RESET;
      SETRANGE("Source Type",NewSourceType);
      SETRANGE("Source No.",NewSourceNo);
      SETRANGE(Usage,NewUsage);
    END;

    [External]
    PROCEDURE FilterEmailBodyUsage@5(NewSourceType@1002 : Integer;NewSourceNo@1001 : Code[20];NewUsage@1000 : Option);
    BEGIN
      FilterReportUsage(NewSourceType,NewSourceNo,NewUsage);
      SETRANGE("Use for Email Body",TRUE);
    END;

    LOCAL PROCEDURE CheckEmailBodyUsage@4();
    VAR
      CustomReportSelection@1001 : Record 9657;
      ReportLayoutSelection@1000 : Record 9651;
    BEGIN
      IF "Use for Email Body" THEN BEGIN
        CustomReportSelection.FilterEmailBodyUsage("Source Type","Source No.",Usage);
        CustomReportSelection.SETFILTER(Sequence,'<>%1',Sequence);
        IF NOT CustomReportSelection.ISEMPTY THEN
          ERROR(EmailBodyIsAlreadyDefinedErr,Usage);

        IF "Email Body Layout Code" = '' THEN
          IF ReportLayoutSelection.GetDefaultType("Report ID") =
             ReportLayoutSelection.Type::"RDLC (built-in)"
          THEN
            ERROR(CannotBeUsedAsAnEmailBodyErr,"Report ID",ReportLayoutSelection.Type);
      END;
    END;

    LOCAL PROCEDURE LookupCustomReportLayout@2(CurrentLayoutCode@1000 : Code[20]) : Code[20];
    VAR
      CustomReportLayout@1002 : Record 9650;
    BEGIN
      IF CustomReportLayout.LookupLayoutOK("Report ID") THEN
        EXIT(CustomReportLayout.Code);

      EXIT(CurrentLayoutCode);
    END;

    [External]
    PROCEDURE LookupCustomReportDescription@6();
    BEGIN
      VALIDATE("Custom Report Layout Code",LookupCustomReportLayout("Custom Report Layout Code"));
    END;

    [External]
    PROCEDURE LookupEmailBodyDescription@7();
    BEGIN
      VALIDATE("Email Body Layout Code",LookupCustomReportLayout("Custom Report Layout Code"));
    END;

    BEGIN
    END.
  }
}

