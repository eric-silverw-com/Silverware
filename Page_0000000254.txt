OBJECT Page 254 Purchase Journal
{
  OBJECT-PROPERTIES
  {
    Date=11/27/18;
    Time=11:10:11 PM;
    Version List=;
  }
  PROPERTIES
  {
    CaptionML=ENU=Purchase Journals;
    ApplicationArea=#Basic,#Suite;
    SaveValues=Yes;
    SourceTable=Table81;
    DataCaptionExpr=DataCaption;
    DelayedInsert=Yes;
    PageType=Worksheet;
    UsageCategory=Tasks;
    AutoSplitKey=Yes;
    PromotedActionCategoriesML=ENU=New,Process,Report,Page;
    OnInit=BEGIN
             TotalBalanceVisible := TRUE;
             BalanceVisible := TRUE;
             AmountVisible := TRUE;

             // Get simple / classic mode for this page except when called from a webservices (SOAP or ODATA)
             IF (CURRENTCLIENTTYPE = CLIENTTYPE::SOAP) OR
                (CURRENTCLIENTTYPE = CLIENTTYPE::OData) OR (CURRENTCLIENTTYPE = CLIENTTYPE::ODataV4)
             THEN
               IsSimplePage := FALSE
             ELSE
               IsSimplePage := GenJnlManagement.GetJournalSimplePageModePreference(PAGE::"Purchase Journal");
           END;

    OnOpenPage=VAR
                 ServerConfigSettingHandler@1001 : Codeunit 6723;
                 JnlSelected@1000 : Boolean;
                 LastGenJnlBatch@1003 : Code[10];
               BEGIN
                 IsSaasExcelAddinEnabled := ServerConfigSettingHandler.GetIsSaasExcelAddinEnabled;
                 IF ClientTypeManagement.GetCurrentClientType = CLIENTTYPE::ODataV4 THEN
                   EXIT;

                 SetControlVisibility;
                 BalAccName := '';
                 IF IsOpenedFromBatch THEN BEGIN
                   CurrentJnlBatchName := "Journal Batch Name";
                   GenJnlManagement.OpenJnl(CurrentJnlBatchName,Rec);
                   EXIT;
                 END;
                 GenJnlManagement.TemplateSelection(PAGE::"Purchase Journal",2,FALSE,Rec,JnlSelected);
                 IF NOT JnlSelected THEN
                   ERROR('');

                 LastGenJnlBatch := GenJnlManagement.GetLastViewedJournalBatchName(PAGE::"Purchase Journal");
                 IF LastGenJnlBatch <> '' THEN
                   CurrentJnlBatchName := LastGenJnlBatch;
                 GenJnlManagement.OpenJnl(CurrentJnlBatchName,Rec);
               END;

    OnAfterGetRecord=BEGIN
                       ShowShortcutDimCode(ShortcutDimCode);
                       DocumentAmount := ABS(Amount);
                       GenJnlManagement.GetAccounts(Rec,AccName,BalAccName);
                     END;

    OnNewRecord=BEGIN
                  UpdateBalance;
                  EnableApplyEntriesAction;
                  SetUpNewLine(xRec,Balance,BelowxRec);
                  CLEAR(DocumentAmount);
                  // Setting account type to Vendor and doc type to invoice on new line when in simple page mode
                  IF IsSimplePage THEN BEGIN
                    VALIDATE("Account Type","Account Type"::Vendor);
                    VALIDATE("Document Type","Document Type"::Invoice);
                  END;
                  CLEAR(ShortcutDimCode);
                END;

    OnAfterGetCurrRecord=BEGIN
                           HasIncomingDocument := "Incoming Document Entry No." <> 0;
                           GenJnlManagement.GetAccounts(Rec,AccName,BalAccName);
                           UpdateBalance;
                           EnableApplyEntriesAction;
                           CurrPage.IncomingDocAttachFactBox.PAGE.LoadDataFromRecord(Rec);
                         END;

    ActionList=ACTIONS
    {
      { 1900000003;0 ;ActionContainer;
                      ActionContainerType=RelatedInformation }
      { 67      ;1   ;ActionGroup;
                      CaptionML=ENU=&Line;
                      Image=Line }
      { 68      ;2   ;Action    ;
                      AccessByPermission=TableData 348=R;
                      ShortCutKey=Shift+Ctrl+D;
                      CaptionML=ENU=Dimensions;
                      ToolTipML=ENU=View or edit dimensions, such as area, project, or department, that you can assign to sales and purchase documents to distribute costs and analyze transaction history.;
                      ApplicationArea=#Dimensions;
                      Promoted=Yes;
                      Image=Dimensions;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 ShowDimensions;
                                 CurrPage.SAVERECORD;
                               END;
                                }
      { 41      ;1   ;ActionGroup;
                      CaptionML=ENU=A&ccount;
                      Image=ChartOfAccounts }
      { 42      ;2   ;Action    ;
                      ShortCutKey=Shift+F7;
                      CaptionML=ENU=Card;
                      ToolTipML=ENU=View or change detailed information about the record on the document or journal line.;
                      ApplicationArea=#Basic,#Suite;
                      RunObject=Codeunit 15;
                      Image=EditLines }
      { 43      ;2   ;Action    ;
                      ShortCutKey=Ctrl+F7;
                      CaptionML=ENU=Ledger E&ntries;
                      ToolTipML=ENU=View the history of transactions that have been posted for the selected record.;
                      ApplicationArea=#Basic,#Suite;
                      RunObject=Codeunit 14;
                      Promoted=No;
                      Image=GLRegisters;
                      PromotedCategory=Process }
      { 1900000004;0 ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 44      ;1   ;ActionGroup;
                      CaptionML=ENU=F&unctions;
                      Image=Action }
      { 7       ;2   ;Action    ;
                      CaptionML=ENU=Renumber Document Numbers;
                      ToolTipML=ENU=Resort the numbers in the Document No. column to avoid posting errors because the document numbers are not in sequence. Entry applications and line groupings are preserved.;
                      ApplicationArea=#Basic,#Suite;
                      Image=EditLines;
                      OnAction=BEGIN
                                 RenumberDocumentNo
                               END;
                                }
      { 97      ;2   ;Action    ;
                      ShortCutKey=Shift+F11;
                      Ellipsis=Yes;
                      CaptionML=ENU=Apply Entries;
                      ToolTipML=ENU=Apply the payment amount on a journal line to a sales or purchase document that was already posted for a customer or vendor. This updates the amount on the posted document, and the document can either be partially paid, or closed as paid or refunded.;
                      ApplicationArea=#Basic,#Suite;
                      RunObject=Codeunit 225;
                      Promoted=Yes;
                      Enabled=ApplyEntriesActionEnabled;
                      Image=ApplyEntries;
                      PromotedCategory=Process }
      { 98      ;2   ;Action    ;
                      CaptionML=ENU=Insert Conv. $ Rndg. Lines;
                      ToolTipML=ENU=Insert a rounding correction line in the journal. This rounding correction line will balance in $ when amounts in the foreign currency also balance. You can then post the journal.;
                      ApplicationArea=#Basic,#Suite;
                      RunObject=Codeunit 407;
                      Image=InsertCurrency }
      { 21      ;2   ;ActionGroup;
                      Name=IncomingDocument;
                      CaptionML=ENU=Incoming Document;
                      ActionContainerType=NewDocumentItems;
                      Image=Documents }
      { 19      ;3   ;Action    ;
                      Name=IncomingDocCard;
                      CaptionML=ENU=View Incoming Document;
                      ToolTipML=ENU=View any incoming document records and file attachments that exist for the entry or document.;
                      ApplicationArea=#Basic,#Suite;
                      Enabled=HasIncomingDocument;
                      Image=ViewOrder;
                      OnAction=VAR
                                 IncomingDocument@1000 : Record 130;
                               BEGIN
                                 IncomingDocument.ShowCardFromEntryNo("Incoming Document Entry No.");
                               END;
                                }
      { 17      ;3   ;Action    ;
                      Name=SelectIncomingDoc;
                      AccessByPermission=TableData 130=R;
                      CaptionML=ENU=Select Incoming Document;
                      ToolTipML=ENU=Select an incoming document record and file attachment that you want to link to the entry or document.;
                      ApplicationArea=#Basic,#Suite;
                      Image=SelectLineToApply;
                      OnAction=VAR
                                 IncomingDocument@1000 : Record 130;
                               BEGIN
                                 VALIDATE("Incoming Document Entry No.",IncomingDocument.SelectIncomingDocument("Incoming Document Entry No.",RECORDID));
                               END;
                                }
      { 15      ;3   ;Action    ;
                      Name=IncomingDocAttachFile;
                      Ellipsis=Yes;
                      CaptionML=ENU=Create Incoming Document from File;
                      ToolTipML=ENU=Create an incoming document record by selecting a file to attach, and then link the incoming document record to the entry or document.;
                      ApplicationArea=#Basic,#Suite;
                      Enabled=NOT HasIncomingDocument;
                      Image=Attach;
                      OnAction=VAR
                                 IncomingDocumentAttachment@1000 : Record 133;
                               BEGIN
                                 IncomingDocumentAttachment.NewAttachmentFromGenJnlLine(Rec);
                               END;
                                }
      { 13      ;3   ;Action    ;
                      Name=RemoveIncomingDoc;
                      CaptionML=ENU=Remove Incoming Document;
                      ToolTipML=ENU=Remove any incoming document records and file attachments.;
                      ApplicationArea=#Basic,#Suite;
                      Enabled=HasIncomingDocument;
                      Image=RemoveLine;
                      OnAction=VAR
                                 IncomingDocument@1000 : Record 130;
                               BEGIN
                                 IF IncomingDocument.GET("Incoming Document Entry No.") THEN
                                   IncomingDocument.RemoveLinkToRelatedRecord;
                                 "Incoming Document Entry No." := 0;
                                 MODIFY(TRUE);
                               END;
                                }
      { 45      ;1   ;ActionGroup;
                      CaptionML=ENU=P&osting;
                      Image=Post }
      { 46      ;2   ;Action    ;
                      ShortCutKey=Ctrl+F11;
                      CaptionML=ENU=Reconcile;
                      ToolTipML=ENU=View the balances on bank accounts that are marked for reconciliation, usually liquid accounts.;
                      ApplicationArea=#Basic,#Suite;
                      Image=Reconcile;
                      OnAction=BEGIN
                                 GLReconcile.SetGenJnlLine(Rec);
                                 GLReconcile.RUN;
                               END;
                                }
      { 47      ;2   ;Action    ;
                      Ellipsis=Yes;
                      CaptionML=ENU=Test Report;
                      ToolTipML=ENU=View a test report so that you can find and correct any errors before you perform the actual posting of the journal or document.;
                      ApplicationArea=#Basic,#Suite;
                      Image=TestReport;
                      OnAction=BEGIN
                                 ReportPrint.PrintGenJnlLine(Rec);
                               END;
                                }
      { 48      ;2   ;Action    ;
                      Name=Post;
                      ShortCutKey=F9;
                      CaptionML=ENU=P&ost;
                      ToolTipML=ENU=Finalize the document or journal by posting the amounts and quantities to the related accounts in your company books.;
                      ApplicationArea=#Basic,#Suite;
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=PostOrder;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 CODEUNIT.RUN(CODEUNIT::"Gen. Jnl.-Post",Rec);
                                 CurrentJnlBatchName := GETRANGEMAX("Journal Batch Name");
                                 CurrPage.UPDATE(FALSE);
                               END;
                                }
      { 9       ;2   ;Action    ;
                      Name=Preview;
                      CaptionML=ENU=Preview Posting;
                      ToolTipML=ENU=Review the different types of entries that will be created when you post the document or journal.;
                      ApplicationArea=#Basic,#Suite;
                      Image=ViewPostedOrder;
                      OnAction=VAR
                                 GenJnlPost@1001 : Codeunit 231;
                               BEGIN
                                 GenJnlPost.Preview(Rec);
                               END;
                                }
      { 49      ;2   ;Action    ;
                      ShortCutKey=Shift+F9;
                      CaptionML=ENU=Post and &Print;
                      ToolTipML=ENU=Finalize and prepare to print the document or journal. The values and quantities are posted to the related accounts. A report request window where you can specify what to include on the print-out.;
                      ApplicationArea=#Basic,#Suite;
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=PostPrint;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 CODEUNIT.RUN(CODEUNIT::"Gen. Jnl.-Post+Print",Rec);
                                 CurrentJnlBatchName := GETRANGEMAX("Journal Batch Name");
                                 CurrPage.UPDATE(FALSE);
                               END;
                                }
      { 25      ;1   ;ActionGroup;
                      CaptionML=ENU=Page }
      { 23      ;2   ;Action    ;
                      Name=EditInExcel;
                      CaptionML=ENU=Edit in Excel;
                      ToolTipML=ENU=Send the data in the journal to an Excel file for analysis or editing.;
                      ApplicationArea=#Basic,#Suite;
                      Promoted=Yes;
                      Visible=IsSaasExcelAddinEnabled;
                      PromotedIsBig=Yes;
                      Image=Excel;
                      PromotedCategory=Category4;
                      PromotedOnly=Yes;
                      OnAction=VAR
                                 ODataUtility@1000 : Codeunit 6710;
                               BEGIN
                                 ODataUtility.EditJournalWorksheetInExcel(CurrPage.CAPTION,CurrPage.OBJECTID(FALSE),"Journal Batch Name","Journal Template Name");
                               END;
                                }
      { 5012    ;2   ;Action    ;
                      Name=ClassicView;
                      CaptionML=ENU=Show more columns;
                      ToolTipML=ENU=Adds additional fields to the journal for a full view of the journal data.;
                      ApplicationArea=#Basic,#Suite;
                      Promoted=Yes;
                      Visible=IsSimplePage;
                      PromotedIsBig=Yes;
                      Image=SetupColumns;
                      PromotedCategory=Category4;
                      PromotedOnly=Yes;
                      OnAction=BEGIN
                                 // set journal preference for this page to be NOT simple mode (classic mode)
                                 CurrPage.CLOSE;
                                 GenJnlManagement.SetJournalSimplePageModePreference(FALSE,PAGE::"Purchase Journal");
                                 GenJnlManagement.SetLastViewedJournalBatchName(PAGE::"Purchase Journal",CurrentJnlBatchName);
                                 PAGE.RUN(PAGE::"Purchase Journal");
                               END;
                                }
      { 6012    ;2   ;Action    ;
                      Name=SimpleView;
                      CaptionML=ENU=Show fewer columns;
                      ToolTipML=ENU=Hides many of the less used fields from the journal for better productivity. The fields are still there but not shown.;
                      ApplicationArea=#Basic,#Suite;
                      Promoted=Yes;
                      Visible=NOT IsSimplePage;
                      PromotedIsBig=Yes;
                      Image=SetupList;
                      PromotedCategory=Category4;
                      PromotedOnly=Yes;
                      OnAction=BEGIN
                                 // set journal preference for this page to be simple mode
                                 CurrPage.CLOSE;
                                 GenJnlManagement.SetJournalSimplePageModePreference(TRUE,PAGE::"Purchase Journal");
                                 GenJnlManagement.SetLastViewedJournalBatchName(PAGE::"Purchase Journal",CurrentJnlBatchName);
                                 PAGE.RUN(PAGE::"Purchase Journal");
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 37  ;1   ;Field     ;
                Lookup=Yes;
                CaptionML=ENU=Batch Name;
                ToolTipML=ENU=Specifies the name of the journal batch, a personalized journal layout, that the journal is based on.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=CurrentJnlBatchName;
                OnValidate=BEGIN
                             GenJnlManagement.CheckName(CurrentJnlBatchName,Rec);
                             CurrentJnlBatchNameOnAfterVali;
                             GenJnlManagement.SetLastViewedJournalBatchName(PAGE::"Purchase Journal",CurrentJnlBatchName);
                           END;

                OnLookup=BEGIN
                           CurrPage.SAVERECORD;
                           GenJnlManagement.LookupName(CurrentJnlBatchName,Rec);
                           GenJnlManagement.SetLastViewedJournalBatchName(PAGE::"Purchase Journal",CurrentJnlBatchName);
                           CurrPage.UPDATE(FALSE);
                         END;
                          }

    { 1   ;1   ;Group     ;
                GroupType=Repeater }

    { 2   ;2   ;Field     ;
                ToolTipML=ENU=Specifies the posting date for the entry.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Posting Date" }

    { 87  ;2   ;Field     ;
                ToolTipML=ENU=Specifies the date when the related document was created.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Document Date";
                Visible=FALSE }

    { 4   ;2   ;Field     ;
                ToolTipML=ENU=Specifies the type of document that the entry on the journal line is.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Document Type";
                OnValidate=BEGIN
                             // Update amount based on doc type
                             IF "Document Type" = "Document Type"::" " THEN
                               ERROR(EmptyDocumentTypeErr,"Document No.");

                             IF xRec."Document Type" <> "Document Type" THEN
                               IF ("Document Type" = "Document Type"::Payment) OR ("Document Type" = "Document Type"::"Credit Memo") THEN
                                 VALIDATE(Amount,ABS(Amount))
                               ELSE
                                 IF Amount > 0 THEN
                                   VALIDATE(Amount,-1 * Amount)
                           END;
                            }

    { 6   ;2   ;Field     ;
                ToolTipML=ENU=Specifies a document number for the journal line.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Document No." }

    { 5   ;2   ;Field     ;
                ToolTipML=ENU=Specifies the number of the incoming document that this general journal line is created for.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Incoming Document Entry No.";
                Visible=FALSE;
                OnAssistEdit=BEGIN
                               IF "Incoming Document Entry No." > 0 THEN
                                 HYPERLINK(GetIncomingDocumentURL);
                             END;
                              }

    { 85  ;2   ;Field     ;
                ToolTipML=ENU=Specifies a document number that refers to the customer's or vendor's numbering system.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="External Document No." }

    { 8   ;2   ;Field     ;
                ToolTipML=ENU=Specifies the type of account that the entry on the journal line will be posted to.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Account Type";
                Visible=NOT IsSimplePage;
                OnValidate=BEGIN
                             GenJnlManagement.GetAccounts(Rec,AccName,BalAccName);
                             EnableApplyEntriesAction;
                           END;
                            }

    { 10  ;2   ;Field     ;
                ToolTipML=ENU=Specifies the account number that the entry on the journal line will be posted to.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Account No.";
                OnValidate=BEGIN
                             GenJnlManagement.GetAccounts(Rec,AccName,BalAccName);
                             ShowShortcutDimCode(ShortcutDimCode);
                           END;
                            }

    { 7012;2   ;Field     ;
                Name=<Vendor Name>;
                CaptionML=ENU=Vendor Name;
                ToolTipML=ENU=Specifies the name of the vendor.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=AccName;
                Editable=FALSE }

    { 12  ;2   ;Field     ;
                ToolTipML=ENU=Specifies a description of the entry.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=Description }

    { 55  ;2   ;Field     ;
                ToolTipML=ENU=Specifies the salesperson or purchaser who is linked to the journal line.;
                ApplicationArea=#Suite;
                SourceExpr="Salespers./Purch. Code";
                Visible=FALSE }

    { 99  ;2   ;Field     ;
                ToolTipML=ENU=Specifies the number of the campaign that the journal line is linked to.;
                ApplicationArea=#RelationshipMgmt;
                SourceExpr="Campaign No.";
                Visible=FALSE }

    { 57  ;2   ;Field     ;
                AssistEdit=Yes;
                ToolTipML=ENU=Specifies the code of the currency for the amounts on the journal line.;
                ApplicationArea=#Suite;
                SourceExpr="Currency Code";
                Visible=FALSE;
                OnAssistEdit=BEGIN
                               ChangeExchangeRate.SetParameter("Currency Code","Currency Factor","Posting Date");
                               IF ChangeExchangeRate.RUNMODAL = ACTION::OK THEN
                                 VALIDATE("Currency Factor",ChangeExchangeRate.GetParameter);

                               CLEAR(ChangeExchangeRate);
                             END;
                              }

    { 14  ;2   ;Field     ;
                ToolTipML=ENU=Specifies the type of transaction.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Gen. Posting Type";
                Visible=NOT IsSimplePage }

    { 81  ;2   ;Field     ;
                ToolTipML=ENU=Specifies the vendor's trade type to link transactions made for this vendor with the appropriate general ledger account according to the general posting setup.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Gen. Bus. Posting Group";
                Visible=NOT IsSimplePage }

    { 16  ;2   ;Field     ;
                ToolTipML=ENU=Specifies the item's product type to link transactions made for this item with the appropriate general ledger account according to the general posting setup.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Gen. Prod. Posting Group";
                Visible=NOT IsSimplePage }

    { 39  ;2   ;Field     ;
                ToolTipML=ENU=Specifies the Tax specification of the involved customer or vendor to link transactions made for this record with the appropriate general ledger account according to the Tax posting setup.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="VAT Bus. Posting Group";
                Visible=FALSE }

    { 91  ;2   ;Field     ;
                ToolTipML=ENU=Specifies the Tax specification of the involved item or resource to link transactions made for this record with the appropriate general ledger account according to the Tax posting setup.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="VAT Prod. Posting Group";
                Visible=FALSE }

    { 8012;2   ;Field     ;
                CaptionML=ENU=Document Amount;
                ToolTipML=ENU=Specifies the total amount (including tax) that the journal line consists of.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=DocumentAmount;
                AutoFormatExpr="Currency Code";
                OnValidate=BEGIN
                             // Document amount can not be less than 0
                             IF DocumentAmount < 0 THEN
                               ERROR(NegativeDocAmountErr);

                             // Validate amount based on document type
                             IF "Document Type" = "Document Type"::" " THEN
                               ERROR(EmptyDocumentTypeErr,"Document No.");

                             IF ("Document Type" = "Document Type"::Payment) OR
                                ("Document Type" = "Document Type"::"Credit Memo")
                             THEN
                               VALIDATE(Amount,DocumentAmount)
                             ELSE
                               VALIDATE(Amount,DocumentAmount * -1)
                           END;
                            }

    { 18  ;2   ;Field     ;
                ToolTipML=ENU=Specifies the total amount (including tax) that the journal line consists of.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=Amount;
                Visible=AmountVisible;
                OnValidate=BEGIN
                             DocumentAmount := ABS(Amount);
                           END;
                            }

    { 27  ;2   ;Field     ;
                ToolTipML=ENU=Specifies the total amount in local currency (including Tax) that the journal line consists of.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Amount (LCY)";
                Visible=AmountVisible }

    { 1000;2   ;Field     ;
                ToolTipML=ENU=Specifies the total of the ledger entries that represent debits.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Debit Amount";
                Visible=DebitCreditVisible;
                OnValidate=BEGIN
                             DocumentAmount := ABS("Debit Amount");
                           END;
                            }

    { 1001;2   ;Field     ;
                ToolTipML=ENU=Specifies the total of the ledger entries that represent credits.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Credit Amount";
                Visible=DebitCreditVisible;
                OnValidate=BEGIN
                             DocumentAmount := ABS("Credit Amount");
                           END;
                            }

    { 1480008;2;Field     ;
                ToolTipML=ENU=Specifies if the customer or vendor is liable for sales tax.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Tax Liable";
                Visible=FALSE }

    { 1480010;2;Field     ;
                ToolTipML=ENU=Specifies the tax area that is used to calculate and post sales tax.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Tax Area Code";
                Visible=FALSE }

    { 1480004;2;Field     ;
                ToolTipML=ENU=Specifies the tax group that is used to calculate and post sales tax.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Tax Group Code";
                Visible=FALSE }

    { 1480006;2;Field     ;
                ToolTipML=ENU=Specifies that the purchase is subject to use tax. Use tax is a sales tax that is paid on items that are purchased by a company and are used by that company instead of being sold to a customer.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Use Tax";
                Visible=FALSE }

    { 105 ;2   ;Field     ;
                ToolTipML=ENU=Specifies the amount of Tax that is included in the total amount.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="VAT Amount";
                Visible=FALSE }

    { 109 ;2   ;Field     ;
                ToolTipML=ENU=Specifies the difference between the calculated Tax amount and a Tax amount that you have entered manually.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="VAT Difference";
                Visible=FALSE }

    { 107 ;2   ;Field     ;
                ToolTipML=ENU=Specifies the amount of Bal. Tax included in the total amount.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Bal. VAT Amount";
                Visible=FALSE }

    { 111 ;2   ;Field     ;
                ToolTipML=ENU=Specifies the difference between the calculate tax amount and the tax amount that you have entered manually.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Bal. VAT Difference";
                Visible=FALSE }

    { 59  ;2   ;Field     ;
                ToolTipML=ENU=Specifies the type of account that a balancing entry is posted to, such as BANK for a cash account.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Bal. Account Type";
                Visible=NOT IsSimplePage;
                OnValidate=BEGIN
                             EnableApplyEntriesAction;
                           END;
                            }

    { 61  ;2   ;Field     ;
                ToolTipML=ENU=Specifies the number of the general ledger, customer, vendor, or bank account that the balancing entry is posted to, such as a cash account for cash purchases.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Bal. Account No.";
                OnValidate=BEGIN
                             GenJnlManagement.GetAccounts(Rec,AccName,BalAccName);
                             ShowShortcutDimCode(ShortcutDimCode);
                           END;
                            }

    { 63  ;2   ;Field     ;
                ToolTipML=ENU=Specifies the general posting type associated with the balancing account that will be used when you post the entry on the journal line.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Bal. Gen. Posting Type";
                Visible=NOT IsSimplePage }

    { 83  ;2   ;Field     ;
                ToolTipML=ENU=Specifies the general business posting group code associated with the balancing account that will be used when you post the entry.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Bal. Gen. Bus. Posting Group";
                Visible=NOT IsSimplePage }

    { 65  ;2   ;Field     ;
                ToolTipML=ENU=Specifies the general product posting group code associated with the balancing account that will be used when you post the entry.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Bal. Gen. Prod. Posting Group";
                Visible=NOT IsSimplePage }

    { 93  ;2   ;Field     ;
                ToolTipML=ENU=Specifies the code of the Tax business posting group that will be used when you post the entry on the journal line.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Bal. VAT Bus. Posting Group";
                Visible=FALSE }

    { 95  ;2   ;Field     ;
                ToolTipML=ENU=Specifies the code of the Tax product posting group that will be used when you post the entry on the journal line.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Bal. VAT Prod. Posting Group";
                Visible=FALSE }

    { 101 ;2   ;Field     ;
                ToolTipML=ENU=Specifies the number of the bill-to customer or pay-to vendor that the entry is linked to.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Bill-to/Pay-to No.";
                Visible=FALSE }

    { 103 ;2   ;Field     ;
                ToolTipML=ENU=Specifies the address code of the ship-to customer or order-from vendor that the entry is linked to.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Ship-to/Order Address Code";
                Visible=FALSE }

    { 35  ;2   ;Field     ;
                ToolTipML=ENU=Specifies the code for Shortcut Dimension 1, which is one of two global dimension codes that you set up in the General Ledger Setup window.;
                ApplicationArea=#Dimensions;
                SourceExpr="Shortcut Dimension 1 Code";
                Visible=FALSE }

    { 53  ;2   ;Field     ;
                ToolTipML=ENU=Specifies the code for Shortcut Dimension 2, which is one of two global dimension codes that you set up in the General Ledger Setup window.;
                ApplicationArea=#Dimensions;
                SourceExpr="Shortcut Dimension 2 Code";
                Visible=FALSE }

    { 300 ;2   ;Field     ;
                ApplicationArea=#Suite;
                SourceExpr=ShortcutDimCode[3];
                CaptionClass='1,2,3';
                TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(3),
                                                            Dimension Value Type=CONST(Standard),
                                                            Blocked=CONST(No));
                Visible=FALSE;
                OnValidate=BEGIN
                             ValidateShortcutDimCode(3,ShortcutDimCode[3]);
                           END;
                            }

    { 302 ;2   ;Field     ;
                ApplicationArea=#Suite;
                SourceExpr=ShortcutDimCode[4];
                CaptionClass='1,2,4';
                TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(4),
                                                            Dimension Value Type=CONST(Standard),
                                                            Blocked=CONST(No));
                Visible=FALSE;
                OnValidate=BEGIN
                             ValidateShortcutDimCode(4,ShortcutDimCode[4]);
                           END;
                            }

    { 304 ;2   ;Field     ;
                ApplicationArea=#Suite;
                SourceExpr=ShortcutDimCode[5];
                CaptionClass='1,2,5';
                TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(5),
                                                            Dimension Value Type=CONST(Standard),
                                                            Blocked=CONST(No));
                Visible=FALSE;
                OnValidate=BEGIN
                             ValidateShortcutDimCode(5,ShortcutDimCode[5]);
                           END;
                            }

    { 306 ;2   ;Field     ;
                ApplicationArea=#Suite;
                SourceExpr=ShortcutDimCode[6];
                CaptionClass='1,2,6';
                TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(6),
                                                            Dimension Value Type=CONST(Standard),
                                                            Blocked=CONST(No));
                Visible=FALSE;
                OnValidate=BEGIN
                             ValidateShortcutDimCode(6,ShortcutDimCode[6]);
                           END;
                            }

    { 308 ;2   ;Field     ;
                ApplicationArea=#Suite;
                SourceExpr=ShortcutDimCode[7];
                CaptionClass='1,2,7';
                TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(7),
                                                            Dimension Value Type=CONST(Standard),
                                                            Blocked=CONST(No));
                Visible=FALSE;
                OnValidate=BEGIN
                             ValidateShortcutDimCode(7,ShortcutDimCode[7]);
                           END;
                            }

    { 310 ;2   ;Field     ;
                ApplicationArea=#Suite;
                SourceExpr=ShortcutDimCode[8];
                CaptionClass='1,2,8';
                TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(8),
                                                            Dimension Value Type=CONST(Standard),
                                                            Blocked=CONST(No));
                Visible=FALSE;
                OnValidate=BEGIN
                             ValidateShortcutDimCode(8,ShortcutDimCode[8]);
                           END;
                            }

    { 69  ;2   ;Field     ;
                ToolTipML=ENU=Specifies the line's net amount (the amount excluding tax) if you are using this journal line for an invoice.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Sales/Purch. (LCY)";
                Visible=FALSE }

    { 73  ;2   ;Field     ;
                ToolTipML=ENU=Specifies the amount of the invoice discount if you are using this journal line for an invoice.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Inv. Discount (LCY)";
                Visible=FALSE }

    { 20  ;2   ;Field     ;
                ToolTipML=ENU=Specifies a formula that calculates the payment due date, payment discount date, and payment discount amount.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Payment Terms Code";
                Visible=FALSE }

    { 22  ;2   ;Field     ;
                ToolTipML=ENU=Specifies the due date on the entry.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Due Date";
                Visible=FALSE }

    { 75  ;2   ;Field     ;
                ToolTipML=ENU=Specifies the date on which the amount in the entry must be paid for a payment discount to be granted.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Pmt. Discount Date";
                Visible=FALSE }

    { 77  ;2   ;Field     ;
                ToolTipML=ENU=Specifies the payment discount percent granted if payment is made on or before the date in the Pmt. Discount Date field.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Payment Discount %";
                Visible=FALSE }

    { 24  ;2   ;Field     ;
                ToolTipML=ENU=Specifies the type of the posted document that this document or journal line will be applied to when you post, for example to register payment.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Applies-to Doc. Type";
                Visible=NOT IsSimplePage }

    { 26  ;2   ;Field     ;
                ToolTipML=ENU=Specifies the number of the posted document that this document or journal line will be applied to when you post, for example to register payment.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Applies-to Doc. No.";
                Visible=NOT IsSimplePage }

    { 71  ;2   ;Field     ;
                ToolTipML=ENU=Specifies the ID of entries that will be applied to when you choose the Apply Entries action.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Applies-to ID";
                Visible=FALSE }

    { 51  ;2   ;Field     ;
                ToolTipML=ENU=Specifies that the related entry represents an unpaid invoice for which either a payment suggestion, a reminder, or a finance charge memo exists.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="On Hold" }

    { 79  ;2   ;Field     ;
                ToolTipML=ENU=Specifies the reason code, a supplementary source code that enables you to trace the entry.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Reason Code";
                Visible=FALSE }

    { 30  ;2   ;Field     ;
                ToolTipML=ENU=Specifies the entry as a corrective entry. You can use the field if you need to post a corrective entry to an account.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=Correction }

    { 3   ;2   ;Field     ;
                ToolTipML=ENU=Specifies a comment related to registering a payment.;
                ApplicationArea=#Comments;
                SourceExpr=Comment;
                Visible=FALSE }

    { 28  ;1   ;Group      }

    { 1902205001;2;Group  ;
                GroupType=FixedLayout }

    { 1901652501;3;Group  ;
                CaptionML=ENU=Account Name }

    { 33  ;4   ;Field     ;
                ToolTipML=ENU=Specifies the name of the account.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=AccName;
                Editable=FALSE;
                ShowCaption=No }

    { 1903867901;3;Group  ;
                CaptionML=ENU=Bal. Account Name }

    { 89  ;4   ;Field     ;
                CaptionML=ENU=Bal. Account Name;
                ToolTipML=ENU=Specifies the name of the balancing account that has been entered on the journal line.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=BalAccName;
                Editable=FALSE }

    { 1903866901;3;Group  ;
                CaptionML=ENU=Balance }

    { 29  ;4   ;Field     ;
                Name=Balance;
                CaptionML=ENU=Balance;
                ToolTipML=ENU=Specifies the balance that has accumulated in the purchase journal on the line where the cursor is.;
                ApplicationArea=#All;
                SourceExpr=Balance + "Balance (LCY)" - xRec."Balance (LCY)";
                AutoFormatType=1;
                Visible=BalanceVisible;
                Editable=FALSE }

    { 1902759701;3;Group  ;
                CaptionML=ENU=Total Balance }

    { 31  ;4   ;Field     ;
                Name=TotalBalance;
                CaptionML=ENU=Total Balance;
                ToolTipML=ENU=Specifies the total balance in the purchase journal.;
                ApplicationArea=#All;
                SourceExpr=TotalBalance + "Balance (LCY)" - xRec."Balance (LCY)";
                AutoFormatType=1;
                Visible=TotalBalanceVisible;
                Editable=FALSE }

    { 1900000007;0;Container;
                ContainerType=FactBoxArea }

    { 1900919607;1;Part   ;
                ApplicationArea=#Basic,#Suite;
                SubPageLink=Dimension Set ID=FIELD(Dimension Set ID);
                PagePartID=Page699;
                PartType=Page }

    { 11  ;1   ;Part      ;
                Name=IncomingDocAttachFactBox;
                ApplicationArea=#Basic,#Suite;
                PagePartID=Page193;
                PartType=Page;
                ShowFilter=No }

    { 1900383207;1;Part   ;
                Visible=FALSE;
                PartType=System;
                SystemPartID=RecordLinks }

    { 1905767507;1;Part   ;
                Visible=FALSE;
                PartType=System;
                SystemPartID=Notes }

  }
  CODE
  {
    VAR
      GenJnlManagement@1002 : Codeunit 230;
      ReportPrint@1003 : Codeunit 228;
      ClientTypeManagement@1077 : Codeunit 4;
      ChangeExchangeRate@1001 : Page 511;
      GLReconcile@1000 : Page 345;
      CurrentJnlBatchName@1004 : Code[10];
      AccName@1005 : Text[50];
      BalAccName@1006 : Text[50];
      Balance@1007 : Decimal;
      TotalBalance@1008 : Decimal;
      ShowBalance@1009 : Boolean;
      ShowTotalBalance@1010 : Boolean;
      ShortcutDimCode@1011 : ARRAY [8] OF Code[20];
      HasIncomingDocument@1014 : Boolean;
      ApplyEntriesActionEnabled@1012 : Boolean;
      BalanceVisible@1015 : Boolean INDATASET;
      TotalBalanceVisible@1016 : Boolean INDATASET;
      AmountVisible@1018 : Boolean;
      DebitCreditVisible@1017 : Boolean;
      IsSaasExcelAddinEnabled@1013 : Boolean;
      IsSimplePage@1219 : Boolean;
      DocumentAmount@1220 : Decimal;
      EmptyDocumentTypeErr@1221 : TextConst '@@@="%1 = Document number.";ENU=You must specify a document type for %1.';
      NegativeDocAmountErr@1222 : TextConst 'ENU=You must specify a positive amount as the document amount. If the journal line is for a document type that has a negative amount, the amount will be tracked correctly.';

    LOCAL PROCEDURE UpdateBalance@1();
    BEGIN
      GenJnlManagement.CalcBalance(
        Rec,xRec,Balance,TotalBalance,ShowBalance,ShowTotalBalance);
      BalanceVisible := ShowBalance;
      TotalBalanceVisible := ShowTotalBalance;
    END;

    LOCAL PROCEDURE EnableApplyEntriesAction@2();
    BEGIN
      ApplyEntriesActionEnabled :=
        ("Account Type" IN ["Account Type"::Customer,"Account Type"::Vendor]) OR
        ("Bal. Account Type" IN ["Bal. Account Type"::Customer,"Bal. Account Type"::Vendor]);
    END;

    LOCAL PROCEDURE CurrentJnlBatchNameOnAfterVali@19002411();
    BEGIN
      CurrPage.SAVERECORD;
      GenJnlManagement.SetName(CurrentJnlBatchName,Rec);
      CurrPage.UPDATE(FALSE);
    END;

    LOCAL PROCEDURE SetControlVisibility@8();
    VAR
      GLSetup@1000 : Record 98;
    BEGIN
      GLSetup.GET;
      // Hide amount when open in simple page mode.
      IF IsSimplePage THEN BEGIN
        AmountVisible := FALSE;
        DebitCreditVisible := FALSE;
      END ELSE BEGIN
        AmountVisible := NOT (GLSetup."Show Amounts" = GLSetup."Show Amounts"::"Debit/Credit Only");
        DebitCreditVisible := NOT (GLSetup."Show Amounts" = GLSetup."Show Amounts"::"Amount Only");
      END;
    END;

    BEGIN
    {
      // This page has two view modes based on global variable 'IsSimplePage' as :-
      // Show more coloums action (IsSimplePage = FALSE)
      // - Default page layout before simple view was introduced.
      // Show less coloums action (IsSimplePage = TRUE)
      // Hide amount, account type coloums and set default account type to Vendor.
      // Show a new column named document amount where user will enter amount for the line
      // By DEFAULT this page opens up in simple page layout.
    }
    END.
  }
}

