OBJECT Report 5190 Delete Interaction Log Entries
{
  OBJECT-PROPERTIES
  {
    Date=09/19/18;
    Time=12:00:00 PM;
    Version List=NAVW113.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Delete Interaction Log Entries;
               ESM=Borrar movs. log interacci¢n;
               FRC=Suppr. ‚cr. journ. interaction;
               ENC=Delete Interaction Log Entries];
    ProcessingOnly=Yes;
    OnPostReport=BEGIN
                   MESSAGE(Text000,NoOfInteractions,"Interaction Log Entry".TABLECAPTION);
                 END;

  }
  DATASET
  {
    { 3056;    ;DataItem;                    ;
               DataItemTable=Table5065;
               DataItemTableView=SORTING(Canceled,Campaign No.,Campaign Entry No.,Date)
                                 WHERE(Canceled=CONST(Yes));
               OnAfterGetRecord=VAR
                                  SalesHeader@1000 : Record 36;
                                  PurchHeader@1001 : Record 38;
                                BEGIN
                                  IF UniqueAttachment THEN BEGIN
                                    Attachment.GET("Attachment No.");
                                    Attachment.DELETE(TRUE);
                                  END;
                                  NoOfInteractions := NoOfInteractions + 1;

                                  IF "Version No." <> 0 THEN
                                    CASE "Document Type" OF
                                      "Document Type"::"Sales Qte.":
                                        SetSalesDocInteraction("Interaction Log Entry",SalesHeader."Document Type"::Quote);
                                      "Document Type"::"Sales Ord. Cnfrmn.":
                                        SetSalesDocInteraction("Interaction Log Entry",SalesHeader."Document Type"::Order);
                                      "Document Type"::"Sales Blnkt. Ord":
                                        SetSalesDocInteraction("Interaction Log Entry",SalesHeader."Document Type"::"Blanket Order");
                                      "Document Type"::"Purch.Qte.":
                                        SetPurchDocInteraction("Interaction Log Entry",PurchHeader."Document Type"::Quote);
                                      "Document Type"::"Purch. Ord.":
                                        SetPurchDocInteraction("Interaction Log Entry",PurchHeader."Document Type"::Order);
                                      "Document Type"::"Purch. Blnkt. Ord.":
                                        SetPurchDocInteraction("Interaction Log Entry",PurchHeader."Document Type"::"Blanket Order");
                                    END;
                                  DELETE(TRUE);
                                END;

               ReqFilterFields=Entry No.,Contact No.,Date,Campaign No.,Interaction Group Code,Interaction Template Code,Salesperson Code }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=%1 %2 has been deleted.;ESM=Se ha borrado %1 %2.;FRC=%1 %2 supprim‚e(s).;ENC=%1 %2 has been deleted.';
      Attachment@1002 : Record 5062;
      NoOfInteractions@1003 : Integer;

    LOCAL PROCEDURE SetPurchDocInteraction@1(InteractionLogEntry@1001 : Record 5065;DocumentType@1000 : Option);
    VAR
      PurchHeader@1002 : Record 38;
      PurchHeaderArchive@1003 : Record 5109;
    BEGIN
      WITH InteractionLogEntry DO BEGIN
        PurchHeaderArchive.GET(DocumentType,"Document No.","Doc. No. Occurrence","Version No.");
        PurchHeader.SETRANGE("Document Type",DocumentType);
        PurchHeader.SETRANGE("No.","Document No.");
        PurchHeader.SETRANGE("Doc. No. Occurrence","Doc. No. Occurrence");
        IF PurchHeader.FINDFIRST THEN BEGIN
          PurchHeaderArchive."Interaction Exist" := FALSE;
          PurchHeaderArchive.MODIFY;
        END ELSE
          PurchHeaderArchive.DELETE(TRUE);
      END;
    END;

    LOCAL PROCEDURE SetSalesDocInteraction@5(InteractionLogEntry@1001 : Record 5065;DocumentType@1000 : Option);
    VAR
      SalesHeader@1002 : Record 36;
      SalesHeaderArchive@1003 : Record 5107;
    BEGIN
      WITH InteractionLogEntry DO BEGIN
        SalesHeaderArchive.GET(DocumentType,"Document No.","Doc. No. Occurrence","Version No.");
        SalesHeader.SETRANGE("Document Type",DocumentType);
        SalesHeader.SETRANGE("No.","Document No.");
        SalesHeader.SETRANGE("Doc. No. Occurrence","Doc. No. Occurrence");
        IF SalesHeader.FINDFIRST THEN BEGIN
          SalesHeaderArchive."Interaction Exist" := FALSE;
          SalesHeaderArchive.MODIFY;
        END ELSE
          SalesHeaderArchive.DELETE(TRUE);
      END;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

