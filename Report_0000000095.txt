OBJECT Report 95 Date Compress VAT Entries
{
  OBJECT-PROPERTIES
  {
    Date=09/19/18;
    Time=12:00:00 PM;
    Version List=NAVW113.00;
  }
  PROPERTIES
  {
    Permissions=TableData 17=rimd,
                TableData 45=rimd,
                TableData 87=rimd,
                TableData 253=rimd,
                TableData 254=rimd;
    CaptionML=[ENU=Date Compress Tax Entries;
               ESM=Compactar por fechas movs. IVA;
               FRC=Compr. date ‚critures taxes;
               ENC=Date Compress Tax Entries];
    ProcessingOnly=Yes;
    OnPreReport=BEGIN
                  VATEntryFilter := COPYSTR("VAT Entry".GETFILTERS,1,MAXSTRLEN(DateComprReg.Filter));
                END;

  }
  DATASET
  {
    { 7612;    ;DataItem;                    ;
               DataItemTable=Table254;
               DataItemTableView=SORTING(Type,Closed);
               OnPreDataItem=BEGIN
                               IF NOT
                                  CONFIRM(
                                    Text000 +
                                    Text002,FALSE)
                               THEN
                                 CurrReport.BREAK;

                               IF EntrdDateComprReg."Ending Date" = 0D THEN
                                 ERROR(Text003,EntrdDateComprReg.FIELDCAPTION("Ending Date"));

                               Window.OPEN(
                                 Text004 +
                                 Text005 +
                                 Text006 +
                                 Text007 +
                                 Text008 +
                                 Text009 +
                                 Text010 +
                                 Text011 +
                                 Text012);

                               SourceCodeSetup.GET;
                               SourceCodeSetup.TESTFIELD("Compress VAT Entries");

                               GLEntry.LOCKTABLE;
                               NewVATEntry.LOCKTABLE;
                               GLReg.LOCKTABLE;
                               DateComprReg.LOCKTABLE;

                               IF GLEntry.FINDLAST THEN;
                               LastGLEntryNo := GLEntry."Entry No.";
                               NextTransactionNo := GLEntry."Transaction No." + 1;
                               IF NewVATEntry.FINDLAST THEN;
                               LastVATEntryNo := NewVATEntry."Entry No.";
                               SETRANGE("Entry No.",0,LastVATEntryNo);
                               SETRANGE("Posting Date",EntrdDateComprReg."Starting Date",EntrdDateComprReg."Ending Date");

                               InitRegisters;
                             END;

               OnAfterGetRecord=BEGIN
                                  VATEntry2 := "VAT Entry";
                                  WITH VATEntry2 DO BEGIN
                                    IF NOT
                                       SETCURRENTKEY(
                                         Type,Closed,"VAT Bus. Posting Group","VAT Prod. Posting Group","Posting Date")
                                    THEN
                                      SETCURRENTKEY(
                                        Type,Closed,"Tax Jurisdiction Code","Use Tax","Posting Date");
                                    COPYFILTERS("VAT Entry");
                                    SETRANGE(Type,Type);
                                    SETRANGE(Closed,Closed);
                                    SETRANGE("VAT Bus. Posting Group","VAT Bus. Posting Group");
                                    SETRANGE("VAT Prod. Posting Group","VAT Prod. Posting Group");
                                    SETRANGE("Tax Jurisdiction Code","Tax Jurisdiction Code");
                                    SETRANGE("Use Tax","Use Tax");
                                    SETFILTER("Posting Date",DateComprMgt.GetDateFilter("Posting Date",EntrdDateComprReg,TRUE));
                                    SETRANGE("Document Type","Document Type");

                                    LastVATEntryNo := LastVATEntryNo + 1;

                                    NewVATEntry.INIT;
                                    NewVATEntry."Entry No." := LastVATEntryNo;
                                    NewVATEntry.Type := Type;
                                    NewVATEntry.Closed := Closed;
                                    NewVATEntry."VAT Bus. Posting Group" := "VAT Bus. Posting Group";
                                    NewVATEntry."VAT Prod. Posting Group" := "VAT Prod. Posting Group";
                                    NewVATEntry."Tax Jurisdiction Code" := "Tax Jurisdiction Code";
                                    NewVATEntry."Use Tax" := "Use Tax";
                                    NewVATEntry."Posting Date" := GETRANGEMIN("Posting Date");
                                    NewVATEntry."Document Type" := "Document Type";
                                    NewVATEntry."Source Code" := SourceCodeSetup."Compress VAT Entries";
                                    NewVATEntry."User ID" := USERID;
                                    NewVATEntry."Transaction No." := NextTransactionNo;
                                    Window.UPDATE(1,NewVATEntry.Type);
                                    Window.UPDATE(2,NewVATEntry."VAT Bus. Posting Group");
                                    Window.UPDATE(3,NewVATEntry."VAT Prod. Posting Group");
                                    Window.UPDATE(4,NewVATEntry."Tax Jurisdiction Code");
                                    Window.UPDATE(5,NewVATEntry."Use Tax");
                                    Window.UPDATE(6,NewVATEntry."Posting Date");
                                    DateComprReg."No. of New Records" := DateComprReg."No. of New Records" + 1;
                                    Window.UPDATE(7,DateComprReg."No. of New Records");

                                    IF RetainNo(FIELDNO("Document No.")) THEN BEGIN
                                      SETRANGE("Document No.","Document No.");
                                      NewVATEntry."Document No." := "Document No.";
                                    END;
                                    IF RetainNo(FIELDNO("Bill-to/Pay-to No.")) THEN BEGIN
                                      SETRANGE("Bill-to/Pay-to No.","Bill-to/Pay-to No.");
                                      NewVATEntry."Bill-to/Pay-to No." := "Bill-to/Pay-to No.";
                                    END;
                                    IF RetainNo(FIELDNO("EU 3-Party Trade")) THEN BEGIN
                                      SETRANGE("EU 3-Party Trade","EU 3-Party Trade");
                                      NewVATEntry."EU 3-Party Trade" := "EU 3-Party Trade";
                                    END;
                                    IF RetainNo(FIELDNO("Country/Region Code")) THEN BEGIN
                                      SETRANGE("Country/Region Code","Country/Region Code");
                                      NewVATEntry."Country/Region Code" := "Country/Region Code";
                                    END;
                                    IF RetainNo(FIELDNO("Internal Ref. No.")) THEN BEGIN
                                      SETRANGE("Internal Ref. No.","Internal Ref. No.");
                                      NewVATEntry."Internal Ref. No." := "Internal Ref. No.";
                                    END;
                                    IF Base >= 0 THEN
                                      SETFILTER(Base,'>=0')
                                    ELSE
                                      SETFILTER(Base,'<0');
                                    REPEAT
                                      NewVATEntry.Base := NewVATEntry.Base + Base;
                                      NewVATEntry.Amount := NewVATEntry.Amount + Amount;
                                      NewVATEntry."Unrealized Amount" := NewVATEntry."Unrealized Amount" + "Unrealized Amount";
                                      NewVATEntry."Unrealized Base" := NewVATEntry."Unrealized Base" + "Unrealized Base";
                                      NewVATEntry."Additional-Currency Base" :=
                                        NewVATEntry."Additional-Currency Base" + "Additional-Currency Base";
                                      NewVATEntry."Additional-Currency Amount" :=
                                        NewVATEntry."Additional-Currency Amount" + "Additional-Currency Amount";
                                      NewVATEntry."Add.-Currency Unrealized Amt." :=
                                        NewVATEntry."Add.-Currency Unrealized Amt." + "Add.-Currency Unrealized Amt.";
                                      NewVATEntry."Add.-Currency Unrealized Base" :=
                                        NewVATEntry."Add.-Currency Unrealized Base" + "Add.-Currency Unrealized Base";
                                      NewVATEntry."Remaining Unrealized Amount" :=
                                        NewVATEntry."Remaining Unrealized Amount" + "Remaining Unrealized Amount";
                                      NewVATEntry."Remaining Unrealized Base" :=
                                        NewVATEntry."Remaining Unrealized Base" + "Remaining Unrealized Base";
                                      DELETE;
                                      GLEntryVATEntryLink.SETRANGE("VAT Entry No.","Entry No.");
                                      IF GLEntryVATEntryLink.FINDSET THEN
                                        REPEAT
                                          GLEntryVATEntryLink2 := GLEntryVATEntryLink;
                                          GLEntryVATEntryLink2.DELETE;
                                          GLEntryVATEntryLink2."VAT Entry No." := NewVATEntry."Entry No.";
                                          IF GLEntryVATEntryLink2.INSERT THEN;
                                        UNTIL GLEntryVATEntryLink.NEXT = 0;
                                      DateComprReg."No. Records Deleted" := DateComprReg."No. Records Deleted" + 1;
                                      Window.UPDATE(8,DateComprReg."No. Records Deleted");
                                    UNTIL NEXT = 0;
                                    NewVATEntry.INSERT;
                                  END;

                                  IF DateComprReg."No. Records Deleted" >= NoOfDeleted + 10 THEN BEGIN
                                    NoOfDeleted := DateComprReg."No. Records Deleted";
                                    InsertRegisters(GLReg,DateComprReg);
                                  END;
                                END;

               OnPostDataItem=BEGIN
                                IF DateComprReg."No. Records Deleted" > NoOfDeleted THEN
                                  InsertRegisters(GLReg,DateComprReg);
                              END;

               ReqFilterFields=Type,VAT Bus. Posting Group,VAT Prod. Posting Group,Tax Jurisdiction Code,Use Tax,Closed }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
      OnOpenPage=BEGIN
                   IF EntrdDateComprReg."Ending Date" = 0D THEN
                     EntrdDateComprReg."Ending Date" := TODAY;

                   WITH "VAT Entry" DO BEGIN
                     InsertField(FIELDNO("Document No."),FIELDCAPTION("Document No."));
                     InsertField(FIELDNO("Bill-to/Pay-to No."),FIELDCAPTION("Bill-to/Pay-to No."));
                     InsertField(FIELDNO("EU 3-Party Trade"),FIELDCAPTION("EU 3-Party Trade"));
                     InsertField(FIELDNO("Country/Region Code"),FIELDCAPTION("Country/Region Code"));
                     InsertField(FIELDNO("Internal Ref. No."),FIELDCAPTION("Internal Ref. No."));
                   END;
                 END;

    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[ENU=Options;
                             ESM=Opciones;
                             FRC=Options;
                             ENC=Options] }

      { 1   ;2   ;Field     ;
                  CaptionML=[ENU=Starting Date;
                             ESM=Fecha inicial;
                             FRC=Date d‚but;
                             ENC=Starting Date];
                  ToolTipML=[ENU=Specifies the first date to be included in the date compression. The compression affects all Tax entries from this date to the Ending Date field.;
                             ESM=Especifica la primera fecha que se incluir  en la compresi¢n por fechas. La compresi¢n afecta a todos los movimientos de IVA desde esta fecha hasta la indicada en el campo Fecha final.;
                             FRC=Sp‚cifie la premiŠre date … inclure dans la compression de date. La compression affecte toutes les ‚critures TVA comprises entre cette date et le champ Date fin.;
                             ENC=Specifies the first date to be included in the date compression. The compression affects all Tax entries from this date to the Ending Date field.];
                  ApplicationArea=#Basic,#Suite;
                  SourceExpr=EntrdDateComprReg."Starting Date" }

      { 2   ;2   ;Field     ;
                  CaptionML=[ENU=Ending Date;
                             ESM=Fecha final;
                             FRC=Date fin;
                             ENC=Ending Date];
                  ToolTipML=[ENU=Specifies the last date to be included in the date compression. The compression affects all Tax entries from the Starting Date field.;
                             ESM=Especifica la £ltima fecha que se incluir  en la compresi¢n por fechas. La compresi¢n afecta a todos los movimientos de IVA desde la fecha del campo Fecha inicial.;
                             FRC=Sp‚cifie la derniŠre date … inclure dans la compression. La compression affecte toutes les ‚critures TVA du champ Date d‚but.;
                             ENC=Specifies the last date to be included in the date compression. The compression affects all Tax entries from the Starting Date field.];
                  ApplicationArea=#Basic,#Suite;
                  SourceExpr=EntrdDateComprReg."Ending Date" }

      { 3   ;2   ;Field     ;
                  CaptionML=[ENU=Period Length;
                             ESM=Longitud periodo;
                             FRC=Dur‚e de la p‚riode;
                             ENC=Period Length];
                  ToolTipML=[ENU=Specifies the length of the period whose entries will be combined. To see the options, choose the field.;
                             ESM=Especifica la duraci¢n del per¡odo cuyos movimientos se combinan. Elija el campo para ver las opciones.;
                             FRC=Sp‚cifie la longueur de la p‚riode dont les ‚critures seront combin‚es. S‚lectionnez le champ pour visualiser les options.;
                             ENC=Specifies the length of the period whose entries will be combined. To see the options, choose the field.];
                  OptionCaptionML=[ENU=Day,Week,Month,Quarter,Year,Period;
                                   ESM=D¡a,Semana,Mes,Trimestre,A¤o,Periodo;
                                   FRC=Jour,Semaine,Mois,Trimestre,Ann‚e,P‚riode;
                                   ENC=Day,Week,Month,Quarter,Year,Period];
                  ApplicationArea=#Basic,#Suite;
                  SourceExpr=EntrdDateComprReg."Period Length" }

      { 1907651001;2;Group  ;
                  CaptionML=[ENU=Retain Field Contents;
                             ESM=Guardar contenido campo;
                             FRC=Conserver champ;
                             ENC=Retain Field Contents] }

      { 4   ;3   ;Field     ;
                  CaptionML=[ENU=Document No.;
                             ESM=N§ documento;
                             FRC=Nø de document;
                             ENC=Document No.];
                  ToolTipML=[ENU="Specifies if you want to retain the contents of the Document No. field. ";
                             ESM="Especifica si desea conservar el contenido del campo N.§ documento. ";
                             FRC="Sp‚cifie si vous souhaitez conserver le contenu du champ Nø document. ";
                             ENC="Specifies if you want to retain the contents of the Document No. field. "];
                  ApplicationArea=#Basic,#Suite;
                  SourceExpr=Retain[1] }

      { 5   ;3   ;Field     ;
                  CaptionML=[ENU=No.;
                             ESM=Fact./Pago a N§.;
                             FRC=Nø de Facture …/Paiement …;
                             ENC=Bill-to/Pay-to No.];
                  ToolTipML=[ENU="Specifies whether you want to retain the contents of the Bill-to/Pay-to No. field. ";
                             ESM="Especifica si desea conservar el contenido del campo Fact./Pago a N§. ";
                             FRC="Sp‚cifie si vous souhaitez conserver le contenu du champ Nødonneur/preneur d'ordre. ";
                             ENC="Specifies whether you want to retain the contents of the Bill-to/Pay-to No. field. "];
                  ApplicationArea=#Basic,#Suite;
                  SourceExpr=Retain[2] }

      { 6   ;3   ;Field     ;
                  CaptionML=[ENU=EU 3-Party Trade;
                             ESM=Op. triangular;
                             FRC=Trans. tripartite UE;
                             ENC=EU 3-Party Trade];
                  ToolTipML=[ENU="Specifies if you want to retain the contents of the EU 3-Party Trade field. ";
                             ESM="Especifica si desea conservar el contenido del campo Op. triangular UE. ";
                             FRC="Sp‚cifie si vous souhaitez conserver le contenu du champ Trans. tripartite UE. ";
                             ENC="Specifies if you want to retain the contents of the EU 3-Party Trade field. "];
                  ApplicationArea=#Basic,#Suite;
                  SourceExpr=Retain[3] }

      { 7   ;3   ;Field     ;
                  CaptionML=[ENU=Country/Region Code;
                             ESM=C¢d. pa¡s/regi¢n;
                             FRC=Code pays/r‚gion;
                             ENC=Country/Region Code];
                  ToolTipML=[ENU=Specifies if you want to retain the address country/region field contents.;
                             ESM=Especifica si desea conservar el contenido del campo pa¡s/regi¢n de la direcci¢n.;
                             FRC=Sp‚cifie si vous souhaitez conserver le contenu du champ Pays/r‚gion de l'adresse.;
                             ENC=Specifies if you want to retain the address country/region field contents.];
                  ApplicationArea=#Basic,#Suite;
                  SourceExpr=Retain[4] }

      { 8   ;3   ;Field     ;
                  CaptionML=[ENU=Internal Ref. No.;
                             ESM=N§ ref. interno;
                             FRC=Nø r‚f‚rence interne;
                             ENC=Internal Ref. No.];
                  ToolTipML=[ENU=Specifies if you want to retain the contents of the Internal Ref. No. field.;
                             ESM=Especifica si desea conservar el contenido del campo N.§ ref. interno.;
                             FRC=Sp‚cifie si vous souhaitez conserver le contenu du champ Nø r‚f‚rence interne.;
                             ENC=Specifies if you want to retain the contents of the Internal Ref. No. field.];
                  ApplicationArea=#Basic,#Suite;
                  SourceExpr=Retain[5] }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=This batch job deletes entries. Therefore, it is important that you make a backup of the database before you run the batch job.\\;ESM=Este trabajo por lotes borra movimientos. Por tanto, es muy importante realizar una copia de seguridad de la base de datos antes de ejecutar este proceso.\\;FRC=Ce traitement en lot supprime des ‚critures. Par cons‚quent, il est important de faire une sauvegarde de la base de donn‚es avant d''ex‚cuter le traitement en lot.\\;ENC=This batch job deletes entries. Therefore, it is important that you make a backup of the database before you run the batch job.\\';
      Text002@1002 : TextConst 'ENU=Do you want to date compress the entries?;ESM=¨Confirma que desea compactar por fechas los movimientos?;FRC=Voulez-vous compresser les donn‚es par date?;ENC=Do you want to date compress the entries?';
      Text003@1003 : TextConst 'ENU=%1 must be specified.;ESM=Se debe indicar %1.;FRC=%1 doit ˆtre sp‚cifi‚.;ENC=%1 must be specified.';
      Text004@1004 : TextConst 'ENU=Date compressing Tax entries...\\;ESM=Compactando por fechas movs. IVA...\\;FRC=Compression par date des ‚critures taxes...\\;ENC=Date compressing Tax entries...\\';
      Text005@1005 : TextConst 'ENU=Type                     #1##########\;ESM=Tipo                           #1##########\;FRC=Type                         #1##########\;ENC=Type                     #1##########\';
      Text006@1006 : TextConst 'ENU=Tax Bus. Posting Group   #2##########\;ESM=Grupo reg. IVA negocio         #2##########\;FRC=Par. report march‚ taxe      #2##########\;ENC=Tax Bus. Posting Group   #2##########\';
      Text007@1007 : TextConst 'ENU=Tax Prod. Posting Group  #3##########\;ESM=Grupo reg. IVA producto        #3##########\;FRC=Par. report prod. taxe       #3##########\;ENC=Tax Prod. Posting Group  #3##########\';
      Text008@1008 : TextConst 'ENU=Tax Jurisdiction         #4##########\;ESM=Jurisd. impto.                 #4##########\;FRC=Juridiction fiscale          #4##########\;ENC=Tax Jurisdiction         #4##########\';
      Text009@1009 : TextConst 'ENU=Use Tax                  #5##########\;ESM=Impto. importaci¢n             #5##########\;FRC=Taxe de service              #5##########\;ENC=Use Tax                  #5##########\';
      Text010@1010 : TextConst 'ENU=Date                     #6######\\;ESM=Fecha                          #6######\\;FRC=Date                         #6######\\;ENC=Date                     #6######\\';
      Text011@1011 : TextConst 'ENU=No. of new entries       #7######\;ESM=Nuevo n§ movs.                 #7######\;FRC=Nbre nouvelles ‚critures     #7######\;ENC=No. of new entries       #7######\';
      Text012@1012 : TextConst 'ENU=No. of entries deleted   #8######;ESM=N§ movs. eliminados            #8######;FRC=Nbre d''‚critures supprim‚es  #8######;ENC=No. of entries deleted   #8######';
      SourceCodeSetup@1013 : Record 242;
      EntrdDateComprReg@1014 : Record 87;
      DateComprReg@1015 : Record 87;
      GLReg@1016 : Record 45;
      NewVATEntry@1017 : Record 254;
      VATEntry2@1018 : Record 254;
      GLEntry@1019 : Record 17;
      GLEntryVATEntryLink@1033 : Record 253;
      GLEntryVATEntryLink2@1034 : Record 253;
      DateComprMgt@1020 : Codeunit 356;
      Window@1021 : Dialog;
      VATEntryFilter@1022 : Text[250];
      NoOfFields@1023 : Integer;
      Retain@1024 : ARRAY [10] OF Boolean;
      FieldNumber@1025 : ARRAY [10] OF Integer;
      FieldNameArray@1026 : ARRAY [10] OF Text[100];
      LastGLEntryNo@1027 : Integer;
      LastVATEntryNo@1028 : Integer;
      NextTransactionNo@1029 : Integer;
      NoOfDeleted@1030 : Integer;
      GLRegExists@1031 : Boolean;
      i@1032 : Integer;

    LOCAL PROCEDURE InitRegisters@4();
    BEGIN
      IF GLReg.FIND('+') THEN;
      GLReg.INIT;
      GLReg."No." := GLReg."No." + 1;
      GLReg."Creation Date" := TODAY;
      GLReg."Source Code" := SourceCodeSetup."Compress VAT Entries";
      GLReg."User ID" := USERID;
      GLReg."From Entry No." := LastGLEntryNo + 1;
      GLReg."From VAT Entry No." := LastVATEntryNo + 1;

      IF DateComprReg.FINDLAST THEN;
      DateComprReg.INIT;
      DateComprReg."No." := DateComprReg."No." + 1;
      DateComprReg."Table ID" := DATABASE::"VAT Entry";
      DateComprReg."Creation Date" := TODAY;
      DateComprReg."Starting Date" := EntrdDateComprReg."Starting Date";
      DateComprReg."Ending Date" := EntrdDateComprReg."Ending Date";
      DateComprReg."Period Length" := EntrdDateComprReg."Period Length";
      FOR i := 1 TO NoOfFields DO
        IF Retain[i] THEN
          DateComprReg."Retain Field Contents" :=
            COPYSTR(
              DateComprReg."Retain Field Contents" + ',' + FieldNameArray[i],1,
              MAXSTRLEN(DateComprReg."Retain Field Contents"));
      DateComprReg."Retain Field Contents" := COPYSTR(DateComprReg."Retain Field Contents",2);
      DateComprReg.Filter := VATEntryFilter;
      DateComprReg."Register No." := GLReg."No.";
      DateComprReg."Source Code" := SourceCodeSetup."Compress VAT Entries";
      DateComprReg."User ID" := USERID;

      GLRegExists := FALSE;
      NoOfDeleted := 0;
    END;

    LOCAL PROCEDURE InsertRegisters@1(VAR GLReg@1000 : Record 45;VAR DateComprReg@1001 : Record 87);
    BEGIN
      GLEntry.INIT;
      LastGLEntryNo := LastGLEntryNo + 1;
      GLEntry."Entry No." := LastGLEntryNo;
      GLEntry."Posting Date" := TODAY;
      GLEntry."Source Code" := SourceCodeSetup."Compress VAT Entries";
      GLEntry."System-Created Entry" := TRUE;
      GLEntry."User ID" := USERID;
      GLEntry."Transaction No." := NextTransactionNo;
      GLEntry.INSERT;
      GLEntry.CONSISTENT(GLEntry.Amount = 0);
      GLReg."To Entry No." := GLEntry."Entry No.";
      GLReg."To VAT Entry No." := NewVATEntry."Entry No.";

      IF GLRegExists THEN BEGIN
        GLReg.MODIFY;
        DateComprReg.MODIFY;
      END ELSE BEGIN
        GLReg.INSERT;
        DateComprReg.INSERT;
        GLRegExists := TRUE;
      END;
      COMMIT;

      GLEntry.LOCKTABLE;
      NewVATEntry.LOCKTABLE;
      GLReg.LOCKTABLE;
      DateComprReg.LOCKTABLE;

      IF GLEntry.FINDLAST THEN;
      IF NewVATEntry.FINDLAST THEN;
      IF (LastGLEntryNo <> GLEntry."Entry No.") OR
         (LastVATEntryNo <> NewVATEntry."Entry No.")
      THEN BEGIN
        LastGLEntryNo := GLEntry."Entry No.";
        LastVATEntryNo := NewVATEntry."Entry No.";
        NextTransactionNo := GLEntry."Transaction No." + 1;
        InitRegisters;
      END;
    END;

    LOCAL PROCEDURE InsertField@2(Number@1000 : Integer;Name@1001 : Text[100]);
    BEGIN
      NoOfFields := NoOfFields + 1;
      FieldNumber[NoOfFields] := Number;
      FieldNameArray[NoOfFields] := Name;
    END;

    LOCAL PROCEDURE RetainNo@3(Number@1000 : Integer) : Boolean;
    BEGIN
      EXIT(Retain[Index(Number)]);
    END;

    LOCAL PROCEDURE Index@6(Number@1000 : Integer) : Integer;
    BEGIN
      FOR i := 1 TO NoOfFields DO
        IF Number = FieldNumber[i] THEN
          EXIT(i);
    END;

    LOCAL PROCEDURE InitializeParameter@10();
    BEGIN
      IF EntrdDateComprReg."Ending Date" = 0D THEN
        EntrdDateComprReg."Ending Date" := TODAY;

      WITH "VAT Entry" DO BEGIN
        InsertField(FIELDNO("Document No."),FIELDCAPTION("Document No."));
        InsertField(FIELDNO("Bill-to/Pay-to No."),FIELDCAPTION("Bill-to/Pay-to No."));
        InsertField(FIELDNO("EU 3-Party Trade"),FIELDCAPTION("EU 3-Party Trade"));
        InsertField(FIELDNO("Country/Region Code"),FIELDCAPTION("Country/Region Code"));
        InsertField(FIELDNO("Internal Ref. No."),FIELDCAPTION("Internal Ref. No."));
      END;
    END;

    [External]
    PROCEDURE InitializeRequest@15(StartingDate@1007 : Date;EndingDate@1006 : Date;PeriodLength@1005 : Option;RetainDocumentNo@1003 : Boolean;RetainBilltoPaytoNo@1002 : Boolean;RetainEU3PartyTrade@1001 : Boolean;RetainCountryRegionCode@1000 : Boolean;RetainInternalRefNo@1008 : Boolean);
    BEGIN
      InitializeParameter;
      EntrdDateComprReg."Starting Date" := StartingDate;
      EntrdDateComprReg."Ending Date" := EndingDate;
      EntrdDateComprReg."Period Length" := PeriodLength;
      Retain[1] := RetainDocumentNo;
      Retain[2] := RetainBilltoPaytoNo;
      Retain[3] := RetainEU3PartyTrade;
      Retain[4] := RetainCountryRegionCode;
      Retain[5] := RetainInternalRefNo;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

