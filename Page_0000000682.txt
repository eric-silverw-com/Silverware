OBJECT Page 682 Schedule a Report
{
  OBJECT-PROPERTIES
  {
    Date=11/27/18;
    Time=11:10:27 PM;
    Version List=;
  }
  PROPERTIES
  {
    CaptionML=ENU=Schedule a Report;
    InsertAllowed=No;
    DeleteAllowed=No;
    SourceTable=Table472;
    DataCaptionExpr=Description;
    PageType=StandardDialog;
    SourceTableTemporary=Yes;
    OnOpenPage=BEGIN
                 IF NOT FINDFIRST THEN BEGIN
                   INIT;
                   ReportEditable := TRUE;
                   OutPutEditable := TRUE;
                   Status := Status::"On Hold";
                   VALIDATE("Object Type to Run","Object Type to Run"::Report);
                   INSERT(TRUE);
                 END ELSE
                   OutPutEditable := REPORT.DEFAULTLAYOUT("Object ID to Run") <> DEFAULTLAYOUT::None; // Processing Only
               END;

    OnQueryClosePage=VAR
                       JobQueueEntry@1000 : Record 472;
                     BEGIN
                       IF CloseAction <> ACTION::OK THEN
                         EXIT(TRUE);

                       IF "Object ID to Run" = 0 THEN BEGIN
                         MESSAGE(NoIdMsg);
                         EXIT(FALSE);
                       END;

                       CALCFIELDS(XML);
                       JobQueueEntry := Rec;
                       CLEAR(JobQueueEntry.ID); // "Job Queue - Enqueue" defines it on the real record insert
                       JobQueueEntry."Run in User Session" := TRUE;
                       IF JobQueueEntry.Description = '' THEN
                         JobQueueEntry.Description := COPYSTR("Object Caption to Run",1,MAXSTRLEN(JobQueueEntry.Description));
                       CODEUNIT.RUN(CODEUNIT::"Job Queue - Enqueue",JobQueueEntry);
                       IF JobQueueEntry.IsToReportInbox THEN
                         MESSAGE(ReportScheduledMsg);
                       EXIT(TRUE);
                     END;

  }
  CONTROLS
  {
    { 1   ;0   ;Container ;
                ContainerType=ContentArea }

    { 3   ;1   ;Field     ;
                CaptionML=ENU=Report ID;
                ToolTipML=ENU=Specifies the ID of the object that is to be run for this job. You can select an ID that is of the object type that you have specified in the Object Type to Run field.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Object ID to Run";
                Editable=ReportEditable;
                OnValidate=BEGIN
                             IF "Object ID to Run" <> 0 THEN
                               RunReportRequestPage;
                             OutPutEditable := REPORT.DEFAULTLAYOUT("Object ID to Run") <> DEFAULTLAYOUT::None; // Processing Only
                           END;

                OnLookup=VAR
                           NewObjectID@1000 : Integer;
                         BEGIN
                           IF LookupObjectID(NewObjectID) THEN BEGIN
                             Text := FORMAT(NewObjectID);
                             EXIT(TRUE);
                           END;
                           EXIT(FALSE);
                         END;
                          }

    { 4   ;1   ;Field     ;
                CaptionML=ENU=Report Name;
                ToolTipML=ENU=Specifies the name of the object that is selected in the Object ID to Run field.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Object Caption to Run";
                Enabled=FALSE }

    { 10  ;1   ;Field     ;
                ToolTipML=ENU=Specifies a description of the job queue entry. You can edit and update the description on the job queue entry card. The description is also displayed in the Job Queue Entries window, but it cannot be updated there. You can enter a maximum of 50 characters, both numbers and letters.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=Description }

    { 5   ;1   ;Field     ;
                ToolTipML=ENU=Specifies whether options on the report request page have been set for scheduled report job. If the check box is selected, then options have been set for the scheduled report.;
                ApplicationArea=#All;
                SourceExpr="Report Request Page Options";
                Visible=ReportEditable }

    { 6   ;1   ;Field     ;
                ToolTipML=ENU=Specifies the output of the scheduled report.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Report Output Type";
                Enabled=OutPutEditable }

    { 7   ;1   ;Field     ;
                ToolTipML=ENU=Specifies the printer to use to print the scheduled report.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Printer Name";
                Importance=Additional;
                Enabled="Report Output Type" = "Report Output Type"::Print }

    { 8   ;1   ;Field     ;
                ToolTipML=ENU=Specifies the earliest date and time when the job queue entry should be run.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Earliest Start Date/Time";
                Importance=Additional }

    { 9   ;1   ;Field     ;
                ToolTipML=ENU=Specifies the date and time when the job queue entry is to expire, after which the job queue entry will not be run.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Expiration Date/Time";
                Importance=Additional }

  }
  CODE
  {
    VAR
      NoIdMsg@1000 : TextConst 'ENU=You must specify a report number.';
      ReportEditable@1001 : Boolean;
      OutPutEditable@1002 : Boolean;
      ReportScheduledMsg@1003 : TextConst 'ENU=The report has been scheduled. It will appear in the Report Inbox part when it is completed.';

    [External]
    PROCEDURE ScheduleAReport@1(ReportId@1000 : Integer;RequestPageXml@1001 : Text) : Boolean;
    VAR
      ScheduleAReport@1002 : Page 682;
    BEGIN
      ScheduleAReport.SetParameters(ReportId,RequestPageXml);
      EXIT(ScheduleAReport.RUNMODAL = ACTION::OK);
    END;

    [External]
    PROCEDURE SetParameters@2(ReportId@1000 : Integer;RequestPageXml@1001 : Text);
    BEGIN
      INIT;
      Status := Status::"On Hold";
      VALIDATE("Object Type to Run","Object Type to Run"::Report);
      VALIDATE("Object ID to Run",ReportId);
      INSERT(TRUE);
      SetReportParameters(RequestPageXml);
    END;

    BEGIN
    END.
  }
}

