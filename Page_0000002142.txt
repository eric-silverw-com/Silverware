OBJECT Page 2142 O365 First Invoice Wizard
{
  OBJECT-PROPERTIES
  {
    Date=09/19/18;
    Time=12:00:00 PM;
    Version List=NAVW113.00,NAVNA13.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=" ";
               ESM=" ";
               FRC=" ";
               ENC=" "];
    PageType=NavigatePage;
    OnInit=BEGIN
             Initialize
           END;

    OnOpenPage=BEGIN
                 Step := Step::First;
                 EnableControls;

                 ItemBaseUnitOfMeasure := O365TemplateManagement.GetDefaultBaseUnitOfMeasure;
                 SetDefaultTaxAreaCodeForCA;
                 IF IsUsingVAT THEN BEGIN
                   VATBusinessPostingGroupCode := O365TemplateManagement.GetDefaultVATBusinessPostingGroup;
                   VATProductPostingGroup.GET(O365TemplateManagement.GetDefaultVATProdPostingGroup);
                 END;
               END;

    OnQueryClosePage=BEGIN
                       IF Customer."No." <> '' THEN
                         IF Step <> Step::Finish THEN
                           Customer.DELETE(TRUE);
                     END;

    ActionList=ACTIONS
    {
      { 10      ;    ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 14      ;1   ;Action    ;
                      Name=ActionNext;
                      CaptionML=[ENU=Next;
                                 ESM=Siguiente;
                                 FRC=Suivant;
                                 ENC=Next];
                      ApplicationArea=#Basic,#Suite,#Invoicing;
                      Visible=NextActionEnabled;
                      InFooterBar=Yes;
                      Image=NextRecord;
                      OnAction=BEGIN
                                 NextStep;
                               END;
                                }
      { 35      ;1   ;Action    ;
                      Name=ActionBack;
                      CaptionML=[ENU=Back;
                                 ESM=Atr s;
                                 FRC=Retour;
                                 ENC=Back];
                      ApplicationArea=#Basic,#Suite,#Invoicing;
                      Visible=BackActionEnabled;
                      InFooterBar=Yes;
                      Image=PreviousRecord;
                      OnAction=BEGIN
                                 PrevStep;
                               END;
                                }
      { 34      ;1   ;Action    ;
                      Name=ActionCreateInvoice;
                      CaptionML=[ENU=Create Invoice;
                                 ESM=Crear factura;
                                 FRC=Cr‚er facture;
                                 ENC=Create Invoice];
                      ApplicationArea=#Basic,#Suite,#Invoicing;
                      Visible=CreateInvoiceActionEnabled;
                      InFooterBar=Yes;
                      OnAction=BEGIN
                                 Step := Step::Customer;
                                 EnableControls;
                               END;
                                }
      { 36      ;1   ;Action    ;
                      Name=ActionDone;
                      CaptionML=[ENU=Done;
                                 ESM=Hecho;
                                 FRC=Termin‚;
                                 ENC=Done];
                      ApplicationArea=#Basic,#Suite,#Invoicing;
                      Visible=DoneActionEnabled;
                      InFooterBar=Yes;
                      OnAction=BEGIN
                                 CurrPage.CLOSE;
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1   ;0   ;Container ;
                ContainerType=ContentArea }

    { 96  ;1   ;Group     ;
                Visible=TopBannerVisible AND CustomerStepVisible;
                Editable=FALSE;
                GroupType=Group }

    { 97  ;2   ;Field     ;
                ApplicationArea=#Basic,#Suite,#Invoicing;
                SourceExpr=MediaResourcesFirstInv1."Media Reference";
                Editable=FALSE;
                ShowCaption=No }

    { 98  ;1   ;Group     ;
                Visible=TopBannerVisible AND ItemStepVisible;
                Editable=FALSE;
                GroupType=Group }

    { 99  ;2   ;Field     ;
                ApplicationArea=#Basic,#Suite,#Invoicing;
                SourceExpr=MediaResourcesFirstInv2."Media Reference";
                Editable=FALSE;
                ShowCaption=No }

    { 42  ;1   ;Group     ;
                Visible=TopBannerVisible AND TaxStepVisible;
                Editable=FALSE;
                GroupType=Group }

    { 41  ;2   ;Field     ;
                ApplicationArea=#Basic,#Suite,#Invoicing;
                SourceExpr=MediaResourcesFirstInv3."Media Reference";
                Editable=FALSE;
                ShowCaption=No }

    { 24  ;1   ;Group     ;
                Name=FirstStep;
                Visible=FirstStepVisible;
                GroupType=Group }

    { 23  ;2   ;Group     ;
                CaptionML=[ENU=Hi;
                           ESM=Hola;
                           FRC=Bonjour;
                           ENC=Hi];
                Visible=NOT UserNameAvailable;
                GroupType=Group }

    { 1020004;2;Group     ;
                CaptionML=[ENU=Hi,;
                           ESM=Hola,;
                           FRC=Bonjour;
                           ENC=Hi,];
                Visible=UserNameAvailable;
                GroupType=Group }

    { 1020003;3;Field     ;
                Name=UserName;
                CaptionML=[ENU=User;
                           ESM=Usuario;
                           FRC=Utilisateur;
                           ENC=User];
                ApplicationArea=#Basic,#Suite,#Invoicing;
                SourceExpr=UserName;
                Editable=FALSE;
                OnValidate=BEGIN
                             EnableControls;
                           END;

                ShowCaption=No }

    { 1020006;2;Group     ;
                Visible=ImagesVisible;
                Editable=FALSE;
                GroupType=Group }

    { 1020005;3;Field     ;
                Name=<MediaRepositoryFirstInvFirst>;
                CaptionML=[ENU=<MediaRepositoryFirstInvFirst>;
                           ESM=<MediaRepositoryFirstInvFirst>;
                           FRC=<MediaRepositoryFirstInvFirst>;
                           ENC=<MediaRepositoryFirstInvFirst>];
                ApplicationArea=#Basic,#Suite,#Invoicing;
                SourceExpr=MediaResourcesFirstInvFirst."Media Reference";
                Editable=FALSE;
                ShowCaption=No }

    { 19  ;2   ;Group     ;
                GroupType=Group;
                InstructionalTextML=[ENU=Let's create your first invoice.;
                                     ESM=Vamos a crear su primera factura.;
                                     FRC=Cr‚ez votre premiŠre facture.;
                                     ENC=Let's create your first invoice.] }

    { 8   ;1   ;Group     ;
                Name=CustomerStep;
                Visible=CustomerStepVisible;
                GroupType=Group }

    { 13  ;2   ;Group     ;
                CaptionML=[ENU=Who are you invoicing?;
                           ESM=¨A qui‚n factura?;
                           FRC=Qui facturez-vousÿ?;
                           ENC=Who are you invoicing?];
                GroupType=Group }

    { 18  ;3   ;Group     ;
                GroupType=Group;
                InstructionalTextML=[ENU=We'll file this away for next time.;
                                     ESM=Archivaremos esto para la pr¢xima vez.;
                                     FRC=Nous allons classer cela jusqu'… la prochaine fois.;
                                     ENC=We'll file this away for next time.] }

    { 3   ;4   ;Field     ;
                Name=CustomerName;
                CaptionML=[ENU=Customer name;
                           ESM=Nombre del cliente;
                           FRC=Nom du client;
                           ENC=Customer name];
                ApplicationArea=#Basic,#Suite,#Invoicing;
                SourceExpr=CustomerName;
                OnValidate=BEGIN
                             ModifyCustomer;
                             EnableControls;
                           END;

                ShowCaption=No }

    { 1020002;4;Field     ;
                Name=CustomerEmail;
                ExtendedDatatype=E-Mail;
                CaptionML=[ENU=Email (optional);
                           ESM=Correo electr¢nico (opcional);
                           FRC=Courriel (facultatif);
                           ENC=Email (optional)];
                ApplicationArea=#Basic,#Suite,#Invoicing;
                SourceExpr=CustomerEmail;
                OnValidate=VAR
                             MailManagement@1000 : Codeunit 9520;
                           BEGIN
                             IF CustomerEmail <> '' THEN
                               MailManagement.CheckValidEmailAddress(CustomerEmail);
                             ModifyCustomer;
                             EnableControls;
                           END;

                ShowCaption=No }

    { 22  ;4   ;Field     ;
                Name=<FullAddress>;
                CaptionML=[ENU=Address (optional);
                           ESM=Direcci¢n (opcional);
                           FRC=Adresse (facultatif);
                           ENC=Address (optional)];
                ApplicationArea=#Basic,#Suite,#Invoicing;
                SourceExpr=FullAddress;
                OnAssistEdit=VAR
                               TempStandardAddress@1000 : TEMPORARY Record 730;
                             BEGIN
                               CreateCustomer(CustomerName);
                               TempStandardAddress.CopyFromCustomer(Customer);
                               IF PAGE.RUNMODAL(PAGE::"O365 Address",TempStandardAddress) = ACTION::LookupOK THEN BEGIN
                                 Customer.GET(Customer."No.");
                                 FullAddress := TempStandardAddress.ToString;
                               END;
                             END;

                ShowCaption=No;
                QuickEntry=FALSE }

    { 7   ;1   ;Group     ;
                Name=ItemStep;
                Visible=ItemStepVisible;
                GroupType=Group }

    { 26  ;2   ;Group     ;
                CaptionML=[ENU=What did you sell?;
                           ESM=¨Qu‚ vendi¢?;
                           FRC=Qu'avez-vous venduÿ?;
                           ENC=What did you sell?];
                GroupType=Group }

    { 2   ;3   ;Group     ;
                GroupType=Group;
                InstructionalTextML=[ENU=Start adding items. You can always edit later.;
                                     ESM=Permite empezar a agregar productos. Siempre puede editar m s adelante.;
                                     FRC=Commencez … ajouter des articles. Vous pouvez les modifier ult‚rieurement.;
                                     ENC=Start adding items. You can always edit later.] }

    { 4   ;4   ;Field     ;
                Name=ItemDescription;
                CaptionML=[ENU=Description of product or service;
                           ESM=Descripci¢n del producto o servicio;
                           FRC=Description du produit ou service;
                           ENC=Description of product or service];
                ApplicationArea=#Basic,#Suite,#Invoicing;
                SourceExpr=ItemDescription;
                OnValidate=BEGIN
                             EnableControls;
                           END;

                ShowCaption=No }

    { 5   ;4   ;Field     ;
                Name=ItemPrice;
                CaptionML=[ENU=Price (excl. tax);
                           ESM=Precio (IVA excl.);
                           FRC=Prix (hors taxe);
                           ENC=Price (excl. tax)];
                ApplicationArea=#Basic,#Suite,#Invoicing;
                DecimalPlaces=2:5;
                BlankZero=Yes;
                SourceExpr=ItemPrice;
                MinValue=0;
                ShowCaption=No }

    { 28  ;1   ;Group     ;
                Name=TaxStep;
                Visible=TaxStepVisible;
                GroupType=Group }

    { 27  ;2   ;Group     ;
                CaptionML=[ENU=Need to add sales tax?;
                           ESM=¨Necesita agregar el impuesto sobre las ventas?;
                           FRC=Besoin d'ajouter la taxe de venteÿ?;
                           ENC=Need to add sales tax?];
                Visible=(NOT IsUsingVAT);
                GroupType=Group }

    { 17  ;3   ;Group     ;
                Visible=(NOT IsUsingVAT) AND (NOT IsCanada);
                GroupType=Group;
                InstructionalTextML=[ENU=Tell us the tax rate for your region.;
                                     ESM=Necesitamos que nos indique la tasa de impuestos de su regi¢n.;
                                     FRC=Dites-nous le taux de taxe pour votre r‚gion.;
                                     ENC=Tell us the tax rate for your region.] }

    { 9   ;4   ;Field     ;
                Name=CityTax;
                CaptionML=[ENU=City Tax %;
                           ESM=Impuesto municipal %;
                           FRC=% de taxe municipale;
                           ENC=City Tax %];
                ApplicationArea=#Basic,#Suite,#Invoicing;
                DecimalPlaces=1:3;
                BlankZero=Yes;
                SourceExpr=CityTax;
                MinValue=0;
                OnValidate=BEGIN
                             EnableControls;
                           END;

                ShowCaption=No }

    { 6   ;4   ;Field     ;
                Name=StateTax;
                CaptionML=[ENU=State Tax %;
                           ESM=Impuesto provincial %;
                           FRC=% de taxe provinciale;
                           ENC=Province/State Tax %];
                ApplicationArea=#Basic,#Suite,#Invoicing;
                DecimalPlaces=1:3;
                BlankZero=Yes;
                SourceExpr=StateTax;
                MinValue=0;
                ShowCaption=No }

    { 1020001;3;Group     ;
                Visible=(NOT IsUsingVAT) AND IsCanada;
                GroupType=Group;
                InstructionalTextML=[ENU=Tell us your region.;
                                     ESM=Ind¡quenos su regi¢n.;
                                     FRC=Pr‚cisez votre r‚gion.;
                                     ENC=Tell us your region.] }

    { 1020000;4;Field     ;
                Name=TaxAreaCode;
                CaptionML=[ENU=Tax Area Code;
                           ESM=C¢digo de  rea fiscal;
                           FRC=Code de r‚gion fiscale;
                           ENC=Tax Area Code];
                ApplicationArea=#Basic,#Suite,#Invoicing;
                SourceExpr=TaxAreaCodeCA;
                TableRelation="Tax Area".Code;
                OnLookup=VAR
                           TaxArea@1020001 : Record 318;
                           CloseAction@1020000 : Action;
                         BEGIN
                           CloseAction := PAGE.RUNMODAL(PAGE::"O365 Tax Area List",TaxArea);
                           IF CloseAction <> ACTION::LookupOK THEN
                             EXIT;

                           TaxAreaCodeCA := TaxArea.Code;
                         END;

                QuickEntry=FALSE }

    { 1020007;2;Group     ;
                CaptionML=[ENU=Here is your default Tax.;
                           ESM=Este es su IVA predeterminado.;
                           FRC=Voici votre TVA par d‚faut.;
                           ENC=Here is your default Tax.];
                Visible=IsUsingVAT;
                GroupType=Group }

    { 21  ;3   ;Group     ;
                Visible=IsUsingVAT;
                GroupType=Group;
                InstructionalTextML=[ENU=You can always edit it later.;
                                     ESM=Siempre puede editarlo m s adelante.;
                                     FRC=Vous pouvez toujours la modifier ult‚rieurement.;
                                     ENC=You can always edit it later.] }

    { 16  ;4   ;Field     ;
                Name=VAT Group;
                CaptionML=[ENU=Tax;
                           ESM=IVA;
                           FRC=TVA;
                           ENC=Tax];
                ToolTipML=[ENU=Specifies the Tax group code for this item.;
                           ESM=Especifica el c¢digo de grupo de IVA de este producto.;
                           FRC=Sp‚cifie un code groupe TVA pour cet article.;
                           ENC=Specifies the Tax group code for this item.];
                ApplicationArea=#Basic,#Suite,#Invoicing;
                NotBlank=Yes;
                SourceExpr=VATProductPostingGroup.Description;
                Visible=IsUsingVAT;
                OnLookup=BEGIN
                           IF PAGE.RUNMODAL(PAGE::"O365 VAT Product Posting Gr.",VATProductPostingGroup) = ACTION::LookupOK THEN;
                         END;

                ShowCaption=No;
                QuickEntry=FALSE }

    { 49  ;1   ;Group     ;
                Name=FinalStep;
                Visible=FinalStepVisible;
                GroupType=Group }

    { 20  ;2   ;Group     ;
                Visible=ImagesVisible;
                Editable=FALSE;
                GroupType=Group }

    { 12  ;3   ;Field     ;
                Name=<MediaRepositoryLastInvFirst>;
                CaptionML=[ENU=<MediaRepositoryLastInvFirst>;
                           ESM=<éltimaFacturaRepositorioMediosPrimero>;
                           FRC=<DerniŠreFactureR‚f‚rentielM‚diaEnPremier>;
                           ENC=<MediaRepositoryLastInvFirst>];
                ApplicationArea=#Basic,#Suite,#Invoicing;
                SourceExpr=MediaResourcesFirstInvLast."Media Reference";
                Editable=FALSE;
                ShowCaption=No }

    { 48  ;2   ;Group     ;
                CaptionML=[ENU=Voila!;
                           ESM=­Ya est !;
                           FRC=Voil…ÿ!;
                           ENC=Voila!];
                GroupType=Group }

    { 46  ;3   ;Group     ;
                GroupType=Group;
                InstructionalTextML=[ENU=Your first invoice is ready. Preview it, send it, or add more details whenever you want.;
                                     ESM=Su primera factura est  lista. Muestre una vista previa de ella, env¡ela o agregue m s detalles siempre que desee.;
                                     FRC=Votre premiŠre facture est prˆte. Affichez-en un aper‡u, envoyez-la ou ajoutez d'autres d‚tails quand vous le souhaitez.;
                                     ENC=Your first invoice is ready. Preview it, send it, or add more details whenever you want.] }

  }
  CODE
  {
    VAR
      Customer@1001 : Record 18;
      Item@1002 : Record 27;
      SalesHeader@1003 : Record 36;
      SalesLine@1030 : Record 37;
      MediaRepositoryFirstInv1@1040 : Record 9400;
      MediaRepositoryFirstInv2@1034 : Record 9400;
      MediaRepositoryFirstInv3@1041 : Record 9400;
      MediaRepositoryFirstInvFirst@1033 : Record 9400;
      MediaRepositoryFirstInvLast@1032 : Record 9400;
      MediaResourcesFirstInv1@1028 : Record 2000000182;
      MediaResourcesFirstInv2@1027 : Record 2000000182;
      MediaResourcesFirstInv3@1039 : Record 2000000182;
      MediaResourcesFirstInvFirst@1038 : Record 2000000182;
      MediaResourcesFirstInvLast@1029 : Record 2000000182;
      O365SalesInitialSetup@1023 : Record 2110;
      TaxSetup@1020008 : Record 326;
      SalesTaxSetupWizard@1020007 : Record 10807;
      VATProductPostingGroup@1037 : Record 324;
      O365TemplateManagement@1024 : Codeunit 2142;
      Step@1012 : 'First,Customer,Item,Tax,Finish';
      TopBannerVisible@1042 : Boolean;
      ImagesVisible@1036 : Boolean;
      FirstStepVisible@1009 : Boolean;
      CustomerStepVisible@1010 : Boolean;
      ItemStepVisible@1011 : Boolean;
      TaxStepVisible@1019 : Boolean;
      FinalStepVisible@1015 : Boolean;
      BackActionEnabled@1008 : Boolean;
      NextActionEnabled@1014 : Boolean;
      CreateInvoiceActionEnabled@1016 : Boolean;
      DoneActionEnabled@1017 : Boolean;
      UserNameAvailable@1013 : Boolean;
      UserName@1035 : Text;
      CustomerName@1005 : Text[50];
      ItemDescription@1007 : Text[50];
      FullAddress@1025 : Text;
      ItemPrice@1000 : Decimal;
      ItemBaseUnitOfMeasure@1004 : Code[10];
      CityTax@1020 : Decimal;
      StateTax@1022 : Decimal;
      VATBusinessPostingGroupCode@1018 : Code[20];
      IsUsingVAT@1021 : Boolean;
      ItemNoDescriptionErr@1026 : TextConst 'ENU=Give your product or service a description.;ESM=Permite a¤adir al producto o servicio una descripci¢n.;FRC=Donnez une description … votre produit ou service.;ENC=Give your product or service a description.';
      CustNoNameErr@1031 : TextConst 'ENU=Enter a customer name to create your first invoice for them.;ESM=Permite escribir un nombre de cliente para crear la primera factura a su nombre.;FRC=Entrez un nom de client pour cr‚er votre premiŠre facture … son intention.;ENC=Enter a customer name to create your first invoice for them.';
      IsCanada@1020001 : Boolean;
      TaxAreaCodeCA@1020000 : Code[20];
      DefaultCityTxt@1020005 : TextConst 'ENU=Default;ESM=Predet.;FRC=Par d‚faut;ENC=Default';
      DefaultStateTxt@1020004 : TextConst 'ENU=NN;ESM=NN;FRC=NN;ENC=NN';
      DefaultLbl@1020003 : TextConst 'ENU=Default;ESM=Predeterminado;FRC=Par d‚faut;ENC=Default';
      TaxableCodeTxt@1020002 : TextConst '@@@={Locked};ENU=TAXABLE;ESM=TAXABLE;FRC=TAXABLE;ENC=TAXABLE';
      CustomerEmail@1020006 : Text[80];

    PROCEDURE HasCompleted@9() : Boolean;
    BEGIN
      EXIT(Step = Step::Finish);
    END;

    PROCEDURE GetInvoiceNo@15() : Code[20];
    BEGIN
      EXIT(SalesHeader."No.");
    END;

    LOCAL PROCEDURE EnableControls@2();
    BEGIN
      ResetControls;

      CASE Step OF
        Step::First:
          ShowFirstStep;
        Step::Customer:
          ShowCustomerStep;
        Step::Item:
          ShowItemStep;
        Step::Tax:
          ShowTaxStep;
        Step::Finish:
          ShowFinishStep;
      END;
    END;

    LOCAL PROCEDURE PrevStep@12();
    BEGIN
      CASE Step OF
        Step::First:
          ShowFirstStep;
        Step::Customer:
          ShowFirstStep;
        Step::Item:
          ShowCustomerStep;
        Step::Tax:
          ShowItemStep;
        Step::Finish:
          ShowFinishStep;
      END;
      EnableControls;
    END;

    LOCAL PROCEDURE NextStep@41();
    BEGIN
      CASE Step OF
        Step::First:
          ShowCustomerStep;
        Step::Customer:
          IF ValidateCustomer THEN
            ShowItemStep;
        Step::Item:
          IF ValidateItem THEN
            ShowTaxStep;
        Step::Tax:
          ShowFinishStep;
      END;
      EnableControls;
    END;

    LOCAL PROCEDURE ShowFirstStep@7();
    BEGIN
      Step := Step::First;
      FirstStepVisible := TRUE;
      CreateInvoiceActionEnabled := TRUE;
    END;

    LOCAL PROCEDURE ShowCustomerStep@33();
    BEGIN
      Step := Step::Customer;
      CustomerStepVisible := TRUE;
      NextActionEnabled := TRUE;
      BackActionEnabled := TRUE;
    END;

    LOCAL PROCEDURE ShowItemStep@34();
    BEGIN
      Step := Step::Item;
      ItemStepVisible := TRUE;
      NextActionEnabled := TRUE;
      BackActionEnabled := TRUE;
    END;

    LOCAL PROCEDURE ShowTaxStep@8();
    BEGIN
      Step := Step::Tax;
      TaxStepVisible := TRUE;
      NextActionEnabled := TRUE;
      BackActionEnabled := TRUE;
    END;

    LOCAL PROCEDURE ShowFinishStep@35();
    BEGIN
      CreateInvoice;
      Step := Step::Finish;
      FinalStepVisible := TRUE;
      DoneActionEnabled := TRUE;
    END;

    LOCAL PROCEDURE ResetControls@1();
    BEGIN
      NextActionEnabled := FALSE;
      FirstStepVisible := FALSE;
      CustomerStepVisible := FALSE;
      ItemStepVisible := FALSE;
      TaxStepVisible := FALSE;
      FinalStepVisible := FALSE;
      BackActionEnabled := FALSE;
      DoneActionEnabled := FALSE;
      CreateInvoiceActionEnabled := FALSE;
    END;

    LOCAL PROCEDURE LoadTopBanners@40();
    BEGIN
      IF MediaRepositoryFirstInv1.GET('FirstInvoice1.png','PHONE') AND
         MediaRepositoryFirstInv2.GET('FirstInvoice2.png','PHONE') AND
         MediaRepositoryFirstInv3.GET('FirstInvoice3.png','PHONE')
      THEN
        IF MediaResourcesFirstInv1.GET(MediaRepositoryFirstInv1."Media Resources Ref") AND
           MediaResourcesFirstInv2.GET(MediaRepositoryFirstInv2."Media Resources Ref") AND
           MediaResourcesFirstInv3.GET(MediaRepositoryFirstInv3."Media Resources Ref")
        THEN
          TopBannerVisible := MediaResourcesFirstInv1."Media Reference".HASVALUE;
    END;

    LOCAL PROCEDURE LoadImages@6();
    BEGIN
      IF MediaRepositoryFirstInvFirst.GET('FirstInvoiceSplash.png','PHONE') AND
         MediaRepositoryFirstInvLast.GET('FirstInvoiceSplash.png','PHONE')
      THEN
        IF MediaResourcesFirstInvFirst.GET(MediaRepositoryFirstInvFirst."Media Resources Ref") AND
           MediaResourcesFirstInvLast.GET(MediaRepositoryFirstInvLast."Media Resources Ref")
        THEN
          ImagesVisible := MediaResourcesFirstInvFirst."Media Reference".HASVALUE;
    END;

    LOCAL PROCEDURE CreateCustomer@13(CustomerName@1002 : Text[50]);
    VAR
      Customer2@1003 : Record 18;
      MiniCustomerTemplate@1000 : Record 1300;
      CustContUpdate@1004 : Codeunit 5056;
    BEGIN
      IF Customer."No." <> '' THEN
        EXIT;

      Customer2.INIT;
      IF MiniCustomerTemplate.NewCustomerFromTemplate(Customer2) THEN BEGIN
        Customer2.VALIDATE(Name,CustomerName);
        Customer2.VALIDATE("E-Mail",CustomerEmail);
        Customer2.VALIDATE("Tax Liable",TRUE);
        Customer2.MODIFY(TRUE);
        CustContUpdate.OnModify(Customer2);
        COMMIT;
      END;

      Customer := Customer2;
    END;

    LOCAL PROCEDURE CreateItem@14();
    VAR
      ItemTemplate@1000 : Record 1301;
    BEGIN
      IF Item."No." = '' THEN BEGIN
        Item.INIT;
        IF ItemTemplate.NewItemFromTemplate(Item) THEN BEGIN
          Item.VALIDATE(Description,ItemDescription);
          Item.VALIDATE("Unit Price",ItemPrice);
          IF IsUsingVAT THEN
            Item.VALIDATE("VAT Prod. Posting Group",VATProductPostingGroup.Code);
          IF ItemBaseUnitOfMeasure <> '' THEN
            Item.VALIDATE("Base Unit of Measure",ItemBaseUnitOfMeasure);
          Item.MODIFY(TRUE);
          COMMIT;
        END;
      END;
    END;

    LOCAL PROCEDURE CreateInvoice@11();
    BEGIN
      IF SalesHeader."No." <> '' THEN
        EXIT;
      CreateCustomer(CustomerName);
      CreateItem;

      StoreSalesTaxSetup;

      SalesHeader.INIT;
      SalesHeader."Document Type" := SalesHeader."Document Type"::Invoice;
      SalesHeader.INSERT(TRUE);
      SalesHeader.VALIDATE("Sell-to Customer No.",Customer."No.");
      SalesHeader.MODIFY;

      SalesLine.INIT;
      SalesLine."Document Type" := SalesHeader."Document Type";
      SalesLine."Document No." := SalesHeader."No.";
      SalesLine."Line No." := 10000;
      SalesLine.INSERT(TRUE);
      SalesLine."Sell-to Customer No." := SalesHeader."Sell-to Customer No.";
      SalesLine.Type := SalesLine.Type::Item;
      IF IsUsingVAT THEN BEGIN
        SalesLine."VAT Bus. Posting Group" := VATBusinessPostingGroupCode;
        SalesLine."VAT Prod. Posting Group" := VATProductPostingGroup.Code;
      END;
      SalesLine.VALIDATE("No.",Item."No.");
      SalesLine.VALIDATE(Quantity,1);
      SalesLine.MODIFY;
    END;

    LOCAL PROCEDURE ValidateItem@3() : Boolean;
    BEGIN
      IF ItemDescription = '' THEN
        ERROR(ItemNoDescriptionErr);

      EXIT(TRUE);
    END;

    LOCAL PROCEDURE ValidateCustomer@4() : Boolean;
    BEGIN
      IF CustomerName = '' THEN
        ERROR(CustNoNameErr);

      EXIT(TRUE);
    END;

    LOCAL PROCEDURE GetUserFirstName@16();
    VAR
      User@1000 : Record 2000000120;
      TempString@1001 : Text;
    BEGIN
      IF User.GET(USERSECURITYID) THEN BEGIN
        TempString := User."Full Name";
        TempString := DELCHR(TempString,'<>',' ');
        TempString := CONVERTSTR(TempString,' ',',');
        UserName := SELECTSTR(1,TempString);
        IF UserName <> '' THEN
          UserNameAvailable := TRUE;
      END;
    END;

    LOCAL PROCEDURE Initialize@5();
    VAR
      CompanyInformation@1020000 : Record 79;
    BEGIN
      LoadTopBanners;
      LoadImages;
      GetUserFirstName;
      IsUsingVAT := O365SalesInitialSetup.IsUsingVAT;

      IsCanada := CompanyInformation.IsCanada;
    END;

    LOCAL PROCEDURE ModifyCustomer@10();
    VAR
      CustContUpdate@1000 : Codeunit 5056;
    BEGIN
      IF Customer."No." <> '' THEN BEGIN
        Customer.VALIDATE(Name,CustomerName);
        Customer.VALIDATE("E-Mail",CustomerEmail);
        Customer.MODIFY(TRUE);
        CustContUpdate.OnModify(Customer);
      END;
    END;

    LOCAL PROCEDURE SetDefaultTaxAreaCodeForCA@1020004();
    VAR
      TaxArea@1020001 : Record 318;
      CompanyInformation@1020000 : Record 79;
    BEGIN
      IF TaxAreaCodeCA <> '' THEN
        EXIT;

      IF NOT IsCanada THEN
        EXIT;

      TaxArea.SETRANGE("Country/Region",TaxArea."Country/Region"::CA);

      IF CompanyInformation.GET AND (CompanyInformation.County <> '') THEN BEGIN
        TaxArea.SETRANGE(Code,CompanyInformation.County);
        IF TaxArea.FINDFIRST THEN
          TaxAreaCodeCA := TaxArea.Code
        ELSE BEGIN
          TaxArea.SETRANGE(Code);
          TaxArea.SETRANGE(Description,CompanyInformation.County);
          IF TaxArea.FINDFIRST THEN
            TaxAreaCodeCA := TaxArea.Code
        END;
      END;

      IF TaxAreaCodeCA = '' THEN BEGIN
        TaxArea.SETRANGE(Code);
        TaxArea.SETRANGE(Description);
        IF TaxArea.FINDFIRST THEN
          TaxAreaCodeCA := TaxArea.Code;
      END;
    END;

    LOCAL PROCEDURE StoreSalesTaxSetup@1020013();
    BEGIN
      IF NOT SalesTaxSetupWizard.GET THEN BEGIN
        SalesTaxSetupWizard.INIT;
        SalesTaxSetupWizard.INSERT;
      END;
      IF TaxSetup.GET THEN;
      IF IsCanada THEN
        StoreSalesTaxSetupForCA
      ELSE
        StoreSalesTaxSetupForUS
    END;

    LOCAL PROCEDURE StoreSalesTaxSetupForUS@1020003();
    VAR
      TaxGroup@1020001 : Record 321;
      CompanyInformation@1020005 : Record 79;
      ConfigTemplateManagement@1020007 : Codeunit 8612;
      O365TaxSettingsManagement@1020000 : Codeunit 10150;
      TaxAreaCode@1020006 : Code[20];
      TaxJurisdictionCityCode@1020004 : Code[10];
      TaxJurisdictionStateCode@1020002 : Code[10];
    BEGIN
      TaxAreaCode := DefaultLbl;
      TaxJurisdictionCityCode := DefaultCityTxt;
      TaxJurisdictionStateCode := DefaultStateTxt;

      IF CompanyInformation.GET THEN BEGIN
        IF CompanyInformation.City <> '' THEN BEGIN
          TaxAreaCode := UPPERCASE(COPYSTR(CompanyInformation.City,1,MAXSTRLEN(TaxAreaCode) - 4));
          TaxJurisdictionCityCode := COPYSTR(TaxAreaCode,1,MAXSTRLEN(TaxJurisdictionCityCode));
        END;
        IF CompanyInformation.County <> '' THEN BEGIN// 2 char state
          TaxJurisdictionStateCode := COPYSTR(CompanyInformation.County,1,2);
          TaxAreaCode := COPYSTR(TaxJurisdictionCityCode + ', ' + TaxJurisdictionStateCode,1,MAXSTRLEN(TaxAreaCode));
        END;
      END;

      SalesTaxSetupWizard.SetTaxGroup(TaxGroup);
      SalesTaxSetupWizard."Tax Area Code" := TaxAreaCode;
      SalesTaxSetupWizard."Tax Account (Sales)" := TaxSetup."Tax Account (Sales)";
      SalesTaxSetupWizard."Tax Account (Purchases)" := TaxSetup."Tax Account (Purchases)";
      SalesTaxSetupWizard.City := TaxJurisdictionCityCode;
      SalesTaxSetupWizard.State := COPYSTR(TaxJurisdictionStateCode,1,2);
      SalesTaxSetupWizard."State Rate" := StateTax;
      SalesTaxSetupWizard."City Rate" := CityTax;
      SalesTaxSetupWizard.StoreSalesTaxSetup;
      SalesTaxSetupWizard.MODIFY;
      COMMIT;

      Customer.MODIFYALL("Tax Area Code",TaxAreaCode);
      COMMIT;

      ConfigTemplateManagement.ReplaceDefaultValueForAllTemplates(
        DATABASE::Item,Item.FIELDNO("Tax Group Code"),TaxGroup.Code);

      O365TaxSettingsManagement.AssignDefaultTaxArea(TaxAreaCode);
    END;

    LOCAL PROCEDURE StoreSalesTaxSetupForCA@1020010();
    VAR
      TaxGroup@1020001 : Record 321;
      TaxArea@1020003 : Record 318;
      ConfigTemplateManagement@1020002 : Codeunit 8612;
      O365TaxSettingsManagement@1020000 : Codeunit 10150;
      TaxAreaCode@1020006 : Code[20];
    BEGIN
      IF NOT TaxGroup.GET(TaxableCodeTxt) THEN
        EXIT;

      IF NOT TaxArea.GET(TaxAreaCodeCA) THEN
        EXIT;

      TaxAreaCode := TaxArea.Code;
      SalesTaxSetupWizard."Tax Area Code" := TaxAreaCode;
      SalesTaxSetupWizard."Tax Account (Sales)" := TaxSetup."Tax Account (Sales)";
      SalesTaxSetupWizard."Tax Account (Purchases)" := TaxSetup."Tax Account (Purchases)";
      SalesTaxSetupWizard.SetTaxGroup(TaxGroup);
      SalesTaxSetupWizard."Country/Region" := SalesTaxSetupWizard."Country/Region"::CA;
      SalesTaxSetupWizard.MODIFY;

      ConfigTemplateManagement.ReplaceDefaultValueForAllTemplates(
        DATABASE::Item,Item.FIELDNO("Tax Group Code"),TaxGroup.Code);

      O365TaxSettingsManagement.AssignDefaultTaxArea(TaxAreaCode);
    END;

    BEGIN
    END.
  }
}

