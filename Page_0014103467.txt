OBJECT Page 14103467 EasyPDF Addresses
{
  OBJECT-PROPERTIES
  {
    Date=06/28/17;
    Time=[ 4:54:10 PM];
    Modified=Yes;
    Version List=EZP2.30.00;
  }
  PROPERTIES
  {
    SourceTable=Table14103464;
    DelayedInsert=Yes;
    PageType=List;
    OnOpenPage=VAR
                 ContBusRel@1000000002 : Record 5054;
                 OwnerNoFilter@1000000001 : Text;
                 OwnerTypeFilter@1000000000 : Text;
               BEGIN

                 GlobalUserID := 'DEFAULT';
                 IF "User ID" <> '' THEN
                   GlobalUserID := "User ID";
                 GlobalOwnerType := OwnerType;
                 GlobalDocumentCode := DocumentCode;
                 FlagDocumentCodeEditable := NOT (OwnerType = OwnerType::Report);

                 // Look for a related company contact card

                 ContactCompanyCardExists := FALSE;

                 OwnerNoFilter := GETFILTER(OwnerNo);
                 IF OwnerNoFilter = '' THEN
                   EXIT;

                 OwnerTypeFilter := GETFILTER(OwnerType);
                 IF NOT (OwnerTypeFilter = 'Customer') THEN
                   IF NOT (OwnerTypeFilter = 'Vendor') THEN
                     EXIT;

                 ContactRec.RESET;

                 ContBusRel.SETCURRENTKEY("Link to Table","No.");
                 IF OwnerTypeFilter = 'Customer' THEN
                   ContBusRel.SETRANGE("Link to Table",ContBusRel."Link to Table"::Customer)
                 ELSE
                   ContBusRel.SETRANGE("Link to Table",ContBusRel."Link to Table"::Vendor);
                 ContBusRel.SETRANGE("No.",OwnerNoFilter);
                 IF ContBusRel.FINDFIRST THEN
                 BEGIN
                   ContactCompanyCardExists := TRUE;
                   ContactRec.SETCURRENTKEY("Company Name","Company No.",Type,Name);
                   ContactRec.SETRANGE("Company No.",ContBusRel."Contact No.");
                 END;
               END;

    OnNewRecord=BEGIN

                  "User ID" := GlobalUserID;
                  OwnerType := GlobalOwnerType;
                  IF OwnerType = OwnerType::Report THEN
                    DocumentCode := GlobalDocumentCode;
                END;

    OnInsertRecord=BEGIN

                     IF Address = '' THEN
                       EXIT(FALSE);
                   END;

    ActionList=ACTIONS
    {
      { 1000000002;  ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 1000000006;1 ;ActionGroup }
      { 1000000007;2 ;Action    ;
                      CaptionML=ENU=Address from Contact;
                      ToolTipML=ENU=Retrieve an E-Mail Address or FAX Number from a related Contact Card;
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=ContactReference;
                      PromotedCategory=Process;
                      OnAction=VAR
                                 ContactListPage@1000000000 : Page 5052;
                                 ContRec@1000000001 : Record 5050;
                                 EmailAddress@1000000002 : Text;
                               BEGIN

                                 IF NOT ContactCompanyCardExists THEN
                                   EXIT;

                                 ContactListPage.SETTABLEVIEW(ContactRec);
                                 ContactListPage.LOOKUPMODE := TRUE;
                                 IF ContactListPage.RUNMODAL = ACTION::LookupOK THEN
                                 BEGIN
                                   ContactListPage.GETRECORD(ContRec);
                                   IF AddressType = AddressType::Fax THEN
                                     Address := DELCHR(ContRec."Fax No.",'<>')
                                   ELSE
                                     Address := DELCHR(ContRec."E-Mail",'<>');
                                 END;
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1000000000;0;Container;
                ContainerType=ContentArea }

    { 1000000001;1;Group  ;
                Name=Group;
                GroupType=Repeater }

    { 1000000005;2;Field  ;
                CaptionML=ENU=Document Code;
                SourceExpr=DocumentCode;
                Editable=FlagDocumentCodeEditable;
                QuickEntry=FALSE }

    { 1000000003;2;Field  ;
                CaptionML=ENU=Address Type;
                SourceExpr=AddressType }

    { 1000000004;2;Field  ;
                CaptionML=ENU=Address;
                SourceExpr=Address;
                OnValidate=BEGIN

                             IF AddressType <> AddressType::Fax THEN
                               EasyPDF.CheckValidEmailAddress(Address, FORMAT(AddressType));
                           END;
                            }

  }
  CODE
  {
    VAR
      EasyPDF@1000000000 : Codeunit 37062300;
      GlobalOwnerType@1000000004 : Integer;
      GlobalDocumentCode@1000000003 : Code[50];
      GlobalUserID@1000000002 : Code[50];
      FlagDocumentCodeEditable@1000000001 : Boolean INDATASET;
      ContactRec@1000000005 : Record 5050;
      ContactCompanyCardExists@1000000006 : Boolean INDATASET;

    BEGIN
    END.
  }
}

