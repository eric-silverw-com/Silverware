OBJECT Codeunit 232 Gen. Jnl.-Post+Print
{
  OBJECT-PROPERTIES
  {
    Date=09/19/18;
    Time=12:00:00 PM;
    Version List=NAVW113.00;
  }
  PROPERTIES
  {
    TableNo=81;
    OnRun=BEGIN
            GenJnlLine.COPY(Rec);
            Code;
            COPY(GenJnlLine);
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=cannot be filtered when posting recurring journals;ESM=no puede contener un filtro cuando se registra un diario peri¢dico;FRC=ne peut pas ˆtre filtr‚ lors du report des journaux r‚currents;ENC=cannot be filtered when posting recurring journals';
      Text001@1001 : TextConst 'ENU=Do you want to post the journal lines and print the report(s)?;ESM=¨Confirma que desea registrar las l¡ns. del diario e imprimir el informe(s)?;FRC=Voulez-vous reporter les lignes du journal et imprimer les rapports?;ENC=Do you want to post the journal lines and print the report(s)?';
      Text002@1002 : TextConst 'ENU=There is nothing to post.;ESM=No hay nada que registrar.;FRC=Il n''y a rien … reporter.;ENC=There is nothing to post.';
      Text003@1003 : TextConst 'ENU=The journal lines were successfully posted.;ESM=Se han registrado correctamente las l¡neas del diario.;FRC=Les lignes de journal ont ‚t‚ report‚es avec succŠs.;ENC=The journal lines were successfully posted.';
      Text004@1004 : TextConst 'ENU=The journal lines were successfully posted. You are now in the %1 journal.;ESM=Se registraron correctamente las l¡neas diario. Se encuentra en el diario %1.;FRC=Les lignes du journal ont ‚t‚ report‚es correctement. Vous ˆtes maintenant dans le journal %1.;ENC=The journal lines were successfully posted. You are now in the %1 journal.';
      GenJnlTemplate@1005 : Record 80;
      GenJnlLine@1006 : Record 81;
      GLReg@1007 : Record 45;
      CustLedgEntry@1010 : Record 21;
      VendLedgEntry@1011 : Record 25;
      TempJnlBatchName@1009 : Code[10];

    LOCAL PROCEDURE Code@1();
    VAR
      Handled@1000 : Boolean;
    BEGIN
      WITH GenJnlLine DO BEGIN
        GenJnlTemplate.GET("Journal Template Name");
        IF GenJnlTemplate."Force Posting Report" OR
           (GenJnlTemplate."Cust. Receipt Report ID" = 0) AND (GenJnlTemplate."Vendor Receipt Report ID" = 0)
        THEN
          GenJnlTemplate.TESTFIELD("Posting Report ID");
        IF GenJnlTemplate.Recurring AND (GETFILTER("Posting Date") <> '') THEN
          FIELDERROR("Posting Date",Text000);

        OnBeforePostJournalBatch(GenJnlLine);

        IF NOT CONFIRM(Text001,FALSE) THEN
          EXIT;

        TempJnlBatchName := "Journal Batch Name";

        CODEUNIT.RUN(CODEUNIT::"Gen. Jnl.-Post Batch",GenJnlLine);

        IF GLReg.GET("Line No.") THEN BEGIN
          IF GenJnlTemplate."Cust. Receipt Report ID" <> 0 THEN BEGIN
            CustLedgEntry.SETRANGE("Entry No.",GLReg."From Entry No.",GLReg."To Entry No.");
            REPORT.RUN(GenJnlTemplate."Cust. Receipt Report ID",FALSE,FALSE,CustLedgEntry);
          END;
          IF GenJnlTemplate."Vendor Receipt Report ID" <> 0 THEN BEGIN
            VendLedgEntry.SETRANGE("Entry No.",GLReg."From Entry No.",GLReg."To Entry No.");
            REPORT.RUN(GenJnlTemplate."Vendor Receipt Report ID",FALSE,FALSE,VendLedgEntry);
          END;
          IF GenJnlTemplate."Posting Report ID" <> 0 THEN BEGIN
            GLReg.SETRECFILTER;
            OnBeforeGLRegPostingReportPrint(GenJnlTemplate."Posting Report ID",FALSE,FALSE,GLReg,Handled);
            IF NOT Handled THEN
              REPORT.RUN(GenJnlTemplate."Posting Report ID",FALSE,FALSE,GLReg);
          END;
        END;

        IF "Line No." = 0 THEN
          MESSAGE(Text002)
        ELSE
          IF TempJnlBatchName = "Journal Batch Name" THEN
            MESSAGE(Text003)
          ELSE
            MESSAGE(
              Text004,
              "Journal Batch Name");

        IF NOT FIND('=><') OR (TempJnlBatchName <> "Journal Batch Name") THEN BEGIN
          RESET;
          FILTERGROUP(2);
          SETRANGE("Journal Template Name","Journal Template Name");
          SETRANGE("Journal Batch Name","Journal Batch Name");
          FILTERGROUP(0);
          "Line No." := 1;
        END;
      END;
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeGLRegPostingReportPrint@2(VAR ReportID@1000 : Integer;ReqWindow@1001 : Boolean;SystemPrinter@1002 : Boolean;VAR GLRegister@1003 : Record 45;VAR Handled@1004 : Boolean);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforePostJournalBatch@3(VAR GenJournalLine@1000 : Record 81);
    BEGIN
    END;

    BEGIN
    END.
  }
}

