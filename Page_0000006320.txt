OBJECT Page 6320 Upload Power BI Report
{
  OBJECT-PROPERTIES
  {
    Date=09/19/18;
    Time=12:00:00 PM;
    Version List=NAVW113.00;
  }
  PROPERTIES
  {
    Editable=Yes;
    CaptionML=[ENU=Upload Power BI Report;
               ESM=Cargar informe de Power BI;
               FRC=T‚l‚charger un rapport Power BI;
               ENC=Upload Power BI Report];
    PageType=NavigatePage;
    OnOpenPage=BEGIN
                 IF NOT PermissionManager.IsSuper(USERSECURITYID) THEN
                   ERROR(PermissionErr);

                 IF NOT PowerBIServiceMgt.IsUserReadyForPowerBI THEN
                   ERROR(NotReadyErr);

                 ReportID := CREATEGUID;
                 Version := 1;

                 MaxReportLimit := 20;
               END;

    ActionList=ACTIONS
    {
      { 5       ;    ;ActionContainer;
                      Name=ActionGroup;
                      ActionContainerType=NewDocumentItems }
      { 6       ;1   ;Action    ;
                      Name=OK;
                      ApplicationArea=#Basic,#Suite;
                      Visible=TRUE;
                      InFooterBar=Yes;
                      OnAction=BEGIN
                                 ClosedOk := TRUE;
                                 CurrPage.CLOSE;
                               END;
                                }
      { 8       ;1   ;Action    ;
                      Name=Upload Report;
                      ApplicationArea=#Basic,#Suite;
                      Visible=TRUE;
                      InFooterBar=Yes;
                      OnAction=VAR
                                 PowerBICustomerReports@1001 : Record 6310;
                               BEGIN
                                 UploadedReportCount := PowerBICustomerReports.COUNT;

                                 IF FileName = '' THEN
                                   ERROR(FileNameErr);

                                 IF ReportName = '' THEN
                                   ERROR(ReportNameErr);

                                 PowerBICustomerReports.RESET;
                                 PowerBICustomerReports.SETFILTER(Id,ReportID);
                                 IF NOT PowerBICustomerReports.ISEMPTY THEN
                                   ERROR(BlobIdErr);

                                 IF UploadedReportCount < MaxReportLimit THEN BEGIN
                                   PowerBICustomerReports.INIT;
                                   PowerBICustomerReports.Id := ReportID;
                                   PowerBICustomerReports.Name := ReportName;
                                   PowerBICustomerReports."Blob File" := TempBlob.Blob;
                                   PowerBICustomerReports.Version := Version;
                                   PowerBICustomerReports.INSERT
                                 END ELSE
                                   MESSAGE(TableLimitMsg);

                                 FileName := '';
                                 ReportName := '';

                                 ReportID := CREATEGUID;
                                 CurrPage.UPDATE;
                               END;
                                }
      { 7       ;1   ;Action    ;
                      Name=Cancel;
                      ApplicationArea=#Basic,#Suite;
                      Visible=TRUE;
                      InFooterBar=Yes;
                      OnAction=BEGIN
                                 ClosedOk := FALSE;
                                 CurrPage.CLOSE;
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1   ;0   ;Container ;
                ContainerType=ContentArea }

    { 10  ;1   ;Group     ;
                GroupType=Group }

    { 2   ;2   ;Field     ;
                Name=FileName;
                AssistEdit=Yes;
                CaptionML=[ENU=File;
                           ESM=Archivo;
                           FRC=Fichier;
                           ENC=File];
                ToolTipML=[ENU=Specifies File Name;
                           ESM=Especifique un nombre de archivo;
                           FRC=Sp‚cifie le nom du fichier;
                           ENC=Specifies File Name];
                ApplicationArea=#All;
                SourceExpr=FileName;
                Editable=FALSE;
                OnValidate=BEGIN
                             IF NOT FileManagement.ClientFileExists(FileName) THEN
                               ERROR(FileExistErr,FileName);
                           END;

                OnAssistEdit=VAR
                               TempFileName@1000 : Text;
                             BEGIN
                               // Event handler for the ellipsis button that opens the file selection dialog.
                               TempFileName := FileManagement.BLOBImportWithFilter(TempBlob,FileDialogTxt,'',FileFilterTxt,ExtFilterTxt);

                               IF TempFileName = '' THEN
                                 // User clicked Cancel in the file selection dialog.
                                 EXIT;

                               FileName := TempFileName;

                               IF ReportName = '' THEN BEGIN
                                 ReportName := COPYSTR(FileManagement.GetFileNameWithoutExtension(FileName),1,200);
                                 IsFileLoaded := TRUE;
                               END;
                             END;

                ShowMandatory=TRUE }

    { 3   ;1   ;Field     ;
                Name=ReportName;
                CaptionML=[ENU=Report Name;
                           ESM=Nombre de informe;
                           FRC=Nom du rapport;
                           ENC=Report Name];
                ToolTipML=[ENU=Specifies Report Name;
                           ESM=Especifica un nombre de informe;
                           FRC=Sp‚cifie le nom du rapport;
                           ENC=Specifies Report Name];
                ApplicationArea=#All;
                SourceExpr=ReportName;
                Editable=IsFileLoaded;
                ShowMandatory=TRUE }

  }
  CODE
  {
    VAR
      ReportNameErr@1005 : TextConst 'ENU=You must enter a report name.;ESM=Debe introducir un nombre de informe.;FRC=Vous devez entrer un nom de rapport.;ENC=You must enter a report name.';
      FileNameErr@1007 : TextConst 'ENU=You must enter a file name.;ESM=Debe introducir un nombre de archivo.;FRC=Vous devez entrer un nom de fichier.;ENC=You must enter a file name.';
      NotReadyErr@1012 : TextConst 'ENU=whoops.;ESM=vaya.;FRC=Oups.;ENC=whoops.';
      FileExistErr@1013 : TextConst '@@@=asdf;ENU=The file %1 does not exist.;ESM=El archivo %1 no existe.;FRC=Le fichier %1 n''existe pas.;ENC=The file %1 does not exist.';
      BlobIdErr@1010 : TextConst 'ENU=A blob with this ID already exists.;ESM=Ya existe un blob con este id.;FRC=Un objet blob portant ce code existe d‚j….;ENC=A blob with this ID already exists.';
      TempBlob@1000 : Record 99008535;
      PowerBIServiceMgt@1014 : Codeunit 6301;
      PermissionManager@1009 : Codeunit 9002;
      FileManagement@1015 : Codeunit 419;
      FileDialogTxt@1016 : TextConst 'ENU=Select a PBIX report file.;ESM=Seleccione un archivo de informe PBIX.;FRC=S‚lectionnez un fichier de rapport PBIX.;ENC=Select a PBIX report file.';
      FileFilterTxt@1017 : TextConst 'ENU=Power BI Files(*.pbix)|*.pbix;ESM=Archivos de Power BI (*.pbix)|*.pbix;FRC=Fichiers Power BI (*.pbix)|*.pbix;ENC=Power BI Files(*.pbix)|*.pbix';
      ExtFilterTxt@1018 : TextConst 'ENU=pbix;ESM=pbix;FRC=pbix;ENC=pbix';
      ReportID@1008 : GUID;
      Version@1006 : Integer;
      FileName@1004 : Text;
      ReportName@1003 : Text[200];
      ClosedOk@1001 : Boolean;
      PermissionErr@1019 : TextConst 'ENU=User does not have permissions to operate this page.;ESM=El usuario no tiene permiso para operar esta p gina.;FRC=L''utilisateur ne dispose pas d''autorisations pour utiliser cette page.;ENC=User does not have permissions to operate this page.';
      IsFileLoaded@1020 : Boolean;
      MaxReportLimit@1002 : Integer;
      UploadedReportCount@1011 : Integer;
      TableLimitMsg@1021 : TextConst 'ENU=The Customer Report table is full. Remove a report and try again.;ESM=El tabla Informe del cliente est  llena. Elimine un informe y vuelva a intentarlo.;FRC=La table Rapport client est pleine. Supprimez un rapport et r‚essayez.;ENC=The Customer Report table is full. Remove a report and try again.';

    PROCEDURE GetClosedOk@1() : Boolean;
    BEGIN
      // Returns the ClosedOk variable which tells the parent window if the user hit OK to actually upload a report,
      // rather than canceling.
      EXIT(ClosedOk);
    END;

    BEGIN
    {
      // Test page for manually importing PBIX blobs into database.
      // TODO: Remove before check-in.
    }
    END.
  }
}

