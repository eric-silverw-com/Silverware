OBJECT Page 36640 Order Header Status Factbox
{
  OBJECT-PROPERTIES
  {
    Date=11/27/18;
    Time=11:12:13 PM;
    Version List=;
  }
  PROPERTIES
  {
    CaptionML=ENU=Sales Order Status;
    InsertAllowed=No;
    DeleteAllowed=No;
    SourceTable=Table36;
    SourceTableView=SORTING(Document Type,Combine Shipments,Bill-to Customer No.,Currency Code)
                    WHERE(Document Type=FILTER(Order|Return Order));
    PageType=ListPart;
    OnOpenPage=BEGIN
                 UpdateTotal;
               END;

    OnAfterGetRecord=BEGIN
                       GetLastShipmentInvoice;
                       UpdateTotal;
                     END;

    OnModifyRecord=BEGIN
                     UpdateTotal;
                   END;

  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 1   ;1   ;Group     ;
                GroupType=Repeater }

    { 1020004;2;Field     ;
                ToolTipML=ENU=Specifies the type of the document.;
                SourceExpr="Document Type";
                Visible=FALSE;
                Editable=FALSE }

    { 2   ;2   ;Field     ;
                ToolTipML=ENU=Specifies the number of the record.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="No.";
                Editable=FALSE;
                OnDrillDown=BEGIN
                              CASE "Document Type" OF
                                "Document Type"::Order:
                                  IF PAGE.RUNMODAL(PAGE::"Sales Order",Rec) = ACTION::LookupOK THEN;
                                "Document Type"::"Return Order":
                                  IF PAGE.RUNMODAL(PAGE::"Sales Return Order",Rec) = ACTION::LookupOK THEN;
                              END;
                            END;
                             }

    { 21  ;2   ;Field     ;
                ToolTipML=ENU=Specifies the customer's reference. The content will be printed on sales documents.;
                SourceExpr="Your Reference";
                Visible=FALSE }

    { 4   ;2   ;Field     ;
                ToolTipML=ENU=Specifies the date on which the related sales order was created.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Order Date";
                Editable=FALSE }

    { 14  ;2   ;Field     ;
                CaptionML=ENU=Last Shipment Date;
                ToolTipML=ENU=Specifies the date of the last shipment.;
                SourceExpr=LastShipmentDate;
                Visible=FALSE;
                Editable=FALSE;
                OnDrillDown=BEGIN
                              CASE "Document Type" OF
                                "Document Type"::Order:
                                  BEGIN
                                    GetLastShipment;
                                    IF PAGE.RUNMODAL(PAGE::"Posted Sales Shipments",SalesShipmentHeader) = ACTION::LookupOK THEN;
                                  END;
                                "Document Type"::"Return Order":
                                  BEGIN
                                    GetLastRetReceipt;
                                    IF PAGE.RUNMODAL(PAGE::"Posted Return Receipts",RetReceiptHeader) = ACTION::LookupOK THEN;
                                  END;
                              END;
                            END;
                             }

    { 1020006;2;Field     ;
                ToolTipML=ENU=Specifies the requested delivery date.;
                SourceExpr="Requested Delivery Date";
                Visible=FALSE;
                Editable=FALSE }

    { 1020008;2;Field     ;
                ToolTipML=ENU=Specifies the promised delivery date for the customer order.;
                SourceExpr="Promised Delivery Date";
                Visible=FALSE;
                Editable=FALSE }

    { 6   ;2   ;Field     ;
                CaptionML=ENU=Next Shipment Date;
                ToolTipML=ENU=Specifies the next data a shipment is planned for the order.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Shipment Date";
                Editable=FALSE }

    { 1020010;2;Field     ;
                ToolTipML=ENU="Specifies the shipping time for the order. ";
                SourceExpr="Shipping Time";
                Visible=FALSE;
                Editable=FALSE }

    { 1020002;2;Field     ;
                ToolTipML=ENU=Specifies whether all the items on the order have been shipped or, in the case of inbound items, completely received.;
                SourceExpr="Completely Shipped";
                Visible=FALSE;
                Editable=FALSE }

    { 16  ;2   ;Field     ;
                CaptionML=ENU=Last Invoice Date;
                ToolTipML=ENU=Specifies the date of the last invoice.;
                SourceExpr=LastInvoiceDate;
                Visible=FALSE;
                Editable=FALSE;
                OnDrillDown=BEGIN
                              CASE "Document Type" OF
                                "Document Type"::Order:
                                  BEGIN
                                    GetLastInvoice;
                                    IF PAGE.RUNMODAL(PAGE::"Posted Sales Invoices",SalesInvoiceHeader) = ACTION::LookupOK THEN;
                                  END;
                                "Document Type"::"Return Order":
                                  BEGIN
                                    GetLastCrMemo;
                                    IF PAGE.RUNMODAL(PAGE::"Posted Sales Credit Memos",RetCreditMemoHeader) = ACTION::LookupOK THEN;
                                  END;
                              END;
                            END;
                             }

    { 18  ;2   ;Field     ;
                CaptionML=ENU=Open Amount;
                ToolTipML=ENU=Specifies the outstanding amount that is calculated, based on the Sales Line table and the Outstanding Amount ($) field.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Outstanding Amount ($)";
                Editable=FALSE }

    { 1020000;2;Field     ;
                ToolTipML=ENU=Specifies the status of the document.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=Status;
                Editable=FALSE }

    { 8   ;2   ;Field     ;
                ToolTipML=ENU=Specifies if the document was put on hold when it was posted, for example because payment of the resulting customer ledger entries is overdue.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="On Hold" }

    { 20  ;1   ;Group     ;
                CaptionML=ENU=Open Amounts }

    { 10  ;2   ;Field     ;
                CaptionML=ENU=Total;
                ToolTipML=ENU=Specifies the total amount less any invoice discount amount and exclusive of tax for the posted document.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=TotalOpenAmount;
                Editable=FALSE }

    { 12  ;2   ;Field     ;
                CaptionML=ENU=On Hold;
                ToolTipML=ENU=Specifies lines that are on orders that are on hold.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=TotalOpenAmountOnHold;
                Editable=FALSE }

    { 1020012;2;Field     ;
                CaptionML=ENU=Pending Approval;
                ToolTipML=ENU=Specifies that the document remains to be approved.;
                ApplicationArea=#Suite;
                SourceExpr=TotalOpenAmountPendingApproval;
                Editable=FALSE }

  }
  CODE
  {
    VAR
      SalesHeader@1020000 : Record 36;
      SalesShipmentHeader@1020001 : Record 110;
      SalesInvoiceHeader@1020002 : Record 112;
      RetReceiptHeader@1020009 : Record 6660;
      RetCreditMemoHeader@1020010 : Record 114;
      LastShipmentDate@1020004 : Date;
      LastInvoiceDate@1020005 : Date;
      TotalOpenAmount@1020007 : Decimal;
      TotalOpenAmountOnHold@1020008 : Decimal;
      TotalOpenAmountPendingApproval@1020011 : Decimal;

    [External]
    PROCEDURE UpdateTotal@2();
    BEGIN
      TotalOpenAmount := 0;
      TotalOpenAmountOnHold := 0;
      SalesHeader.COPY(Rec);
      IF SalesHeader.FINDSET THEN
        REPEAT
          SalesHeader.CALCFIELDS("Outstanding Amount ($)");
          TotalOpenAmount := TotalOpenAmount + SalesHeader."Outstanding Amount ($)";
          IF SalesHeader."On Hold" <> '' THEN
            TotalOpenAmountOnHold := TotalOpenAmountOnHold + SalesHeader."Outstanding Amount ($)";
          IF SalesHeader.Status = SalesHeader.Status::"Pending Approval" THEN
            TotalOpenAmountPendingApproval := TotalOpenAmountPendingApproval + SalesHeader."Outstanding Amount ($)";
        UNTIL SalesHeader.NEXT = 0;
    END;

    [External]
    PROCEDURE GetLastShipmentInvoice@3();
    BEGIN
      // Calculate values for this row
      CASE "Document Type" OF
        "Document Type"::Order:
          BEGIN
            IF GetLastShipment THEN
              LastShipmentDate := SalesShipmentHeader."Shipment Date"
            ELSE
              LastShipmentDate := 0D;
            IF GetLastInvoice THEN
              LastInvoiceDate := SalesInvoiceHeader."Posting Date"
            ELSE
              LastInvoiceDate := 0D;
          END;
        "Document Type"::"Return Order":
          BEGIN
            IF GetLastRetReceipt THEN
              LastShipmentDate := RetReceiptHeader."Posting Date"
            ELSE
              LastShipmentDate := 0D;
            IF GetLastCrMemo THEN
              LastInvoiceDate := RetCreditMemoHeader."Posting Date"
            ELSE
              LastInvoiceDate := 0D;
          END;
        ELSE
          BEGIN
          LastShipmentDate := 0D;
          LastInvoiceDate := 0D;
        END;
      END;
    END;

    [External]
    PROCEDURE GetLastShipment@4() : Boolean;
    BEGIN
      SalesShipmentHeader.SETCURRENTKEY("Order No."{, "Shipment Date"}); // may want to create this key
      SalesShipmentHeader.SETRANGE("Order No.","No.");
      EXIT(SalesShipmentHeader.FINDLAST);
    END;

    [External]
    PROCEDURE GetLastInvoice@5() : Boolean;
    BEGIN
      SalesInvoiceHeader.SETCURRENTKEY("Order No."{, "Shipment Date"}); // may want to create this key
      SalesInvoiceHeader.SETRANGE("Order No.","No.");
      EXIT(SalesInvoiceHeader.FINDLAST);
    END;

    [External]
    PROCEDURE GetLastRetReceipt@6() : Boolean;
    BEGIN
      RetReceiptHeader.SETCURRENTKEY("Return Order No."{, "Shipment Date"}); // may want to create this key
      RetReceiptHeader.SETRANGE("Return Order No.","No.");
      EXIT(RetReceiptHeader.FINDLAST);
    END;

    [External]
    PROCEDURE GetLastCrMemo@7() : Boolean;
    BEGIN
      RetCreditMemoHeader.SETCURRENTKEY("Return Order No."{, "Shipment Date"}); // may want to create this key
      RetCreditMemoHeader.SETRANGE("Return Order No.","No.");
      EXIT(RetCreditMemoHeader.FINDLAST);
    END;

    BEGIN
    END.
  }
}

