OBJECT Table 272 Check Ledger Entry
{
  OBJECT-PROPERTIES
  {
    Date=11/27/18;
    Time=11:14:11 PM;
    Version List=;
  }
  PROPERTIES
  {
    CaptionML=ENU=Check Ledger Entry;
    LookupPageID=Page374;
    DrillDownPageID=Page374;
  }
  FIELDS
  {
    { 1   ;   ;Entry No.           ;Integer       ;CaptionML=ENU=Entry No. }
    { 2   ;   ;Bank Account No.    ;Code20        ;TableRelation="Bank Account";
                                                   CaptionML=ENU=Bank Account No. }
    { 3   ;   ;Bank Account Ledger Entry No.;Integer;
                                                   TableRelation="Bank Account Ledger Entry";
                                                   CaptionML=ENU=Bank Account Ledger Entry No. }
    { 4   ;   ;Posting Date        ;Date          ;CaptionML=ENU=Posting Date }
    { 5   ;   ;Document Type       ;Option        ;CaptionML=ENU=Document Type;
                                                   OptionCaptionML=ENU=" ,Payment,Invoice,Credit Memo,Finance Charge Memo,Reminder,Refund";
                                                   OptionString=[ ,Payment,Invoice,Credit Memo,Finance Charge Memo,Reminder,Refund] }
    { 6   ;   ;Document No.        ;Code20        ;CaptionML=ENU=Document No. }
    { 7   ;   ;Description         ;Text50        ;CaptionML=ENU=Description }
    { 8   ;   ;Amount              ;Decimal       ;CaptionML=ENU=Amount;
                                                   AutoFormatType=1;
                                                   AutoFormatExpr=GetCurrencyCodeFromBank }
    { 9   ;   ;Check Date          ;Date          ;CaptionML=ENU=Check Date }
    { 10  ;   ;Check No.           ;Code20        ;CaptionML=ENU=Check No. }
    { 11  ;   ;Check Type          ;Option        ;CaptionML=ENU=Check Type;
                                                   OptionCaptionML=ENU=Total Check,Partial Check;
                                                   OptionString=Total Check,Partial Check }
    { 12  ;   ;Bank Payment Type   ;Option        ;CaptionML=ENU=Bank Payment Type;
                                                   OptionCaptionML=ENU=" ,Computer Check,Manual Check,Electronic Payment";
                                                   OptionString=[ ,Computer Check,Manual Check,Electronic Payment] }
    { 13  ;   ;Entry Status        ;Option        ;CaptionML=ENU=Entry Status;
                                                   OptionCaptionML=ENU=,Printed,Voided,Posted,Financially Voided,Test Print,Exported,Transmitted;
                                                   OptionString=,Printed,Voided,Posted,Financially Voided,Test Print,Exported,Transmitted }
    { 14  ;   ;Original Entry Status;Option       ;CaptionML=ENU=Original Entry Status;
                                                   OptionCaptionML=ENU=" ,Printed,Voided,Posted,Financially Voided,,Exported,Transmitted";
                                                   OptionString=[ ,Printed,Voided,Posted,Financially Voided,,Exported,Transmitted] }
    { 15  ;   ;Bal. Account Type   ;Option        ;CaptionML=ENU=Bal. Account Type;
                                                   OptionCaptionML=ENU=G/L Account,Customer,Vendor,Bank Account,Fixed Asset,,Employee;
                                                   OptionString=G/L Account,Customer,Vendor,Bank Account,Fixed Asset,,Employee }
    { 16  ;   ;Bal. Account No.    ;Code20        ;TableRelation=IF (Bal. Account Type=CONST(G/L Account)) "G/L Account"
                                                                 ELSE IF (Bal. Account Type=CONST(Customer)) Customer
                                                                 ELSE IF (Bal. Account Type=CONST(Vendor)) Vendor
                                                                 ELSE IF (Bal. Account Type=CONST(Bank Account)) "Bank Account"
                                                                 ELSE IF (Bal. Account Type=CONST(Fixed Asset)) "Fixed Asset"
                                                                 ELSE IF (Bal. Account Type=CONST(Employee)) Employee;
                                                   CaptionML=ENU=Bal. Account No. }
    { 17  ;   ;Open                ;Boolean       ;CaptionML=ENU=Open }
    { 18  ;   ;Statement Status    ;Option        ;CaptionML=ENU=Statement Status;
                                                   OptionCaptionML=ENU=Open,Bank Acc. Entry Applied,Check Entry Applied,Closed;
                                                   OptionString=Open,Bank Acc. Entry Applied,Check Entry Applied,Closed }
    { 19  ;   ;Statement No.       ;Code20        ;TableRelation=IF (Statement Status=FILTER(Bank Acc. Entry Applied|Check Entry Applied)) "Bank Rec. Header"."Statement No." WHERE (Bank Account No.=FIELD(Bank Account No.))
                                                                 ELSE IF (Statement Status=CONST(Closed)) "Posted Bank Rec. Header"."Statement No." WHERE (Bank Account No.=FIELD(Bank Account No.));
                                                   TestTableRelation=No;
                                                   CaptionML=ENU=Statement No. }
    { 20  ;   ;Statement Line No.  ;Integer       ;TableRelation=IF (Statement Status=FILTER(Bank Acc. Entry Applied|Check Entry Applied)) "Bank Rec. Line"."Line No." WHERE (Bank Account No.=FIELD(Bank Account No.),
                                                                                                                                                                              Statement No.=FIELD(Statement No.))
                                                                                                                                                                              ELSE IF (Statement Status=CONST(Closed)) "Posted Bank Rec. Line"."Line No." WHERE (Bank Account No.=FIELD(Bank Account No.),
                                                                                                                                                                                                                                                                 Statement No.=FIELD(Statement No.));
                                                   TestTableRelation=No;
                                                   CaptionML=ENU=Statement Line No. }
    { 21  ;   ;User ID             ;Code50        ;TableRelation=User."User Name";
                                                   OnLookup=VAR
                                                              UserMgt@1000 : Codeunit 418;
                                                            BEGIN
                                                              UserMgt.LookupUserID("User ID");
                                                            END;

                                                   TestTableRelation=No;
                                                   DataClassification=EndUserIdentifiableInformation;
                                                   CaptionML=ENU=User ID }
    { 22  ;   ;External Document No.;Code35       ;CaptionML=ENU=External Document No. }
    { 23  ;   ;Data Exch. Entry No.;Integer       ;TableRelation="Data Exch.";
                                                   CaptionML=ENU=Data Exch. Entry No.;
                                                   Editable=No }
    { 24  ;   ;Data Exch. Voided Entry No.;Integer;TableRelation="Data Exch.";
                                                   CaptionML=ENU=Data Exch. Voided Entry No.;
                                                   Editable=No }
    { 25  ;   ;Positive Pay Exported;Boolean      ;CaptionML=ENU=Positive Pay Exported }
    { 26  ;   ;Record ID to Print  ;RecordID      ;DataClassification=SystemMetadata;
                                                   CaptionML=ENU=Record ID to Print }
    { 10005;  ;Trace No.           ;Code30        ;CaptionML=ENU=Trace No. }
    { 10006;  ;Transmission File Name;Text30      ;CaptionML=ENU=Transmission File Name }
  }
  KEYS
  {
    {    ;Entry No.                               ;Clustered=Yes }
    {    ;Bank Account No.,Check Date              }
    {    ;Bank Account No.,Entry Status,Check No.  }
    {    ;Bank Account Ledger Entry No.            }
    {    ;Bank Account No.,Open                    }
    {    ;Document No.,Posting Date                }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      NothingToExportErr@1000 : TextConst 'ENU=There is nothing to export.';

    [External]
    PROCEDURE GetCurrencyCodeFromBank@1() : Code[10];
    VAR
      BankAcc@1000 : Record 270;
    BEGIN
      IF "Bank Account No." = BankAcc."No." THEN
        EXIT(BankAcc."Currency Code");

      IF BankAcc.GET("Bank Account No.") THEN
        EXIT(BankAcc."Currency Code");

      EXIT('');
    END;

    [External]
    PROCEDURE CopyFromBankAccLedgEntry@2(BankAccLedgEntry@1000 : Record 271);
    BEGIN
      "Bank Account No." := BankAccLedgEntry."Bank Account No.";
      "Bank Account Ledger Entry No." := BankAccLedgEntry."Entry No.";
      "Posting Date" := BankAccLedgEntry."Posting Date";
      "Document Type" := BankAccLedgEntry."Document Type";
      "Document No." := BankAccLedgEntry."Document No.";
      "External Document No." := BankAccLedgEntry."External Document No.";
      Description := BankAccLedgEntry.Description;
      "Bank Payment Type" := "Bank Payment Type";
      "Bal. Account Type" := BankAccLedgEntry."Bal. Account Type";
      "Bal. Account No." := BankAccLedgEntry."Bal. Account No.";
      "Entry Status" := "Entry Status"::Posted;
      Open := TRUE;
      "User ID" := USERID;
      "Check Date" := BankAccLedgEntry."Posting Date";
      "Check No." := BankAccLedgEntry."Document No.";

      OnAfterCopyFromBankAccLedgEntry(Rec,BankAccLedgEntry);
    END;

    [External]
    PROCEDURE GetCheckAmountText@1020000(CurrencyCode@1020001 : Code[10];VAR CurrencySymbol@1020000 : Code[5]) CheckAmountText : Text;
    VAR
      Currency@1020002 : Record 4;
      Decimals@1020004 : Decimal;
    BEGIN
      Currency.Initialize(CurrencyCode);

      Decimals := Amount - ROUND(Amount,1,'<');

      IF (GetFractionPartLength(Amount) <> GetFractionPartLength(Currency."Amount Rounding Precision")) THEN
        IF (Decimals = 0) OR (GetFractionPartLength(Amount) > GetFractionPartLength(Currency."Amount Rounding Precision")) THEN
          CheckAmountText :=
            FORMAT(
              ROUND(Amount,1,'<')) +
              GetDecimalSeparator +
              PADSTR('',STRLEN(FORMAT(ROUND(Currency."Amount Rounding Precision",Currency."Amount Rounding Precision"))) - 2,'0')
        ELSE
          CheckAmountText := FORMAT(ROUND(Amount,Currency."Amount Rounding Precision")) +
            PADSTR('',GetFractionPartLength(Currency."Amount Rounding Precision") - GetFractionPartLength(Amount),'0')
      ELSE
        CheckAmountText := FORMAT(Amount,0,0);

      CurrencySymbol := Currency.Symbol;
    END;

    LOCAL PROCEDURE GetFractionPartLength@1020002(DecimalValue@1020000 : Decimal) : Integer;
    BEGIN
      IF STRPOS(FORMAT(DecimalValue),GetDecimalSeparator) = 0 THEN
        EXIT(0);

      EXIT(STRLEN(FORMAT(DecimalValue)) - STRPOS(FORMAT(DecimalValue),GetDecimalSeparator));
    END;

    LOCAL PROCEDURE GetDecimalSeparator@1020001() : Code[1];
    BEGIN
      EXIT(COPYSTR(FORMAT(0.01),2,1));
    END;

    [External]
    PROCEDURE ExportCheckFile@3();
    VAR
      BankAcc@1000 : Record 270;
    BEGIN
      IF NOT FINDSET THEN
        ERROR(NothingToExportErr);

      IF NOT BankAcc.GET("Bank Account No.") THEN
        ERROR(NothingToExportErr);

      IF BankAcc.GetPosPayExportCodeunitID > 0 THEN
        CODEUNIT.RUN(BankAcc.GetPosPayExportCodeunitID,Rec)
      ELSE
        CODEUNIT.RUN(CODEUNIT::"Exp. Launcher Pos. Pay",Rec);
    END;

    [External]
    PROCEDURE GetPayee@4() Payee : Text[50];
    VAR
      Vendor@1003 : Record 23;
      Customer@1002 : Record 18;
      GLAccount@1001 : Record 15;
      BankAccount@1000 : Record 270;
    BEGIN
      CASE "Bal. Account Type" OF
        "Bal. Account Type"::"G/L Account":
          IF "Bal. Account No." <> '' THEN BEGIN
            GLAccount.GET("Bal. Account No.");
            Payee := GLAccount.Name;
          END;
        "Bal. Account Type"::Customer:
          IF "Bal. Account No." <> '' THEN BEGIN
            Customer.GET("Bal. Account No.");
            Payee := Customer.Name;
          END;
        "Bal. Account Type"::Vendor:
          IF "Bal. Account No." <> '' THEN BEGIN
            Vendor.GET("Bal. Account No.");
            Payee := Vendor.Name;
          END;
        "Bal. Account Type"::"Bank Account":
          IF "Bal. Account No." <> '' THEN BEGIN
            BankAccount.GET("Bal. Account No.");
            Payee := BankAccount.Name;
          END;
        "Bal. Account Type"::"Fixed Asset":
          Payee := "Bal. Account No.";
      END;
    END;

    [External]
    PROCEDURE SetFilterBankAccNoOpen@6(BankAccNo@1000 : Code[20]);
    BEGIN
      RESET;
      SETCURRENTKEY("Bank Account No.",Open);
      SETRANGE("Bank Account No.",BankAccNo);
      SETRANGE(Open,TRUE);
    END;

    [Integration]
    [External]
    PROCEDURE OnAfterCopyFromBankAccLedgEntry@7(VAR CheckLedgerEntry@1000 : Record 272;BankAccountLedgerEntry@1001 : Record 271);
    BEGIN
    END;

    BEGIN
    END.
  }
}

