OBJECT Page 1910 Camera Interaction
{
  OBJECT-PROPERTIES
  {
    Date=09/19/18;
    Time=12:00:00 PM;
    Version List=NAVW113.00;
  }
  PROPERTIES
  {
    Editable=No;
    CaptionML=[ENU=Camera Interaction;
               ESM=Interacci¢n de la c mara;
               FRC=Interaction avec l'appareil photo;
               ENC=Camera Interaction];
    LinksAllowed=No;
    PageType=Card;
    OnOpenPage=BEGIN
                 CameraAvailable := CameraProvider.IsAvailable;

                 IF NOT CameraAvailable THEN
                   EXIT;

                 CameraOptions := CameraOptions.CameraOptions;
                 CameraOptions.Quality := RequestedQuality;
                 CameraOptions.AllowEdit := RequestedAllowEdit;
                 CameraOptions.EncodingType := RequestedEncodingType;
                 CameraOptions.MediaType := RequestedMediaType;
                 CameraOptions.SourceType := RequestedSourceType;

                 CameraProvider := CameraProvider.Create;
                 CameraProvider.RequestPictureAsync(CameraOptions);
               END;

  }
  CONTROLS
  {
    { 1   ;    ;Container ;
                Name=Content;
                CaptionML=[ENU=Content;
                           ESM=Contenido;
                           FRC=Contenu;
                           ENC=Content];
                ContainerType=ContentArea }

    { 3   ;1   ;Group     ;
                Name=TakingPicture;
                CaptionML=[ENU=Taking picture...;
                           ESM=Realizando fotograf¡a...;
                           FRC=Prise d'une photo...;
                           ENC=Taking picture...];
                Visible=CameraAvailable;
                GroupType=Group;
                InstructionalTextML=[ENU=Please take the picture using your camera.;
                                     ESM=Realice una fotograf¡a con la c mara.;
                                     FRC=Prenez la photo … l'aide de votre appareil photo.;
                                     ENC=Please take the picture using your camera.] }

    { 4   ;1   ;Group     ;
                Name=CameraNotSupported;
                CaptionML=[ENU=Could not connect to camera;
                           ESM=No se puede conectar con la c mara.;
                           FRC=Impossible de se connecter … l'appareil photo;
                           ENC=Could not connect to camera];
                Visible=NOT CameraAvailable;
                GroupType=Group;
                InstructionalTextML=[ENU=The camera on the device could not be accessed. Please make sure you are using a Dynamics Tenerife app for Windows, Android or iOS.;
                                     ESM=No se ha podido acceder a la c mara del dispositivo. Aseg£rese de que est  utilizando una aplicaci¢n Dynamics Tenerife para Windows, Android o iOS.;
                                     FRC=Impossible d'acc‚der … l'appareil photo de l'appareil. V‚rifiez que vous utilisez une application Dynamics Tenerife pour Windows, Android ou iOS.;
                                     ENC=The camera on the device could not be accessed. Please make sure you are using a Dynamics Tenerife app for Windows, Android or iOS.] }

  }
  CODE
  {
    VAR
      TempPictureTempBlob@1011 : TEMPORARY Record 99008535;
      CameraProvider@1001 : DotNet "'Microsoft.Dynamics.Nav.ClientExtensions, Version=13.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Client.Capabilities.CameraProvider" WITHEVENTS RUNONCLIENT;
      CameraOptions@1000 : DotNet "'Microsoft.Dynamics.Nav.ClientExtensions, Version=13.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Client.Capabilities.CameraOptions";
      CameraAvailable@1002 : Boolean INDATASET;
      RequestedAllowEdit@1003 : Boolean;
      SavedPictureName@1004 : Text;
      SavedPictureFilePath@1005 : Text;
      RequestedEncodingType@1006 : Text;
      RequestedMediaType@1007 : Text;
      RequestedSourceType@1008 : Text;
      PictureNotAvailableErr@1009 : TextConst 'ENU=The picture is not available.;ESM=La imagen no est  disponible.;FRC=La photo n''est pas disponible.;ENC=The picture is not available.';
      RequestedIgnoreError@1010 : Boolean;
      RequestedQuality@1012 : Integer;

    [External]
    PROCEDURE AllowEdit@2(AllowEdit@1000 : Boolean);
    BEGIN
      RequestedAllowEdit := AllowEdit;
    END;

    [External]
    PROCEDURE GetPictureName@5() : Text;
    BEGIN
      EXIT(SavedPictureName);
    END;

    [External]
    PROCEDURE GetPicture@6(Stream@1000 : InStream) : Boolean;
    BEGIN
      IF SavedPictureFilePath = '' THEN BEGIN
        IF NOT RequestedIgnoreError THEN
          ERROR(PictureNotAvailableErr);

        EXIT(FALSE);
      END;

      TempPictureTempBlob.INIT;
      TempPictureTempBlob.Blob.IMPORT(SavedPictureFilePath);
      TempPictureTempBlob.Blob.CREATEINSTREAM(Stream);
      TempPictureTempBlob.INSERT;

      EXIT(TRUE);
    END;

    [External]
    PROCEDURE EncodingType@17(EncodingType@1000 : Text);
    BEGIN
      RequestedEncodingType := EncodingType;
    END;

    [External]
    PROCEDURE MediaType@18(MediaType@1000 : Text);
    BEGIN
      RequestedMediaType := MediaType;
    END;

    [External]
    PROCEDURE SourceType@19(SourceType@1000 : Text);
    BEGIN
      RequestedSourceType := SourceType;
    END;

    [External]
    PROCEDURE IgnoreError@31(IgnoreError@1000 : Boolean);
    BEGIN
      RequestedIgnoreError := IgnoreError;
    END;

    [External]
    PROCEDURE Quality@46(Quality@1000 : Integer);
    BEGIN
      RequestedQuality := Quality;
    END;

    EVENT CameraProvider@1001::PictureAvailable@10(PictureName@1001 : Text;PictureFilePath@1000 : Text);
    BEGIN
      SavedPictureFilePath := PictureFilePath;
      SavedPictureName := PictureName;

      CurrPage.CLOSE;
    END;

    BEGIN
    END.
  }
}

