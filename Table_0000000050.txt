OBJECT Table 50 Accounting Period
{
  OBJECT-PROPERTIES
  {
    Date=11/27/18;
    Time=11:14:02 PM;
    Version List=;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               AccountingPeriod2 := Rec;
               IF AccountingPeriod2.FIND('>') THEN
                 AccountingPeriod2.TESTFIELD("Date Locked",FALSE);
             END;

    OnDelete=BEGIN
               TESTFIELD("Date Locked",FALSE);
             END;

    OnRename=BEGIN
               TESTFIELD("Date Locked",FALSE);
               AccountingPeriod2 := Rec;
               IF AccountingPeriod2.FIND('>') THEN
                 AccountingPeriod2.TESTFIELD("Date Locked",FALSE);
             END;

    CaptionML=ENU=Accounting Period;
    LookupPageID=Page100;
  }
  FIELDS
  {
    { 1   ;   ;Starting Date       ;Date          ;OnValidate=BEGIN
                                                                Name := FORMAT("Starting Date",0,Text000);
                                                              END;

                                                   CaptionML=ENU=Starting Date;
                                                   NotBlank=Yes }
    { 2   ;   ;Name                ;Text10        ;CaptionML=ENU=Name }
    { 3   ;   ;New Fiscal Year     ;Boolean       ;OnValidate=BEGIN
                                                                TESTFIELD("Date Locked",FALSE);
                                                                IF "New Fiscal Year" THEN BEGIN
                                                                  IF NOT InvtSetup.GET THEN
                                                                    EXIT;
                                                                  "Average Cost Calc. Type" := InvtSetup."Average Cost Calc. Type";
                                                                  "Average Cost Period" := InvtSetup."Average Cost Period";
                                                                END ELSE BEGIN
                                                                  "Average Cost Calc. Type" := "Average Cost Calc. Type"::" ";
                                                                  "Average Cost Period" := "Average Cost Period"::" ";
                                                                END;
                                                              END;

                                                   CaptionML=ENU=New Fiscal Year }
    { 4   ;   ;Closed              ;Boolean       ;CaptionML=ENU=Closed;
                                                   Editable=No }
    { 5   ;   ;Date Locked         ;Boolean       ;CaptionML=ENU=Date Locked;
                                                   Editable=No }
    { 5804;   ;Average Cost Calc. Type;Option     ;AccessByPermission=TableData 27=R;
                                                   CaptionML=ENU=Average Cost Calc. Type;
                                                   OptionCaptionML=ENU=" ,Item,Item & Location & Variant";
                                                   OptionString=[ ,Item,Item & Location & Variant];
                                                   Editable=No }
    { 5805;   ;Average Cost Period ;Option        ;AccessByPermission=TableData 27=R;
                                                   CaptionML=ENU=Average Cost Period;
                                                   OptionCaptionML=ENU=" ,Day,Week,Month,Quarter,Year,Accounting Period";
                                                   OptionString=[ ,Day,Week,Month,Quarter,Year,Accounting Period];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Starting Date                           ;Clustered=Yes }
    {    ;New Fiscal Year,Date Locked              }
    {    ;Closed                                   }
  }
  FIELDGROUPS
  {
    { 1   ;Brick               ;Starting Date,Name,New Fiscal Year,Closed }
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=<Month Text>';
      AccountingPeriod2@1001 : Record 50;
      InvtSetup@1002 : Record 313;

    PROCEDURE UpdateAvgItems@1();
    VAR
      ChangeAvgCostSetting@1002 : Codeunit 5810;
    BEGIN
      ChangeAvgCostSetting.UpdateAvgCostFromAccPeriodChg(Rec);
    END;

    [External]
    PROCEDURE GetFiscalYearEndDate@9(ReferenceDate@1000 : Date) : Date;
    VAR
      AccountingPeriod@1001 : Record 50;
    BEGIN
      IF AccountingPeriod.ISEMPTY THEN
        EXIT(CALCDATE('<CY>',ReferenceDate));

      WITH AccountingPeriod DO BEGIN
        SETRANGE("New Fiscal Year",TRUE);
        SETRANGE("Starting Date",0D,ReferenceDate);
        IF FINDLAST THEN
          SETRANGE("Starting Date");
        IF FIND('>') THEN
          EXIT("Starting Date" - 1);
      END;
    END;

    [External]
    PROCEDURE GetFiscalYearStartDate@10(ReferenceDate@1000 : Date) : Date;
    VAR
      AccountingPeriod@1001 : Record 50;
    BEGIN
      IF AccountingPeriod.ISEMPTY THEN
        EXIT(CALCDATE('<-CY>',ReferenceDate));

      WITH AccountingPeriod DO BEGIN
        SETRANGE("New Fiscal Year",TRUE);
        SETRANGE("Starting Date",0D,ReferenceDate);
        IF FINDLAST THEN
          EXIT("Starting Date")
      END;
    END;

    BEGIN
    END.
  }
}

