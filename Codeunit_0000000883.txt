OBJECT Codeunit 883 OCR Master Data Mgt.
{
  OBJECT-PROPERTIES
  {
    Date=11/22/17;
    Time=12:00:00 PM;
    Version List=NAVW111.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {

    PROCEDURE UpdateIntegrationRecords@3(OnlyRecordsWithoutID@1009 : Boolean);
    VAR
      IntegrationRecord@1007 : Record 5151;
      UpdatedIntegrationRecord@1006 : Record 5151;
      Vendor@1000 : Record 23;
      IntegrationManagement@1005 : Codeunit 5150;
      VendorRecordRef@1001 : RecordRef;
      NullGuid@1004 : GUID;
    BEGIN
      IF NOT IntegrationManagement.IsIntegrationActivated THEN
        EXIT;

      IF OnlyRecordsWithoutID THEN
        Vendor.SETRANGE(Id,NullGuid);

      IF Vendor.FINDSET THEN
        REPEAT
          IF NOT IntegrationRecord.GET(Vendor.Id) THEN BEGIN
            VendorRecordRef.GETTABLE(Vendor);
            IntegrationManagement.InsertUpdateIntegrationRecord(VendorRecordRef,CURRENTDATETIME);
            IF ISNULLGUID(FORMAT(Vendor.Id)) THEN BEGIN
              UpdatedIntegrationRecord.SETRANGE("Record ID",Vendor.RECORDID);
              UpdatedIntegrationRecord.FINDFIRST;
              Vendor.Id := IntegrationManagement.GetIdWithoutBrackets(UpdatedIntegrationRecord."Integration ID");
            END;
            Vendor.MODIFY(FALSE);
          END;
        UNTIL Vendor.NEXT = 0;
    END;

    [EventSubscriber(Codeunit,5150,OnUpdateReferencedIdField)]
    LOCAL PROCEDURE HandleUpdateReferencedIdFieldOnVendor@19(VAR RecRef@1000 : RecordRef;NewId@1001 : GUID;VAR Handled@1002 : Boolean);
    VAR
      DummyVendor@1004 : Record 23;
      IdFieldRef@1003 : FieldRef;
    BEGIN
      IF Handled THEN
        EXIT;

      IF RecRef.NUMBER = DATABASE::Vendor THEN BEGIN
        IdFieldRef := RecRef.FIELD(DummyVendor.FIELDNO(Id));
        IdFieldRef.VALUE(NewId);
        Handled := TRUE;
        EXIT;
      END;

      IF RecRef.NUMBER = DATABASE::"Vendor Bank Account" THEN
        Handled := TRUE;
    END;

    [EventSubscriber(Codeunit,5150,OnGetPredefinedIdValue)]
    LOCAL PROCEDURE HandleGetPredefinedIdValue@21(VAR Id@1000 : GUID;VAR RecRef@1001 : RecordRef;VAR Handled@1002 : Boolean);
    VAR
      DummyVendor@1004 : Record 23;
      GraphMgtGeneralTools@1003 : Codeunit 5465;
    BEGIN
      GraphMgtGeneralTools.HandleGetPredefinedIdValue(Id,RecRef,Handled,DATABASE::Vendor,DummyVendor.FIELDNO(Id));
    END;

    [EventSubscriber(Codeunit,5150,OnGetIntegrationActivated)]
    LOCAL PROCEDURE OnGetIntegrationActivated@12(VAR IsSyncEnabled@1000 : Boolean);
    VAR
      ReadSoftOCRMasterDataSync@1001 : Codeunit 884;
    BEGIN
      IF IsSyncEnabled THEN
        EXIT;

      IsSyncEnabled := ReadSoftOCRMasterDataSync.IsSyncEnabled;
    END;

    BEGIN
    END.
  }
}

