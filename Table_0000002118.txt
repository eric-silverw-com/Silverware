OBJECT Table 2118 O365 Email Setup
{
  OBJECT-PROPERTIES
  {
    Date=09/19/18;
    Time=12:00:00 PM;
    Version List=NAVW113.00;
  }
  PROPERTIES
  {
    OnInsert=VAR
               IntegrationManagement@1001 : Codeunit 5150;
             BEGIN
               IF Email = '' THEN
                 ERROR(EmailAddressEmptyErr);

               Code := UPPERCASE(IntegrationManagement.GetIdWithoutBrackets(CREATEGUID));

               CheckAlreadyAdded;
             END;

    OnModify=BEGIN
               IF Email <> '' THEN
                 CheckAlreadyAdded
               ELSE
                 IF DELETE THEN; // If Email validate was called rec may have been deleted already
             END;

    OnRename=BEGIN
               CheckAlreadyAdded;
             END;

    CaptionML=[ENU=O365 Email Setup;
               ESM=Configuraci¢n correo electr¢nico de O365;
               FRC=Configuration de la messagerie O365;
               ENC=O365 Email Setup];
  }
  FIELDS
  {
    { 1   ;   ;Code                ;Code80        ;OnValidate=VAR
                                                                GuidVar@1000 : GUID;
                                                              BEGIN
                                                                IF NOT EVALUATE(GuidVar,Code) THEN
                                                                  ERROR(CodeFormatErr);

                                                                IF ISNULLGUID(GuidVar) THEN
                                                                  ERROR(CodeEmptyErr);
                                                              END;

                                                   CaptionML=[ENU=Code;
                                                              ESM=C¢digo;
                                                              FRC=Code;
                                                              ENC=Code] }
    { 2   ;   ;Email               ;Text80        ;OnValidate=VAR
                                                                MailManagement@1000 : Codeunit 9520;
                                                              BEGIN
                                                                IF DELCHR(Email,'=',' ') = '' THEN
                                                                  IF DELETE THEN
                                                                    EXIT;

                                                                MailManagement.CheckValidEmailAddress(Email);
                                                              END;

                                                   ExtendedDatatype=E-Mail;
                                                   CaptionML=[ENU=Email;
                                                              ESM=Correo electr¢nico;
                                                              FRC=Courriel;
                                                              ENC=Email] }
    { 3   ;   ;RecipientType       ;Option        ;CaptionML=[ENU=RecipientType;
                                                              ESM=RecipientType;
                                                              FRC=RecipientType;
                                                              ENC=RecipientType];
                                                   OptionCaptionML=[ENU=CC,BCC;
                                                                    ESM=CC,CCO;
                                                                    FRC=CC,CCI;
                                                                    ENC=CC,BCC];
                                                   OptionString=CC,BCC }
  }
  KEYS
  {
    {    ;Code,RecipientType                      ;Clustered=Yes }
    {    ;Email,RecipientType                      }
    {    ;Email                                    }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      EmailAddressEmptyErr@1000 : TextConst 'ENU=An email address must be specified.;ESM=Se debe especificar una direcci¢n de correo electr¢nico.;FRC=Une adresse ‚lectronique doit ˆtre sp‚cifi‚e.;ENC=An email address must be specified.';
      ConfigAlreadyExistsErr@1001 : TextConst 'ENU=The email address has already been added.;ESM=Ya se ha agregado la direcci¢n de correo electr¢nico.;FRC=L''adresse de courriel a d‚j… ‚t‚ ajout‚e.;ENC=The email address has already been added.';
      CodeFormatErr@1002 : TextConst 'ENU=The code must be a GUID.;ESM=El c¢digo debe ser un GUID.;FRC=Le code doit ˆtre un GUID.;ENC=The code must be a GUID.';
      CodeEmptyErr@1003 : TextConst 'ENU=The code must be not null.;ESM=El c¢digo no puede ser nulo.;FRC=Ce code ne doit pas ˆtre nul.;ENC=The code must be not null.';

    [External]
    PROCEDURE GetCCAddressesFromO365EmailSetup@1() Addresses : Text[250];
    BEGIN
      RESET;
      SETCURRENTKEY(Email,RecipientType);
      SETRANGE(RecipientType,RecipientType::CC);

      IF FINDSET THEN BEGIN
        REPEAT
          Addresses += Email + ';';
        UNTIL NEXT = 0;
      END;
      Addresses := DELCHR(Addresses,'>',';');
    END;

    [External]
    PROCEDURE GetBCCAddressesFromO365EmailSetup@3() Addresses : Text[250];
    BEGIN
      RESET;
      SETCURRENTKEY(Email,RecipientType);
      SETRANGE(RecipientType,RecipientType::BCC);
      IF FINDSET THEN BEGIN
        REPEAT
          Addresses += Email + ';';
        UNTIL NEXT = 0;
      END;
      Addresses := DELCHR(Addresses,'>',';');
    END;

    LOCAL PROCEDURE CheckAlreadyAdded@7();
    VAR
      O365EmailSetup@1000 : Record 2118;
    BEGIN
      O365EmailSetup.SETFILTER(Code,'<>%1',Code);
      SETCURRENTKEY(Email,RecipientType);
      O365EmailSetup.SETRANGE(Email,Email);
      O365EmailSetup.SETRANGE(RecipientType,RecipientType);
      IF NOT O365EmailSetup.ISEMPTY THEN
        ERROR(ConfigAlreadyExistsErr);
    END;

    BEGIN
    END.
  }
}

