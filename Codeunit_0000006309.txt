OBJECT Codeunit 6309 PBI Sales Pipeline Chart Calc.
{
  OBJECT-PROPERTIES
  {
    Date=11/22/17;
    Time=12:00:00 PM;
    Version List=NAVW111.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      SalesPipelineChartMgt@1000 : Codeunit 781;

    PROCEDURE GetValues@3(VAR TempPowerBIChartBuffer@1001 : TEMPORARY Record 6305);
    VAR
      TempSalesCycleStage@1005 : TEMPORARY Record 5091;
      SalesCycle@1000 : Record 5090;
    BEGIN
      IF SalesCycle.FINDSET THEN BEGIN
        REPEAT
          SalesPipelineChartMgt.InsertTempSalesCycleStage(TempSalesCycleStage,SalesCycle);
          IF TempSalesCycleStage.FINDSET THEN
            REPEAT
              InsertToBuffer(TempPowerBIChartBuffer,TempSalesCycleStage);
            UNTIL TempSalesCycleStage.NEXT = 0;
        UNTIL SalesCycle.NEXT = 0;
      END;
    END;

    LOCAL PROCEDURE InsertToBuffer@27(VAR TempPowerBIChartBuffer@1000 : TEMPORARY Record 6305;TempSalesCycleStage@1001 : TEMPORARY Record 5091);
    BEGIN
      WITH TempPowerBIChartBuffer DO BEGIN
        IF FINDLAST THEN
          ID += 1
        ELSE
          ID := 1;
        "Row No." := FORMAT(TempSalesCycleStage.Stage);
        Value := SalesPipelineChartMgt.GetOppEntryCount(TempSalesCycleStage."Sales Cycle Code",TempSalesCycleStage.Stage);
        "Measure Name" := TempSalesCycleStage.Description;
        "Measure No." := TempSalesCycleStage."Sales Cycle Code";
        INSERT;
      END;
    END;

    BEGIN
    END.
  }
}

