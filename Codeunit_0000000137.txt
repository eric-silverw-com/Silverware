OBJECT Codeunit 137 OCR Inc. Doc. via Job Queue
{
  OBJECT-PROPERTIES
  {
    Date=11/22/17;
    Time=12:00:00 PM;
    Version List=NAVW111.00;
  }
  PROPERTIES
  {
    TableNo=472;
    Permissions=TableData 472=rimd;
    OnRun=VAR
            IncomingDocument@1000 : Record 130;
            SendIncomingDocumentToOCR@1003 : Codeunit 133;
            RecRef@1001 : RecordRef;
          BEGIN
            TESTFIELD("Record ID to Process");
            RecRef.GET("Record ID to Process");
            RecRef.SETTABLE(IncomingDocument);
            IncomingDocument.FIND;
            SetJobQueueStatus(IncomingDocument,IncomingDocument."Job Queue Status"::Processing);

            CASE IncomingDocument."OCR Status" OF
              IncomingDocument."OCR Status"::Ready:
                IF NOT SendIncomingDocumentToOCR.TrySendToOCR(IncomingDocument) THEN BEGIN
                  SetJobQueueStatus(IncomingDocument,IncomingDocument."Job Queue Status"::Error);
                  ERROR(GETLASTERRORTEXT);
                END;
              IncomingDocument."OCR Status"::Sent,IncomingDocument."OCR Status"::"Awaiting Verification":
                IF NOT (SendIncomingDocumentToOCR.TryRetrieveFromOCR(IncomingDocument) AND
                        (IncomingDocument."OCR Status" = IncomingDocument."OCR Status"::Success))
                THEN BEGIN
                  SetJobQueueStatus(IncomingDocument,IncomingDocument."Job Queue Status"::Processing);
                  ERROR(GETLASTERRORTEXT);
                END;
            END;

            SetJobQueueStatus(IncomingDocument,IncomingDocument."Job Queue Status"::" ");
          END;

  }
  CODE
  {
    VAR
      OCRSendReceiveDescriptionTxt@1000 : TextConst '@@@="%1 = document type, %2 = document number. Example: Post Purchase Order 1234.";ENU=OCR Incoming Document No. %1.;ESM=Documento de OCR entrante n.§ %1.;FRC=Nø document OCR entrantÿ: %1.;ENC=OCR Incoming Document No. %1.';
      IncomingDocumentScheduledMsg@1001 : TextConst '@@@="%1=document type, %2=number, e.g. Order 123  or Invoice 234.";ENU=Incoming Document No. %1 has been scheduled for OCR.;ESM=El documento entrante n.§ %1 ya se ha programado para OCR.;FRC=Le nø de document entrant %1 a ‚t‚ programm‚ pour OCR.;ENC=Incoming Document No. %1 has been scheduled for OCR.';
      WrongJobQueueStatusErr@1003 : TextConst '@@@="%1 = document type, %2 = document number. Example: Purchase Order 1234 or Invoice 1234.";ENU=Incoming Document No. %1 cannot be processed because it has already been scheduled for OCR. Choose the Remove from Job Queue action to reset the job queue status and then OCR again.;ESM=No se puede procesar el documento entrante n.§ %1 porque ya se ha programado para OCR. Elija la acci¢n "Quitar de cola de proyecto" para restablecer el estado de la cola de proyecto y vuelva a programarlo para OCR.;FRC=Impossible de traiter le document entrant nøÿ%1, car ce document a d‚j… ‚t‚ programm‚ pour OCR. Choisissez l''action Supprimer de la file d''attente des travaux pour r‚initialiser l''‚tat de la file d''attente des travaux, puis recommencez l''OCR.;ENC=Incoming Document No. %1 cannot be processed because it has already been scheduled for OCR. Choose the Remove from Job Queue action to reset the job queue status and then OCR again.';

    LOCAL PROCEDURE SetJobQueueStatus@5(VAR IncomingDocument@1001 : Record 130;NewStatus@1000 : Option);
    BEGIN
      IncomingDocument.LOCKTABLE;
      IF IncomingDocument.FIND THEN BEGIN
        IncomingDocument."Job Queue Status" := NewStatus;
        IncomingDocument.MODIFY;
        COMMIT;
      END;
    END;

    [External]
    PROCEDURE EnqueueIncomingDoc@1(VAR IncomingDocument@1000 : Record 130);
    BEGIN
      WITH IncomingDocument DO BEGIN
        IF NOT ("Job Queue Status" IN ["Job Queue Status"::" ","Job Queue Status"::Error]) THEN
          ERROR(WrongJobQueueStatusErr,"Entry No.");
        IF Status = Status::New THEN
          CODEUNIT.RUN(CODEUNIT::"Release Incoming Document",IncomingDocument);

        "Job Queue Status" := "Job Queue Status"::Scheduled;
        "Job Queue Entry ID" := EnqueueJobEntry(IncomingDocument);
        MODIFY;
        MESSAGE(IncomingDocumentScheduledMsg,"Entry No.");
      END;
    END;

    LOCAL PROCEDURE EnqueueJobEntry@3(IncomingDocument@1001 : Record 130) : GUID;
    VAR
      JobQueueEntry@1000 : Record 472;
    BEGIN
      WITH JobQueueEntry DO BEGIN
        "Object Type to Run" := "Object Type to Run"::Codeunit;
        "Object ID to Run" := CODEUNIT::"OCR Inc. Doc. via Job Queue";
        "Record ID to Process" := IncomingDocument.RECORDID;
        // Set Timeout to prevent the Job Queue from hanging (eg. as a result of a printer dialog).
        "Maximum No. of Attempts to Run" := 10;
        "Rerun Delay (sec.)" := 5;
        Priority := 1000;
        Description :=
          COPYSTR(STRSUBSTNO(OCRSendReceiveDescriptionTxt,IncomingDocument."Entry No."),1,MAXSTRLEN(Description));
        "Notify On Success" := TRUE;
        CODEUNIT.RUN(CODEUNIT::"Job Queue - Enqueue",JobQueueEntry);
        EXIT(ID);
      END;
    END;

    [External]
    PROCEDURE CancelQueueEntry@2(VAR IncomingDocument@1000 : Record 130);
    VAR
      JobQueueEntry@1002 : Record 472;
    BEGIN
      WITH IncomingDocument DO BEGIN
        IF "Job Queue Status" = "Job Queue Status"::" " THEN
          EXIT;
        IF NOT ISNULLGUID("Job Queue Entry ID") THEN
          JobQueueEntry.SETRANGE(ID,"Job Queue Entry ID");
        JobQueueEntry.SETRANGE("Record ID to Process",RECORDID);
        IF NOT JobQueueEntry.ISEMPTY THEN
          JobQueueEntry.DELETEALL(TRUE);
        "Job Queue Status" := "Job Queue Status"::" ";
        MODIFY;
      END;
    END;

    BEGIN
    END.
  }
}

