OBJECT Table 6085596 Temp. Document Line
{
  OBJECT-PROPERTIES
  {
    Date=03/20/17;
    Time=[ 1:00:00 AM];
    Version List=DCW13.70.00.4.50;
  }
  PROPERTIES
  {
    CaptionML=ENU=Temp. Document Line;
    PasteIsValid=No;
  }
  FIELDS
  {
    { 1   ;   ;Line No.            ;Integer       ;CaptionML=ENU=Line No. }
    { 2   ;   ;Field Value 1       ;Text250       ;CaptionML=ENU=Field Value 1 }
    { 3   ;   ;Field Value 2       ;Text250       ;CaptionML=ENU=Field Value 2 }
    { 4   ;   ;Field Value 3       ;Text250       ;CaptionML=ENU=Field Value 3 }
    { 5   ;   ;Field Value 4       ;Text250       ;CaptionML=ENU=Field Value 4 }
    { 6   ;   ;Field Value 5       ;Text250       ;CaptionML=ENU=Field Value 5 }
    { 7   ;   ;Field Value 6       ;Text200       ;CaptionML=ENU=Field Value 6 }
    { 8   ;   ;Field Value 7       ;Text200       ;CaptionML=ENU=Field Value 7 }
    { 9   ;   ;Field Value 8       ;Text200       ;CaptionML=ENU=Field Value 8 }
    { 10  ;   ;Field Value 9       ;Text200       ;CaptionML=ENU=Field Value 9 }
    { 11  ;   ;Field Value 10      ;Text200       ;CaptionML=ENU=Field Value 10 }
    { 12  ;   ;Field Value 11      ;Text150       ;CaptionML=ENU=Field Value 11 }
    { 13  ;   ;Field Value 12      ;Text150       ;CaptionML=ENU=Field Value 12 }
    { 14  ;   ;Field Value 13      ;Text150       ;CaptionML=ENU=Field Value 13 }
    { 15  ;   ;Field Value 14      ;Text150       ;CaptionML=ENU=Field Value 14 }
    { 16  ;   ;Field Value 15      ;Text150       ;CaptionML=ENU=Field Value 15 }
    { 17  ;   ;Field Value 16      ;Text100       ;CaptionML=ENU=Field Value 16 }
    { 18  ;   ;Field Value 17      ;Text100       ;CaptionML=ENU=Field Value 17 }
    { 19  ;   ;Field Value 18      ;Text100       ;CaptionML=ENU=Field Value 18 }
    { 20  ;   ;Field Value 19      ;Text100       ;CaptionML=ENU=Field Value 19 }
    { 21  ;   ;Field Value 20      ;Text100       ;CaptionML=ENU=Field Value 20 }
    { 22  ;   ;OK                  ;Boolean       ;CaptionML=ENU=OK }
    { 23  ;   ;Document No.        ;Code20        ;CaptionML=ENU=Document No. }
    { 24  ;   ;Template No.        ;Code20        ;CaptionML=ENU=Template No. }
    { 25  ;   ;Page No.            ;Integer       ;CaptionML=ENU=Page No. }
    { 26  ;   ;Translate to Type   ;Option        ;OnValidate=BEGIN
                                                                TestfieldNo;
                                                              END;

                                                   CaptionML=ENU=Translate to Type;
                                                   OptionCaptionML=ENU=" ,G/L Account,Item,Resource,Fixed Asset,Charge (Item)";
                                                   OptionString=[ ,G/L Account,Item,Resource,Fixed Asset,Charge (Item)] }
    { 27  ;   ;Translate to No.    ;Code20        ;TableRelation=IF (Translate to Type=CONST(" ")) Item
                                                                 ELSE IF (Translate to Type=CONST(G/L Account)) "G/L Account" WHERE (Direct Posting=CONST(Yes),
                                                                                                                                     Account Type=CONST(Posting),
                                                                                                                                     Blocked=CONST(No))
                                                                                                                                     ELSE IF (Translate to Type=CONST(Item)) Item
                                                                                                                                     ELSE IF (Translate to Type=CONST(Resource)) Resource
                                                                                                                                     ELSE IF (Translate to Type=CONST(Fixed Asset)) "Fixed Asset"
                                                                                                                                     ELSE IF (Translate to Type=CONST("Charge (Item)")) "Item Charge";
                                                   OnValidate=VAR
                                                                Document@1004 : Record 6085590;
                                                                Template@1160040000 : Record 6085579;
                                                                SalesDocMgt@1002 : Codeunit 6085770;
                                                                PurchDocMgt@1003 : Codeunit 6085709;
                                                              BEGIN
                                                                TestfieldNo;

                                                                Document.GET("Document No.");

                                                                IF "Translate to Type" = 0 THEN BEGIN
                                                                  Template.GET("Template No.");
                                                                  Template.TESTFIELD("Translate to Type");
                                                                  "Translate to Type" := Template."Translate to Type";
                                                                END;

                                                                CASE Document.GetSourceTableNo OF
                                                                  DATABASE::Customer:
                                                                    SalesDocMgt.UpdateLineNoTrans(Document,"Line No.","Translate to Type","Translate to No.");
                                                                  DATABASE::Vendor:
                                                                    PurchDocMgt.UpdateLineNoTrans(Document,"Line No.","Translate to Type","Translate to No.");
                                                                  ELSE
                                                                    EXIT;
                                                                END;
                                                              END;

                                                   ValidateTableRelation=No;
                                                   CaptionML=ENU=Translate to No. }
    { 28  ;   ;Skip                ;Boolean       ;CaptionML=ENU=Skip }
  }
  KEYS
  {
    {    ;Document No.,Line No.                   ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      BlankNoErr@1160040000 : TextConst 'ENU="You must specify %1 in Document Line %2=''%3''."';

    PROCEDURE UpdateTranslInfo@1160040001();
    VAR
      LineTransl@1160040001 : Record 6085584;
      Document@1001 : Record 6085590;
      SalesDocMgt@1160040000 : Codeunit 6085770;
      PurchDocMgt@1160040002 : Codeunit 6085709;
    BEGIN
      IF ("Document No." = '') OR ("Template No." = '') THEN
        EXIT;

      "Translate to Type" := 0;
      "Translate to No." := '';

      Document.GET("Document No.");

      CASE Document.GetSourceTableNo OF
        DATABASE::Customer:
          IF NOT SalesDocMgt.GetTranslLineInfo2(Document,"Line No.",LineTransl) THEN
            EXIT;
        DATABASE::Vendor:
          IF NOT PurchDocMgt.GetTranslLineInfo2(Document,"Line No.",LineTransl) THEN
            EXIT;
        ELSE
          EXIT;
      END;

      "Translate to Type" := LineTransl."Translate to Type";
      "Translate to No." := LineTransl."Translate to No.";
    END;

    PROCEDURE ShowCard@1160040000();
    VAR
      FixedAsset@1160040004 : Record 5600;
      GLAccount@1160040003 : Record 15;
      Item@1160040002 : Record 27;
      ItemCharge@1160040001 : Record 5800;
      Resource@1160040000 : Record 156;
    BEGIN
      CASE "Translate to Type" OF
        "Translate to Type"::"G/L Account":
          BEGIN
            GLAccount.GET("Translate to No.");
            PAGE.RUN(PAGE::"G/L Account Card",GLAccount);
          END;

        "Translate to Type"::Item:
          BEGIN
            Item.GET("Translate to No.");
            PAGE.RUN(PAGE::"Item Card",Item);
          END;

        "Translate to Type"::Resource:
          BEGIN
            Resource.GET("Translate to No.");
            PAGE.RUN(PAGE::"Resource Card",Resource);
          END;

        "Translate to Type"::"Fixed Asset":
          BEGIN
            FixedAsset.GET("Translate to No.");
            PAGE.RUN(PAGE::"Fixed Asset Card",FixedAsset);
          END;

        "Translate to Type"::"Charge (Item)":
          BEGIN
            ItemCharge.GET("Translate to No.");
            PAGE.RUN(PAGE::"Item Charges",ItemCharge);
          END;
      END;
    END;

    PROCEDURE LookupTranslToNo@1160040003(VAR Text@1160040000 : Text[1024]) : Boolean;
    VAR
      Document@1160040003 : Record 6085590;
      Template@1160040002 : Record 6085579;
      GLAccount@1160040004 : Record 15;
      Item@1160040005 : Record 27;
      Resource@1160040006 : Record 156;
      FA@1160040007 : Record 5600;
      ItemCharge@1160040008 : Record 5800;
      TranslToType@1160040001 : Integer;
    BEGIN
      Document.GET("Document No.");

      IF "Translate to Type" <> 0 THEN
        TranslToType := "Translate to Type"
      ELSE BEGIN
        Template.GET("Template No.");
        Template.TESTFIELD("Translate to Type");
        TranslToType := Template."Translate to Type";
      END;

      CASE TranslToType OF
        Template."Translate to Type"::" ":;
        Template."Translate to Type"::"G/L Account":
          BEGIN
            GLAccount.SETRANGE("Direct Posting",TRUE);
            GLAccount.SETRANGE("Account Type",GLAccount."Account Type"::Posting);
            GLAccount.SETRANGE(Blocked,FALSE);
            IF GLAccount.GET(Text) THEN;
            IF PAGE.RUNMODAL(0,GLAccount) = ACTION::LookupOK THEN BEGIN
              Text := GLAccount."No.";
              EXIT(TRUE);
            END;
          END;

        Template."Translate to Type"::Item:
          BEGIN
            IF Item.GET(Text) THEN;
            IF PAGE.RUNMODAL(0,Item) = ACTION::LookupOK THEN BEGIN
              Text := Item."No.";
              EXIT(TRUE);
            END;
          END;

        Template."Translate to Type"::Resource:
          BEGIN
            IF Resource.GET(Text) THEN;
            IF PAGE.RUNMODAL(0,Resource) = ACTION::LookupOK THEN BEGIN
              Text := Resource."No.";
              EXIT(TRUE);
            END;
          END;

        Template."Translate to Type"::"Fixed Asset":
          BEGIN
            IF FA.GET(Text) THEN;
            IF PAGE.RUNMODAL(0,FA) = ACTION::LookupOK THEN BEGIN
              Text := FA."No.";
              EXIT(TRUE);
            END;
          END;

        Template."Translate to Type"::"Charge (Item)":
          BEGIN
            IF ItemCharge.GET(Text) THEN;
            IF PAGE.RUNMODAL(0,ItemCharge) = ACTION::LookupOK THEN BEGIN
              Text := ItemCharge."No.";
              EXIT(TRUE);
            END;
          END;

      END;
    END;

    PROCEDURE ManuallyInsertLine@1160040002(NewLineLineNo@1160040004 : Integer);
    VAR
      TempDocLine@1160040000 : Record 6085596;
      Value@1160040002 : Record 6085593;
      Value2@1160040003 : Record 6085593;
      CurrView@1160040001 : Text[250];
      CurrAscending@6085573 : Boolean;
    BEGIN
      TempDocLine := Rec;
      CurrView := GETVIEW;
      CurrAscending := ASCENDING;
      RESET;

      SETRANGE("Document No.","Document No.");
      SETFILTER("Line No.",'>=%1',NewLineLineNo);
      ASCENDING(FALSE);
      IF FINDFIRST THEN BEGIN
        Value.SETCURRENTKEY("Document No.","Is Value",Type,"Line No.");
        Value.SETRANGE("Document No.","Document No.");
        Value.SETRANGE("Is Value",TRUE);
        Value.SETRANGE(Type,Value.Type::Line);

        REPEAT
          "Line No." := "Line No." + 1;
          INSERT;
          GET("Document No.","Line No." - 1);
          DELETE;

          Value.SETRANGE("Line No.","Line No.");
          IF Value.FINDSET(TRUE,FALSE) THEN
            REPEAT
              Value2 := Value;
              Value2."Line No." := "Line No." + 1;
              Value2.INSERT;
              Value.DELETE;
            UNTIL Value.NEXT = 0;
        UNTIL NEXT = 0;
      END;

      SETVIEW(CurrView);
      ASCENDING(CurrAscending);

      Rec := TempDocLine;
      "Line No." := NewLineLineNo;
    END;

    PROCEDURE GetLastLineNo@1160040005() LastLineNo : Integer;
    VAR
      TempDocLine@1160040001 : Record 6085596;
      CurrView@1160040000 : Text[250];
    BEGIN
      TempDocLine := Rec;
      CurrView := GETVIEW;
      RESET;
      SETRANGE("Document No.","Document No.");
      IF FINDLAST THEN
        LastLineNo := "Line No.";

      SETVIEW(CurrView);
      Rec := TempDocLine;
    END;

    PROCEDURE TestfieldNo@1160040004();
    VAR
      Document@1160040002 : Record 6085590;
      Field@1160040001 : Record 6085580;
      SalesDocMgt@1160040000 : Codeunit 6085770;
    BEGIN
      TESTFIELD("Document No.");
      Document.GET("Document No.");
      IF SalesDocMgt.GetLineAccountNo(Document,"Line No.") = '' THEN BEGIN
        IF Field.GET("Template No.",Field.Type::Line,'NO') THEN;
        ERROR(BlankNoErr,Field."Field Name",FIELDCAPTION("Line No."),"Line No.");
      END;
    END;

    PROCEDURE ShowMatchSpec@6085574();
    VAR
      MatchSpec@161024012 : TEMPORARY Record 6085761;
      PurchDocMatch@1160040001 : Record 6085700;
      PurchaseLine@6085573 : Record 39;
      PurchRcptLine@6085574 : Record 121;
      ReturnShipmentLine@6085576 : Record 6651;
      Currency@6085577 : Record 4;
    BEGIN
      PurchDocMatch.SETRANGE("Document No.","Document No.");
      PurchDocMatch.SETRANGE("Document Line No.","Line No.");

      IF PurchDocMatch.FINDSET THEN
        REPEAT
          MatchSpec.INIT;

          CASE PurchDocMatch."Purch. Doc. Type" OF
            PurchDocMatch."Purch. Doc. Type"::Order: MatchSpec.Type := MatchSpec.Type::"Purchase Order";
            PurchDocMatch."Purch. Doc. Type"::Receipt: MatchSpec.Type := MatchSpec.Type::"Purchase Receipt";
            PurchDocMatch."Purch. Doc. Type"::"Return Order": MatchSpec.Type := MatchSpec.Type::"Purchase Return Order";
            PurchDocMatch."Purch. Doc. Type"::"Return Shipment": MatchSpec.Type := MatchSpec.Type::"Return Shipment";
          END;

          MatchSpec."Document No." := PurchDocMatch."Purch. Doc. No.";
          MatchSpec."Line No." := PurchDocMatch."Purch. Line No.";

          IF MatchSpec."Line No." <> 0 THEN
            CASE PurchDocMatch."Purch. Doc. Type" OF
              PurchDocMatch."Purch. Doc. Type"::Order:
                BEGIN
                  PurchaseLine.GET(PurchaseLine."Document Type"::Order,PurchDocMatch."Purch. Doc. No.",PurchDocMatch."Purch. Line No.");
                  MatchSpec."No." := PurchaseLine."No.";
                  MatchSpec.Description := PurchaseLine.Description;
                  MatchSpec.Quantity := PurchaseLine.Quantity;
                  MatchSpec."Direct Unit Cost" := PurchaseLine."Direct Unit Cost";
                  MatchSpec."Line Discount %" := PurchaseLine."Line Discount %";
                  MatchSpec."Line Discount Amount" := PurchaseLine."Line Discount Amount";
                END;
              PurchDocMatch."Purch. Doc. Type"::Receipt:
                BEGIN
                  PurchRcptLine.GET(PurchDocMatch."Purch. Doc. No.",PurchDocMatch."Purch. Line No.");
                  IF PurchRcptLine."Currency Code" <> '' THEN
                    Currency.GET(PurchRcptLine."Currency Code")
                  ELSE
                    CLEAR(Currency);
                  Currency.InitRoundingPrecision;

                  MatchSpec."No." := PurchRcptLine."No.";
                  MatchSpec.Description := PurchRcptLine.Description;
                  MatchSpec.Quantity := PurchRcptLine.Quantity;
                  MatchSpec."Direct Unit Cost" := PurchRcptLine."Direct Unit Cost";
                  MatchSpec."Line Discount %" := PurchRcptLine."Line Discount %";
                  MatchSpec."Line Discount Amount" := ROUND(ROUND(MatchSpec.Quantity * MatchSpec."Direct Unit Cost",Currency.
                    "Amount Rounding Precision") * MatchSpec."Line Discount %" / 100,Currency."Amount Rounding Precision");
                END;
              PurchDocMatch."Purch. Doc. Type"::"Return Order":
                BEGIN
                  PurchaseLine.GET(PurchaseLine."Document Type"::"Return Order",PurchDocMatch."Purch. Doc. No.",PurchDocMatch.
                    "Purch. Line No.");
                  MatchSpec."No." := PurchaseLine."No.";
                  MatchSpec.Description := PurchaseLine.Description;
                  MatchSpec.Quantity := PurchaseLine.Quantity;
                  MatchSpec."Direct Unit Cost" := PurchaseLine."Direct Unit Cost";
                  MatchSpec."Line Discount %" := PurchaseLine."Line Discount %";
                  MatchSpec."Line Discount Amount" := PurchaseLine."Line Discount Amount";
                END;
              PurchDocMatch."Purch. Doc. Type"::"Return Shipment":
                BEGIN
                  ReturnShipmentLine.GET(PurchDocMatch."Purch. Doc. No.",PurchDocMatch."Purch. Line No.");
                  IF ReturnShipmentLine."Currency Code" <> '' THEN
                    Currency.GET(ReturnShipmentLine."Currency Code")
                  ELSE
                    CLEAR(Currency);
                  Currency.InitRoundingPrecision;

                  MatchSpec."No." := ReturnShipmentLine."No.";
                  MatchSpec.Description := ReturnShipmentLine.Description;
                  MatchSpec.Quantity := ReturnShipmentLine.Quantity;
                  MatchSpec."Direct Unit Cost" := ReturnShipmentLine."Direct Unit Cost";
                  MatchSpec."Line Discount %" := ReturnShipmentLine."Line Discount %";
                  MatchSpec."Line Discount Amount" := ROUND(ROUND(MatchSpec.Quantity * MatchSpec."Direct Unit Cost",Currency.
                    "Amount Rounding Precision") * MatchSpec."Line Discount %" / 100,Currency."Amount Rounding Precision");
                END;
            END;

          MatchSpec."Matched Quantity" := PurchDocMatch.Quantity;
          MatchSpec."Matched Direct Unit Cost" := PurchDocMatch."Direct Unit Cost";
          MatchSpec."Matched Line Discount %" := PurchDocMatch."Line Discount %";
          MatchSpec.INSERT;
        UNTIL PurchDocMatch.NEXT = 0;

      PAGE.RUN(0,MatchSpec);
    END;

    BEGIN
    END.
  }
}

