OBJECT Table 91 User Setup
{
  OBJECT-PROPERTIES
  {
    Date=09/19/18;
    Time=12:00:00 PM;
    Version List=NAVW113.00;
  }
  PROPERTIES
  {
    OnInsert=VAR
               User@1000 : Record 2000000120;
             BEGIN
               IF "E-Mail" <> '' THEN
                 EXIT;
               IF "User ID" <> '' THEN
                 EXIT;
               User.SETRANGE("User Name","User ID");
               IF User.FINDFIRST THEN
                 "E-Mail" := COPYSTR(User."Contact Email",1,MAXSTRLEN("E-Mail"));
             END;

    OnDelete=VAR
               NotificationSetup@1000 : Record 1512;
             BEGIN
               NotificationSetup.SETRANGE("User ID","User ID");
               NotificationSetup.DELETEALL(TRUE);
             END;

    CaptionML=[ENU=User Setup;
               ESM=Configuraci¢n usuarios;
               FRC=Configuration des utilisateurs;
               ENC=User Setup];
    LookupPageID=Page119;
    DrillDownPageID=Page119;
  }
  FIELDS
  {
    { 1   ;   ;User ID             ;Code50        ;TableRelation=User."User Name";
                                                   OnValidate=VAR
                                                                UserMgt@1000 : Codeunit 418;
                                                              BEGIN
                                                                UserMgt.ValidateUserID("User ID");
                                                              END;

                                                   OnLookup=VAR
                                                              UserMgt@1000 : Codeunit 418;
                                                            BEGIN
                                                              UserMgt.LookupUserID("User ID");
                                                            END;

                                                   ValidateTableRelation=No;
                                                   DataClassification=EndUserIdentifiableInformation;
                                                   CaptionML=[ENU=User ID;
                                                              ESM=Id. usuario;
                                                              FRC=Code utilisateur;
                                                              ENC=User ID];
                                                   NotBlank=Yes }
    { 2   ;   ;Allow Posting From  ;Date          ;OnValidate=BEGIN
                                                                CheckAllowedPostingDates(0);
                                                              END;

                                                   CaptionML=[ENU=Allow Posting From;
                                                              ESM=Permitir registro desde;
                                                              FRC=D‚but p‚riode report;
                                                              ENC=Allow Posting From] }
    { 3   ;   ;Allow Posting To    ;Date          ;OnValidate=BEGIN
                                                                CheckAllowedPostingDates(0);
                                                              END;

                                                   CaptionML=[ENU=Allow Posting To;
                                                              ESM=Permitir registro hasta;
                                                              FRC=Fin p‚riode report;
                                                              ENC=Allow Posting To] }
    { 4   ;   ;Register Time       ;Boolean       ;CaptionML=[ENU=Register Time;
                                                              ESM=Registrar tiempo conexi¢n;
                                                              FRC=Registre temps;
                                                              ENC=Register Time] }
    { 10  ;   ;Salespers./Purch. Code;Code20      ;TableRelation=Salesperson/Purchaser;
                                                   OnValidate=VAR
                                                                UserSetup@1000 : Record 91;
                                                              BEGIN
                                                                IF "Salespers./Purch. Code" <> '' THEN BEGIN
                                                                  ValidateSalesPersonPurchOnUserSetup(Rec);
                                                                  UserSetup.SETCURRENTKEY("Salespers./Purch. Code");
                                                                  UserSetup.SETRANGE("Salespers./Purch. Code","Salespers./Purch. Code");
                                                                  IF UserSetup.FINDFIRST THEN
                                                                    ERROR(Text001,"Salespers./Purch. Code",UserSetup."User ID");
                                                                  UpdateSalesPerson;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Salespers./Purch. Code;
                                                              ESM=C¢d. vendedor/comprador;
                                                              FRC=Code de repr‚sentant/d'achat;
                                                              ENC=Salespers./Purch. Code] }
    { 11  ;   ;Approver ID         ;Code50        ;TableRelation="User Setup"."User ID";
                                                   OnValidate=BEGIN
                                                                IF "Approver ID" = "User ID" THEN
                                                                  FIELDERROR("Approver ID");
                                                              END;

                                                   OnLookup=VAR
                                                              UserSetup@1000 : Record 91;
                                                            BEGIN
                                                              UserSetup.SETFILTER("User ID",'<>%1',"User ID");
                                                              IF PAGE.RUNMODAL(PAGE::"Approval User Setup",UserSetup) = ACTION::LookupOK THEN
                                                                VALIDATE("Approver ID",UserSetup."User ID");
                                                            END;

                                                   DataClassification=EndUserIdentifiableInformation;
                                                   CaptionML=[ENU=Approver ID;
                                                              ESM=Id. aprobador;
                                                              FRC=Code approbateur;
                                                              ENC=Approver ID] }
    { 12  ;   ;Sales Amount Approval Limit;Integer;OnValidate=BEGIN
                                                                IF "Unlimited Sales Approval" AND ("Sales Amount Approval Limit" <> 0) THEN
                                                                  ERROR(Text003,FIELDCAPTION("Sales Amount Approval Limit"),FIELDCAPTION("Unlimited Sales Approval"));
                                                                IF "Sales Amount Approval Limit" < 0 THEN
                                                                  ERROR(Text005);
                                                              END;

                                                   CaptionML=[ENU=Sales Amount Approval Limit;
                                                              ESM=Importe l¡mite aprob. ventas;
                                                              FRC=Limite d'approbation de montant de vente;
                                                              ENC=Sales Amount Approval Limit];
                                                   BlankZero=Yes }
    { 13  ;   ;Purchase Amount Approval Limit;Integer;
                                                   OnValidate=BEGIN
                                                                IF "Unlimited Purchase Approval" AND ("Purchase Amount Approval Limit" <> 0) THEN
                                                                  ERROR(Text003,FIELDCAPTION("Purchase Amount Approval Limit"),FIELDCAPTION("Unlimited Purchase Approval"));
                                                                IF "Purchase Amount Approval Limit" < 0 THEN
                                                                  ERROR(Text005);
                                                              END;

                                                   CaptionML=[ENU=Purchase Amount Approval Limit;
                                                              ESM=Importe l¡mite aprob. compras;
                                                              FRC=Limite d'approbation de montant d'achat;
                                                              ENC=Purchase Amount Approval Limit];
                                                   BlankZero=Yes }
    { 14  ;   ;Unlimited Sales Approval;Boolean   ;OnValidate=BEGIN
                                                                IF "Unlimited Sales Approval" THEN
                                                                  "Sales Amount Approval Limit" := 0;
                                                              END;

                                                   CaptionML=[ENU=Unlimited Sales Approval;
                                                              ESM=Aprobaci¢n venta ilimitada;
                                                              FRC=Montant illimit‚ de vente autoris‚;
                                                              ENC=Unlimited Sales Approval] }
    { 15  ;   ;Unlimited Purchase Approval;Boolean;OnValidate=BEGIN
                                                                IF "Unlimited Purchase Approval" THEN
                                                                  "Purchase Amount Approval Limit" := 0;
                                                              END;

                                                   CaptionML=[ENU=Unlimited Purchase Approval;
                                                              ESM=Aprobaci¢n compra ilimitada;
                                                              FRC=Montant illimit‚ d'achat autoris‚;
                                                              ENC=Unlimited Purchase Approval] }
    { 16  ;   ;Substitute          ;Code50        ;TableRelation="User Setup"."User ID";
                                                   OnValidate=BEGIN
                                                                IF Substitute = "User ID" THEN
                                                                  FIELDERROR(Substitute);
                                                              END;

                                                   OnLookup=VAR
                                                              UserSetup@1000 : Record 91;
                                                            BEGIN
                                                              UserSetup.SETFILTER("User ID",'<>%1',"User ID");
                                                              IF PAGE.RUNMODAL(PAGE::"Approval User Setup",UserSetup) = ACTION::LookupOK THEN
                                                                VALIDATE(Substitute,UserSetup."User ID");
                                                            END;

                                                   DataClassification=EndUserIdentifiableInformation;
                                                   CaptionML=[ENU=Substitute;
                                                              ESM=Sustituir;
                                                              FRC=Substitut;
                                                              ENC=Substitute] }
    { 17  ;   ;E-Mail              ;Text100       ;OnValidate=VAR
                                                                MailManagement@1000 : Codeunit 9520;
                                                              BEGIN
                                                                UpdateSalesPerson;
                                                                MailManagement.ValidateEmailAddressField("E-Mail");
                                                              END;

                                                   ExtendedDatatype=E-Mail;
                                                   CaptionML=[ENU=E-Mail;
                                                              ESM=Correo electr¢nico;
                                                              FRC=Courriel;
                                                              ENC=E-Mail] }
    { 19  ;   ;Request Amount Approval Limit;Integer;
                                                   OnValidate=BEGIN
                                                                IF "Unlimited Request Approval" AND ("Request Amount Approval Limit" <> 0) THEN
                                                                  ERROR(Text003,FIELDCAPTION("Request Amount Approval Limit"),FIELDCAPTION("Unlimited Request Approval"));
                                                                IF "Request Amount Approval Limit" < 0 THEN
                                                                  ERROR(Text005);
                                                              END;

                                                   CaptionML=[ENU=Request Amount Approval Limit;
                                                              ESM=Imp. l¡m. aprob. solic. compra;
                                                              FRC=Limite d'approbation de demande d'achat;
                                                              ENC=Request Amount Approval Limit];
                                                   BlankZero=Yes }
    { 20  ;   ;Unlimited Request Approval;Boolean ;OnValidate=BEGIN
                                                                IF "Unlimited Request Approval" THEN
                                                                  "Request Amount Approval Limit" := 0;
                                                              END;

                                                   CaptionML=[ENU=Unlimited Request Approval;
                                                              ESM=Aprob. solic. compra ilimitada;
                                                              FRC=Montant illimit‚ de demande d'achat autoris‚;
                                                              ENC=Unlimited Request Approval] }
    { 21  ;   ;Approval Administrator;Boolean     ;OnValidate=VAR
                                                                UserSetup@1000 : Record 91;
                                                              BEGIN
                                                                IF "Approval Administrator" THEN BEGIN
                                                                  UserSetup.SETRANGE("Approval Administrator",TRUE);
                                                                  IF NOT UserSetup.ISEMPTY THEN
                                                                    FIELDERROR("Approval Administrator");
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Approval Administrator;
                                                              ESM=Administrador aprobaci¢n;
                                                              FRC=Administrateur approbation;
                                                              ENC=Approval Administrator] }
    { 31  ;   ;License Type        ;Option        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(User."License Type" WHERE (User Name=FIELD(User ID)));
                                                   CaptionML=[ENU=License Type;
                                                              ESM=Tipo de licencia;
                                                              FRC=Type licence;
                                                              ENC=Licence Type];
                                                   OptionCaptionML=[ENU=Full User,Limited User,Device Only User,Windows Group,External User;
                                                                    ESM=Usuario completo,Usuario limitado,Usuario solo dispositivo,Grupo Windows,Usuario externo;
                                                                    FRC=Utilisateur complet,Utilisateur limit‚,Utilisateur avec p‚riph‚rique uniquement,Groupe Windows,Utilisateur externe;
                                                                    ENC=Full User,Limited User,Device Only User,Windows Group,External User];
                                                   OptionString=Full User,Limited User,Device Only User,Windows Group,External User }
    { 950 ;   ;Time Sheet Admin.   ;Boolean       ;CaptionML=[ENU=Time Sheet Admin.;
                                                              ESM=Admin. parte horas;
                                                              FRC=Admin. feuille de temps;
                                                              ENC=Time Sheet Admin.] }
    { 5600;   ;Allow FA Posting From;Date         ;CaptionML=[ENU=Allow FA Posting From;
                                                              ESM=A/F Permitir registro desde;
                                                              FRC=Autoriser report immo. de;
                                                              ENC=Allow FA Posting From] }
    { 5601;   ;Allow FA Posting To ;Date          ;CaptionML=[ENU=Allow FA Posting To;
                                                              ESM=A/F Permitir registro hasta;
                                                              FRC=Autoriser report immo. …;
                                                              ENC=Allow FA Posting To] }
    { 5700;   ;Sales Resp. Ctr. Filter;Code10     ;TableRelation="Responsibility Center".Code;
                                                   CaptionML=[ENU=Sales Resp. Ctr. Filter;
                                                              ESM=Filtro centro resp. vent.;
                                                              FRC=Filtre centre gestion vente;
                                                              ENC=Sales Resp. Ctr. Filter] }
    { 5701;   ;Purchase Resp. Ctr. Filter;Code10  ;TableRelation="Responsibility Center";
                                                   CaptionML=[ENU=Purchase Resp. Ctr. Filter;
                                                              ESM=Filtro centro resp. compras;
                                                              FRC=Filtre centre gestion achat;
                                                              ENC=Purchase Resp. Ctr. Filter] }
    { 5900;   ;Service Resp. Ctr. Filter;Code10   ;TableRelation="Responsibility Center";
                                                   CaptionML=[ENU=Service Resp. Ctr. Filter;
                                                              ESM=Filtro centro resp. servicio;
                                                              FRC=Filtre centre gestion service;
                                                              ENC=Service Resp. Ctr. Filter] }
  }
  KEYS
  {
    {    ;User ID                                 ;Clustered=Yes }
    {    ;Salespers./Purch. Code                   }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text001@1000 : TextConst 'ENU=The %1 Salesperson/Purchaser code is already assigned to another User ID %2.;ESM=El c¢digo de Vendedor/Comprador %1 ya est  asignado a otro id. usuario %2.;FRC=Le code de vendeur/acheteur %1 est d‚j… attribu‚ … un autre code utilisateur %2.;ENC=The %1 Salesperson/Purchaser code is already assigned to another User ID %2.';
      Text003@1002 : TextConst 'ENU="You cannot have both a %1 and %2. ";ESM="No puede tener un %1 y una %2. ";FRC="Vous ne pouvez pas avoir … la fois un %1 et un %2. ";ENC="You cannot have both a %1 and %2. "';
      Text005@1004 : TextConst 'ENU=You cannot have approval limits less than zero.;ESM=No puede tener l¡mites de aprobaci¢n inferiores a cero.;FRC=Impossible d''avoir une limite d''approbation inf‚rieure … z‚ro.;ENC=You cannot have approval limits less than zero.';
      SalesPersonPurchaser@1011 : Record 13;
      PrivacyBlockedGenericErr@1013 : TextConst '@@@="%1 = salesperson / purchaser code.";ENU=Privacy Blocked must not be true for Salesperson / Purchaser %1.;ESM=La opci¢n Privacidad bloqueada no debe ser verdadera para el Vendedor/Comprador %1.;FRC=Le champ Confidentialit‚ bloqu‚e ne doit pas avoir la valeur true pour le repr‚sentant/l''acheteur %1.;ENC=Privacy Blocked must not be true for Salesperson / Purchaser %1.';
      UserSetupManagement@1001 : Codeunit 5700;

    [External]
    PROCEDURE CreateApprovalUserSetup@3(User@1000 : Record 2000000120);
    VAR
      UserSetup@1001 : Record 91;
      ApprovalUserSetup@1002 : Record 91;
    BEGIN
      ApprovalUserSetup.INIT;
      ApprovalUserSetup.VALIDATE("User ID",User."User Name");
      ApprovalUserSetup.VALIDATE("Sales Amount Approval Limit",GetDefaultSalesAmountApprovalLimit);
      ApprovalUserSetup.VALIDATE("Purchase Amount Approval Limit",GetDefaultPurchaseAmountApprovalLimit);
      ApprovalUserSetup.VALIDATE("E-Mail",User."Contact Email");
      UserSetup.SETRANGE("Sales Amount Approval Limit",UserSetup.GetDefaultSalesAmountApprovalLimit);
      IF UserSetup.FINDFIRST THEN
        ApprovalUserSetup.VALIDATE("Approver ID",UserSetup."Approver ID");
      IF ApprovalUserSetup.INSERT THEN;
    END;

    [External]
    PROCEDURE GetDefaultSalesAmountApprovalLimit@1() : Integer;
    VAR
      UserSetup@1001 : Record 91;
      DefaultApprovalLimit@1000 : Integer;
      LimitedApprovers@1002 : Integer;
    BEGIN
      UserSetup.SETRANGE("Unlimited Sales Approval",FALSE);

      IF UserSetup.FINDFIRST THEN BEGIN
        DefaultApprovalLimit := UserSetup."Sales Amount Approval Limit";
        LimitedApprovers := UserSetup.COUNT;
        UserSetup.SETRANGE("Sales Amount Approval Limit",DefaultApprovalLimit);
        IF LimitedApprovers = UserSetup.COUNT THEN
          EXIT(DefaultApprovalLimit);
      END;

      // Return 0 if no user setup exists or no default value is found
      EXIT(0);
    END;

    [External]
    PROCEDURE GetDefaultPurchaseAmountApprovalLimit@2() : Integer;
    VAR
      UserSetup@1002 : Record 91;
      DefaultApprovalLimit@1001 : Integer;
      LimitedApprovers@1000 : Integer;
    BEGIN
      UserSetup.SETRANGE("Unlimited Purchase Approval",FALSE);

      IF UserSetup.FINDFIRST THEN BEGIN
        DefaultApprovalLimit := UserSetup."Purchase Amount Approval Limit";
        LimitedApprovers := UserSetup.COUNT;
        UserSetup.SETRANGE("Purchase Amount Approval Limit",DefaultApprovalLimit);
        IF LimitedApprovers = UserSetup.COUNT THEN
          EXIT(DefaultApprovalLimit);
      END;

      // Return 0 if no user setup exists or no default value is found
      EXIT(0);
    END;

    [External]
    PROCEDURE HideExternalUsers@5();
    VAR
      PermissionManager@1001 : Codeunit 9002;
      OriginalFilterGroup@1000 : Integer;
    BEGIN
      IF NOT PermissionManager.SoftwareAsAService THEN
        EXIT;

      OriginalFilterGroup := FILTERGROUP;
      FILTERGROUP := 2;
      CALCFIELDS("License Type");
      SETFILTER("License Type",'<>%1',"License Type"::"External User");
      FILTERGROUP := OriginalFilterGroup;
    END;

    LOCAL PROCEDURE UpdateSalesPerson@4();
    VAR
      SalespersonPurchaser@1000 : Record 13;
    BEGIN
      IF ("E-Mail" <> '') AND SalespersonPurchaser.GET("Salespers./Purch. Code") THEN BEGIN
        SalespersonPurchaser."E-Mail" := COPYSTR("E-Mail",1,MAXSTRLEN(SalespersonPurchaser."E-Mail"));
        SalespersonPurchaser.MODIFY;
      END;
    END;

    LOCAL PROCEDURE ValidateSalesPersonPurchOnUserSetup@298(UserSetup2@1000 : Record 91);
    BEGIN
      IF UserSetup2."Salespers./Purch. Code" <> '' THEN
        IF SalesPersonPurchaser.GET(UserSetup2."Salespers./Purch. Code") THEN
          IF SalesPersonPurchaser.VerifySalesPersonPurchaserPrivacyBlocked(SalesPersonPurchaser) THEN
            ERROR(PrivacyBlockedGenericErr,UserSetup2."Salespers./Purch. Code")
    END;

    [External]
    PROCEDURE CheckAllowedPostingDates@6(NotificationType@1000 : 'Error,Notification');
    BEGIN
      UserSetupManagement.CheckAllowedPostingDatesRange(
        "Allow Posting From","Allow Posting To",NotificationType,DATABASE::"User Setup");
    END;

    BEGIN
    END.
  }
}

