OBJECT Table 204 Unit of Measure
{
  OBJECT-PROPERTIES
  {
    Date=11/27/18;
    Time=11:14:08 PM;
    Version List=;
  }
  PROPERTIES
  {
    DataCaptionFields=Code,Description;
    OnInsert=BEGIN
               SetLastDateTimeModified;
             END;

    OnModify=BEGIN
               SetLastDateTimeModified;
             END;

    OnDelete=BEGIN
               UnitOfMeasureTranslation.SETRANGE(Code,Code);
               UnitOfMeasureTranslation.DELETEALL;
             END;

    OnRename=BEGIN
               UpdateItemBaseUnitOfMeasure;
             END;

    CaptionML=ENU=Unit of Measure;
    LookupPageID=Page209;
    DrillDownPageID=Page209;
  }
  FIELDS
  {
    { 1   ;   ;Code                ;Code10        ;CaptionML=ENU=Code;
                                                   NotBlank=Yes }
    { 2   ;   ;Description         ;Text10        ;CaptionML=ENU=Description }
    { 3   ;   ;International Standard Code;Code10 ;CaptionML=ENU=International Standard Code }
    { 4   ;   ;Symbol              ;Text10        ;CaptionML=ENU=Symbol }
    { 5   ;   ;Last Modified Date Time;DateTime   ;CaptionML=ENU=Last Modified Date Time;
                                                   Editable=No }
    { 8000;   ;Id                  ;GUID          ;CaptionML=ENU=Id }
    { 27000;  ;SAT UofM Classification;Code10     ;TableRelation="SAT Unit of Measure";
                                                   CaptionML=ENU=SAT UofM Classification }
  }
  KEYS
  {
    {    ;Code                                    ;Clustered=Yes }
    {    ;Description                              }
  }
  FIELDGROUPS
  {
    { 1   ;Brick               ;Code,Description,International Standard Code }
  }
  CODE
  {
    VAR
      UnitOfMeasureTranslation@1000 : Record 5402;

    LOCAL PROCEDURE UpdateItemBaseUnitOfMeasure@1();
    VAR
      Item@1000 : Record 27;
    BEGIN
      Item.SETCURRENTKEY("Base Unit of Measure");
      Item.SETRANGE("Base Unit of Measure",xRec.Code);
      IF NOT Item.ISEMPTY THEN
        Item.MODIFYALL("Base Unit of Measure",Code,TRUE);
    END;

    PROCEDURE GetDescriptionInCurrentLanguage@2() : Text[10];
    VAR
      Language@1000 : Record 8;
      UnitOfMeasureTranslation@1001 : Record 5402;
    BEGIN
      IF UnitOfMeasureTranslation.GET(Code,Language.GetUserLanguage) THEN
        EXIT(UnitOfMeasureTranslation.Description);
      EXIT(Description);
    END;

    PROCEDURE CreateListInCurrentLanguage@3(VAR TempUnitOfMeasure@1000 : TEMPORARY Record 204);
    VAR
      UnitOfMeasure@1001 : Record 204;
    BEGIN
      IF UnitOfMeasure.FINDSET THEN
        REPEAT
          TempUnitOfMeasure := UnitOfMeasure;
          TempUnitOfMeasure.Description := UnitOfMeasure.GetDescriptionInCurrentLanguage;
          TempUnitOfMeasure.INSERT;
        UNTIL UnitOfMeasure.NEXT = 0;
    END;

    LOCAL PROCEDURE SetLastDateTimeModified@4();
    VAR
      DateFilterCalc@1000 : Codeunit 358;
    BEGIN
      "Last Modified Date Time" := DateFilterCalc.ConvertToUtcDateTime(CURRENTDATETIME);
    END;

    BEGIN
    END.
  }
}

