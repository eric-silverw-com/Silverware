OBJECT Report 5183 Resend Attachments
{
  OBJECT-PROPERTIES
  {
    Date=11/22/17;
    Time=12:00:00 PM;
    Version List=NAVW111.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Resend Attachments;
               ESM=Reenviar anexos;
               FRC=Renvoyer les piŠces jointes;
               ENC=Resend Attachments];
    ProcessingOnly=Yes;
    OnPreReport=VAR
                  LoggedSeg@1000 : Record 5075;
                BEGIN
                  IF "Interaction Log Entry".GETFILTER("Logged Segment Entry No.") = '' THEN
                    ERROR(Text000,"Interaction Log Entry".FIELDCAPTION("Logged Segment Entry No."));

                  IF "Interaction Log Entry".GETFILTER("Logged Segment Entry No.") <> '0' THEN BEGIN
                    LoggedSeg.SETFILTER("Entry No.","Interaction Log Entry".GETFILTER("Logged Segment Entry No."));
                    IF LoggedSeg.COUNT <> 1 THEN
                      ERROR(
                        Text001,LoggedSeg.TABLECAPTION);
                  END;
                END;

  }
  DATASET
  {
    { 3056;    ;DataItem;                    ;
               DataItemTable=Table5065;
               DataItemTableView=SORTING(Logged Segment Entry No.)
                                 WHERE(Postponed=CONST(No));
               OnAfterGetRecord=VAR
                                  Attachment@1000 : Record 5062;
                                  InteractionLogEntryNew@1001 : Record 5065;
                                  SegLine@1007 : Record 5077;
                                  InterLogEntryCommentLine@1008 : Record 5123;
                                  SegManagement@1006 : Codeunit 5051;
                                  NewEntryNo@1002 : Integer;
                                BEGIN
                                  IF NOT Attachment.GET("Attachment No.") THEN
                                    CurrReport.SKIP;
                                  IF Attachment."Storage Type" = Attachment."Storage Type"::"Exchange Storage" THEN
                                    CurrReport.SKIP;
                                  IF CorrespondenceType = CorrespondenceType::"Same as Entry" THEN
                                    TESTFIELD("Correspondence Type");

                                  IF UpdateMergeFields THEN BEGIN
                                    IF DeliveryBufferTemp.GET("Entry No.") THEN
                                      CurrReport.SKIP;
                                    InteractionLogEntryNew.TRANSFERFIELDS("Interaction Log Entry",FALSE);
                                    InteractionLogEntryNew.VALIDATE(Date,WORKDATE);
                                    InteractionLogEntryNew.VALIDATE("Time of Interaction",TIME);
                                    IF STRPOS(Description,Text003) <> 1 THEN BEGIN
                                      IF STRLEN(Description) + STRLEN(Text003) <= MAXSTRLEN(InteractionLogEntryNew.Description) THEN
                                        InteractionLogEntryNew.VALIDATE(Description,Text003 + Description)
                                      ELSE
                                        InteractionLogEntryNew.VALIDATE(Description,COPYSTR(
                                            Text003 + Description,1,MAXSTRLEN(InteractionLogEntryNew.Description)))
                                    END;
                                    IF CorrespondenceType <> CorrespondenceType::"Same as Entry" THEN
                                      InteractionLogEntryNew."Correspondence Type" := CorrespondenceType;
                                    SegLine.CopyFromInteractLogEntry(InteractionLogEntryNew);
                                    InterLogEntryCommentLine.SETRANGE("Entry No.","Entry No.");
                                    IF InterLogEntryCommentLine.FINDFIRST THEN;
                                    Attachment.CALCFIELDS("Attachment File");
                                    CLEAR(Attachment."Merge Source");
                                    NewEntryNo := SegManagement.LogInteraction(SegLine,Attachment,InterLogEntryCommentLine,FALSE,FALSE);
                                    InteractionLogEntryNew.GET(NewEntryNo);
                                    InteractionLogEntryNew.VALIDATE("Logged Segment Entry No.","Logged Segment Entry No.");
                                    InteractionLogEntryNew.VALIDATE("Delivery Status","Delivery Status"::"In Progress");
                                    InteractionLogEntryNew.VALIDATE("E-Mail Logged",FALSE);
                                    InteractionLogEntryNew.MODIFY(TRUE)
                                  END ELSE BEGIN
                                    IF CorrespondenceType <> CorrespondenceType::"Same as Entry" THEN
                                      "Correspondence Type" := CorrespondenceType;
                                    "Delivery Status" := "Delivery Status"::"In Progress";
                                    "E-Mail Logged" := FALSE;
                                    MODIFY
                                  END;

                                  DeliveryBufferTemp.INIT;
                                  IF UpdateMergeFields THEN BEGIN
                                    DeliveryBufferTemp."No." := NewEntryNo;
                                    DeliveryBufferTemp."Attachment No." := InteractionLogEntryNew."Attachment No.";
                                    DeliveryBufferTemp."Correspondence Type" := InteractionLogEntryNew."Correspondence Type"
                                  END ELSE BEGIN
                                    DeliveryBufferTemp."No." := "Entry No.";
                                    DeliveryBufferTemp."Attachment No." := "Attachment No.";
                                    DeliveryBufferTemp."Correspondence Type" := "Correspondence Type"
                                  END;
                                  DeliveryBufferTemp.Subject := Subject;
                                  DeliveryBufferTemp."Send Word Docs. as Attmt." := "Send Word Docs. as Attmt.";
                                  DeliveryBufferTemp."Language Code" := "Interaction Language Code";
                                  DeliveryBufferTemp.INSERT;
                                END;

               OnPostDataItem=BEGIN
                                IF DeliveryBufferTemp.COUNT = 0 THEN
                                  ERROR(Text002);

                                COMMIT;
                                AttachmentManagement.Send(DeliveryBufferTemp);
                              END;

               ReqFilterFields=Logged Segment Entry No.,Entry No.,Delivery Status,Correspondence Type,Contact No.,Campaign No. }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[ENU=Options;
                             ESM=Opciones;
                             FRC=Options;
                             ENC=Options] }

      { 3   ;2   ;Field     ;
                  CaptionML=[ENU=Correspondence Type;
                             ESM=Tipo correspondencia;
                             FRC=Type correspondance;
                             ENC=Correspondence Type];
                  ToolTipML=[ENU=Specifies a correspondence type to specify how you want the program to resend the attachment.;
                             ESM=Especifica un tipo de correspondencia para indicar la forma en que desea que el programa reenv¡e el archivo adjunto.;
                             FRC=Sp‚cifie un type de correspondance pour indiquer au programme le mode de renvoi de la piŠce jointe.;
                             ENC=Specifies a correspondence type to specify how you want the program to resend the attachment.];
                  OptionCaptionML=[ENU=Same as Entry,Hard Copy,Email,Fax;
                                   ESM=Igual que mov.,Impresi¢n,Correo electr¢nico,Fax;
                                   FRC=Identique … l'‚criture,Impression,Courriel,T‚l‚copieur;
                                   ENC=Same as Entry,Hard Copy,Email,Fax];
                  ApplicationArea=#RelationshipMgmt;
                  SourceExpr=CorrespondenceType }

      { 7   ;2   ;Field     ;
                  Name=Update Merge Fields;
                  CaptionML=[ENU=Update Merge Fields;
                             ESM=Actualizar campos combinaci¢n;
                             FRC=Mettre … jour les champs de fusion;
                             ENC=Update Merge Fields];
                  ToolTipML=[ENU=Specifies if you want to refresh the information in your Word document merge.;
                             ESM=Especifica si desea actualizar la informaci¢n en la combinaci¢n de documentos de Word.;
                             FRC=Indique si vous souhaitez actualiser les informations de votre fusion de documents Word.;
                             ENC=Specifies if you want to refresh the information in your Word document merge.];
                  ApplicationArea=#RelationshipMgmt;
                  SourceExpr=UpdateMergeFields }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=%1 must be specified.;ESM=Se debe indicar %1.;FRC=%1 doit ˆtre sp‚cifi‚.;ENC=%1 must be specified.';
      Text001@1001 : TextConst 'ENU=The interaction log entries must always be from the same %1.;ESM=Los movs. log interacci¢n debe ser siempre del mismo %1.;FRC=Les ‚critures du journal interaction doivent appartenir au mˆme %1.;ENC=The interaction log entries must always be from the same %1.';
      Text002@1002 : TextConst 'ENU=There is nothing to send.\\Only Microsoft Word documents can be resent.;ESM=No hay nada que enviar.\\Solo se pueden reenviar documentos de Microsoft Word.;FRC=Il n''y a rien … envoyer.\\Seuls les documents Microsoft Word peuvent ˆtre renvoy‚s.;ENC=There is nothing to send.\\Only Microsoft Word documents can be resent.';
      DeliveryBufferTemp@1003 : TEMPORARY Record 5074;
      AttachmentManagement@1004 : Codeunit 5052;
      CorrespondenceType@1005 : 'Same as Entry,Hard Copy,Email,Fax';
      UpdateMergeFields@1006 : Boolean;
      Text003@1007 : TextConst 'ENU=Resend:;ESM=Reenviar:;FRC=Renvoyer :;ENC=Resend:';

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

