OBJECT Page 2132 O365 Invoice Send Settings
{
  OBJECT-PROPERTIES
  {
    Date=11/27/18;
    Time=11:10:46 PM;
    Version List=;
  }
  PROPERTIES
  {
    Editable=No;
    CaptionML=ENU=Invoice Send Options;
    InsertAllowed=No;
    DeleteAllowed=No;
    ModifyAllowed=No;
    SourceTable=Table2132;
    PageType=List;
    SourceTableTemporary=Yes;
    RefreshOnActivate=Yes;
    OnOpenPage=BEGIN
                 InsertMenuItems;
               END;

    OnAfterGetCurrRecord=VAR
                           GraphMail@1000 : Codeunit 405;
                           O365SetupEmail@1001 : Codeunit 2135;
                         BEGIN
                           IF Title = EmailAccountTitleTxt THEN BEGIN
                             IF (O365SetupEmail.SMTPEmailIsSetUp AND (NOT GraphMail.IsEnabled)) OR (NOT GraphMail.HasConfiguration) THEN
                               "Page ID" := PAGE::"O365 Email Account Settings"
                             ELSE
                               "Page ID" := PAGE::"Graph Mail Setup";

                             Description := GetEmailAccountDescription;
                           END;
                         END;

    ActionList=ACTIONS
    {
      { 4       ;    ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 6       ;1   ;Action    ;
                      Name=Open;
                      ShortCutKey=Return;
                      CaptionML=ENU=Open;
                      ToolTipML=ENU=Open the card for the selected record.;
                      ApplicationArea=#Basic,#Suite,#Invoicing;
                      Image=DocumentEdit;
                      Scope=Repeater;
                      OnAction=BEGIN
                                 OpenPage;
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1   ;    ;Container ;
                ContainerType=ContentArea }

    { 2   ;1   ;Group     ;
                GroupType=Repeater }

    { 3   ;2   ;Field     ;
                ApplicationArea=#Basic,#Suite,#Invoicing;
                SourceExpr=Title }

    { 5   ;2   ;Field     ;
                ToolTipML=ENU=Specifies a description of the invoice send setting.;
                ApplicationArea=#Basic,#Suite,#Invoicing;
                SourceExpr=Description }

  }
  CODE
  {
    VAR
      EmailAccountTitleTxt@1000 : TextConst 'ENU=Email account';
      EmailAccountDescriptionTxt@1001 : TextConst 'ENU=Set up your email account.';
      CCAndBCCTitleTxt@1002 : TextConst 'ENU=CC and BCC';
      CCAndBCCDescriptionTxt@1003 : TextConst 'ENU=Add CC and BCC recipients on all new invoices.';
      InvoiceEmailMessageTxt@1004 : TextConst 'ENU=Default message for invoices';
      InvoiceEmailMessageDescriptionTxt@1005 : TextConst 'ENU=Change your default email message for invoices';
      QuoteEmailMessageTxt@1006 : TextConst 'ENU=Default message for estimates';
      QuoteEmailMessageDescriptionTxt@1007 : TextConst 'ENU=Change your default email message for estimates';

    LOCAL PROCEDURE InsertMenuItems@1();
    VAR
      O365SetupEmail@1001 : Codeunit 2135;
      GraphMail@1000 : Codeunit 405;
    BEGIN
      IF (O365SetupEmail.SMTPEmailIsSetUp AND (NOT GraphMail.IsEnabled)) OR (NOT GraphMail.HasConfiguration) THEN
        InsertPageMenuItem(PAGE::"O365 Email Account Settings",EmailAccountTitleTxt,GetEmailAccountDescription)
      ELSE
        InsertPageMenuItem(PAGE::"Graph Mail Setup",EmailAccountTitleTxt,GetEmailAccountDescription);

      InsertPageMenuItem(PAGE::"O365 Email CC and BCC Settings",CCAndBCCTitleTxt,CCAndBCCDescriptionTxt);
      InsertPageMenuItem(PAGE::"O365 Default Invoice Email Msg",InvoiceEmailMessageTxt,InvoiceEmailMessageDescriptionTxt);
      InsertPageMenuItem(PAGE::"O365 Default Quote Email Msg",QuoteEmailMessageTxt,QuoteEmailMessageDescriptionTxt);
    END;

    LOCAL PROCEDURE GetEmailAccountDescription@2() : Text[80];
    VAR
      GraphMailSetup@1002 : Record 407;
      SMTPMailSetup@1001 : Record 409;
      O365SetupEmail@1000 : Codeunit 2135;
      GraphMail@1003 : Codeunit 405;
    BEGIN
      IF GraphMail.IsEnabled AND GraphMail.HasConfiguration THEN
        IF GraphMailSetup.GET THEN
          IF GraphMailSetup."Sender Email" <> '' THEN
            EXIT(COPYSTR(GraphMailSetup."Sender Email",1,MAXSTRLEN(Description)));

      IF O365SetupEmail.SMTPEmailIsSetUp THEN
        IF SMTPMailSetup.GetSetup THEN
          IF SMTPMailSetup."User ID" <> '' THEN
            EXIT(COPYSTR(SMTPMailSetup."User ID",1,MAXSTRLEN(Description)));

      EXIT(EmailAccountDescriptionTxt);
    END;

    BEGIN
    END.
  }
}

