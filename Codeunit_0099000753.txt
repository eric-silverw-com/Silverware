OBJECT Codeunit 99000753 Routing Line-Copy Lines
{
  OBJECT-PROPERTIES
  {
    Date=11/27/18;
    Time=11:09:33 PM;
    Version List=;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=The %1 cannot be copied to itself.';
      Text001@1001 : TextConst 'ENU=%1 on %2 %3 must not be %4';
      Text002@1002 : TextConst 'ENU=%1 on %2 %3 %4 must not be %5';

    [External]
    PROCEDURE CopyRouting@1(FromRoutingHeaderNo@1000 : Code[20];FromVersionCode@1001 : Code[20];VAR RoutingHeader@1002 : Record 99000763;ToVersionCode@1003 : Code[20]);
    VAR
      RoutingVersion@1004 : Record 99000786;
      RoutingLine@1005 : Record 99000764;
      RoutingLine2@1006 : Record 99000764;
      RoutingTool@1007 : Record 99000802;
      FromRoutingTool@1008 : Record 99000802;
      RoutingPersonnel@1009 : Record 99000803;
      FromRoutingPersonnel@1010 : Record 99000803;
      RoutingQualityMeasure@1011 : Record 99000805;
      FromRoutingQualityMeasure@1012 : Record 99000805;
      RoutingCommentLine@1013 : Record 99000775;
      FromRoutingCommentLine@1014 : Record 99000775;
    BEGIN
      IF (FromRoutingHeaderNo = RoutingHeader."No.") AND (FromVersionCode = ToVersionCode) THEN
        ERROR(Text000,RoutingHeader.TABLECAPTION);

      IF ToVersionCode = '' THEN BEGIN
        IF RoutingHeader.Status = RoutingHeader.Status::Certified THEN
          ERROR(
            Text001,
            RoutingHeader.FIELDCAPTION(Status),
            RoutingHeader.TABLECAPTION,
            RoutingHeader."No.",
            RoutingHeader.Status);
      END ELSE BEGIN
        RoutingVersion.GET(RoutingHeader."No.",ToVersionCode);
        IF RoutingVersion.Status = RoutingVersion.Status::Certified THEN
          ERROR(
            Text002,
            RoutingVersion.FIELDCAPTION(Status),
            RoutingVersion.TABLECAPTION,
            RoutingVersion."Routing No.",
            RoutingVersion."Version Code",
            RoutingVersion.Status);
      END;

      RoutingLine.SETRANGE("Routing No.",RoutingHeader."No.");
      RoutingLine.SETRANGE("Version Code",ToVersionCode);
      RoutingLine.DELETEALL(TRUE);

      OnAfterDeleteRouting(RoutingHeader,ToVersionCode);

      RoutingLine2.SETRANGE("Routing No.",FromRoutingHeaderNo);
      RoutingLine2.SETRANGE("Version Code",FromVersionCode);
      IF RoutingLine2.FIND('-') THEN
        REPEAT
          RoutingLine := RoutingLine2;
          RoutingLine."Routing No." := RoutingHeader."No.";
          RoutingLine."Version Code" := ToVersionCode;
          RoutingLine.INSERT;
          OnCopyRountingOnAfterRoutingLineInsert(RoutingLine,RoutingLine2);
        UNTIL RoutingLine2.NEXT = 0;

      FromRoutingTool.SETRANGE("Routing No.",FromRoutingHeaderNo);
      FromRoutingTool.SETRANGE("Version Code",FromVersionCode);
      IF FromRoutingTool.FIND('-') THEN
        REPEAT
          RoutingTool := FromRoutingTool;
          RoutingTool."Routing No." := RoutingHeader."No.";
          RoutingTool."Version Code" := ToVersionCode;
          RoutingTool.INSERT;
        UNTIL FromRoutingTool.NEXT = 0;

      FromRoutingPersonnel.SETRANGE("Routing No.",FromRoutingHeaderNo);
      FromRoutingPersonnel.SETRANGE("Version Code",FromVersionCode);
      IF FromRoutingPersonnel.FIND('-') THEN
        REPEAT
          RoutingPersonnel := FromRoutingPersonnel;
          RoutingPersonnel."Routing No." := RoutingHeader."No.";
          RoutingPersonnel."Version Code" := ToVersionCode;
          RoutingPersonnel.INSERT;
        UNTIL FromRoutingPersonnel.NEXT = 0;

      FromRoutingQualityMeasure.SETRANGE("Routing No.",FromRoutingHeaderNo);
      FromRoutingQualityMeasure.SETRANGE("Version Code",FromVersionCode);
      IF FromRoutingQualityMeasure.FIND('-') THEN
        REPEAT
          RoutingQualityMeasure := FromRoutingQualityMeasure;
          RoutingQualityMeasure."Routing No." := RoutingHeader."No.";
          RoutingQualityMeasure."Version Code" := ToVersionCode;
          RoutingQualityMeasure.INSERT;
        UNTIL FromRoutingQualityMeasure.NEXT = 0;

      FromRoutingCommentLine.SETRANGE("Routing No.",FromRoutingHeaderNo);
      FromRoutingCommentLine.SETRANGE("Version Code",FromVersionCode);
      IF FromRoutingCommentLine.FIND('-') THEN
        REPEAT
          RoutingCommentLine := FromRoutingCommentLine;
          RoutingCommentLine."Routing No." := RoutingHeader."No.";
          RoutingCommentLine."Version Code" := ToVersionCode;
          RoutingCommentLine.INSERT;
        UNTIL FromRoutingCommentLine.NEXT = 0;

      OnAfterCopyRouting(RoutingHeader,FromRoutingHeaderNo,FromVersionCode,ToVersionCode);
    END;

    [External]
    PROCEDURE SelectCopyFromVersionList@2(VAR FromRoutingVersion@1000 : Record 99000786);
    VAR
      RoutingHeader@1001 : Record 99000763;
      OldRoutingVersion@1002 : Record 99000786;
    BEGIN
      OldRoutingVersion := FromRoutingVersion;

      RoutingHeader."No." := FromRoutingVersion."Routing No.";
      IF PAGE.RUNMODAL(0,FromRoutingVersion) = ACTION::LookupOK THEN BEGIN
        IF OldRoutingVersion.Status = OldRoutingVersion.Status::Certified THEN
          ERROR(
            Text002,
            OldRoutingVersion.FIELDCAPTION(Status),
            OldRoutingVersion.TABLECAPTION,
            OldRoutingVersion."Routing No.",
            OldRoutingVersion."Version Code",
            OldRoutingVersion.Status);
        CopyRouting(RoutingHeader."No.",FromRoutingVersion."Version Code",RoutingHeader,OldRoutingVersion."Version Code");
      END;
      FromRoutingVersion := OldRoutingVersion;
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterCopyRouting@9(VAR RoutingHeader@1000 : Record 99000763;FromRoutingHeaderNo@1001 : Code[20];FromVersionCode@1002 : Code[20];ToVersionCode@1003 : Code[20]);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterDeleteRouting@3(VAR RoutingHeader@1000 : Record 99000763;ToVersionCode@1001 : Code[20]);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnCopyRountingOnAfterRoutingLineInsert@4(VAR RoutingLineTo@1000 : Record 99000764;VAR RoutingLineFrom@1001 : Record 99000764);
    BEGIN
    END;

    BEGIN
    END.
  }
}

