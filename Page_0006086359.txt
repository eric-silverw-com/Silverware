OBJECT Page 6086359 Expense Mgt. Setup Wizard
{
  OBJECT-PROPERTIES
  {
    Date=11/29/17;
    Time=[ 1:00:00 AM];
    Version List=EMW17.00.00.2.60.03;
  }
  PROPERTIES
  {
    Permissions=TableData 2000000072=rm;
    CaptionML=ENU=Expense Management Setup Wizard;
    InsertAllowed=No;
    DeleteAllowed=No;
    PageType=NavigatePage;
    OnInit=VAR
             ExpSetup@1000 : Record 6086300;
           BEGIN
             IF NOT ExpSetup.GET THEN
               NewCompany := TRUE
             ELSE BEGIN
               WizardAction := WizardAction::Setup;
               NewCompany := ExpSetup."Archive Path" = '';
             END;

             ActivateCompany := NewCompany;

             SetupBankAgreement := TRUE;
             TestConnection := TRUE;
           END;

    OnOpenPage=BEGIN
                 ShowNextStep(0);
               END;

    ActionList=ACTIONS
    {
      { 1160040012;  ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 1160040011;1 ;Action    ;
                      Name=Back;
                      CaptionML=ENU=&Back;
                      Promoted=Yes;
                      Enabled=BackEnabled;
                      InFooterBar=Yes;
                      PromotedIsBig=Yes;
                      Image=PreviousRecord;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 ShowNextStep(-1);
                               END;
                                }
      { 1160040010;1 ;Action    ;
                      Name=Next;
                      CaptionML=ENU=&Next;
                      Promoted=Yes;
                      Enabled=NextEnabled;
                      InFooterBar=Yes;
                      PromotedIsBig=Yes;
                      Image=NextRecord;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 ShowNextStep(1);
                               END;
                                }
      { 1160040009;1 ;Action    ;
                      Name=Finish;
                      CaptionML=ENU=&Finish;
                      Promoted=Yes;
                      Enabled=FinishEnabled;
                      InFooterBar=Yes;
                      PromotedIsBig=Yes;
                      Image=Approve;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 CurrPage.CLOSE;
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1160040003;0;Container;
                ContainerType=ContentArea }

    { 1160040000;1;Group  ;
                Name=WizardStart;
                Visible=WizardStartVisible;
                GroupType=Group;
                InstructionalTextML=ENU=This wizard helps you to import or export Expense Management configurations and to set it up. }

    { 6085573;2;Field     ;
                CaptionML=ENU=Action;
                OptionCaptionML=ENU=Import Configuration,Export Configuration,Setup Company;
                SourceExpr=WizardAction;
                OnValidate=BEGIN
                             EnableWizardStartFields;
                           END;
                            }

    { 1160040002;2;Field  ;
                AssistEdit=Yes;
                CaptionML=ENU=File Name;
                SourceExpr=ClientFilename;
                Enabled=FilenameEnabled;
                OnAssistEdit=BEGIN
                               IF WizardAction = WizardAction::Import THEN
                                 ClientFilename := FileMgt.OpenFileDialog(CurrPage.CAPTION,ClientFilename,'')
                               ELSE
                                 ClientFilename := FileMgt.SaveFileDialog(CurrPage.CAPTION,ClientFilename,'');

                               IF STRPOS(ClientFilename,'.xml') = 0 THEN
                                 ClientFilename := ClientFilename + '.xml';
                             END;
                              }

    { 1160040004;1;Group  ;
                Name=ImportExportSelection;
                CaptionML=ENU=Configurations;
                Visible=ImportExportSelectionVisible;
                GroupType=Group;
                InstructionalTextML=ENU=You can exclude a table by unticking 'Include'. You can expand to view records for each table. }

    { 1160040007;2;Part   ;
                Name=ConfigLines;
                PagePartID=Page6086360;
                Visible=ConfigLinesVisible;
                PartType=Page }

    { 1160040005;1;Group  ;
                Name=ImportExportCompleted;
                CaptionML=ENU=Import/Export Completed;
                Visible=ImportExportCompletedVisible;
                GroupType=Group;
                InstructionalTextML=ENU=Expense Management configurations have been imported/exported. }

    { 1   ;2   ;Field     ;
                Name=TEMP1;
                CaptionClass='3,' }

    { 6085579;1;Group     ;
                Name=WizardSetupCompanyStory;
                CaptionML=ENU=Setup Expense Management;
                Visible=WizardSetupCompanyStoryVisible;
                GroupType=Group;
                InstructionalTextML=ENU=This wizard will help you set up some of the key Expense Management configurations. Settings will be saved after each step, so at any point you can click 'Finish' to end. }

    { 6085578;2;Field     ;
                Name=TEMP3;
                CaptionClass='3,' }

    { 6085577;1;Group     ;
                Name=SpecifyClientCredentials;
                Visible=SpecifyClientCredentialsVisible;
                GroupType=Group;
                InstructionalTextML=ENU=In order to activate and use Expense Management you will need to specify your Continia client credentials. These should have been received by your reseller. }

    { 6085576;2;Field     ;
                CaptionML=ENU=Client ID;
                SourceExpr=ClientID;
                OnValidate=VAR
                             ContiniaClientCredentials@1000 : Record 6192771;
                           BEGIN
                             ClientPassword := '';
                             IF ClientID <> '' THEN BEGIN
                               IF ContiniaClientCredentials.GET(ClientID) THEN
                                 IF ContiniaClientCredentials."Client Password" <> '' THEN
                                   ClientPassword := '****************'
                             END;
                           END;

                OnLookup=VAR
                           ContiniaClientCredentials@1000 : Record 6192771;
                         BEGIN
                           IF PAGE.RUNMODAL(PAGE::"Continia Client Credentials",ContiniaClientCredentials) = ACTION::LookupOK THEN BEGIN
                             ClientID := ContiniaClientCredentials."Client ID";
                             IF ContiniaClientCredentials."Client Password" <> '' THEN
                               ClientPassword := '****************'
                             ELSE
                               ClientPassword := '';
                           END;
                         END;
                          }

    { 6085580;2;Field     ;
                CaptionML=ENU=Client Password;
                SourceExpr=ClientPassword }

    { 6085581;2;Field     ;
                CaptionML=ENU=Test Connection;
                SourceExpr=TestConnection }

    { 18  ;1   ;Group     ;
                Name=SpecifyWebApproval;
                CaptionML=ENU=Web Approval;
                Visible=SpecifyWebApprovalVisible;
                GroupType=Group;
                InstructionalTextML=ENU=Please specify required settings to use Web Approval. You must specify the NAV Web Service URL for the credential types you would like to enable. }

    { 15  ;2   ;Field     ;
                CaptionML=ENU=Enable Web Approval;
                SourceExpr=EnableWebApproval;
                OnValidate=VAR
                             ContiniaCompanySetup@6086302 : Record 6192773;
                             ContiniaWebPortal@6086301 : Record 6192774;
                             Environment@6086300 : DotNet "'mscorlib, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Environment";
                           BEGIN
                             IF EnableWebApproval THEN BEGIN
                               IF WebPortalCode = '' THEN BEGIN
                                 IF ContiniaCompanySetup.GET THEN
                                   WebPortalCode := ContiniaCompanySetup."Web Portal Code";

                                 IF WebPortalCode = '' THEN
                                   IF ContiniaWebPortal.FINDFIRST THEN
                                     WebPortalCode := ContiniaWebPortal.Code
                                   ELSE
                                     WebPortalCode := 'WEB';

                                 OnValidateWebPortalCode(WebPortalCode);
                               END;

                               IF WindowsWSUrl = '' THEN
                                 WindowsWSUrl := 'http://my-nav-server:7047/NAV/WS';
                               IF DatabaseWSUrl = '' THEN
                                 DatabaseWSUrl := 'http://my-nav-server:7047/NAV/WS';

                               IF LocalDomainName = '' THEN
                                 IF OnlineMgt.IsDev OR OnlineMgt.IsDemo THEN
                                   LocalDomainName := Environment.MachineName
                                 ELSE
                                   LocalDomainName := Environment.UserDomainName;

                               IF WebSiteUrl = '' THEN
                                 CASE TRUE OF
                                   OnlineMgt.IsDev:
                                     WebSiteUrl := 'https://dev.continiaonline.com';
                                   OnlineMgt.IsDemo:
                                     WebSiteUrl := 'https://demo.continiaonline.com'
                                   ELSE
                                     WebSiteUrl := 'https://www.continiaonline.com';
                                 END;
                             END;

                             CurrPage.UPDATE(FALSE);
                           END;
                            }

    { 16  ;2   ;Group     ;
                Enabled=WebPortalCode <> '';
                GroupType=Group }

    { 17  ;3   ;Field     ;
                CaptionML=ENU=Web Portal;
                SourceExpr=WebPortalCode;
                Enabled=EnableWebApproval;
                OnValidate=BEGIN
                             OnValidateWebPortalCode(WebPortalCode);
                           END;

                OnLookup=VAR
                           ContiniaWebPortal@6085573 : Record 6192774;
                         BEGIN
                           IF PAGE.RUNMODAL(PAGE::"Continia Web Portal List",ContiniaWebPortal) = ACTION::LookupOK THEN BEGIN
                             Text := ContiniaWebPortal.Code;
                             EXIT(TRUE);
                           END;
                         END;
                          }

    { 14  ;3   ;Field     ;
                CaptionML=ENU=Use Continia Online Web Portal;
                SourceExpr=UseContiniaOnlineWeb;
                Enabled=EnableWebApproval }

    { 13  ;3   ;Field     ;
                CaptionML=ENU=Local Domain Name;
                SourceExpr=LocalDomainName;
                Enabled=EnableWebApproval }

    { 12  ;3   ;Field     ;
                CaptionML=ENU=Windows Web Service URL;
                SourceExpr=WindowsWSUrl;
                Enabled=EnableWebApproval }

    { 11  ;3   ;Field     ;
                CaptionML=ENU=Database Web Service URL;
                SourceExpr=DatabaseWSUrl;
                Enabled=EnableWebApproval }

    { 10  ;3   ;Field     ;
                CaptionML=ENU=Web Site URL;
                SourceExpr=WebSiteUrl;
                Enabled=EnableWebApproval }

    { 6085585;1;Group     ;
                Name=SpecifyGeneralEMSetupInfo;
                CaptionML=ENU=General Expense Management Settings;
                Visible=SpecifyGeneralEMSetupInfoVisible;
                GroupType=Group;
                InstructionalTextML=ENU=Please specify general Expense Management settings below. }

    { 6085584;2;Field     ;
                CaptionML=ENU=Company Code;
                CharAllowed=0123456789ABCDEFGHIJKLMNOPQRSTUVWXYZ+-;
                SourceExpr=CompanyCode }

    { 6086300;2;Field     ;
                CaptionML=ENU=Document Storage Type;
                OptionCaptionML=ENU=File System,Database;
                SourceExpr=DocStorageType;
                OnValidate=VAR
                             EMSetupLocal@6086300 : Record 6086300;
                           BEGIN
                             ArchiveFilePathEnabled := DocStorageType = EMSetupLocal."Document Storage Type"::"File System";
                           END;
                            }

    { 6085583;2;Field     ;
                Name=ArchiveFilePath;
                CaptionML=ENU=Archive File Path;
                SourceExpr=ArchiveFilePath;
                Enabled=ArchiveFilePathEnabled }

    { 6086301;2;Field     ;
                CaptionML=ENU=Default Web/App Language;
                SourceExpr=DefaultWebLanguage;
                TableRelation=Language;
                OnValidate=VAR
                             ContiniaClientCredentials@6086300 : Record 6192771;
                           BEGIN
                           END;
                            }

    { 6085582;2;Field     ;
                CaptionML=ENU=Activate Company;
                SourceExpr=ActivateCompany }

    { 6085587;2;Field     ;
                CaptionML=ENU=Setup Bank Agreement;
                SourceExpr=SetupBankAgreement }

    { 6   ;2   ;Field     ;
                Name=<CreateEMProfile>;
                CaptionML=ENU=Create Expense Management Role Center;
                SourceExpr=CreateEMProfile }

    { 5   ;2   ;Field     ;
                CaptionML=ENU=Set as Default Role Center;
                SourceExpr=SetEMDefaultProfile }

    { 6085590;1;Group     ;
                Name=SpecifyBankAgreement;
                CaptionML=ENU=Bank Agreement;
                Visible=SpecifyBankAgreementVisible;
                GroupType=Group;
                InstructionalTextML=ENU=Please chose a bank agreement for Expense Management }

    { 2   ;2   ;Field     ;
                CaptionML=ENU=Bank Code;
                SourceExpr=BankCode;
                OnValidate=BEGIN
                             IF BankCode = '' THEN BEGIN
                               BankAgreementID := '';
                               BankName := '';
                             END;
                           END;

                OnLookup=VAR
                           CEMBank@1000 : Record 6086314;
                         BEGIN
                           IF CEMBank.GET(BankCode,BankCountryCode) THEN;

                           IF PAGE.RUNMODAL(0,CEMBank) = ACTION::LookupOK THEN BEGIN
                             BankCode := CEMBank.Code;
                             BankCountryCode := CEMBank."Country/Region Code";
                             BankName := CEMBank.Name;
                             BankAgreementID := '';
                           END;
                         END;
                          }

    { 3   ;2   ;Field     ;
                CaptionML=ENU=Bank Name;
                SourceExpr=BankName;
                Editable=FALSE }

    { 4   ;2   ;Field     ;
                CaptionML=ENU=Bank Agreement ID;
                SourceExpr=BankAgreementID }

    { 7   ;1   ;Group     ;
                Name=SpecifyContiniaUserSetup;
                CaptionML=ENU=Continia User Setup;
                Visible=SpecifyContiniaUsersVisible;
                GroupType=Group;
                InstructionalTextML=ENU=You need to configure Expense Accounts and Continia Users to start working with Expense Management. }

    { 9   ;2   ;Field     ;
                CaptionML=ENU=Setup demo data;
                ToolTipML=ENU=This is recommende only for testing in a CRONUS database.;
                SourceExpr=SetupDemoData }

    { 8   ;2   ;Field     ;
                CaptionML=ENU=Open screen to configure Continia Users;
                SourceExpr=SetContiniaUsers }

    { 6085596;1;Group     ;
                Name=WizardCompleted;
                CaptionML=ENU=Wizard is complete;
                Visible=WizardCompletedVisible;
                GroupType=Group;
                InstructionalTextML=ENU=This wizard has now completed. We hope you enjoy working with Expense Management. }

    { 6085595;2;Field     ;
                Name=TEMP4;
                CaptionClass='3,' }

  }
  CODE
  {
    VAR
      Text001@1005 : TextConst 'ENU=You must specify a filename.';
      Text002@1004 : TextConst 'ENU=You must specify a Company Code.';
      FileMgt@6085573 : Codeunit 419;
      OnlineMgt@6085602 : Codeunit 6192772;
      WizardAction@6085574 : 'Import,Export,Setup';
      BackEnabled@1160040000 : Boolean INDATASET;
      WizardStartVisible@1013 : Boolean INDATASET;
      ConfigLinesVisible@1160040005 : Boolean INDATASET;
      ImportExportSelectionVisible@6085576 : Boolean INDATASET;
      ImportExportCompletedVisible@1160040004 : Boolean INDATASET;
      WizardSetupCompanyStoryVisible@6085593 : Boolean INDATASET;
      SpecifyClientCredentialsVisible@6085594 : Boolean INDATASET;
      SpecifyWebApprovalVisible@1018 : Boolean INDATASET;
      SpecifyGeneralEMSetupInfoVisible@6085595 : Boolean INDATASET;
      SpecifyBankAgreementVisible@6085596 : Boolean INDATASET;
      SpecifyContiniaUsersVisible@1016 : Boolean INDATASET;
      WizardCompletedVisible@6085597 : Boolean INDATASET;
      FinishEnabled@1160040006 : Boolean INDATASET;
      NextEnabled@1160040007 : Boolean INDATASET;
      FilenameEnabled@6085598 : Boolean INDATASET;
      Step@1160040002 : Integer;
      Steps@6085577 : 'WizardStart,ImportExportSelection,ImportExportCompleted,WizardSetupCompanyStory,SpecifyClientCredentials,SpecifyWebApproval,SpecifyGeneralEMSetupInfo,SpecifyBankAgreement,SpecifyContiniaUserSetup,WizardCompleted';
      ClientFilename@1160040010 : Text[250];
      ServerFilename@1001 : Text[250];
      TEMP@1000 : Text INDATASET;
      ClientID@6085591 : Code[20];
      CompanyCode@6085590 : Code[10];
      WebPortalCode@1019 : Code[20];
      EnableWebApproval@1021 : Boolean;
      BankCode@1011 : Code[10];
      BankAgreementID@1010 : Text[30];
      BankName@1009 : Text[30];
      BankCountryCode@1008 : Code[10];
      xBankCode@1007 : Code[10];
      xBankAgreementID@1006 : Text[30];
      ArchiveFilePath@6085589 : Text[250];
      ClientPassword@6085588 : Text[50];
      TestConnection@6085587 : Boolean;
      ActivateCompany@6085586 : Boolean;
      NewCompany@1003 : Boolean;
      UseContiniaOnlineWeb@6085584 : Boolean;
      LocalDomainName@6085583 : Code[50];
      WindowsWSUrl@6085582 : Text[250];
      DatabaseWSUrl@6085581 : Text[250];
      WebSiteUrl@6085580 : Text[80];
      UseCloudOCR@6085579 : Boolean;
      SetupBankAgreement@6085578 : Boolean;
      CreateEMProfile@1002 : Boolean;
      SetEMDefaultProfile@1012 : Boolean;
      NewCompanyImport@1014 : TextConst 'ENU=Setting a new company requires importing the setup file.';
      SetContiniaUsers@1015 : Boolean;
      SetupDemoData@1017 : Boolean INDATASET;
      Text004@6086300 : TextConst 'ENU=You must specify a Web Portal Code.';
      ArchiveFilePathEnabled@6086302 : Boolean INDATASET;
      DocStorageType@6086301 : 'File System,Database';
      DefaultWebLanguage@6086303 : Code[10];

    LOCAL PROCEDURE ShowNextStep@6085573(NextStep@1160040000 : Integer);
    VAR
      ContiniaCompanySetup@6086300 : Record 6192773;
      ContiniaClientCredentials@1006 : Record 6192771;
      ContiniaWebPortal@6086301 : Record 6192774;
      ExpSetup@1005 : Record 6086300;
      CEMBank@1004 : Record 6086314;
      BankAgr@1003 : Record 6086315;
      Profile@1001 : Record 2000000072;
      DCSetup@1009 : Record 6085573;
      Object@6086302 : Record 2000000001;
      ContiniaUserSetupList@1007 : Page 6086051;
      CreateDemoData@1008 : Codeunit 6086357;
      CEMOnline@1000 : Codeunit 6086305;
      GotClientCredentials@6086303 : Boolean;
    BEGIN
      IF NewCompany AND (WizardAction = WizardAction::Setup) AND (NextStep = 1) THEN
        ERROR(NewCompanyImport);

      IF (WizardAction = WizardAction::Setup) AND (Step = 0) AND (NextStep <> 0) THEN
        Step := 2;

      CASE (Step + NextStep) OF
        0:
          BEGIN
            CurrPage.ConfigLines.PAGE.ClearTempTable;

            EnableWizardStartFields;
            ShowStep(Steps::WizardStart,FALSE,TRUE,FALSE);
          END;

        1:
          BEGIN
            IF ClientFilename = '' THEN
              ERROR(Text001);

            IF WizardAction = WizardAction::Import THEN BEGIN
              ServerFilename := FileMgt.UploadFileSilent(ClientFilename);
              CurrPage.ConfigLines.PAGE.SetParam(0,ServerFilename,NewCompany);
            END ELSE BEGIN
              ServerFilename := FileMgt.ServerTempFileName('xml');
              CurrPage.ConfigLines.PAGE.SetParam(1,ServerFilename,NewCompany);
            END;

            CurrPage.ConfigLines.PAGE.CreateTempTable;

            ShowStep(Steps::ImportExportSelection,TRUE,TRUE,FALSE);
          END;

        2:
          BEGIN
            CurrPage.ConfigLines.PAGE.PerformAction;

            IF WizardAction = WizardAction::Export THEN BEGIN
              FileMgt.DownloadToFile(ServerFilename,ClientFilename);
              ShowStep(Steps::ImportExportCompleted,FALSE,FALSE,TRUE);
            END ELSE
              ShowStep(Steps::ImportExportCompleted,FALSE,TRUE,FALSE);
          END;

        3:
          ShowStep(Steps::WizardSetupCompanyStory,FALSE,TRUE,TRUE);

        4:
          BEGIN
            GotClientCredentials := FALSE;
            IF ContiniaCompanySetup.GET THEN
              GotClientCredentials := ContiniaClientCredentials.GET(ContiniaCompanySetup."Client ID");
            IF NOT GotClientCredentials THEN
              IF ContiniaClientCredentials.COUNT = 1 THEN
                GotClientCredentials := ContiniaClientCredentials.FINDFIRST;

            IF GotClientCredentials THEN BEGIN
              ClientID := ContiniaClientCredentials."Client ID";
              IF ContiniaClientCredentials."Client Password" <> '' THEN
                ClientPassword := '****************'
              ELSE
                ClientPassword := '';
            END;

            ShowStep(Steps::SpecifyClientCredentials,TRUE,TRUE,TRUE);
          END;

        5:
          BEGIN
            IF NextStep = 1 THEN BEGIN
              // PROCESS THE CONTINIA ONLINE SETTINGS AND TEST THE CONNECTION IF REQUESTED
              IF NOT ContiniaClientCredentials.GET(ClientID) THEN BEGIN
                ContiniaClientCredentials.INIT;
                ContiniaClientCredentials.VALIDATE("Client ID",ClientID);
                ContiniaClientCredentials.INSERT(TRUE);
              END;

              IF ClientPassword <> '****************' THEN
                ContiniaClientCredentials."Client Password" := ClientPassword;
              ContiniaClientCredentials.MODIFY;

              IF NOT ContiniaCompanySetup.GET THEN BEGIN
                ContiniaCompanySetup.INIT;
                ContiniaCompanySetup.VALIDATE("Client ID",ClientID);
                ContiniaCompanySetup.INSERT(TRUE);
              END;

              IF ContiniaCompanySetup."Client ID" <> ClientID THEN BEGIN
                ContiniaCompanySetup.VALIDATE("Client ID",ClientID);
                ContiniaCompanySetup.MODIFY(TRUE);
              END;

              IF Object.GET(Object.Type::Table,'',2000000069) THEN
                CODEUNIT.RUN(CODEUNIT::"Capture RTC Library");

              IF TestConnection THEN
                OnlineMgt.TestConnection;

              CreateEMProfile := OnlineMgt.IsDev OR OnlineMgt.IsDemo;
              SetEMDefaultProfile := OnlineMgt.IsDev OR OnlineMgt.IsDemo;
            END;

            // SETUP THE DEFAULT VALUES FOR EM SETUP AND SHOW THE STEP
            ExpSetup.GET;
            ContiniaCompanySetup.GET;
            CompanyCode := ContiniaCompanySetup."Company Code";

            ArchiveFilePath := ExpSetup."Archive Path";
            DocStorageType := ExpSetup."Document Storage Type";
            DefaultWebLanguage := ExpSetup."Default Web/App Language";
            ArchiveFilePathEnabled := DocStorageType = ExpSetup."Document Storage Type"::"File System";

            ShowStep(Steps::SpecifyGeneralEMSetupInfo,TRUE,TRUE,TRUE);
          END;

        6:
          BEGIN
            ExpSetup.GET;
            ContiniaCompanySetup.GET;

            IF NextStep = 1 THEN BEGIN
              // PROCESS THE SETTINGS, ACTIVATE THE COMPANY
              COMMIT;

              IF CompanyCode = '' THEN
                ERROR(Text002);

              ContiniaCompanySetup."Company Code" := CompanyCode;
              ContiniaCompanySetup.MODIFY;

              ExpSetup.VALIDATE("Archive Path",ArchiveFilePath);
              ExpSetup.VALIDATE("Document Storage Type",DocStorageType);
              ExpSetup.VALIDATE("Default Web/App Language",DefaultWebLanguage);
              ExpSetup.TESTFIELD("Default Web/App Language");
              ExpSetup.MODIFY;
              COMMIT;

              IF CreateEMProfile THEN BEGIN
                IF NOT Profile.GET('EXPENSEMANAGEMENT') THEN BEGIN
                  Profile."Profile ID" := 'EXPENSEMANAGEMENT';
                  Profile.Description := 'Expense Management';
                  Profile."Role Center ID" := PAGE::"Expense Management Role Center";
                  Profile.INSERT(TRUE);
                END;

                IF NOT Profile.GET('EXPENSEMANAGEMENT LIMITED') THEN BEGIN
                  Profile."Profile ID" := 'EXPENSEMANAGEMENT LIMITED';
                  Profile.Description := 'Expense Management Limited User';
                  Profile."Role Center ID" := PAGE::"EM Limited Role Center";
                  Profile.INSERT(TRUE);
                END;
              END;

              IF SetEMDefaultProfile THEN BEGIN
                Profile.MODIFYALL("Default Role Center",FALSE);

                Profile.GET('EXPENSEMANAGEMENT');
                Profile."Default Role Center" := TRUE;
                Profile.MODIFY;
              END;
              COMMIT;

              IF ActivateCompany THEN BEGIN
                CEMOnline.ActivateCompany;
                CEMOnline.ForceFieldUpdateInCO;
                CEMOnline.SetupCloud;
              END;
            END;

            IF OnlineMgt.IsDev OR OnlineMgt.IsDemo THEN
              SetupDemoData := TRUE;

            // SETUP THE DEFAULT VALUES FOR WEB APPROVAL SETUP AND SHOW THE STEP
            ContiniaCompanySetup.GET;
            IF ContiniaWebPortal.GET(ContiniaCompanySetup."Web Portal Code") THEN BEGIN
              EnableWebApproval := TRUE;
              WebPortalCode := ContiniaWebPortal.Code;
              UseContiniaOnlineWeb := ContiniaWebPortal."Use Continia Online Web Portal";
              WebSiteUrl := ContiniaWebPortal."Web Site Url";
              WindowsWSUrl := ContiniaWebPortal."Windows Web Service URL";
              DatabaseWSUrl := ContiniaWebPortal."Database Web Service URL";
              LocalDomainName := ContiniaWebPortal."Local Domain Name";
            END;

            ShowStep(Steps::SpecifyWebApproval,TRUE,TRUE,TRUE);
          END;

        7:
          BEGIN
            IF NextStep = 1 THEN BEGIN
              //UPDATE THE WEB APPROVAL SETUP
              ContiniaCompanySetup.GET;
              IF EnableWebApproval THEN BEGIN
                IF WebPortalCode = '' THEN
                  ERROR(Text004);

                WITH ContiniaWebPortal DO BEGIN
                  IF GET(WebPortalCode) THEN BEGIN
                    "Web Site Url" := WebSiteUrl;
                    "Use Continia Online Web Portal" := UseContiniaOnlineWeb;
                    "Windows Web Service URL" := WindowsWSUrl;
                    "Database Web Service URL" := DatabaseWSUrl;
                    "Local Domain Name" := LocalDomainName;
                    MODIFY;
                  END ELSE BEGIN
                    InitValues;
                    Code := WebPortalCode;
                    "Web Site Url" := WebSiteUrl;
                    "Use Continia Online Web Portal" := UseContiniaOnlineWeb;
                    "Windows Web Service URL" := WindowsWSUrl;
                    "Database Web Service URL" := DatabaseWSUrl;
                    "Local Domain Name" := LocalDomainName;
                    INSERT(TRUE);
                  END;

                  ContiniaCompanySetup."Web Portal Code" := Code;
                  ContiniaCompanySetup.MODIFY;
                END;
              END ELSE BEGIN
                ContiniaCompanySetup."Web Portal Code" := '';
                ContiniaCompanySetup.MODIFY;
              END;
            END;

            // SETUP THE DEFAULT VALUES FOR BANK ACCOUNT SETUP AND SHOW THE STEP
            IF SetupBankAgreement THEN BEGIN
              IF BankAgr.COUNT < 2 THEN
                CEMOnline.GetBanks;

              BankCode := '';
              BankAgreementID := '';

              IF BankAgr.FINDSET THEN BEGIN
                xBankCode := BankAgr."Bank Code";
                xBankAgreementID := BankAgr."Agreement ID";
                BankCode := BankAgr."Bank Code";
                BankAgreementID := BankAgr."Agreement ID";

                CEMBank.GET(BankAgr."Bank Code",BankAgr."Country/Region Code");
                BankName := CEMBank.Name;
              END ELSE
                IF OnlineMgt.IsDev OR OnlineMgt.IsDemo THEN BEGIN
                  BankCode := 'TEST';
                  BankName := 'Continia Test Bank';
                  BankAgreementID := '1111111111';
                  BankCountryCode := '';
                END;

              ShowStep(Steps::SpecifyBankAgreement,TRUE,TRUE,TRUE);
            END ELSE
              IF SetupDemoData THEN BEGIN
                ShowStep(Steps::SpecifyContiniaUserSetup,FALSE,TRUE,TRUE);
                Step := 7;
              END ELSE BEGIN
                ShowStep(Steps::WizardCompleted,FALSE,FALSE,TRUE);
                Step := 8;
              END;
          END;

        8:
          BEGIN
            IF NextStep = 1 THEN
              IF (xBankCode <> BankCode) OR (BankAgreementID <> xBankAgreementID) THEN BEGIN
                IF BankCode = '' THEN BEGIN
                  IF CEMBank.GET(xBankCode,'') THEN
                    CEMBank.DELETE;
                  IF BankAgr.GET(xBankCode,xBankAgreementID,'') THEN
                    BankAgr.DELETE;
                END;

                IF (BankCode <> '') AND (BankAgreementID <> xBankAgreementID) THEN
                  IF BankAgr.GET(BankCode,xBankAgreementID,BankCountryCode) THEN BEGIN
                    BankAgr."Agreement ID" := BankAgreementID;
                    BankAgr.MODIFY;
                  END ELSE BEGIN
                    BankAgr."Bank Code" := BankCode;
                    BankAgr."Agreement ID" := BankAgreementID;
                    BankAgr."Country/Region Code" := BankCountryCode;
                    BankAgr.INSERT;
                  END;

              END;

            IF OnlineMgt.IsDev OR OnlineMgt.IsDemo THEN BEGIN
              SetupDemoData := TRUE;
              ShowStep(Steps::SpecifyContiniaUserSetup,FALSE,TRUE,TRUE);
            END ELSE
              ShowStep(Steps::WizardCompleted,FALSE,FALSE,TRUE);
          END;

        9:
          BEGIN
            IF NextStep = 1 THEN BEGIN
              IF SetContiniaUsers THEN
                ContiniaUserSetupList.RUN;

              IF SetupDemoData THEN
                CreateDemoData.RUN;
            END;

            ShowStep(Steps::WizardCompleted,FALSE,FALSE,TRUE);
          END;
      END;

      Step += NextStep;
      CurrPage.UPDATE(FALSE);
    END;

    PROCEDURE ShowStep@1160040013(NewStep@1160040000 : Integer;EnableBack@1160040001 : Boolean;EnableNext@1160040002 : Boolean;EnableFinish@1160040003 : Boolean);
    BEGIN
      WizardStartVisible := FALSE;
      ImportExportSelectionVisible := FALSE;
      ImportExportCompletedVisible := FALSE;
      ConfigLinesVisible := FALSE;
      WizardSetupCompanyStoryVisible := FALSE;
      SpecifyClientCredentialsVisible := FALSE;
      SpecifyWebApprovalVisible := FALSE;
      SpecifyGeneralEMSetupInfoVisible := FALSE;
      SpecifyBankAgreementVisible := FALSE;
      SpecifyContiniaUsersVisible := FALSE;
      WizardCompletedVisible := FALSE;

      CASE NewStep OF
        Steps::WizardStart:
          WizardStartVisible := TRUE;

        Steps::ImportExportSelection:
          BEGIN
            ImportExportSelectionVisible := TRUE;
            ConfigLinesVisible := TRUE;
          END;

        Steps::ImportExportCompleted:
          ImportExportCompletedVisible := TRUE;

        Steps::WizardSetupCompanyStory:
          WizardSetupCompanyStoryVisible := TRUE;

        Steps::SpecifyClientCredentials:
          SpecifyClientCredentialsVisible := TRUE;

        Steps::SpecifyWebApproval:
          SpecifyWebApprovalVisible := TRUE;

        Steps::SpecifyGeneralEMSetupInfo:
          SpecifyGeneralEMSetupInfoVisible := TRUE;

        Steps::SpecifyBankAgreement:
          SpecifyBankAgreementVisible := TRUE;

        Steps::SpecifyContiniaUserSetup:
          SpecifyContiniaUsersVisible := TRUE;

        Steps::WizardCompleted:
          WizardCompletedVisible := TRUE;
      END;

      BackEnabled := EnableBack;
      NextEnabled := EnableNext;
      FinishEnabled := EnableFinish;
    END;

    PROCEDURE EnableWizardStartFields@6085574();
    BEGIN
      FilenameEnabled := WizardAction <> WizardAction::Setup;
    END;

    PROCEDURE OnValidateWebPortalCode@6086300(_WebPortalCode@6086300 : Code[20]);
    VAR
      ContiniaWebPortal@6086302 : Record 6192774;
    BEGIN
      IF ContiniaWebPortal.GET(_WebPortalCode) THEN BEGIN
        WebPortalCode := ContiniaWebPortal.Code;
        UseContiniaOnlineWeb := ContiniaWebPortal."Use Continia Online Web Portal";
        WebSiteUrl := ContiniaWebPortal."Web Site Url";
        WindowsWSUrl := ContiniaWebPortal."Windows Web Service URL";
        DatabaseWSUrl := ContiniaWebPortal."Database Web Service URL";
        LocalDomainName := ContiniaWebPortal."Local Domain Name";
      END;
    END;

    BEGIN
    END.
  }
}

