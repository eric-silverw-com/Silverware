OBJECT Codeunit 5505 Graph Mgt - Sales Quote
{
  OBJECT-PROPERTIES
  {
    Date=11/22/17;
    Time=12:00:00 PM;
    Version List=NAVW111.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {

    PROCEDURE ProcessComplexTypes@1(VAR SalesQuoteEntityBuffer@1000 : Record 5505;BillToAddressJSON@1001 : Text);
    BEGIN
      ParseBillToCustomerAddressFromJSON(BillToAddressJSON,SalesQuoteEntityBuffer);
    END;

    PROCEDURE ParseBillToCustomerAddressFromJSON@10(BillToAddressJSON@1000 : Text;VAR SalesQuoteEntityBuffer@1002 : Record 5505);
    VAR
      GraphMgtComplexTypes@1003 : Codeunit 5468;
      RecRef@1001 : RecordRef;
    BEGIN
      IF BillToAddressJSON <> '' THEN
        WITH SalesQuoteEntityBuffer DO BEGIN
          RecRef.GETTABLE(SalesQuoteEntityBuffer);
          GraphMgtComplexTypes.ApplyPostalAddressFromJSON(BillToAddressJSON,RecRef,
            FIELDNO("Sell-to Address"),FIELDNO("Sell-to Address 2"),FIELDNO("Sell-to City"),FIELDNO("Sell-to County"),
            FIELDNO("Sell-to Country/Region Code"),FIELDNO("Sell-to Post Code"));
          RecRef.SETTABLE(SalesQuoteEntityBuffer);
        END;
    END;

    PROCEDURE BillToCustomerAddressToJSON@5(SalesQuoteEntityBuffer@1000 : Record 5505) JSON : Text;
    VAR
      GraphMgtComplexTypes@1001 : Codeunit 5468;
    BEGIN
      WITH SalesQuoteEntityBuffer DO
        GraphMgtComplexTypes.GetPostalAddressJSON("Sell-to Address","Sell-to Address 2",
          "Sell-to City","Sell-to County","Sell-to Country/Region Code","Sell-to Post Code",JSON);
    END;

    PROCEDURE UpdateIntegrationRecordIds@34(OnlyRecordsWithoutID@1007 : Boolean);
    VAR
      DummySalesQuoteEntityBuffer@1001 : Record 5505;
      DummyCustomer@1005 : Record 18;
      GraphMgtGeneralTools@1008 : Codeunit 5465;
      SalesQuoteEntityBufferRecordRef@1000 : RecordRef;
      CustomerRecordRef@1004 : RecordRef;
    BEGIN
      CustomerRecordRef.OPEN(DATABASE::Customer);
      GraphMgtGeneralTools.UpdateIntegrationRecords(
        CustomerRecordRef,DummyCustomer.FIELDNO(Id),TRUE);

      SalesQuoteEntityBufferRecordRef.OPEN(DATABASE::"Sales Header");
      GraphMgtGeneralTools.UpdateIntegrationRecords(
        SalesQuoteEntityBufferRecordRef,DummySalesQuoteEntityBuffer.FIELDNO(Id),OnlyRecordsWithoutID);
    END;

    LOCAL PROCEDURE CheckSupportedRcords@3(VAR RecRef@1000 : RecordRef) : Boolean;
    VAR
      SalesHeader@1002 : Record 36;
    BEGIN
      CASE RecRef.NUMBER OF
        DATABASE::"Sales Header":
          BEGIN
            RecRef.SETTABLE(SalesHeader);
            EXIT(SalesHeader."Document Type" = SalesHeader."Document Type"::Quote);
          END;
        ELSE
          EXIT(FALSE);
      END;
    END;

    [EventSubscriber(Codeunit,5150,OnUpdateReferencedIdField)]
    LOCAL PROCEDURE HandleUpdateReferencedIdFieldOnSalesQuoteHeader@19(VAR RecRef@1000 : RecordRef;NewId@1001 : GUID;VAR Handled@1002 : Boolean);
    VAR
      DummySalesQuoteEntityBuffer@1004 : Record 5505;
      GraphMgtGeneralTools@1003 : Codeunit 5465;
    BEGIN
      IF NOT CheckSupportedRcords(RecRef) THEN
        EXIT;

      GraphMgtGeneralTools.HandleUpdateReferencedIdFieldOnItem(
        RecRef,NewId,Handled,RecRef.NUMBER,DummySalesQuoteEntityBuffer.FIELDNO(Id));
    END;

    [EventSubscriber(Codeunit,5150,OnGetPredefinedIdValue)]
    LOCAL PROCEDURE HandleGetPredefinedIdValue@21(VAR Id@1000 : GUID;VAR RecRef@1001 : RecordRef;VAR Handled@1002 : Boolean);
    VAR
      DummySalesQuoteEntityBuffer@1004 : Record 5505;
      GraphMgtGeneralTools@1007 : Codeunit 5465;
    BEGIN
      IF NOT CheckSupportedRcords(RecRef) THEN
        EXIT;

      GraphMgtGeneralTools.HandleGetPredefinedIdValue(
        Id,RecRef,Handled,RecRef.NUMBER,DummySalesQuoteEntityBuffer.FIELDNO(Id));
    END;

    [EventSubscriber(Codeunit,5465,ApiSetup)]
    LOCAL PROCEDURE HandleApiSetup@16();
    VAR
      GraphMgtSalesQuoteBuffer@1000 : Codeunit 5506;
    BEGIN
      UpdateIntegrationRecordIds(FALSE);
      GraphMgtSalesQuoteBuffer.UpdateBufferTableRecords;
    END;

    BEGIN
    END.
  }
}

