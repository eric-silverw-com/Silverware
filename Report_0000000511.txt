OBJECT Report 511 Complete IC Inbox Action
{
  OBJECT-PROPERTIES
  {
    Date=11/22/17;
    Time=12:00:00 PM;
    Version List=NAVW111.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Complete IC Inbox Action;
               ESM=Completa acc. band. entrada IC;
               FRC=Compl‚ter action bte r‚cep. IC;
               ENC=Complete IC Inbox Action];
    ProcessingOnly=Yes;
  }
  DATASET
  {
    { 1153;    ;DataItem;                    ;
               DataItemTable=Table418;
               DataItemTableView=SORTING(Transaction No.,IC Partner Code,Transaction Source,Document Type);
               OnPreDataItem=BEGIN
                               IF TempGenJnlLine."Journal Template Name" <> '' THEN BEGIN
                                 GenJnlTemplate.GET(TempGenJnlLine."Journal Template Name");
                                 GenJnlTemplate.TESTFIELD(Type,GenJnlTemplate.Type::Intercompany);
                                 TempGenJnlLine.SETRANGE("Journal Template Name",GenJnlTemplate.Name);
                                 TempGenJnlLine.SETRANGE("Journal Batch Name",GenJnlBatch.Name);
                               END;
                               GetGLSetup;
                             END;

               OnAfterGetRecord=VAR
                                  InboxTransaction2@1000 : Record 418;
                                  HandledInboxTransaction2@1001 : Record 420;
                                  ICCommentLine@1002 : Record 424;
                                  ICPartner@1003 : Record 413;
                                BEGIN
                                  IF "Line Action" = "Line Action"::"No Action" THEN
                                    CurrReport.SKIP;
                                  InboxTransaction2 := "IC Inbox Transaction";
                                  IF ("Source Type" = "Source Type"::Journal) AND
                                     (InboxTransaction2."Line Action" <> InboxTransaction2."Line Action"::Cancel) AND
                                     (InboxTransaction2."Line Action" <> InboxTransaction2."Line Action"::"Return to IC Partner")
                                  THEN BEGIN
                                    TempGenJnlLine.TESTFIELD("Journal Template Name");
                                    TempGenJnlLine.TESTFIELD("Journal Batch Name");
                                  END;
                                  IF (InboxTransaction2."Line Action" <> InboxTransaction2."Line Action"::Cancel) AND
                                     ICPartner.GET(InboxTransaction2."IC Partner Code")
                                  THEN
                                    ICPartner.TESTFIELD(Blocked,FALSE);
                                  HandledInboxTransaction2.TRANSFERFIELDS(InboxTransaction2);
                                  CASE InboxTransaction2."Line Action" OF
                                    InboxTransaction2."Line Action"::Accept:
                                      HandledInboxTransaction2.Status := HandledInboxTransaction2.Status::Accepted;
                                    InboxTransaction2."Line Action"::"Return to IC Partner":
                                      HandledInboxTransaction2.Status := HandledInboxTransaction2.Status::"Returned to IC Partner";
                                    InboxTransaction2."Line Action"::Cancel:
                                      HandledInboxTransaction2.Status := HandledInboxTransaction2.Status::Cancelled;
                                  END;
                                  IF NOT HandledInboxTransaction2.INSERT THEN
                                    ERROR(
                                      Text001,InboxTransaction2.FIELDCAPTION("Transaction No."),
                                      InboxTransaction2."Transaction No.",InboxTransaction2."IC Partner Code",
                                      HandledInboxTransaction2.TABLECAPTION);
                                  InboxTransaction2.DELETE;

                                  ICIOMgt.HandleICComments(ICCommentLine."Table Name"::"IC Inbox Transaction",
                                    ICCommentLine."Table Name"::"Handled IC Inbox Transaction","Transaction No.",
                                    "IC Partner Code","Transaction Source");

                                  Forward := FALSE;
                                END;

               ReqFilterFields=IC Partner Code,Transaction Source,Line Action }

    { 1687;1   ;DataItem;                    ;
               DataItemTable=Table419;
               DataItemTableView=SORTING(Transaction No.,IC Partner Code,Transaction Source,Line No.);
               OnAfterGetRecord=VAR
                                  InboxJnlLine2@1001 : Record 419;
                                  HandledInboxJnlLine@1000 : Record 421;
                                BEGIN
                                  InboxJnlLine2 := "IC Inbox Jnl. Line";
                                  CASE "IC Inbox Transaction"."Line Action" OF
                                    "IC Inbox Transaction"."Line Action"::Accept:
                                      ICIOMgt.CreateJournalLines("IC Inbox Transaction","IC Inbox Jnl. Line",TempGenJnlLine,GenJnlTemplate);
                                    "IC Inbox Transaction"."Line Action"::"Return to IC Partner":
                                      IF NOT Forward THEN BEGIN
                                        ICIOMgt.ForwardToOutBox("IC Inbox Transaction");
                                        Forward := TRUE;
                                      END;
                                    "IC Inbox Transaction"."Line Action"::Cancel:
                                      BEGIN
                                        HandledInboxJnlLine.TRANSFERFIELDS(InboxJnlLine2);
                                        HandledInboxJnlLine.INSERT;
                                      END;
                                  END;

                                  ICIOMgt.MoveICJnlDimToHandled(DATABASE::"IC Inbox Jnl. Line",DATABASE::"Handled IC Inbox Jnl. Line",
                                    "IC Inbox Transaction"."Transaction No.","IC Inbox Transaction"."IC Partner Code",
                                    TRUE,InboxJnlLine2."Line No.");
                                  InboxJnlLine2.DELETE(TRUE);
                                END;

               OnPostDataItem=BEGIN
                                TempGenJnlLine."Document No." := INCSTR(TempGenJnlLine."Document No.");
                              END;

               DataItemLink=Transaction No.=FIELD(Transaction No.),
                            IC Partner Code=FIELD(IC Partner Code),
                            Transaction Source=FIELD(Transaction Source) }

    { 2880;1   ;DataItem;                    ;
               DataItemTable=Table434;
               DataItemTableView=SORTING(IC Transaction No.,IC Partner Code,Transaction Source);
               OnAfterGetRecord=VAR
                                  InboxSalesHeader2@1000 : Record 434;
                                  HandledInboxSalesHeader@1005 : Record 438;
                                  InboxSalesLine@1004 : Record 435;
                                  HandledInboxSalesLine@1003 : Record 439;
                                  ICDocDim@1002 : Record 442;
                                  ICDocDim2@1001 : Record 442;
                                BEGIN
                                  InboxSalesHeader2 := "IC Inbox Sales Header";
                                  CASE "IC Inbox Transaction"."Line Action" OF
                                    "IC Inbox Transaction"."Line Action"::Accept:
                                      ICIOMgt.CreateSalesDocument("IC Inbox Sales Header",
                                        ReplaceDocPostingDate,DocPostingDate);
                                    "IC Inbox Transaction"."Line Action"::"Return to IC Partner":
                                      ICIOMgt.ForwardToOutBox("IC Inbox Transaction");
                                    "IC Inbox Transaction"."Line Action"::Cancel:
                                      BEGIN
                                        HandledInboxSalesHeader.TRANSFERFIELDS(InboxSalesHeader2);
                                        HandledInboxSalesHeader.INSERT;
                                        DimMgt.SetICDocDimFilters(
                                          ICDocDim,DATABASE::"IC Inbox Sales Header","IC Transaction No.","IC Partner Code","Transaction Source",0);
                                        IF ICDocDim.FINDFIRST THEN
                                          DimMgt.MoveICDocDimtoICDocDim(ICDocDim,ICDocDim2,DATABASE::"Handled IC Inbox Sales Header","Transaction Source");
                                        WITH InboxSalesLine DO  BEGIN
                                          SETRANGE("IC Transaction No.",InboxSalesHeader2."IC Transaction No.");
                                          SETRANGE("IC Partner Code",InboxSalesHeader2."IC Partner Code");
                                          SETRANGE("Transaction Source",InboxSalesHeader2."Transaction Source");
                                          IF FIND('-') THEN
                                            REPEAT
                                              HandledInboxSalesLine.TRANSFERFIELDS(InboxSalesLine);
                                              HandledInboxSalesLine.INSERT;
                                              DimMgt.SetICDocDimFilters(
                                                ICDocDim,DATABASE::"IC Inbox Sales Line","IC Transaction No.","IC Partner Code","Transaction Source","Line No.");
                                              IF ICDocDim.FINDFIRST THEN
                                                DimMgt.MoveICDocDimtoICDocDim(ICDocDim,ICDocDim2,DATABASE::"Handled IC Inbox Sales Line","Transaction Source");
                                            UNTIL NEXT = 0;
                                        END;
                                      END;
                                  END;
                                  InboxSalesHeader2.DELETE(TRUE);
                                END;

               DataItemLink=IC Transaction No.=FIELD(Transaction No.),
                            IC Partner Code=FIELD(IC Partner Code),
                            Transaction Source=FIELD(Transaction Source) }

    { 3959;1   ;DataItem;                    ;
               DataItemTable=Table436;
               DataItemTableView=SORTING(IC Transaction No.,IC Partner Code,Transaction Source);
               OnAfterGetRecord=VAR
                                  InboxPurchHeader2@1001 : Record 436;
                                  HandledInboxPurchHeader@1005 : Record 440;
                                  InboxPurchLine@1004 : Record 437;
                                  HandledInboxPurchLine@1003 : Record 441;
                                  ICDocDim@1002 : Record 442;
                                  ICDocDim2@1000 : Record 442;
                                BEGIN
                                  InboxPurchHeader2 := "IC Inbox Purchase Header";
                                  CASE "IC Inbox Transaction"."Line Action" OF
                                    "IC Inbox Transaction"."Line Action"::Accept:
                                      ICIOMgt.CreatePurchDocument("IC Inbox Purchase Header",
                                        ReplaceDocPostingDate,DocPostingDate);
                                    "IC Inbox Transaction"."Line Action"::"Return to IC Partner":
                                      ICIOMgt.ForwardToOutBox("IC Inbox Transaction");
                                    "IC Inbox Transaction"."Line Action"::Cancel:
                                      BEGIN
                                        HandledInboxPurchHeader.TRANSFERFIELDS(InboxPurchHeader2);
                                        HandledInboxPurchHeader.INSERT;
                                        DimMgt.SetICDocDimFilters(
                                          ICDocDim,DATABASE::"IC Inbox Purchase Header","IC Transaction No.","IC Partner Code","Transaction Source",0);
                                        IF ICDocDim.FINDFIRST THEN
                                          DimMgt.MoveICDocDimtoICDocDim(ICDocDim,ICDocDim2,DATABASE::"Handled IC Inbox Purch. Header","Transaction Source");
                                        WITH InboxPurchLine DO  BEGIN
                                          SETRANGE("IC Transaction No.",InboxPurchHeader2."IC Transaction No.");
                                          SETRANGE("IC Partner Code",InboxPurchHeader2."IC Partner Code");
                                          SETRANGE("Transaction Source",InboxPurchHeader2."Transaction Source");
                                          IF FIND('-') THEN
                                            REPEAT
                                              HandledInboxPurchLine.TRANSFERFIELDS(InboxPurchLine);
                                              HandledInboxPurchLine.INSERT;
                                              DimMgt.SetICDocDimFilters(
                                                ICDocDim,DATABASE::"IC Inbox Purchase Line","IC Transaction No.","IC Partner Code",
                                                "Transaction Source","Line No.");
                                              IF ICDocDim.FINDFIRST THEN
                                                DimMgt.MoveICDocDimtoICDocDim(ICDocDim,ICDocDim2,DATABASE::"Handled IC Inbox Purch. Line","Transaction Source");
                                            UNTIL NEXT = 0;
                                        END;
                                      END;
                                  END;
                                  InboxPurchHeader2.DELETE(TRUE);
                                END;

               DataItemLink=IC Transaction No.=FIELD(Transaction No.),
                            IC Partner Code=FIELD(IC Partner Code),
                            Transaction Source=FIELD(Transaction Source) }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
      OnInit=BEGIN
               PostingDateEditable := TRUE;
               DocPostingDateEditable := TRUE;
             END;

      OnOpenPage=BEGIN
                   ValidateJnl;
                   IF ReplaceDocPostingDate THEN
                     DocPostingDateEditable := TRUE
                   ELSE
                     DocPostingDateEditable := FALSE;
                 END;

    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[ENU=Options;
                             ESM=Opciones;
                             FRC=Options;
                             ENC=Options] }

      { 17  ;2   ;Group     ;
                  CaptionML=[ENU=Journals;
                             ESM=Diarios;
                             FRC=Journaux;
                             ENC=Journals] }

      { 4   ;3   ;Field     ;
                  CaptionML=[ENU=IC Gen. Journal Template;
                             ESM=Plantilla diario general IC;
                             FRC=ModŠle de journal g‚n‚ral IC;
                             ENC=IC Gen. Journal Template];
                  ToolTipML=[ENU=Specifies the journal template in which you want to create the journal lines.;
                             ESM=Especifica la plantilla de diario en la que desea crear las l¡neas del diario.;
                             FRC=Sp‚cifie le modŠle journal dans lequel vous souhaitez cr‚er les lignes journal.;
                             ENC=Specifies the journal template in which you want to create the journal lines.];
                  ApplicationArea=#Intercompany;
                  SourceExpr=TempGenJnlLine."Journal Template Name";
                  TableRelation="Gen. Journal Template";
                  OnValidate=BEGIN
                               TempGenJnlLine."Journal Batch Name" := '';
                               ValidateJnl;
                             END;

                  OnLookup=BEGIN
                             IF PAGE.RUNMODAL(0,GenJnlTemplate) = ACTION::LookupOK THEN BEGIN
                               TempGenJnlLine."Journal Template Name" := GenJnlTemplate.Name;
                               ValidateJnl;
                             END;
                           END;
                            }

      { 6   ;3   ;Field     ;
                  Lookup=Yes;
                  CaptionML=[ENU=Gen. Journal Batch;
                             ESM=Secci¢n diario general;
                             FRC=Lot journal g‚n‚ral;
                             ENC=Gen. Journal Batch];
                  ToolTipML=[ENU=Specifies the journal batch in which you want to create the journal lines.;
                             ESM=Especifica el lote de diario en el que desea crear las l¡neas del diario.;
                             FRC=Sp‚cifie le lot journal dans lequel vous souhaitez cr‚er les lignes journal.;
                             ENC=Specifies the journal batch in which you want to create the journal lines.];
                  ApplicationArea=#Intercompany;
                  SourceExpr=TempGenJnlLine."Journal Batch Name";
                  OnValidate=BEGIN
                               IF TempGenJnlLine."Journal Batch Name" <> '' THEN BEGIN
                                 TempGenJnlLine.TESTFIELD("Journal Template Name");
                                 GenJnlBatch.GET(TempGenJnlLine."Journal Template Name",TempGenJnlLine."Journal Batch Name");
                               END;
                               ValidateJnl;
                             END;

                  OnLookup=BEGIN
                             TempGenJnlLine.TESTFIELD("Journal Template Name");
                             GenJnlTemplate.GET(TempGenJnlLine."Journal Template Name");
                             GenJnlBatch.FILTERGROUP(2);
                             GenJnlBatch.SETRANGE("Journal Template Name",TempGenJnlLine."Journal Template Name");
                             GenJnlBatch.FILTERGROUP(0);
                             GenJnlBatch."Journal Template Name" := TempGenJnlLine."Journal Template Name";
                             GenJnlBatch.Name := TempGenJnlLine."Journal Batch Name";
                             IF PAGE.RUNMODAL(0,GenJnlBatch) = ACTION::LookupOK THEN BEGIN
                               Text := GenJnlBatch.Name;
                               EXIT(TRUE);
                             END;
                           END;
                            }

      { 1   ;3   ;Field     ;
                  CaptionML=[ENU=Starting Document No.;
                             ESM=N§ documento incial;
                             FRC=Nø de document initial;
                             ENC=Starting Document No.];
                  ToolTipML=[ENU="Specifies the next available number in the number series for the journal batch that is linked to the payment journal. When you run the batch job, this is the document number that appears on the first payment journal line. The batch job automatically fills in this field, but you can also fill in this field manually. ";
                             ESM="Especifica el siguiente n£mero disponible de la serie num‚rica para la secci¢n de diario vinculada al diario de pagos. Al ejecutar el trabajo por lotes, este es el n£mero de documento que aparece en la primera l¡nea del diario de pagos. El proceso rellena autom ticamente este campo, pero tambi‚n se puede rellenar el campo manualmente. ";
                             FRC="Sp‚cifie le num‚ro suivant disponible dans la s‚rie de num‚ros pour le lot journal associ‚ au journal paiement. Lorsque vous ex‚cutez le traitement en lot, il s'agit du num‚ro de document qui s'affiche sur la premiŠre ligne journal paiement. Le traitement en lot renseigne automatiquement ce champ, mais vous pouvez ‚galement le compl‚ter manuellement. ";
                             ENC="Specifies the next available number in the number series for the journal batch that is linked to the payment journal. When you run the batch job, this is the document number that appears on the first payment journal line. The batch job automatically fills in this field, but you can also fill in this field manually. "];
                  ApplicationArea=#Intercompany;
                  SourceExpr=TempGenJnlLine."Document No." }

      { 9   ;3   ;Field     ;
                  Name=Replace Posting Date;
                  CaptionML=[ENU=Replace Posting Date;
                             ESM=Reemplazar fecha reg.;
                             FRC=Remplacer la date de report;
                             ENC=Replace Posting Date];
                  ToolTipML=[ENU=Specifies if you want to replace the journals' posting date with the date that is entered in the Posting Date field.;
                             ESM=Especifica si desea reemplazar la fecha de registro de los diarios por la especificada en el campo Fecha registro.;
                             FRC=Sp‚cifie si vous souhaitez remplacer la date de report des journaux par la date saisie dans le champ Date de report.;
                             ENC=Specifies if you want to replace the journals' posting date with the date that is entered in the Posting Date field.];
                  ApplicationArea=#Intercompany;
                  SourceExpr=ReplacePostingDate;
                  OnValidate=BEGIN
                               IF ReplacePostingDate THEN
                                 PostingDateEditable := TRUE
                               ELSE BEGIN
                                 TempGenJnlLine."Posting Date" := 0D;
                                 PostingDateEditable := FALSE;
                               END;
                             END;
                              }

      { 3   ;3   ;Field     ;
                  Name=Posting Date;
                  CaptionML=[ENU=Posting Date;
                             ESM=Fecha registro;
                             FRC=Date de report;
                             ENC=Posting Date];
                  ToolTipML=[ENU="Specifies the date that will be used when you post, if you have selected the Replace Posting Date field. If the posting date on a document is blank, the date in the Posting Date field is used even if you have not selected it. ";
                             ESM="Introduzca la fecha que se usar  como fecha de registro cuando efect£e el registro, si ha seleccionado el campo Reemplazar fecha reg. Si est  en blanco la fecha de registro de un documento, se utilizar  la fecha del campo Fecha registro, aunque no lo haya seleccionado. ";
                             FRC="Sp‚cifie la date qui est utilis‚e lors du report, si vous avez s‚lectionn‚ le champ Remplacer date de report. Si la date de report figurant sur un document est vide, la date indiqu‚e dans le champ Date de report est utilis‚e mˆme si vous ne l'avez pas s‚lectionn‚e. ";
                             ENC="Specifies the date that will be used when you post, if you have selected the Replace Posting Date field. If the posting date on a document is blank, the date in the Posting Date field is used even if you have not selected it. "];
                  ApplicationArea=#Intercompany;
                  SourceExpr=TempGenJnlLine."Posting Date";
                  Editable=PostingDateEditable;
                  OnValidate=BEGIN
                               ValidateJnl;
                             END;
                              }

      { 15  ;2   ;Group     ;
                  CaptionML=[ENU=Documents;
                             ESM=Documentos;
                             FRC=Documents;
                             ENC=Documents] }

      { 11  ;3   ;Field     ;
                  Name=ReplaceDocPostingDate;
                  CaptionML=[ENU=Replace Posting Date;
                             ESM=Reemplazar fecha reg.;
                             FRC=Remplacer la date de report;
                             ENC=Replace Posting Date];
                  ToolTipML=[ENU=Specifies if you want to replace the journals' posting date with the date that is entered in the Posting Date field.;
                             ESM=Especifica si desea reemplazar la fecha de registro de los diarios por la especificada en el campo Fecha registro.;
                             FRC=Sp‚cifie si vous souhaitez remplacer la date de report des journaux par la date saisie dans le champ Date de report.;
                             ENC=Specifies if you want to replace the journals' posting date with the date that is entered in the Posting Date field.];
                  ApplicationArea=#Intercompany;
                  SourceExpr=ReplaceDocPostingDate;
                  OnValidate=BEGIN
                               IF ReplaceDocPostingDate THEN BEGIN
                                 DocPostingDateEditable := TRUE
                               END ELSE BEGIN
                                 DocPostingDate := 0D;
                                 DocPostingDateEditable := FALSE;
                               END;
                             END;
                              }

      { 14  ;3   ;Field     ;
                  Name=Doc Posting Date;
                  CaptionML=[ENU=Posting Date;
                             ESM=Fecha registro;
                             FRC=Date de report;
                             ENC=Posting Date];
                  ToolTipML=[ENU=Specifies Posting Date.;
                             ESM=Especifica la fecha de registro.;
                             FRC=Sp‚cifie la date de report.;
                             ENC=Specifies Posting Date.];
                  ApplicationArea=#Intercompany;
                  SourceExpr=DocPostingDate;
                  Editable=DocPostingDateEditable }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      TempGenJnlLine@1003 : TEMPORARY Record 81;
      GenJnlTemplate@1001 : Record 80;
      GenJnlBatch@1002 : Record 232;
      GLSetup@1007 : Record 98;
      NoSeriesMgt@1006 : Codeunit 396;
      ICIOMgt@1009 : Codeunit 427;
      DimMgt@1000 : Codeunit 408;
      GLSetupFound@1005 : Boolean;
      ReplacePostingDate@1014 : Boolean;
      ReplaceDocPostingDate@1015 : Boolean;
      DocPostingDate@1016 : Date;
      Forward@1017 : Boolean;
      Text001@1004 : TextConst 'ENU=%1 %2 from IC Partner %3 already exists in the %4 window. You have to delete %1 %2 in the %4 window before you complete the line action.;ESM=%1 %2 de socio IC %3 ya existe en la ventana %4. Debe eliminar %1 %2 en la ventana %4 antes de completar la acci¢n de l¡nea.;FRC=%1 %2 du partenaire IC %3 existe d‚j… dans la fenˆtre %4. Vous devez supprimer %1 %2 dans la fenˆtre %4 avant de terminer l''action de la ligne.;ENC=%1 %2 from IC Partner %3 already exists in the %4 window. You have to delete %1 %2 in the %4 window before you complete the line action.';
      DocPostingDateEditable@19025509 : Boolean INDATASET;
      PostingDateEditable@19008133 : Boolean INDATASET;

    LOCAL PROCEDURE ValidateJnl@3();
    BEGIN
      PageValidateJnl;
    END;

    LOCAL PROCEDURE GetGLSetup@1();
    BEGIN
      IF NOT GLSetupFound THEN
        GLSetup.GET;
      GLSetupFound := TRUE;
    END;

    LOCAL PROCEDURE PageValidateJnl@19046080();
    VAR
      GenJnlLine@1000 : Record 81;
    BEGIN
      TempGenJnlLine."Document No." := '';
      GenJnlLine.SETRANGE("Journal Template Name",TempGenJnlLine."Journal Template Name");
      GenJnlLine.SETRANGE("Journal Batch Name",TempGenJnlLine."Journal Batch Name");
      IF GenJnlLine.FINDLAST THEN BEGIN
        TempGenJnlLine."Document No." := INCSTR(GenJnlLine."Document No.");
        TempGenJnlLine."Line No." := GenJnlLine."Line No.";
      END ELSE
        IF GenJnlBatch.GET(TempGenJnlLine."Journal Template Name",TempGenJnlLine."Journal Batch Name") THEN
          IF GenJnlBatch."No. Series" = '' THEN
            TempGenJnlLine."Document No." := ''
          ELSE BEGIN
            TempGenJnlLine."Document No." := NoSeriesMgt.GetNextNo(GenJnlBatch."No. Series",TempGenJnlLine."Posting Date",FALSE);
            CLEAR(NoSeriesMgt);
          END;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

