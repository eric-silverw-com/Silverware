OBJECT Page 5172 Answer Points
{
  OBJECT-PROPERTIES
  {
    Date=09/19/18;
    Time=12:00:00 PM;
    Version List=NAVW113.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Answer Points;
               ESM=Puntos respuesta;
               FRC=Points de r‚ponses;
               ENC=Answer Points];
    InsertAllowed=No;
    DeleteAllowed=No;
    ModifyAllowed=No;
    SourceTable=Table5088;
    DataCaptionExpr=GetCaption;
    PageType=List;
    OnOpenPage=VAR
                 TempProfileQuestnLine@1000 : Record 5088;
               BEGIN
                 TargetQuestnCode := "Profile Questionnaire Code";
                 TargetQuestLineNo := "Line No.";
                 TargetQuestnLineNoEnd := "Line No.";
                 CurrQuestnCode := "Profile Questionnaire Code";

                 IF TempProfileQuestnLine.GET(TargetQuestnCode,"Line No.") THEN
                   WHILE (TempProfileQuestnLine.NEXT <> 0) AND
                         (TempProfileQuestnLine.Type = TempProfileQuestnLine.Type::Answer)
                   DO
                     TargetQuestnLineNoEnd := TempProfileQuestnLine."Line No.";

                 SetRatingFilter;
               END;

    OnAfterGetRecord=VAR
                       Rating@1000 : Record 5111;
                     BEGIN
                       IF Rating.GET(TargetQuestnCode,TargetQuestLineNo,"Profile Questionnaire Code","Line No.") THEN
                         Points := Rating.Points
                       ELSE
                         Points := 0;

                       StyleIsStrong := Type = Type::Question;
                       IF Type <> Type::Question THEN
                         DescriptionIndent := 1
                       ELSE
                         DescriptionIndent := 0;
                     END;

    OnAfterGetCurrRecord=VAR
                           Rating@1000 : Record 5111;
                         BEGIN
                           IF Rating.GET(TargetQuestnCode,TargetQuestLineNo,"Profile Questionnaire Code","Line No.") THEN
                             Points := Rating.Points
                           ELSE
                             Points := 0;
                         END;

    ActionList=ACTIONS
    {
      { 1900000003;0 ;ActionContainer;
                      ActionContainerType=RelatedInformation }
      { 3       ;1   ;Action    ;
                      Name=Questionnaire;
                      ApplicationArea=#RelationshipMgmt;
                      Promoted=Yes;
                      Image=Questionnaire;
                      PromotedCategory=Process;
                      OnAction=VAR
                                 ProfileQuestnHeader@1000 : Record 5087;
                               BEGIN
                                 IF ProfileQuestnHeader.GET("Profile Questionnaire Code") THEN
                                   IF PAGE.RUNMODAL(
                                        PAGE::"Profile Questionnaire List",ProfileQuestnHeader) = ACTION::LookupOK
                                   THEN BEGIN
                                     SETRANGE("Profile Questionnaire Code",ProfileQuestnHeader.Code);
                                     CurrQuestnCode := ProfileQuestnHeader.Code;
                                     SetRatingFilter;
                                     CurrPage.UPDATE;
                                   END;
                               END;
                                }
      { 4       ;1   ;ActionGroup;
                      CaptionML=[ENU=&Points;
                                 ESM=&Puntos;
                                 FRC=&Points;
                                 ENC=&Points] }
      { 7       ;2   ;Action    ;
                      ShortCutKey=Shift+Ctrl+L;
                      CaptionML=[ENU=List;
                                 ESM=Lista;
                                 FRC=Liste;
                                 ENC=List];
                      ToolTipML=[ENU=View the answers from which a contact can gain points when you calculate the rating.;
                                 ESM=Permite ver las respuestas a partir de las cuales un contacto puede obtener puntos cuando se calcula la clasificaci¢n.;
                                 FRC=Affichez les r‚ponses … partir desquelles un contact peut acqu‚rir des points lorsque vous calculez l'‚valuation.;
                                 ENC=View the answers from which a contact can gain points when you calculate the rating.];
                      ApplicationArea=#RelationshipMgmt;
                      Image=OpportunitiesList;
                      OnAction=VAR
                                 Rating@1000 : Record 5111;
                               BEGIN
                                 Rating.SETRANGE("Profile Questionnaire Code",TargetQuestnCode);
                                 Rating.SETRANGE("Profile Questionnaire Line No.",TargetQuestLineNo);
                                 IF PAGE.RUNMODAL(PAGE::"Answer Points List",Rating) = ACTION::LookupOK THEN BEGIN
                                   CurrQuestnCode := Rating."Rating Profile Quest. Code";
                                   SETRANGE("Profile Questionnaire Code",CurrQuestnCode);
                                   GET(CurrQuestnCode,Rating."Rating Profile Quest. Line No.");
                                 END;
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 13  ;1   ;Group     ;
                Name=Questions;
                IndentationColumnName=DescriptionIndent;
                IndentationControls=Description;
                GroupType=Repeater }

    { 15  ;2   ;Field     ;
                ToolTipML=[ENU=Specifies whether the entry is a question or an answer.;
                           ESM=Especifica si la entrada es una pregunta o una respuesta.;
                           FRC=Indique s'il s'agit d'une question ou d'une r‚ponse.;
                           ENC=Specifies whether the entry is a question or an answer.];
                ApplicationArea=#All;
                SourceExpr=Type;
                Editable=FALSE;
                Style=Strong;
                StyleExpr=StyleIsStrong }

    { 17  ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the profile question or answer.;
                           ESM=Especifica la pregunta o la respuesta del perfil.;
                           FRC=Sp‚cifie la question ou la r‚ponse profil.;
                           ENC=Specifies the profile question or answer.];
                ApplicationArea=#All;
                SourceExpr=Description;
                Editable=FALSE;
                Style=Strong;
                StyleExpr=StyleIsStrong }

    { 8   ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the number of contacts that have given this answer.;
                           ESM=Especifica el n£mero de contactos que proporcionaron esta respuesta.;
                           FRC=Affiche le nombre de contacts ayant donn‚ cette r‚ponse.;
                           ENC=Specifies the number of contacts that have given this answer.];
                ApplicationArea=#RelationshipMgmt;
                SourceExpr="No. of Contacts" }

    { 1   ;2   ;Field     ;
                CaptionML=[ENU=Points;
                           ESM=Puntos;
                           FRC=Points;
                           ENC=Points];
                ToolTipML=[ENU=Specifies all questions and answers in the profile questionnaire.;
                           ESM=Especifica todas las preguntas y respuestas del cuestionario de perfil.;
                           FRC=Sp‚cifie toutes les questions et les r‚ponses du questionnaire du profil.;
                           ENC=Specifies all questions and answers in the profile questionnaire.];
                ApplicationArea=#RelationshipMgmt;
                DecimalPlaces=0:0;
                BlankZero=Yes;
                SourceExpr=Points;
                OnValidate=VAR
                             Rating@1000 : Record 5111;
                           BEGIN
                             TESTFIELD(Type,Type::Answer);

                             IF Rating.GET(TargetQuestnCode,TargetQuestLineNo,"Profile Questionnaire Code","Line No.") THEN
                               IF Points = 0 THEN
                                 Rating.DELETE
                               ELSE BEGIN
                                 Rating.Points := Points;
                                 Rating.MODIFY;
                               END
                             ELSE BEGIN
                               Rating."Profile Questionnaire Code" := TargetQuestnCode;
                               Rating."Profile Questionnaire Line No." := TargetQuestLineNo;
                               Rating."Rating Profile Quest. Code" := "Profile Questionnaire Code";
                               Rating."Rating Profile Quest. Line No." := "Line No.";
                               Rating.Points := Points;
                               Rating.INSERT(TRUE);
                             END;
                           END;
                            }

    { 1900000007;0;Container;
                ContainerType=FactBoxArea }

    { 1900383207;1;Part   ;
                Visible=FALSE;
                PartType=System;
                SystemPartID=RecordLinks }

    { 1905767507;1;Part   ;
                Visible=FALSE;
                PartType=System;
                SystemPartID=Notes }

  }
  CODE
  {
    VAR
      ClientTypeManagement@1077 : Codeunit 4;
      CurrQuestnCode@1003 : Code[20];
      TargetQuestnCode@1000 : Code[20];
      TargetQuestLineNo@1002 : Integer;
      TargetQuestnLineNoEnd@1005 : Integer;
      Points@1001 : Decimal;
      StyleIsStrong@1006 : Boolean INDATASET;
      DescriptionIndent@19057867 : Integer INDATASET;

    [External]
    PROCEDURE SetRatingFilter@1();
    BEGIN
      FILTERGROUP(2);
      IF CurrQuestnCode = TargetQuestnCode THEN
        SETFILTER("Line No.",'<%1|>%2',TargetQuestLineNo,TargetQuestnLineNoEnd)
      ELSE
        SETRANGE("Line No.");
      FILTERGROUP(0);
    END;

    LOCAL PROCEDURE GetCaption@2() : Text;
    BEGIN
      IF ClientTypeManagement.GetCurrentClientType = CLIENTTYPE::Phone THEN
        EXIT(STRSUBSTNO('%1 %2',CurrPage.CAPTION,"Profile Questionnaire Code"));

      EXIT(FORMAT("Profile Questionnaire Code"));
    END;

    BEGIN
    END.
  }
}

