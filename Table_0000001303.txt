OBJECT Table 1303 Mini Vendor Template
{
  OBJECT-PROPERTIES
  {
    Date=09/19/18;
    Time=12:00:00 PM;
    Version List=NAVW113.00,NAVNA13.00;
  }
  PROPERTIES
  {
    OnInsert=VAR
               FieldRefArray@1001 : ARRAY [20] OF FieldRef;
               RecRef@1000 : RecordRef;
             BEGIN
               TESTFIELD("Template Name");
               RecRef.GETTABLE(Rec);
               CreateFieldRefArray(FieldRefArray,RecRef);

               InsertConfigurationTemplateHeaderAndLines;
             END;

    OnModify=VAR
               FieldRefArray@1000 : ARRAY [20] OF FieldRef;
               RecRef@1001 : RecordRef;
             BEGIN
               TESTFIELD(Code);
               TESTFIELD("Template Name");
               RecRef.GETTABLE(Rec);
               CreateFieldRefArray(FieldRefArray,RecRef);
               ConfigTemplateManagement.UpdateConfigTemplateAndLines(Code,"Template Name",DATABASE::Vendor,FieldRefArray);
             END;

    OnDelete=VAR
               ConfigTemplateHeader@1000 : Record 8618;
             BEGIN
               IF ConfigTemplateHeader.GET(Code) THEN BEGIN
                 ConfigTemplateManagement.DeleteRelatedTemplates(Code,DATABASE::"Default Dimension");
                 ConfigTemplateHeader.DELETE(TRUE);
               END;
             END;

    ReplicateData=No;
    CaptionML=[ENU=Mini Vendor Template;
               ESM=Miniplantilla proveedor;
               FRC=Modäle fournisseur mini;
               ENC=Mini Vendor Template];
  }
  FIELDS
  {
    { 1   ;   ;Key                 ;Integer       ;AutoIncrement=Yes;
                                                   CaptionML=[ENU=Key;
                                                              ESM=Clave;
                                                              FRC=ClÇ;
                                                              ENC=Key] }
    { 2   ;   ;Code                ;Code10        ;CaptionML=[ENU=Code;
                                                              ESM=C¢digo;
                                                              FRC=Code;
                                                              ENC=Code];
                                                   NotBlank=Yes }
    { 3   ;   ;Template Name       ;Text50        ;CaptionML=[ENU=Template Name;
                                                              ESM=Nombre libro;
                                                              FRC=Nom modäle;
                                                              ENC=Template Name];
                                                   NotBlank=Yes }
    { 7   ;   ;City                ;Text30        ;TableRelation=IF (Country/Region Code=CONST()) "Post Code".City
                                                                 ELSE IF (Country/Region Code=FILTER(<>'')) "Post Code".City WHERE (Country/Region Code=FIELD(Country/Region Code));
                                                   OnValidate=VAR
                                                                PostCodeRec@1000 : Record 225;
                                                              BEGIN
                                                                IF City <> '' THEN BEGIN
                                                                  PostCodeRec.SETFILTER("Search City",City);

                                                                  CASE PostCodeRec.COUNT OF
                                                                    0:
                                                                      EXIT;
                                                                    1:
                                                                      PostCodeRec.FINDFIRST;
                                                                    ELSE
                                                                      IF PAGE.RUNMODAL(PAGE::"Post Codes",PostCodeRec,PostCodeRec.Code) <> ACTION::LookupOK THEN
                                                                        EXIT;
                                                                  END;

                                                                  "Post Code" := PostCodeRec.Code;
                                                                  "Country/Region Code" := PostCodeRec."Country/Region Code";
                                                                END;
                                                              END;

                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=[ENU=City;
                                                              ESM=Municipio/Ciudad;
                                                              FRC=Ville;
                                                              ENC=City] }
    { 21  ;   ;Vendor Posting Group;Code20        ;TableRelation="Vendor Posting Group";
                                                   CaptionML=[ENU=Vendor Posting Group;
                                                              ESM=Grupo registro proveedor;
                                                              FRC=Paramätre report fournisseur;
                                                              ENC=Vendor Posting Group] }
    { 22  ;   ;Currency Code       ;Code10        ;TableRelation=Currency;
                                                   CaptionML=[ENU=Currency Code;
                                                              ESM=C¢d. divisa;
                                                              FRC=Code devise;
                                                              ENC=Currency Code] }
    { 24  ;   ;Language Code       ;Code10        ;TableRelation=Language;
                                                   CaptionML=[ENU=Language Code;
                                                              ESM=C¢d. idioma;
                                                              FRC=Code langue;
                                                              ENC=Language Code] }
    { 27  ;   ;Payment Terms Code  ;Code10        ;TableRelation="Payment Terms";
                                                   CaptionML=[ENU=Payment Terms Code;
                                                              ESM=C¢d. tÇrminos pago;
                                                              FRC=Code modalitÇs de paiement;
                                                              ENC=Payment Terms Code] }
    { 28  ;   ;Fin. Charge Terms Code;Code10      ;TableRelation="Finance Charge Terms";
                                                   CaptionML=[ENU=Fin. Charge Terms Code;
                                                              ESM=C¢d. interÇs;
                                                              FRC=Code condition intÇràts;
                                                              ENC=Fin. Charge Terms Code] }
    { 33  ;   ;Invoice Disc. Code  ;Code20        ;TableRelation=Vendor;
                                                   CaptionML=[ENU=Invoice Disc. Code;
                                                              ESM=C¢d. dto. factura;
                                                              FRC=Code escompte facture;
                                                              ENC=Invoice Disc. Code] }
    { 35  ;   ;Country/Region Code ;Code10        ;TableRelation=Country/Region;
                                                   CaptionML=[ENU=Country/Region Code;
                                                              ESM=C¢d. pa°s/regi¢n;
                                                              FRC=Code pays/rÇgion;
                                                              ENC=Country/Region Code] }
    { 47  ;   ;Payment Method Code ;Code10        ;TableRelation="Payment Method";
                                                   CaptionML=[ENU=Payment Method Code;
                                                              ESM=C¢d. forma pago;
                                                              FRC=Code mode de paiement;
                                                              ENC=Payment Method Code] }
    { 80  ;   ;Application Method  ;Option        ;CaptionML=[ENU=Application Method;
                                                              ESM=MÇtodo liquidaci¢n;
                                                              FRC=MÇthode d'affectation;
                                                              ENC=Application Method];
                                                   OptionCaptionML=[ENU=Manual,Apply to Oldest;
                                                                    ESM=Manual,Por antigÅedad;
                                                                    FRC=Manuel,Au plus ancien;
                                                                    ENC=Manual,Apply to Oldest];
                                                   OptionString=Manual,Apply to Oldest }
    { 82  ;   ;Prices Including VAT;Boolean       ;CaptionML=[ENU=Prices Including Tax;
                                                              ESM=Precios IVA incluido;
                                                              FRC=Prix incluant la TVA;
                                                              ENC=Prices Including Tax] }
    { 88  ;   ;Gen. Bus. Posting Group;Code20     ;TableRelation="Gen. Business Posting Group";
                                                   CaptionML=[ENU=Gen. Bus. Posting Group;
                                                              ESM=Grupo registro neg. gen.;
                                                              FRC=Groupe de report de marchÇ;
                                                              ENC=Gen. Bus. Posting Group] }
    { 91  ;   ;Post Code           ;Code20        ;TableRelation=IF (Country/Region Code=CONST()) "Post Code"
                                                                 ELSE IF (Country/Region Code=FILTER(<>'')) "Post Code" WHERE (Country/Region Code=FIELD(Country/Region Code));
                                                   OnValidate=VAR
                                                                PostCodeRec@1000 : Record 225;
                                                              BEGIN
                                                                IF "Post Code" <> '' THEN BEGIN
                                                                  PostCodeRec.SETFILTER(Code,"Post Code");

                                                                  CASE PostCodeRec.COUNT OF
                                                                    0:
                                                                      EXIT;
                                                                    1:
                                                                      PostCodeRec.FINDFIRST;
                                                                    ELSE
                                                                      IF PAGE.RUNMODAL(PAGE::"Post Codes",PostCodeRec,PostCodeRec.Code) <> ACTION::LookupOK THEN
                                                                        EXIT;
                                                                  END;

                                                                  City := PostCodeRec.City;
                                                                  "Country/Region Code" := PostCodeRec."Country/Region Code";
                                                                END;
                                                              END;

                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=[ENU=ZIP Code;
                                                              ESM=C¢digo postal;
                                                              FRC=Code postal;
                                                              ENC=Postal/ZIP Code] }
    { 92  ;   ;County              ;Text30        ;CaptionML=[ENU=State;
                                                              ESM=Provincia;
                                                              FRC=ComtÇ;
                                                              ENC=Province/State];
                                                   CaptionClass='5,1,' + "Country/Region Code" }
    { 109 ;   ;Tax Liable          ;Boolean       ;CaptionML=[ENU=Tax Liable;
                                                              ESM=Sujeto a impuesto;
                                                              FRC=Imposable;
                                                              ENC=Tax Liable] }
    { 110 ;   ;VAT Bus. Posting Group;Code20      ;TableRelation="VAT Business Posting Group";
                                                   CaptionML=[ENU=Tax Bus. Posting Group;
                                                              ESM=Grupo registro IVA neg.;
                                                              FRC=Groupe de reports de taxe sur la valeur ajoutÇe de l'entreprise;
                                                              ENC=Tax Bus. Posting Group] }
    { 116 ;   ;Block Payment Tolerance;Boolean    ;CaptionML=[ENU=Block Payment Tolerance;
                                                              ESM=Bloquear tolerancia pagos;
                                                              FRC=TolÇr. vers. en fonds bloquÇs;
                                                              ENC=Block Payment Tolerance] }
    { 7602;   ;Validate EU Vat Reg. No.;Boolean   ;CaptionML=[ENU=Validate EU Tax Reg. No.;
                                                              ESM=Validar RFC/Curp de la UE;
                                                              FRC=Valider N¯ d'inscription TPS/TVH Union europÇenne;
                                                              ENC=Validate EU GST/HST Reg. No.] }
    { 10018;  ;Check Date Format   ;Option        ;CaptionML=[ENU=Check Date Format;
                                                              ESM=Comprobar formato de fecha;
                                                              FRC=VÇrifier le format de date;
                                                              ENC=Cheque Date Format];
                                                   OptionCaptionML=[ENU=" ,MM DD YYYY,DD MM YYYY,YYYY MM DD";
                                                                    ESM=" ,MM DD AAAA,DD MM AAAA,AAAA MM DD";
                                                                    FRC=" ,MM JJ AAAA,JJ MM AAAA,AAAA MM JJ";
                                                                    ENC=" ,MM DD YYYY,DD MM YYYY,YYYY MM DD"];
                                                   OptionString=[ ,MM DD YYYY,DD MM YYYY,YYYY MM DD] }
    { 10019;  ;Check Date Separator;Option        ;CaptionML=[ENU=Check Date Separator;
                                                              ESM=Comprobar separador de fecha;
                                                              FRC=VÇrifier le sÇparateur de dates;
                                                              ENC=Cheque Date Separator];
                                                   OptionCaptionML=[ENU=" ,-,.,/";
                                                                    ESM=" ,-,.,/";
                                                                    FRC=" ,-,.,/";
                                                                    ENC=" ,-,.,/"];
                                                   OptionString=[ ,-,.,/] }
  }
  KEYS
  {
    {    ;Key                                     ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      ConfigTemplateManagement@1000 : Codeunit 8612;

    [External]
    PROCEDURE CreateFieldRefArray@12(VAR FieldRefArray@1000 : ARRAY [20] OF FieldRef;RecRef@1003 : RecordRef);
    VAR
      I@1002 : Integer;
    BEGIN
      I := 1;

      AddToArray(FieldRefArray,I,RecRef.FIELD(FIELDNO(City)));
      AddToArray(FieldRefArray,I,RecRef.FIELD(FIELDNO("Vendor Posting Group")));
      AddToArray(FieldRefArray,I,RecRef.FIELD(FIELDNO("Currency Code")));
      AddToArray(FieldRefArray,I,RecRef.FIELD(FIELDNO("Language Code")));
      AddToArray(FieldRefArray,I,RecRef.FIELD(FIELDNO("Payment Terms Code")));
      AddToArray(FieldRefArray,I,RecRef.FIELD(FIELDNO("Fin. Charge Terms Code")));
      AddToArray(FieldRefArray,I,RecRef.FIELD(FIELDNO("Invoice Disc. Code")));
      AddToArray(FieldRefArray,I,RecRef.FIELD(FIELDNO("Country/Region Code")));
      AddToArray(FieldRefArray,I,RecRef.FIELD(FIELDNO("Payment Method Code")));
      AddToArray(FieldRefArray,I,RecRef.FIELD(FIELDNO("Application Method")));
      AddToArray(FieldRefArray,I,RecRef.FIELD(FIELDNO("Prices Including VAT")));
      AddToArray(FieldRefArray,I,RecRef.FIELD(FIELDNO("Gen. Bus. Posting Group")));
      AddToArray(FieldRefArray,I,RecRef.FIELD(FIELDNO("Post Code")));
      AddToArray(FieldRefArray,I,RecRef.FIELD(FIELDNO(County)));
      AddToArray(FieldRefArray,I,RecRef.FIELD(FIELDNO("Tax Liable")));
      AddToArray(FieldRefArray,I,RecRef.FIELD(FIELDNO("VAT Bus. Posting Group")));
      AddToArray(FieldRefArray,I,RecRef.FIELD(FIELDNO("Block Payment Tolerance")));
      AddToArray(FieldRefArray,I,RecRef.FIELD(FIELDNO("Check Date Format")));
      AddToArray(FieldRefArray,I,RecRef.FIELD(FIELDNO("Check Date Separator")));
      AddToArray(FieldRefArray,I,RecRef.FIELD(FIELDNO("Validate EU Vat Reg. No.")));
      OnAfterCreateFieldRefArray(FieldRefArray,RecRef);
    END;

    LOCAL PROCEDURE AddToArray@4(VAR FieldRefArray@1000 : ARRAY [21] OF FieldRef;VAR I@1001 : Integer;CurrFieldRef@1002 : FieldRef);
    BEGIN
      FieldRefArray[I] := CurrFieldRef;
      I += 1;
    END;

    [External]
    PROCEDURE InitializeTempRecordFromConfigTemplate@1(VAR TempMiniVendorTemplate@1000 : TEMPORARY Record 1303;ConfigTemplateHeader@1001 : Record 8618);
    VAR
      RecRef@1003 : RecordRef;
    BEGIN
      TempMiniVendorTemplate.INIT;
      TempMiniVendorTemplate.Code := ConfigTemplateHeader.Code;
      TempMiniVendorTemplate."Template Name" := ConfigTemplateHeader.Description;
      TempMiniVendorTemplate.INSERT;

      RecRef.GETTABLE(TempMiniVendorTemplate);

      ConfigTemplateManagement.ApplyTemplateLinesWithoutValidation(ConfigTemplateHeader,RecRef);

      RecRef.SETTABLE(TempMiniVendorTemplate);
    END;

    [External]
    PROCEDURE CreateConfigTemplateFromExistingVendor@5(Vendor@1000 : Record 23;VAR TempMiniVendorTemplate@1001 : TEMPORARY Record 1303);
    VAR
      DimensionsTemplate@1002 : Record 1302;
      ConfigTemplateHeader@1006 : Record 8618;
      RecRef@1005 : RecordRef;
      FieldRefArray@1004 : ARRAY [20] OF FieldRef;
      NewTemplateCode@1003 : Code[10];
    BEGIN
      RecRef.GETTABLE(Vendor);
      CreateFieldRefArray(FieldRefArray,RecRef);

      ConfigTemplateManagement.CreateConfigTemplateAndLines(NewTemplateCode,'',DATABASE::Vendor,FieldRefArray);
      ConfigTemplateHeader.GET(NewTemplateCode);
      DimensionsTemplate.CreateTemplatesFromExistingMasterRecord(Vendor."No.",NewTemplateCode,DATABASE::Vendor);
      InitializeTempRecordFromConfigTemplate(TempMiniVendorTemplate,ConfigTemplateHeader);
    END;

    [External]
    PROCEDURE SaveAsTemplate@7(Vendor@1000 : Record 23);
    VAR
      TempMiniVendorTemplate@1001 : TEMPORARY Record 1303;
      VendorTemplateCard@1002 : Page 1344;
    BEGIN
      VendorTemplateCard.CreateFromVend(Vendor);
      VendorTemplateCard.SETRECORD(TempMiniVendorTemplate);
      VendorTemplateCard.LOOKUPMODE := TRUE;
      IF VendorTemplateCard.RUNMODAL = ACTION::LookupOK THEN;
    END;

    LOCAL PROCEDURE InsertConfigurationTemplateHeaderAndLines@2();
    VAR
      FieldRefArray@1001 : ARRAY [20] OF FieldRef;
      RecRef@1000 : RecordRef;
    BEGIN
      RecRef.GETTABLE(Rec);
      CreateFieldRefArray(FieldRefArray,RecRef);
      ConfigTemplateManagement.CreateConfigTemplateAndLines(Code,"Template Name",DATABASE::Vendor,FieldRefArray);
    END;

    [External]
    PROCEDURE NewVendorFromTemplate@3(VAR Vendor@1002 : Record 23) : Boolean;
    VAR
      ConfigTemplateHeader@1004 : Record 8618;
      ConfigTemplates@1001 : Page 1340;
    BEGIN
      ConfigTemplateHeader.SETRANGE("Table ID",DATABASE::Vendor);
      ConfigTemplateHeader.SETRANGE(Enabled,TRUE);

      IF ConfigTemplateHeader.COUNT = 1 THEN BEGIN
        ConfigTemplateHeader.FINDFIRST;
        InsertVendorFromTemplate(ConfigTemplateHeader,Vendor);
        EXIT(TRUE);
      END;

      IF (ConfigTemplateHeader.COUNT > 1) AND GUIALLOWED THEN BEGIN
        ConfigTemplates.SETTABLEVIEW(ConfigTemplateHeader);
        ConfigTemplates.LOOKUPMODE(TRUE);
        ConfigTemplates.SetNewMode;
        IF ConfigTemplates.RUNMODAL = ACTION::LookupOK THEN BEGIN
          ConfigTemplates.GETRECORD(ConfigTemplateHeader);
          InsertVendorFromTemplate(ConfigTemplateHeader,Vendor);
          EXIT(TRUE);
        END;
      END;

      EXIT(FALSE);
    END;

    [External]
    PROCEDURE UpdateVendorFromTemplate@8(VAR Vendor@1000 : Record 23);
    VAR
      ConfigTemplateHeader@1001 : Record 8618;
      DimensionsTemplate@1004 : Record 1302;
      ConfigTemplates@1003 : Page 1340;
      VendorRecRef@1002 : RecordRef;
    BEGIN
      IF GUIALLOWED THEN BEGIN
        ConfigTemplateHeader.SETRANGE("Table ID",DATABASE::Vendor);
        ConfigTemplateHeader.SETRANGE(Enabled,TRUE);
        ConfigTemplates.SETTABLEVIEW(ConfigTemplateHeader);
        ConfigTemplates.LOOKUPMODE(TRUE);
        IF ConfigTemplates.RUNMODAL = ACTION::LookupOK THEN BEGIN
          ConfigTemplates.GETRECORD(ConfigTemplateHeader);
          VendorRecRef.GETTABLE(Vendor);
          ConfigTemplateManagement.UpdateRecord(ConfigTemplateHeader,VendorRecRef);
          DimensionsTemplate.InsertDimensionsFromTemplates(ConfigTemplateHeader,Vendor."No.",DATABASE::Vendor);
          VendorRecRef.SETTABLE(Vendor);
          Vendor.FIND;
        END;
      END;
    END;

    [External]
    PROCEDURE InsertVendorFromTemplate@16(ConfigTemplateHeader@1006 : Record 8618;VAR Vendor@1003 : Record 23);
    VAR
      DimensionsTemplate@1001 : Record 1302;
      ConfigTemplateMgt@1002 : Codeunit 8612;
      RecRef@1000 : RecordRef;
    BEGIN
      Vendor.SetInsertFromTemplate(TRUE);
      InitVendorNo(Vendor,ConfigTemplateHeader);
      Vendor.INSERT(TRUE);
      RecRef.GETTABLE(Vendor);
      ConfigTemplateMgt.UpdateRecord(ConfigTemplateHeader,RecRef);
      RecRef.SETTABLE(Vendor);

      DimensionsTemplate.InsertDimensionsFromTemplates(ConfigTemplateHeader,Vendor."No.",DATABASE::Vendor);
      Vendor.FIND;
    END;

    [External]
    PROCEDURE UpdateVendorsFromTemplate@6(VAR Vendor@1000 : Record 23);
    VAR
      ConfigTemplateHeader@1001 : Record 8618;
      DimensionsTemplate@1004 : Record 1302;
      ConfigTemplates@1003 : Page 1340;
      VendorRecRef@1002 : RecordRef;
    BEGIN
      IF GUIALLOWED THEN BEGIN
        ConfigTemplateHeader.SETRANGE("Table ID",DATABASE::Vendor);
        ConfigTemplateHeader.SETRANGE(Enabled,TRUE);
        ConfigTemplates.SETTABLEVIEW(ConfigTemplateHeader);
        ConfigTemplates.LOOKUPMODE(TRUE);
        IF ConfigTemplates.RUNMODAL = ACTION::LookupOK THEN BEGIN
          ConfigTemplates.GETRECORD(ConfigTemplateHeader);
          VendorRecRef.GETTABLE(Vendor);
          IF VendorRecRef.FINDSET THEN
            REPEAT
              ConfigTemplateManagement.UpdateRecord(ConfigTemplateHeader,VendorRecRef);
              DimensionsTemplate.InsertDimensionsFromTemplates(ConfigTemplateHeader,Vendor."No.",DATABASE::Vendor);
            UNTIL VendorRecRef.NEXT = 0;
          VendorRecRef.SETTABLE(Vendor);
        END;
      END;
    END;

    LOCAL PROCEDURE InitVendorNo@9(VAR Vendor@1000 : Record 23;ConfigTemplateHeader@1004 : Record 8618);
    VAR
      NoSeriesMgt@1002 : Codeunit 396;
    BEGIN
      IF ConfigTemplateHeader."Instance No. Series" = '' THEN
        EXIT;
      NoSeriesMgt.InitSeries(ConfigTemplateHeader."Instance No. Series",'',0D,Vendor."No.",Vendor."No. Series");
    END;

    [Integration]
    PROCEDURE OnAfterCreateFieldRefArray@10(VAR FieldRefArray@1000 : ARRAY [23] OF FieldRef;RecRef@1001 : RecordRef);
    BEGIN
    END;

    BEGIN
    END.
  }
}

