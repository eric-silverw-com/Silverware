OBJECT Table 6085632 Approval User Group Permission
{
  OBJECT-PROPERTIES
  {
    Date=05/09/17;
    Time=[ 1:00:00 AM];
    Version List=DCW13.70.00.4.50;
  }
  PROPERTIES
  {
    DataCaptionFields=Approval User Group Code;
    OnInsert=VAR
               ContiniaUserGroupMember@1160040000 : Record 6085631;
               ContiniaUserPermission@1160040001 : Record 6085634;
             BEGIN
               TESTFIELD("Approval User Group Code");
               IF Type = Type::Dimension THEN
                 TESTFIELD("Dimension Code")
               ELSE
                 TESTFIELD("Dimension Code",'');

               ContiniaUserGroupMember.SETRANGE("Approval User Group Code","Approval User Group Code");
               IF ContiniaUserGroupMember.FINDSET THEN
                 REPEAT
                   ContiniaUserPermission.TRANSFERFIELDS(Rec);
                   ContiniaUserPermission."Continia User ID" := ContiniaUserGroupMember."Continia User ID";
                   ContiniaUserPermission.INSERT;
                 UNTIL ContiniaUserGroupMember.NEXT = 0;
             END;

    OnDelete=VAR
               AppUserGroupPmsSelection@1160040002 : Record 6085633;
               ContiniaUserPermission@1160040000 : Record 6085634;
               ContiniaUserPmsSelection@1160040001 : Record 6085635;
             BEGIN
               TESTFIELD("Approval User Group Code");

               AppUserGroupPmsSelection.SETRANGE("Approval User Group Code","Approval User Group Code");
               AppUserGroupPmsSelection.SETRANGE(Type,Type);
               AppUserGroupPmsSelection.SETRANGE("Dimension Code","Dimension Code");
               IF NOT AppUserGroupPmsSelection.ISEMPTY THEN
                 AppUserGroupPmsSelection.DELETEALL(TRUE);

               ContiniaUserPermission.SETRANGE("Approval User Group Code","Approval User Group Code");
               ContiniaUserPermission.SETRANGE(Type,Type);
               ContiniaUserPermission.SETRANGE("Dimension Code","Dimension Code");
               IF NOT ContiniaUserPermission.ISEMPTY THEN
                 ContiniaUserPermission.DELETEALL;

               ContiniaUserPmsSelection.SETRANGE("Approval User Group Code","Approval User Group Code");
               ContiniaUserPmsSelection.SETRANGE(Type,Type);
               ContiniaUserPmsSelection.SETRANGE("Dimension Code","Dimension Code");
               IF NOT ContiniaUserPmsSelection.ISEMPTY THEN
                 ContiniaUserPmsSelection.DELETEALL;
             END;

    OnRename=BEGIN
               ERROR(Text000,TABLECAPTION);
             END;

    CaptionML=ENU=Approval User Group Permission;
    PasteIsValid=No;
  }
  FIELDS
  {
    { 2   ;   ;Approval User Group Code;Code20    ;TableRelation="Approval User Group";
                                                   CaptionML=ENU=Approval User Group Code;
                                                   NotBlank=Yes }
    { 3   ;   ;Type                ;Option        ;CaptionML=ENU=Type;
                                                   OptionCaptionML=ENU=G/L Account,Item,,Fixed Asset,Charge (Item),Dimension,Job;
                                                   OptionString=G/L Account,Item,,Fixed Asset,Charge (Item),Dimension,Job }
    { 4   ;   ;Dimension Code      ;Code20        ;TableRelation=IF (Type=CONST(Dimension)) Dimension.Code;
                                                   OnValidate=BEGIN
                                                                IF "Dimension Code" <> '' THEN
                                                                  TESTFIELD(Type,Type::Dimension);
                                                              END;

                                                   CaptionML=ENU=Dimension Code }
    { 5   ;   ;Assigning Permission;Option        ;OnValidate=VAR
                                                                AppUserGroupPmsSelection@1160040004 : Record 6085633;
                                                                ContiniaUserPmsSelection@1160040003 : Record 6085635;
                                                              BEGIN
                                                                IF ("Assigning Filter" <> '') AND ("Assigning Permission" <> "Assigning Permission"::Filter) THEN BEGIN
                                                                  IF NOT CONFIRM(Text001,TRUE,FIELDCAPTION("Assigning Filter"),"Assigning Filter",FIELDCAPTION("Assigning Permission"),
                                                                    "Assigning Permission")
                                                                  THEN
                                                                    ERROR('');

                                                                  "Assigning Filter" := '';
                                                                END;

                                                                IF ((xRec."Assigning Permission" = xRec."Assigning Permission"::"Include Selected") AND
                                                                  ("Assigning Permission" <> "Assigning Permission"::"Include Selected")) OR
                                                                  ((xRec."Assigning Permission" = xRec."Assigning Permission"::"Exclude Selected") AND
                                                                  ("Assigning Permission" <> "Assigning Permission"::"Exclude Selected"))
                                                                THEN BEGIN
                                                                  CALCFIELDS("No. of Assigning Selections");
                                                                  IF "No. of Assigning Selections" <> 0 THEN BEGIN
                                                                    IF NOT CONFIRM(Text002,TRUE,FIELDCAPTION("Assigning Permission"),xRec."Assigning Permission","Assigning Permission",
                                                                      "No. of Assigning Selections")
                                                                    THEN
                                                                      ERROR('');

                                                                    AppUserGroupPmsSelection.SETRANGE("Approval User Group Code","Approval User Group Code");
                                                                    AppUserGroupPmsSelection.SETRANGE(Type,Type);
                                                                    AppUserGroupPmsSelection.SETRANGE("Dimension Code","Dimension Code");
                                                                    AppUserGroupPmsSelection.SETRANGE("Permission Type",AppUserGroupPmsSelection."Permission Type"::"Assigning Permission");
                                                                    IF NOT AppUserGroupPmsSelection.ISEMPTY THEN
                                                                      AppUserGroupPmsSelection.DELETEALL;

                                                                    ContiniaUserPmsSelection.SETRANGE("Approval User Group Code","Approval User Group Code");
                                                                    ContiniaUserPmsSelection.SETRANGE(Type,Type);
                                                                    ContiniaUserPmsSelection.SETRANGE("Dimension Code","Dimension Code");
                                                                    ContiniaUserPmsSelection.SETRANGE("Permission Type",ContiniaUserPmsSelection."Permission Type"::"Assigning Permission");
                                                                    IF NOT ContiniaUserPmsSelection.ISEMPTY THEN
                                                                      ContiniaUserPmsSelection.DELETEALL;

                                                                    CALCFIELDS("No. of Assigning Selections");
                                                                  END;
                                                                END;

                                                                UpdateContiniaUserPermission;
                                                              END;

                                                   CaptionML=ENU=Assigning Permission;
                                                   OptionCaptionML=ENU=" ,All,Include Selected,Exclude Selected,Filter";
                                                   OptionString=[ ,All,Include Selected,Exclude Selected,Filter] }
    { 6   ;   ;Approval Permission ;Option        ;OnValidate=VAR
                                                                AppUserGroupPmsSelection@1160040004 : Record 6085633;
                                                                ContiniaUserPmsSelection@1160040002 : Record 6085635;
                                                              BEGIN
                                                                IF ("Approval Filter" <> '') AND ("Approval Permission" <> "Approval Permission"::"Same as Assigning") THEN BEGIN
                                                                  IF NOT CONFIRM(Text001,TRUE,FIELDCAPTION("Approval Filter"),"Approval Filter",FIELDCAPTION("Approval Permission"),
                                                                    "Approval Permission")
                                                                  THEN
                                                                    ERROR('');

                                                                  "Approval Filter" := '';
                                                                END;

                                                                IF ((xRec."Approval Permission" = xRec."Approval Permission"::"Include Selected") AND
                                                                  ("Approval Permission" <> "Approval Permission"::"Include Selected")) OR
                                                                  ((xRec."Approval Permission" = xRec."Approval Permission"::"Exclude Selected") AND
                                                                  ("Approval Permission" <> "Approval Permission"::"Exclude Selected"))
                                                                THEN BEGIN
                                                                  CALCFIELDS("No. of Approval Selections");
                                                                  IF "No. of Approval Selections" <> 0 THEN BEGIN
                                                                    IF NOT CONFIRM(Text002,TRUE,FIELDCAPTION("Approval Permission"),xRec."Approval Permission","Approval Permission",
                                                                      "No. of Approval Selections")
                                                                    THEN
                                                                      ERROR('');

                                                                    AppUserGroupPmsSelection.SETRANGE("Approval User Group Code","Approval User Group Code");
                                                                    AppUserGroupPmsSelection.SETRANGE(Type,Type);
                                                                    AppUserGroupPmsSelection.SETRANGE("Dimension Code","Dimension Code");
                                                                    AppUserGroupPmsSelection.SETRANGE("Permission Type",AppUserGroupPmsSelection."Permission Type"::"Approval Permission");
                                                                    IF NOT AppUserGroupPmsSelection.ISEMPTY THEN
                                                                      AppUserGroupPmsSelection.DELETEALL;

                                                                    ContiniaUserPmsSelection.SETRANGE("Approval User Group Code","Approval User Group Code");
                                                                    ContiniaUserPmsSelection.SETRANGE(Type,Type);
                                                                    ContiniaUserPmsSelection.SETRANGE("Dimension Code","Dimension Code");
                                                                    ContiniaUserPmsSelection.SETRANGE("Permission Type",ContiniaUserPmsSelection."Permission Type"::"Approval Permission");
                                                                    IF NOT ContiniaUserPmsSelection.ISEMPTY THEN
                                                                      ContiniaUserPmsSelection.DELETEALL;

                                                                    CALCFIELDS("No. of Approval Selections");
                                                                  END;
                                                                END;

                                                                UpdateContiniaUserPermission;
                                                              END;

                                                   CaptionML=ENU=Approval Permission;
                                                   OptionCaptionML=ENU=" ,All,Include Selected,Exclude Selected,Filter,Same as Assigning";
                                                   OptionString=[ ,All,Include Selected,Exclude Selected,Filter,Same as Assigning] }
    { 7   ;   ;Assigning Filter    ;Code250       ;OnValidate=BEGIN
                                                                IF "Assigning Filter" <> '' THEN
                                                                  TESTFIELD("Assigning Permission","Assigning Permission"::Filter);

                                                                UpdateContiniaUserPermission;
                                                              END;

                                                   CaptionML=ENU=Assigning Filter }
    { 8   ;   ;Approval Filter     ;Code250       ;OnValidate=BEGIN
                                                                IF "Approval Filter" <> '' THEN
                                                                  TESTFIELD("Approval Permission","Approval Permission"::Filter);

                                                                UpdateContiniaUserPermission;
                                                              END;

                                                   CaptionML=ENU=Approval Filter }
    { 9   ;   ;No. of Assigning Selections;Integer;FieldClass=FlowField;
                                                   CalcFormula=Count("App. User Group Pms. Selection" WHERE (Approval User Group Code=FIELD(Approval User Group Code),
                                                                                                             Type=FIELD(Type),
                                                                                                             Dimension Code=FIELD(Dimension Code),
                                                                                                             Permission Type=CONST(Assigning Permission)));
                                                   CaptionML=ENU=No. of Assigning Selections;
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 10  ;   ;No. of Approval Selections;Integer ;FieldClass=FlowField;
                                                   CalcFormula=Count("App. User Group Pms. Selection" WHERE (Approval User Group Code=FIELD(Approval User Group Code),
                                                                                                             Type=FIELD(Type),
                                                                                                             Dimension Code=FIELD(Dimension Code),
                                                                                                             Permission Type=CONST(Approval Permission)));
                                                   CaptionML=ENU=No. of Approval Selections;
                                                   BlankZero=Yes;
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Approval User Group Code,Type,Dimension Code;
                                                   Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1160040000 : TextConst 'ENU=You cannot rename a %1.;ENC=You cannot rename a %1.';
      Text001@1160040001 : TextConst 'ENU=The value in %1 is ''%2''. %1 will be set to blank if you change %3 to %4.\\Are you sure you want to continue?';
      Text002@1160040002 : TextConst 'ENU=You have changed %1 from %2 to %3 and there are %4 selected records. If you continue the %4 selected records will be deleted.\\Are you sure you want to continue?';

    PROCEDURE UpdateContiniaUserPermission@1160040003();
    VAR
      ContiniaUserPermission@1160040000 : Record 6085634;
    BEGIN
      ContiniaUserPermission.SETRANGE(Type,Type);
      ContiniaUserPermission.SETRANGE("Dimension Code","Dimension Code");
      ContiniaUserPermission.SETRANGE("Approval User Group Code","Approval User Group Code");
      IF ContiniaUserPermission.FINDSET(TRUE,FALSE) THEN
        REPEAT
          ContiniaUserPermission.TRANSFERFIELDS(Rec,FALSE);
          ContiniaUserPermission.MODIFY;
        UNTIL ContiniaUserPermission.NEXT = 0;

      MODIFY;
    END;

    PROCEDURE DrillDownSelection@1160040000(PermissionType@1160040001 : 'Assigning Permission,Approval Permission');
    VAR
      PermissionSelectionMgt@1160040000 : Codeunit 6085631;
    BEGIN
      TESTFIELD("Approval User Group Code");
      PermissionSelectionMgt.DrillDownNoOfSelection('',PermissionType,TRUE,"Approval User Group Code",'',
        Type,"Dimension Code","Assigning Permission","Approval Permission",FIELDCAPTION("Assigning Permission"),
        FIELDCAPTION("Approval Permission"));
    END;

    PROCEDURE LookUpPmsFilter@57(PermissionType@1160040000 : 'Assigning Permission,Approval Permission,Both';VAR Text@1001 : Text[250]) : Boolean;
    VAR
      PermissionSelectionMgt@1160040004 : Codeunit 6085631;
    BEGIN
      EXIT(PermissionSelectionMgt.LookUpPmsFilter('',PermissionType,Type,"Dimension Code",Text));
    END;

    PROCEDURE LookUpAppUserGrpCode@1160040001(NewCompanyName@1160040001 : Text[30];VAR Text@1160040000 : Text[250];Editable@1160040003 : Boolean) : Boolean;
    VAR
      PermissionSelectionMgt@1160040002 : Codeunit 6085631;
    BEGIN
      EXIT(PermissionSelectionMgt.LookUpAppUserGrpCode(NewCompanyName,Text,Editable));
    END;

    PROCEDURE GetPermissionTxt@6085573(PermissionType@6085580 : 'Assigning Permission,Approval Permission';_CompanyName@6085583 : Text[30]) : Text[1024];
    VAR
      AppUserGrpMgt@6085584 : Codeunit 6085630;
    BEGIN
      EXIT(AppUserGrpMgt.GetPermissionTxt(_CompanyName,TRUE,Type,"Dimension Code",PermissionType,"Assigning Filter",
        "Assigning Permission","Approval Filter","Approval Permission",'',"Approval User Group Code"));
    END;

    PROCEDURE GetTypeTxt@6085574() : Text[1024];
    VAR
      AppUserGrpMgt@6085573 : Codeunit 6085630;
    BEGIN
      EXIT(AppUserGrpMgt.GetTypeTxt(Type,FORMAT(Type),"Dimension Code"));
    END;

    BEGIN
    END.
  }
}

