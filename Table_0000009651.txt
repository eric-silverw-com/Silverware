OBJECT Table 9651 Report Layout Selection
{
  OBJECT-PROPERTIES
  {
    Date=11/22/17;
    Time=12:00:00 PM;
    Version List=NAVW111.00;
  }
  PROPERTIES
  {
    DataPerCompany=No;
    OnInsert=BEGIN
               TESTFIELD("Report ID");
               IF "Company Name" = '' THEN
                 "Company Name" := COMPANYNAME;
             END;

    CaptionML=[ENU=Report Layout Selection;
               ESM=Selecci¢n de dise¤o de informes;
               FRC=S‚lection pr‚sentation rapport;
               ENC=Report Layout Selection];
  }
  FIELDS
  {
    { 1   ;   ;Report ID           ;Integer       ;CaptionML=[ENU=Report ID;
                                                              ESM=Id. informe;
                                                              FRC=Code rapport;
                                                              ENC=Report ID] }
    { 2   ;   ;Report Name         ;Text80        ;CaptionML=[ENU=Report Name;
                                                              ESM=Nombre informe;
                                                              FRC=Nom de rapport;
                                                              ENC=Report Name];
                                                   Editable=No }
    { 3   ;   ;Company Name        ;Text30        ;TableRelation=Company;
                                                   CaptionML=[ENU=Company Name;
                                                              ESM=Nombre empresa;
                                                              FRC=Nom de compagnie;
                                                              ENC=Company Name] }
    { 4   ;   ;Type                ;Option        ;OnValidate=BEGIN
                                                                TESTFIELD("Report ID");
                                                                CALCFIELDS("Report Caption");
                                                                "Report Name" := "Report Caption";
                                                                CASE Type OF
                                                                  Type::"RDLC (built-in)":
                                                                    BEGIN
                                                                      IF NOT HasRdlcLayout("Report ID") THEN
                                                                        ERROR(NoRdlcLayoutErr,"Report Name");
                                                                      "Custom Report Layout Code" := '';
                                                                    END;
                                                                  Type::"Word (built-in)":
                                                                    BEGIN
                                                                      IF NOT HasWordLayout("Report ID") THEN
                                                                        ERROR(NoWordLayoutErr,"Report Name");
                                                                      "Custom Report Layout Code" := '';
                                                                    END;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Type;
                                                              ESM=Tipo;
                                                              FRC=Type;
                                                              ENC=Type];
                                                   OptionCaptionML=[ENU=RDLC (built-in),Word (built-in),Custom Layout;
                                                                    ESM=RDLC (integrado),Word (integrado),Dise¤o personalizado;
                                                                    FRC=RDLC (int‚gr‚),Word (int‚gr‚),Pr‚sentation personnalis‚e;
                                                                    ENC=RDLC (built-in),Word (built-in),Custom Layout];
                                                   OptionString=RDLC (built-in),Word (built-in),Custom Layout }
    { 6   ;   ;Custom Report Layout Code;Code20   ;TableRelation="Custom Report Layout" WHERE (Report ID=FIELD(Report ID));
                                                   OnValidate=BEGIN
                                                                IF "Custom Report Layout Code" = '' THEN
                                                                  Type := GetDefaultType("Report ID")
                                                                ELSE
                                                                  Type := Type::"Custom Layout";
                                                              END;

                                                   CaptionML=[ENU=Custom Report Layout Code;
                                                              ESM=C¢digo de dise¤o de informe personalizado;
                                                              FRC=Code pr‚sentation rapport personnalis‚;
                                                              ENC=Custom Report Layout Code] }
    { 7   ;   ;Report Layout Description;Text80   ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Custom Report Layout".Description WHERE (Code=FIELD(Custom Report Layout Code)));
                                                   CaptionML=[ENU=Report Layout Description;
                                                              ESM=Descripci¢n de dise¤o de informe;
                                                              FRC=Description pr‚sentation rapport;
                                                              ENC=Report Layout Description] }
    { 8   ;   ;Report Caption      ;Text80        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObjWithCaption."Object Caption" WHERE (Object Type=CONST(Report),
                                                                                                                Object ID=FIELD(Report ID)));
                                                   CaptionML=[ENU=Report Caption;
                                                              ESM=T¡tulo informe;
                                                              FRC=Libell‚ du rapport;
                                                              ENC=Report Caption];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Report ID,Company Name                  ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      NoRdlcLayoutErr@1000 : TextConst '@@@="%1=a report name";ENU=Report ''%1'' has no built-in RDLC layout.;ESM=El informe ''%1'' no tiene ning£n dise¤o de RDLC integrado.;FRC=Le rapport ®ÿ%1ÿ¯ n''a pas de pr‚sentation RDLC int‚gr‚e.;ENC=Report ''%1'' has no built-in RDLC layout.';
      NoWordLayoutErr@1001 : TextConst '@@@="%1=a report name";ENU=Report ''%1'' has no built-in Word layout.;ESM=El informe ''%1'' no tiene un dise¤o de Word integrado.;FRC=Le rapport ®ÿ%1ÿ¯ n''a pas de pr‚sentation Word int‚gr‚e.;ENC=Report ''%1'' has no built-in Word layout.';

    [External]
    PROCEDURE GetDefaultType@2(ReportID@1000 : Integer) : Integer;
    BEGIN
      IF REPORT.DEFAULTLAYOUT(ReportID) = DEFAULTLAYOUT::Word THEN
        EXIT(Type::"Word (built-in)");
      EXIT(Type::"RDLC (built-in)");
    END;

    [External]
    PROCEDURE IsProcessingOnly@20(ReportID@1000 : Integer) : Boolean;
    BEGIN
      EXIT(REPORT.DEFAULTLAYOUT(ReportID) = DEFAULTLAYOUT::None);
    END;

    LOCAL PROCEDURE HasRdlcLayout@21(ReportID@1001 : Integer) : Boolean;
    VAR
      InStr@1000 : InStream;
    BEGIN
      IF REPORT.RDLCLAYOUT(ReportID,InStr) THEN
        EXIT(NOT InStr.EOS);
      EXIT(FALSE);
    END;

    [External]
    PROCEDURE HasWordLayout@23(ReportID@1001 : Integer) : Boolean;
    VAR
      InStr@1000 : InStream;
    BEGIN
      IF REPORT.WORDLAYOUT(ReportID,InStr) THEN
        EXIT(NOT InStr.EOS);
      EXIT(FALSE);
    END;

    [External]
    PROCEDURE HasCustomLayout@1(ReportID@1000 : Integer) : Integer;
    VAR
      CustomReportLayout@1001 : Record 9650;
    BEGIN
      // Temporarily selected layout for Design-time report execution?
      IF GetTempLayoutSelected <> '' THEN
        IF CustomReportLayout.GET(GetTempLayoutSelected) THEN BEGIN
          IF CustomReportLayout.Type = CustomReportLayout.Type::RDLC THEN
            EXIT(1);
          EXIT(2);
        END;

      // Normal selection
      IF NOT GET(ReportID,COMPANYNAME) THEN
        EXIT(0);
      CASE Type OF
        Type::"RDLC (built-in)":
          BEGIN
            IF REPORT.DEFAULTLAYOUT(ReportID) = DEFAULTLAYOUT::RDLC THEN
              EXIT(0);
            EXIT(1);
          END;
        Type::"Word (built-in)":
          EXIT(2);
        Type::"Custom Layout":
          BEGIN
            IF NOT CustomReportLayout.GET("Custom Report Layout Code") THEN
              EXIT(0);
            IF CustomReportLayout.Type = CustomReportLayout.Type::RDLC THEN
              EXIT(1);
            EXIT(2);
          END;
      END;
    END;

    [External]
    PROCEDURE GetTempLayoutSelected@3() : Code[20];
    VAR
      DesignTimeReportSelection@1000 : Codeunit 9654;
    BEGIN
      EXIT(DesignTimeReportSelection.GetSelectedCustomLayout);
    END;

    [External]
    PROCEDURE SetTempLayoutSelected@4(NewTempSelectedLayoutCode@1001 : Code[20]);
    VAR
      DesignTimeReportSelection@1000 : Codeunit 9654;
    BEGIN
      DesignTimeReportSelection.SetSelectedCustomLayout(NewTempSelectedLayoutCode);
    END;

    BEGIN
    END.
  }
}

