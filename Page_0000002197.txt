OBJECT Page 2197 O365 Email Setup Wizard
{
  OBJECT-PROPERTIES
  {
    Date=09/19/18;
    Time=12:00:00 PM;
    Version List=NAVW113.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Email Setup;
               ESM=Configuraci¢n correo electr¢nico;
               FRC=Configuration de courriel;
               ENC=Email Setup];
    SourceTable=Table409;
    PageType=NavigatePage;
    SourceTableTemporary=Yes;
    OnOpenPage=VAR
                 SMTPMailSetup@1000 : Record 409;
                 User@1003 : Record 2000000120;
                 SMTPMail@1002 : Codeunit 400;
               BEGIN
                 INIT;
                 IF SMTPMailSetup.GetSetup AND SMTPMail.IsEnabled THEN BEGIN
                   TRANSFERFIELDS(SMTPMailSetup);
                   GetEmailProvider(SMTPMailSetup)
                 END ELSE BEGIN
                   SMTPMail.ApplyOffice365Smtp(Rec);
                   EmailProvider := EmailProvider::"Office 365";
                   IF User.GET(USERSECURITYID) THEN
                     "User ID" := User."Authentication Email";
                 END;
                 INSERT;
                 IF SMTPMailSetup.HasPassword THEN
                   Password := DummyPasswordTxt;

                 Step := Step::Start;
                 EnableControls;
               END;

    OnQueryClosePage=BEGIN
                       IF CloseAction = ACTION::OK THEN
                         IF NOT SetupCompleted THEN
                           IF NOT CONFIRM(NotSetUpQst,FALSE) THEN
                             ERROR('');
                     END;

    ActionList=ACTIONS
    {
      { 10      ;    ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 15      ;1   ;Action    ;
                      Name=ActionBack;
                      CaptionML=[ENU=Back;
                                 ESM=Atr s;
                                 FRC=Pr‚c‚dent;
                                 ENC=Back];
                      ApplicationArea=#Basic,#Suite,#Invoicing;
                      Enabled=BackActionEnabled;
                      InFooterBar=Yes;
                      Image=PreviousRecord;
                      OnAction=BEGIN
                                 NextStep(TRUE);
                               END;
                                }
      { 14      ;1   ;Action    ;
                      Name=ActionNext;
                      CaptionML=[ENU=Next;
                                 ESM=Siguiente;
                                 FRC=Suivant;
                                 ENC=Next];
                      ApplicationArea=#Basic,#Suite,#Invoicing;
                      Enabled=NextActionEnabled;
                      InFooterBar=Yes;
                      Image=NextRecord;
                      OnAction=BEGIN
                                 CASE Step OF
                                   Step::Settings:
                                     IF (Authentication = Authentication::Basic) AND (("User ID" = '') OR (Password = '')) THEN
                                       ERROR(EmailPasswordMissingErr);
                                 END;

                                 NextStep(FALSE);
                               END;
                                }
      { 11      ;1   ;Action    ;
                      Name=ActionFinish;
                      CaptionML=[ENU=Finish;
                                 ESM=Terminar;
                                 FRC=Terminer;
                                 ENC=Finish];
                      ApplicationArea=#Basic,#Suite,#Invoicing;
                      Enabled=FinishActionEnabled;
                      InFooterBar=Yes;
                      Image=Approve;
                      OnAction=BEGIN
                                 FinishAction;
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1   ;0   ;Container ;
                ContainerType=ContentArea }

    { 20  ;1   ;Group     ;
                Visible=FirstStepVisible;
                GroupType=Group }

    { 13  ;2   ;Group     ;
                CaptionML=[ENU=Welcome to Email Setup;
                           ESM=Configuraci¢n de correo electr¢nico;
                           FRC=Bienvenue dans la configuration de courriel;
                           ENC=Welcome to Email Setup];
                Visible=FirstStepVisible;
                GroupType=Group }

    { 18  ;3   ;Group     ;
                GroupType=Group;
                InstructionalTextML=[ENU=Let's set up the account from which invoice emails will be sent.;
                                     ESM=Vamos a configurar la cuenta desde la que se enviar n los mensajes de correo electr¢nico para facturas.;
                                     FRC=Nous allons maintenant configurer le compte … partir duquel les courriels de facture seront envoy‚s.;
                                     ENC=Let's set up the account from which invoice emails will be sent.] }

    { 2   ;1   ;Group     ;
                Visible=FirstStepVisible OR SettingsStepVisible;
                Editable=FirstStepVisible;
                GroupType=Group }

    { 19  ;2   ;Group     ;
                Visible=NOT SettingsStepVisible;
                Editable=FirstStepVisible;
                GroupType=Group;
                InstructionalTextML=[ENU=Choose your email account type;
                                     ESM=Elija el tipo de cuenta de correo electr¢nico;
                                     FRC=Choisir le type de compte de messagerie;
                                     ENC=Choose your email account type] }

    { 3   ;2   ;Field     ;
                Name=Email Provider;
                CaptionML=[ENU=Email Account type;
                           ESM=Tipo de cuenta de correo electr¢nico;
                           FRC=Type de compte de messagerie;
                           ENC=Email Account type];
                ApplicationArea=#Basic,#Suite,#Invoicing;
                SourceExpr=EmailProvider;
                OnValidate=BEGIN
                             CASE EmailProvider OF
                               EmailProvider::"Office 365":
                                 SMTPMail.ApplyOffice365Smtp(Rec);
                               EmailProvider::Outlook:
                                 SMTPMail.ApplyOutlookSmtp(Rec);
                               EmailProvider::Gmail:
                                 SMTPMail.ApplyGmailSmtp(Rec);
                               EmailProvider::Yahoo:
                                 SMTPMail.ApplyYahooSmtp(Rec);
                               EmailProvider::"Other Email Provider":
                                 "SMTP Server" := '';
                             END;
                             AdvancedSettingsVisible := EmailProvider = EmailProvider::"Other Email Provider";
                             EnableControls;
                           END;

                ShowCaption=No }

    { 12  ;1   ;Group     ;
                Visible=SettingsStepVisible;
                GroupType=Group }

    { 27  ;2   ;Group     ;
                Visible=AdvancedSettingsVisible;
                GroupType=Group;
                InstructionalTextML=[ENU=Enter the SMTP Server Details.;
                                     ESM=Especifique los detalles del servidor SMTP.;
                                     FRC=Saisissez les d‚tails du serveur SMTP.;
                                     ENC=Enter the SMTP Server Details.] }

    { 9   ;3   ;Field     ;
                Name=Authentication;
                ApplicationArea=#Basic,#Suite,#Invoicing;
                SourceExpr=Authentication;
                OnValidate=BEGIN
                             EnableControls;
                           END;
                            }

    { 8   ;3   ;Field     ;
                ApplicationArea=#Basic,#Suite,#Invoicing;
                SourceExpr="SMTP Server";
                OnValidate=BEGIN
                             EnableControls;
                           END;

                ShowMandatory=TRUE }

    { 7   ;3   ;Field     ;
                ApplicationArea=#Basic,#Suite,#Invoicing;
                SourceExpr="SMTP Server Port" }

    { 6   ;3   ;Field     ;
                Name=Secure Connection;
                ApplicationArea=#Basic,#Suite,#Invoicing;
                SourceExpr="Secure Connection" }

    { 26  ;2   ;Group     ;
                Visible=MailSettingsVisible;
                GroupType=Group;
                InstructionalTextML=[ENU=Enter the credentials for the account, which will be used for sending the invoice emails.;
                                     ESM=Especifique las credenciales de la cuenta que se usar  para enviar mensajes de correo electr¢nico de facturas.;
                                     FRC=Saisissez les informations d'identification du compte qui seront utilis‚es pour envoyer les courriels de facture.;
                                     ENC=Enter the credentials for the account, which will be used for sending the invoice emails.] }

    { 5   ;3   ;Field     ;
                ExtendedDatatype=E-Mail;
                CaptionML=[ENU=Email;
                           ESM=Correo electr¢nico;
                           FRC=Adresse de messagerie;
                           ENC=Email];
                ApplicationArea=#Basic,#Suite,#Invoicing;
                SourceExpr="User ID";
                OnValidate=BEGIN
                             EnableControls;
                           END;
                            }

    { 4   ;3   ;Field     ;
                Name=Password;
                ExtendedDatatype=Masked;
                CaptionML=[ENU=Password;
                           ESM=Contrase¤a;
                           FRC=Mot de passe;
                           ENC=Password];
                ApplicationArea=#Basic,#Suite,#Invoicing;
                SourceExpr=Password;
                OnValidate=BEGIN
                             EnableControls;
                           END;
                            }

    { 17  ;1   ;Group     ;
                Visible=FinalStepVisible;
                GroupType=Group }

    { 23  ;2   ;Group     ;
                CaptionML=[ENU=That's it!;
                           ESM=Eso es todo.;
                           FRC=Voil…ÿ!;
                           ENC=That's it!];
                GroupType=Group;
                InstructionalTextML=[ENU=If you want to verify that the specified email setup works, click on Send test email.;
                                     ESM=Si quiere comprobar que la configuraci¢n de correo electr¢nico especificada funciona, haga clic en Enviar correo electr¢nico de prueba.;
                                     FRC=Si vous souhaitez v‚rifier le bon fonctionnement de la configuration de courriel sp‚cifi‚e, cliquez sur Envoyer un courriel de test.;
                                     ENC=If you want to verify that the specified email setup works, click on Send test email.] }

    { 16  ;3   ;Field     ;
                Name=TestEmailLbl;
                ApplicationArea=#Basic,#Suite,#Invoicing;
                SourceExpr=TestEmailLbl;
                Editable=FALSE;
                Style=StandardAccent;
                StyleExpr=TRUE;
                OnDrillDown=BEGIN
                              SendTestEmailAction;
                            END;

                ShowCaption=No }

  }
  CODE
  {
    VAR
      SMTPMail@1008 : Codeunit 400;
      FirstStepVisible@1000 : Boolean;
      SettingsStepVisible@1011 : Boolean;
      AdvancedSettingsVisible@1003 : Boolean;
      MailSettingsVisible@1002 : Boolean;
      FinalStepVisible@1015 : Boolean;
      FinishActionEnabled@1009 : Boolean;
      BackActionEnabled@1013 : Boolean;
      NextActionEnabled@1014 : Boolean;
      NotSetUpQst@1004 : TextConst 'ENU=Email has not been set up.\\Are you sure you want to exit?;ESM=No se configur¢ el correo electr¢nico.\\¨Est  seguro de que quiere salir?;FRC=La messagerie n''a pas ‚t‚ configur‚e.\\Voulez-vous vraiment quitter l''installationÿ?;ENC=Email has not been set up.\\Are you sure you want to exit?';
      EmailPasswordMissingErr@1006 : TextConst 'ENU=Please enter a valid email address and password.;ESM=Escriba una direcci¢n de correo electr¢nico y contrase¤a v lidas.;FRC=Veuillez entrer une adresse de courriel et un mot de passe valides.;ENC=Please enter a valid email address and password.';
      SetupCompleted@1012 : Boolean;
      Password@1007 : Text[250];
      DummyPasswordTxt@1017 : TextConst '@@@={Locked};ENU=***;ESM=***;FRC=***;ENC=***';
      Step@1010 : 'Start,Settings,Finish';
      EmailProvider@1001 : 'Office 365,Outlook,Gmail,Yahoo,Other Email Provider';
      TestEmailLbl@1005 : TextConst 'ENU=Send test email;ESM=Enviar correo electr¢nico de prueba;FRC=Envoyer un courriel de test;ENC=Send test email';

    LOCAL PROCEDURE EnableControls@2();
    BEGIN
      ResetControls;

      CASE Step OF
        Step::Start:
          ShowStartStep;
        Step::Settings:
          ShowSettingsStep;
        Step::Finish:
          ShowFinishStep;
      END;
    END;

    LOCAL PROCEDURE StoreSMTPSetup@31();
    VAR
      SMTPMailSetup@1000 : Record 409;
    BEGIN
      IF NOT SMTPMailSetup.GET THEN BEGIN
        SMTPMailSetup.INIT;
        SMTPMailSetup.INSERT;
      END;

      SMTPMailSetup.TRANSFERFIELDS(Rec,FALSE);
      IF Password <> DummyPasswordTxt THEN
        SMTPMailSetup.SetPassword(Password);
      SMTPMailSetup.MODIFY(TRUE);
      COMMIT;
    END;

    LOCAL PROCEDURE SendTestEmailAction@10();
    BEGIN
      StoreSMTPSetup;
      CODEUNIT.RUN(CODEUNIT::"SMTP Test Mail");
    END;

    LOCAL PROCEDURE FinishAction@12();
    BEGIN
      StoreSMTPSetup;
      SetupCompleted := TRUE;
      CurrPage.CLOSE;
    END;

    LOCAL PROCEDURE NextStep@41(Backwards@1000 : Boolean);
    BEGIN
      IF Backwards THEN
        Step := Step - 1
      ELSE
        Step := Step + 1;

      EnableControls;
    END;

    LOCAL PROCEDURE ShowStartStep@24();
    BEGIN
      FirstStepVisible := TRUE;
      FinishActionEnabled := FALSE;
      BackActionEnabled := FALSE;
    END;

    LOCAL PROCEDURE ShowSettingsStep@34();
    BEGIN
      SettingsStepVisible := TRUE;
      AdvancedSettingsVisible := EmailProvider = EmailProvider::"Other Email Provider";
      MailSettingsVisible := Authentication = Authentication::Basic;
      NextActionEnabled := "SMTP Server" <> '';
    END;

    LOCAL PROCEDURE ShowFinishStep@35();
    BEGIN
      FinalStepVisible := TRUE;
      NextActionEnabled := FALSE;
      FinishActionEnabled := TRUE;
    END;

    LOCAL PROCEDURE ResetControls@1();
    BEGIN
      FinishActionEnabled := FALSE;
      BackActionEnabled := TRUE;
      NextActionEnabled := TRUE;

      FirstStepVisible := FALSE;
      SettingsStepVisible := FALSE;
      FinalStepVisible := FALSE;
    END;

    LOCAL PROCEDURE GetEmailProvider@11(VAR SMTPMailSetup@1000 : Record 409);
    BEGIN
      IF SMTPMail.IsOffice365Setup(SMTPMailSetup) THEN
        EmailProvider := EmailProvider::"Office 365"
      ELSE
        IF SMTPMail.IsOutlookSetup(SMTPMailSetup) THEN
          EmailProvider := EmailProvider::Outlook
        ELSE
          IF SMTPMail.IsGmailSetup(SMTPMailSetup) THEN
            EmailProvider := EmailProvider::Gmail
          ELSE
            IF SMTPMail.IsYahooSetup(SMTPMailSetup) THEN
              EmailProvider := EmailProvider::Yahoo
            ELSE
              EmailProvider := EmailProvider::"Other Email Provider";
    END;

    BEGIN
    END.
  }
}

