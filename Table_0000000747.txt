OBJECT Table 747 VAT Report Archive
{
  OBJECT-PROPERTIES
  {
    Date=09/19/18;
    Time=12:00:00 PM;
    Version List=NAVW113.00;
  }
  PROPERTIES
  {
    Permissions=TableData 747=rimd;
    CaptionML=[ENU=Tax Report Archive;
               ESM=Archivo de informe de IVA;
               FRC=Archive d‚claration TVA;
               ENC=Tax Report Archive];
  }
  FIELDS
  {
    { 1   ;   ;VAT Report Type     ;Option        ;CaptionML=[ENU=Tax Report Type;
                                                              ESM=Tipo informe IVA;
                                                              FRC=Type d‚claration TVA;
                                                              ENC=Tax Report Type];
                                                   OptionCaptionML=[ENU=EC Sales List,Tax Return;
                                                                    ESM=Lista venta CE,Devoluci¢n de IVA;
                                                                    FRC=Liste des ventes UE,Retour TVA;
                                                                    ENC=EC Sales List,Tax Return];
                                                   OptionString=EC Sales List,VAT Return }
    { 2   ;   ;VAT Report No.      ;Code20        ;TableRelation="VAT Report Header".No.;
                                                   CaptionML=[ENU=Tax Report No.;
                                                              ESM=N.§ informe IVA;
                                                              FRC=Nø d‚claration TVA;
                                                              ENC=Tax Report No.] }
    { 4   ;   ;Submitted By        ;Code50        ;TableRelation=User."User Name";
                                                   TestTableRelation=No;
                                                   DataClassification=EndUserIdentifiableInformation;
                                                   CaptionML=[ENU=Submitted By;
                                                              ESM=Enviado por;
                                                              FRC=Soumis par;
                                                              ENC=Submitted By] }
    { 5   ;   ;Submission Message BLOB;BLOB       ;CaptionML=[ENU=Submission Message BLOB;
                                                              ESM=Mensaje de env¡o BLOB;
                                                              FRC=Envoi Message BLOB;
                                                              ENC=Submission Message BLOB] }
    { 6   ;   ;Submittion Date     ;Date          ;CaptionML=[ENU=Submittion Date;
                                                              ESM=Fecha de env¡o;
                                                              FRC=Date d'envoi;
                                                              ENC=Submittion Date] }
    { 7   ;   ;Response Message BLOB;BLOB         ;CaptionML=[ENU=Response Message BLOB;
                                                              ESM=Mensaje de respuesta BLOB;
                                                              FRC=R‚ponse Message BLOB;
                                                              ENC=Response Message BLOB] }
    { 8   ;   ;Response Received Date;DateTime    ;CaptionML=[ENU=Response Received Date;
                                                              ESM=Fecha de recepci¢n de respuesta;
                                                              FRC=Date de r‚ception de la r‚ponse;
                                                              ENC=Response Received Date] }
  }
  KEYS
  {
    {    ;VAT Report Type,VAT Report No.          ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      FileManagement@1000 : Codeunit 419;
      NoSubmissionMessageAvailableErr@1001 : TextConst 'ENU=The submission message of the report is not available.;ESM=El mensaje de env¡o del informe no est  disponible.;FRC=Le message d''envoi de la d‚claration n''est pas disponible.;ENC=The submission message of the report is not available.';
      NoResponseMessageAvailableErr@1002 : TextConst 'ENU=The response message of the report is not available.;ESM=El mensaje de respuesta del informe no est  disponible.;FRC=Le message de r‚ponse de la d‚claration n''est pas disponible.;ENC=The response message of the report is not available.';

    [External]
    PROCEDURE ArchiveSubmissionMessage@1000(VATReportTypeValue@1001 : Option;VATReportNoValue@1002 : Code[20];SubmissionMessageTempBlob@1003 : Record 99008535) : Boolean;
    VAR
      VATReportArchive@1000 : Record 747;
    BEGIN
      IF VATReportNoValue = '' THEN
        EXIT(FALSE);
      IF NOT SubmissionMessageTempBlob.Blob.HASVALUE THEN
        EXIT(FALSE);
      IF VATReportArchive.GET(VATReportTypeValue,VATReportNoValue) THEN
        EXIT(FALSE);

      VATReportArchive.INIT;
      VATReportArchive."VAT Report No." := VATReportNoValue;
      VATReportArchive."VAT Report Type" := VATReportTypeValue;
      VATReportArchive."Submitted By" := USERID;
      VATReportArchive."Submittion Date" := TODAY;
      VATReportArchive."Submission Message BLOB" := SubmissionMessageTempBlob.Blob;
      VATReportArchive.INSERT(TRUE);
      EXIT(TRUE);
    END;

    [External]
    PROCEDURE ArchiveResponseMessage@1001(VATReportTypeValue@1010 : Option;VATReportNoValue@1011 : Code[20];ResponseMessageTempBlob@1000 : Record 99008535) : Boolean;
    VAR
      VATReportArchive@1001 : Record 747;
    BEGIN
      IF NOT VATReportArchive.GET(VATReportTypeValue,VATReportNoValue) THEN
        EXIT(FALSE);
      IF NOT ResponseMessageTempBlob.Blob.HASVALUE THEN
        EXIT(FALSE);

      VATReportArchive."Response Received Date" := CURRENTDATETIME;
      VATReportArchive."Response Message BLOB" := ResponseMessageTempBlob.Blob;
      VATReportArchive.MODIFY(TRUE);

      EXIT(TRUE);
    END;

    [External]
    PROCEDURE DownloadSubmissionMessage@3(VATReportTypeValue@1001 : Option;VATReportNoValue@1000 : Code[20]);
    VAR
      VATReportArchive@1004 : Record 747;
      TempBlob@1006 : Record 99008535;
      ServerFileName@1005 : Text;
      ZipFileName@1007 : Text[250];
    BEGIN
      IF NOT VATReportArchive.GET(VATReportTypeValue,VATReportNoValue) THEN
        ERROR(NoSubmissionMessageAvailableErr);

      IF NOT VATReportArchive."Submission Message BLOB".HASVALUE THEN
        ERROR(NoSubmissionMessageAvailableErr);

      VATReportArchive.CALCFIELDS("Submission Message BLOB");
      TempBlob.INIT;
      TempBlob.Blob := VATReportArchive."Submission Message BLOB";

      ServerFileName := FileManagement.ServerTempFileName('xml');
      FileManagement.BLOBExportToServerFile(TempBlob,ServerFileName);

      ZipFileName := VATReportNoValue + '_Submission.txt';
      DownloadZipFile(ZipFileName,ServerFileName);
      FileManagement.DeleteServerFile(ServerFileName);
    END;

    [External]
    PROCEDURE DownloadResponseMessage@2(VATReportTypeValue@1001 : Option;VATReportNoValue@1000 : Code[20]);
    VAR
      VATReportArchive@1008 : Record 747;
      TempBlob@1007 : Record 99008535;
      ServerFileName@1005 : Text;
      ZipFileName@1002 : Text[250];
    BEGIN
      IF NOT VATReportArchive.GET(VATReportTypeValue,VATReportNoValue) THEN
        ERROR(NoResponseMessageAvailableErr);

      IF NOT VATReportArchive."Response Message BLOB".HASVALUE THEN
        ERROR(NoResponseMessageAvailableErr);

      VATReportArchive.CALCFIELDS("Response Message BLOB");
      TempBlob.INIT;
      TempBlob.Blob := VATReportArchive."Response Message BLOB";

      ServerFileName := FileManagement.ServerTempFileName('xml');
      FileManagement.BLOBExportToServerFile(TempBlob,ServerFileName);

      ZipFileName := VATReportNoValue + '_Response.txt';
      DownloadZipFile(ZipFileName,ServerFileName);
      FileManagement.DeleteServerFile(ServerFileName);
    END;

    LOCAL PROCEDURE DownloadZipFile@4(ZipFileName@1000 : Text[250];ServerFileName@1002 : Text);
    VAR
      ZipArchiveName@1001 : Text;
    BEGIN
      ZipArchiveName := FileManagement.CreateZipArchiveObject;
      FileManagement.AddFileToZipArchive(ServerFileName,ZipFileName);

      FileManagement.CloseZipArchive;
      FileManagement.DownloadHandler(ZipArchiveName,'','','',ZipFileName + '.zip');
    END;

    BEGIN
    END.
  }
}

