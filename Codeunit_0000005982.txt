OBJECT Codeunit 5982 Service-Post+Print
{
  OBJECT-PROPERTIES
  {
    Date=09/19/18;
    Time=12:00:00 PM;
    Version List=NAVW113.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      Text000@1002 : TextConst 'ENU=&Ship,&Invoice,Ship &and Invoice,Ship and &Consume;ESM=&Enviar,&Facturar,Enviar &y facturar,Enviar y &consumir;FRC=&Livrer,&Facturer,Livrer &et facturer,Livrer et &consommer;ENC=&Ship,&Invoice,Ship &and Invoice,Ship and &Consume';
      Text001@1001 : TextConst 'ENU=Do you want to post and print the %1?;ESM=¨Confirma que desea registrar e imprimir el/la %1?;FRC=Voulez-vous reporter et imprimer la %1?;ENC=Do you want to post and print the %1?';
      ServiceHeader@1003 : Record 5900;
      ServShptHeader@1004 : Record 5990;
      ServInvHeader@1005 : Record 5992;
      ServCrMemoHeader@1006 : Record 5994;
      ServicePost@1009 : Codeunit 5980;
      Selection@1010 : Integer;
      Ship@1011 : Boolean;
      Consume@1007 : Boolean;
      Invoice@1000 : Boolean;

    PROCEDURE PostDocument@5(VAR Rec@1000 : Record 5900);
    VAR
      DummyServLine@1001 : TEMPORARY Record 5902;
    BEGIN
      ServiceHeader.COPY(Rec);
      Code(DummyServLine);
      Rec := ServiceHeader;
    END;

    LOCAL PROCEDURE Code@2(VAR PassedServLine@1002 : Record 5902);
    VAR
      HideDialog@1000 : Boolean;
      IsHandled@1001 : Boolean;
    BEGIN
      HideDialog := FALSE;
      IsHandled := FALSE;
      OnBeforeConfirmPost(ServiceHeader,HideDialog,Ship,Consume,Invoice,IsHandled);
      IF IsHandled THEN
        EXIT;

      WITH ServiceHeader DO BEGIN
        CASE "Document Type" OF
          "Document Type"::Order:
            BEGIN
              Selection := STRMENU(Text000,3);
              IF Selection = 0 THEN
                EXIT;
              Ship := Selection IN [1,3,4];
              Consume := Selection IN [4];
              Invoice := Selection IN [2,3];
            END
          ELSE
            IF NOT CONFIRM(Text001,FALSE,"Document Type") THEN
              EXIT;
        END;

        OnAfterConfirmPost(ServiceHeader,Ship,Consume,Invoice);

        ServicePost.PostWithLines(ServiceHeader,PassedServLine,Ship,Consume,Invoice);
        GetReport(ServiceHeader);
        COMMIT;
      END;
    END;

    LOCAL PROCEDURE GetReport@3(VAR ServiceHeader@1000 : Record 5900);
    BEGIN
      WITH ServiceHeader DO
        CASE "Document Type" OF
          "Document Type"::Order:
            BEGIN
              IF Ship THEN BEGIN
                ServShptHeader."No." := "Last Shipping No.";
                ServShptHeader.SETRECFILTER;
                ServShptHeader.PrintRecords(FALSE);
              END;
              IF Invoice THEN BEGIN
                ServInvHeader."No." := "Last Posting No.";
                ServInvHeader.SETRECFILTER;
                ServInvHeader.PrintRecords(FALSE);
              END;
            END;
          "Document Type"::Invoice:
            BEGIN
              IF "Last Posting No." = '' THEN
                ServInvHeader."No." := "No."
              ELSE
                ServInvHeader."No." := "Last Posting No.";
              ServInvHeader.SETRECFILTER;
              ServInvHeader.PrintRecords(FALSE);
            END;
          "Document Type"::"Credit Memo":
            BEGIN
              IF "Last Posting No." = '' THEN
                ServCrMemoHeader."No." := "No."
              ELSE
                ServCrMemoHeader."No." := "Last Posting No.";
              ServCrMemoHeader.SETRECFILTER;
              ServCrMemoHeader.PrintRecords(FALSE);
            END;
        END;
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterConfirmPost@1(ServiceHeader@1000 : Record 5900;Ship@1001 : Boolean;Consume@1002 : Boolean;Invoice@1003 : Boolean);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeConfirmPost@4(VAR ServiceHeader@1003 : Record 5900;VAR HideDialog@1004 : Boolean;VAR Ship@1002 : Boolean;VAR Consume@1001 : Boolean;VAR Invoice@1000 : Boolean;VAR IsHandled@1005 : Boolean);
    BEGIN
    END;

    BEGIN
    END.
  }
}

