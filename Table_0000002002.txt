OBJECT Table 2002 Azure Machine Learning Usage
{
  OBJECT-PROPERTIES
  {
    Date=09/19/18;
    Time=12:00:00 PM;
    Version List=NAVW113.00;
  }
  PROPERTIES
  {
    Permissions=TableData 2002=rimd;
    ObsoleteState=Pending;
    ObsoleteReason=Table 2003 replaces this.;
    CaptionML=[ENU=Azure Machine Learning Usage;
               ESM=Uso de Azure Machine Learning;
               FRC=Utilisation de la formation poste Azure;
               ENC=Azure Machine Learning Usage];
  }
  FIELDS
  {
    { 1   ;   ;Primary Key         ;Code10        ;CaptionML=[ENU=Primary Key;
                                                              ESM=Clave principal;
                                                              FRC=Cl‚ primaire;
                                                              ENC=Primary Key] }
    { 2   ;   ;Total Processing Time;Decimal      ;CaptionML=[ENU=Total Processing Time;
                                                              ESM=Tiempo total de procesamiento;
                                                              FRC=Dur‚e totale de traitement;
                                                              ENC=Total Processing Time];
                                                   MinValue=0;
                                                   Description=Processing time of the all Azure ML requests is in seconds for current month.;
                                                   Editable=No }
    { 3   ;   ;Last Date Updated   ;Date          ;CaptionML=[ENU=Last Date Updated;
                                                              ESM=éltima fecha de actualizaci¢n;
                                                              FRC=Date derniŠre mise … jour;
                                                              ENC=Last Date Updated];
                                                   Description=Date when the table was updated last time.;
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Primary Key                             ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      PermissionManager@1000 : Codeunit 9002;
      ProcessingTimeLessThanZeroErr@1001 : TextConst 'ENU=The provided Azure ML processing time is less or equal to zero.;ESM=El tiempo de procesamiento de Azure ML proporcionado es menor o igual que cero.;FRC=La dur‚e de traitement Azure ML fournie est inf‚rieure ou ‚gale … z‚ro.;ENC=The provided Azure ML processing time is less or equal to zero.';

    [External]
    PROCEDURE IncrementTotalProcessingTime@3(AzureMLServiceProcessingTime@1000 : Decimal);
    BEGIN
      IF AzureMLServiceProcessingTime <= 0 THEN
        ERROR(ProcessingTimeLessThanZeroErr);

      IF GetSingleInstance THEN BEGIN
        "Total Processing Time" += AzureMLServiceProcessingTime;
        "Last Date Updated" := TODAY;
        MODIFY(TRUE);
      END;
    END;

    [External]
    PROCEDURE IsAzureMLLimitReached@5(AzureMLUsageLimit@1000 : Decimal) : Boolean;
    BEGIN
      IF GetSingleInstance THEN
        IF GetTotalProcessingTime >= AzureMLUsageLimit THEN
          EXIT(TRUE);
      EXIT(FALSE);
    END;

    [External]
    PROCEDURE GetTotalProcessingTime@8() : Decimal;
    BEGIN
      // in case Azure ML is used by other features processing time should be added here
      IF GetSingleInstance THEN
        EXIT("Total Processing Time");
    END;

    [External]
    PROCEDURE GetSingleInstance@9() : Boolean;
    BEGIN
      IF NOT PermissionManager.SoftwareAsAService THEN
        EXIT(FALSE);
      IF NOT FINDFIRST THEN BEGIN
        INIT;
        "Last Date Updated" := TODAY;
        INSERT;
      END;

      // reset total processing time when new month starts
      IF DATE2DMY(TODAY,2) <> DATE2DMY("Last Date Updated",2) THEN BEGIN
        "Total Processing Time" := 0;
        MODIFY;
      END;
      EXIT(TRUE);
    END;

    BEGIN
    {
      // This table is used for Azure Machine Learning related features to control that amount of time used by all
      // these features in total does not exceed the limit defined by Azure ML.The table is singleton and used only in SaaS.
    }
    END.
  }
}

