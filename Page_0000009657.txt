OBJECT Page 9657 Customer Report Selections
{
  OBJECT-PROPERTIES
  {
    Date=11/27/18;
    Time=11:12:04 PM;
    Version List=;
  }
  PROPERTIES
  {
    CaptionML=ENU=Document Layouts;
    SourceTable=Table9657;
    DelayedInsert=Yes;
    DataCaptionFields=Source No.;
    PageType=List;
    OnAfterGetRecord=BEGIN
                       MapTableUsageValueToPageValue;
                     END;

    OnNewRecord=BEGIN
                  InitUsage;
                  MapTableUsageValueToPageValue;
                END;

  }
  CONTROLS
  {
    { 1   ;0   ;Container ;
                ContainerType=ContentArea }

    { 2   ;1   ;Group     ;
                Name=Group;
                GroupType=Repeater;
                FreezeColumnID=Custom Report Description }

    { 3   ;2   ;Field     ;
                Name=Usage2;
                CaptionML=ENU=Usage;
                ToolTipML=ENU=Specifies which type of document the report is used for.;
                OptionCaptionML=ENU=Quote,Confirmation Order,Invoice,Credit Memo,Customer Statement,Job Quote;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=Usage2;
                OnValidate=BEGIN
                             CASE Usage2 OF
                               Usage2::Quote:
                                 Usage := Usage::"S.Quote";
                               Usage2::"Confirmation Order":
                                 Usage := Usage::"S.Order";
                               Usage2::Invoice:
                                 Usage := Usage::"S.Invoice";
                               Usage2::"Credit Memo":
                                 Usage := Usage::"S.Cr.Memo";
                               Usage2::"Customer Statement":
                                 Usage := Usage::"C.Statement";
                               Usage2::"Job Quote":
                                 Usage := Usage::JQ;
                             END;
                           END;
                            }

    { 4   ;2   ;Field     ;
                Name=ReportID;
                CaptionML=ENU=Report ID;
                ToolTipML=ENU=Specifies the ID of the report.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Report ID" }

    { 5   ;2   ;Field     ;
                Name=ReportCaption;
                CaptionML=ENU=Report Caption;
                ToolTipML=ENU=Specifies the name of the report.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Report Caption" }

    { 7   ;2   ;Field     ;
                Lookup=Yes;
                DrillDown=Yes;
                CaptionML=ENU=Custom Layout Description;
                ToolTipML=ENU=Specifies a description of the custom report layout.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Custom Report Description";
                OnValidate=VAR
                             CustomReportLayout@1000 : Record 9650;
                           BEGIN
                             IF "Custom Report Description" = '' THEN BEGIN
                               VALIDATE("Custom Report Layout Code",'');
                               MODIFY(TRUE);
                             END ELSE BEGIN
                               CustomReportLayout.SETRANGE("Report ID","Report ID");
                               CustomReportLayout.SETFILTER(Description,STRSUBSTNO('@*%1*',"Custom Report Description"));
                               IF NOT CustomReportLayout.FINDFIRST THEN
                                 ERROR(CouldNotFindCustomReportLayoutErr,"Custom Report Description");

                               VALIDATE("Custom Report Layout Code",CustomReportLayout.Code);
                               MODIFY(TRUE);
                             END;
                           END;

                OnLookup=BEGIN
                           LookupCustomReportDescription;
                           CurrPage.UPDATE(TRUE);
                         END;

                OnDrillDown=BEGIN
                              LookupCustomReportDescription;
                              CurrPage.UPDATE(TRUE);
                            END;
                             }

    { 8   ;2   ;Field     ;
                Name=SendToEmail;
                CaptionML=ENU=Send To Email;
                ToolTipML=ENU=Specifies that the report is used when sending emails.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Send To Email" }

    { 6   ;2   ;Field     ;
                ToolTipML=ENU=Specifies that summarized information, such as invoice number, due date, and payment service link, will be inserted in the body of the email that you send.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Use for Email Body" }

    { 9   ;2   ;Field     ;
                ToolTipML=ENU=Specifies the ID of the email body layout that is used.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Email Body Layout Code";
                Visible=FALSE }

    { 10  ;2   ;Field     ;
                Lookup=Yes;
                DrillDown=Yes;
                ToolTipML=ENU=Specifies a description of the email body layout that is used.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Email Body Layout Description";
                OnLookup=BEGIN
                           LookupEmailBodyDescription;
                           CurrPage.UPDATE(TRUE);
                         END;

                OnDrillDown=BEGIN
                              LookupEmailBodyDescription;
                              CurrPage.UPDATE(TRUE);
                            END;
                             }

  }
  CODE
  {
    VAR
      Usage2@1000 : 'Quote,Confirmation Order,Invoice,Credit Memo,Customer Statement,Job Quote';
      CouldNotFindCustomReportLayoutErr@1001 : TextConst '@@@=%1 Description of custom report layout;ENU=There is no custom report layout with %1 in the description.';

    LOCAL PROCEDURE MapTableUsageValueToPageValue@3();
    VAR
      CustomReportSelection@1000 : Record 9657;
    BEGIN
      CASE Usage OF
        CustomReportSelection.Usage::"S.Quote":
          Usage2 := Usage::"S.Quote";
        CustomReportSelection.Usage::"S.Order":
          Usage2 := Usage::"S.Order";
        CustomReportSelection.Usage::"S.Invoice":
          Usage2 := Usage::"S.Invoice";
        CustomReportSelection.Usage::"S.Cr.Memo":
          Usage2 := Usage::"S.Cr.Memo";
        CustomReportSelection.Usage::"C.Statement":
          Usage2 := Usage2::"Customer Statement";
        CustomReportSelection.Usage::JQ:
          Usage2 := Usage2::"Job Quote";
      END;
    END;

    BEGIN
    END.
  }
}

