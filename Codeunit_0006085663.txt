OBJECT Codeunit 6085663 Online Client Resource Mgt.
{
  OBJECT-PROPERTIES
  {
    Date=10/25/17;
    Time=[ 1:00:00 AM];
    Version List=DCW110.00.00.4.50.02;
  }
  PROPERTIES
  {
    OnRun=BEGIN
            DownloadAndImportClientAddins;
          END;

  }
  CODE
  {

    LOCAL PROCEDURE DownloadAndImportClientAddins@6();
    VAR
      Addin@1000 : Record 2000000069;
      COMgt@1001 : Codeunit 6192772;
      FileSysMgt@1002 : Codeunit 6085584;
      AddInFilePath@1003 : Text[1024];
      ScannerFilePath@1004 : Text[1024];
      ServerAssembliesFilePath@1005 : Text[1024];
    BEGIN
      IF NOT Addin.WRITEPERMISSION THEN
        EXIT;

      AddInFilePath := FileSysMgt.GetTempFilename('ClientAddIns','zip');
      ScannerFilePath := FileSysMgt.GetTempFilename('ScannerAddIn','zip');
      ServerAssembliesFilePath := FileSysMgt.GetTempFilename('ServerAssemblies','zip');
      COMgt.DownloadFile(GetClientAddInResourceUrl,AddInFilePath,FALSE);
      COMgt.DownloadFile(GetScannerAddInResourceUrl,ScannerFilePath,FALSE);
      COMgt.DownloadFile(GetServerAssembliesResourceUrl,ServerAssembliesFilePath,FALSE);

      Addin.SETFILTER("Add-in Name",'Continia.DocumentCapture.Dynamics.*|Akeyto.DocumentCapture.Dynamics.*');
      Addin.SETFILTER(Category,'<>%1',Addin.Category::"JavaScript Control Add-in");
      IF Addin.FINDSET(TRUE,FALSE) THEN
        REPEAT
          CASE Addin."Add-in Name" OF
            'Akeyto.DocumentCapture.Dynamics.Nav.EventNotifier',
            'Akeyto.DocumentCapture.Dynamics.Nav.MatchInfoBar',
            'Akeyto.DocumentCapture.Dynamics.Nav.UI',
            'Continia.DocumentCapture.Dynamics.Nav.ClientEventNotifier',
            'Continia.DocumentCapture.Dynamics.Nav.DocDragAndDrop110',
            'Continia.DocumentCapture.Dynamics.Nav.DocDragAndDrop220',
            'Continia.DocumentCapture.Dynamics.Nav.Page.Controls.RadioButton',
            'Continia.DocumentCapture.Dynamics.Nav.Page.EventListener':
              BEGIN
                Addin.Resource.IMPORT(AddInFilePath);
                Addin.MODIFY;
              END;
            'Akeyto.DocumentCapture.Dynamics.Nav.Scanner':
              BEGIN
                Addin.Resource.IMPORT(ScannerFilePath);
                Addin.MODIFY;
              END;
             'Akeyto.DocumentCapture.Dynamics.Nav.Shared',
             'Continia.DocumentCapture.Dynamics.Nav.MessageBus':
               BEGIN
                 Addin.Resource.IMPORT(ServerAssembliesFilePath);
                 Addin.MODIFY;
               END;
          END;
        UNTIL Addin.NEXT = 0;

      FileSysMgt.DeleteFile(AddInFilePath,TRUE);
      FileSysMgt.DeleteFile(ScannerFilePath,TRUE);
      FileSysMgt.DeleteFile(ServerAssembliesFilePath,TRUE);
    END;

    LOCAL PROCEDURE GetClientAddInResourceUrl@2() : Text[1024];
    VAR
      CODC@1000 : Codeunit 6085617;
      AboutDC@1001 : Codeunit 6085589;
      ContiniaVersionMgt@6085573 : Codeunit 6192775;
    BEGIN
      EXIT(STRSUBSTNO('%1/addins/%2/%3/clientaddins.zip',CODC.GetResourceUrl,AboutDC.ControlAddInVersion,ContiniaVersionMgt.NAVVersion));
    END;

    LOCAL PROCEDURE GetScannerAddInResourceUrl@16() : Text[1024];
    VAR
      CODC@1001 : Codeunit 6085617;
      AboutDC@1000 : Codeunit 6085589;
      ContiniaVersionMgt@6085573 : Codeunit 6192775;
    BEGIN
      EXIT(STRSUBSTNO('%1/addins/%2/%3/scanneraddin.zip',CODC.GetResourceUrl,AboutDC.ControlAddInVersion,ContiniaVersionMgt.NAVVersion));
    END;

    LOCAL PROCEDURE GetServerAssembliesResourceUrl@7() : Text[1024];
    VAR
      CODC@1001 : Codeunit 6085617;
      AboutDC@1000 : Codeunit 6085589;
      ContiniaVersionMgt@6085573 : Codeunit 6192775;
    BEGIN
      EXIT(STRSUBSTNO('%1/addins/%2/%3/serverassemblies.zip',CODC.GetResourceUrl,AboutDC.ControlAddInVersion,ContiniaVersionMgt.NAVVersion));
    END;

    BEGIN
    END.
  }
}

