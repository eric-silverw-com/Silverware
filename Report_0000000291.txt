OBJECT Report 291 Delete Invd Blnkt Sales Orders
{
  OBJECT-PROPERTIES
  {
    Date=09/19/18;
    Time=12:00:00 PM;
    Version List=NAVW113.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Delete Invd Blnkt Sales Orders;
               ESM=Elim. peds. abiert/vta. fact.;
               FRC=Suppr. commandes permanentes ventes fact.;
               ENC=Delete Invd Blnkt Sales Orders];
    ProcessingOnly=Yes;
  }
  DATASET
  {
    { 6640;    ;DataItem;                    ;
               DataItemTable=Table36;
               DataItemTableView=SORTING(Document Type,No.)
                                 WHERE(Document Type=CONST(Blanket Order));
               ReqFilterHeadingML=[ENU=Blanket Sales Order;
                                   ESM=Pedido abierto venta;
                                   FRC=Commande permanente ventes;
                                   ENC=Blanket Sales Order];
               OnPreDataItem=BEGIN
                               Window.OPEN(Text000);
                             END;

               OnAfterGetRecord=VAR
                                  ATOLink@1002 : Record 904;
                                  ApprovalsMgmt@1000 : Codeunit 1535;
                                BEGIN
                                  Window.UPDATE(1,"No.");

                                  SalesLine.RESET;
                                  SalesLine.SETRANGE("Document Type","Document Type");
                                  SalesLine.SETRANGE("Document No.","No.");
                                  SalesLine.SETFILTER("Quantity Invoiced",'<>0');
                                  IF SalesLine.FINDFIRST THEN BEGIN
                                    SalesLine.SETRANGE("Quantity Invoiced");
                                    SalesLine.SETFILTER("Outstanding Quantity",'<>0');
                                    IF NOT SalesLine.FINDFIRST THEN BEGIN
                                      SalesLine.SETRANGE("Outstanding Quantity");
                                      SalesLine.SETFILTER("Qty. Shipped Not Invoiced",'<>0');
                                      IF NOT SalesLine.FINDFIRST THEN BEGIN
                                        SalesLine.LOCKTABLE;
                                        IF NOT SalesLine.FINDFIRST THEN BEGIN
                                          SalesLine.SETRANGE("Qty. Shipped Not Invoiced");
                                          SalesLine2.SETRANGE("Blanket Order No.","No.");
                                          IF NOT SalesLine2.FINDFIRST THEN BEGIN
                                            ArchiveManagement.AutoArchiveSalesDocument("Sales Header");
                                            SalesLine.SETFILTER("Qty. to Assemble to Order",'<>0');
                                            IF SalesLine.FINDSET THEN
                                              REPEAT
                                                ATOLink.DeleteAsmFromSalesLine(SalesLine);
                                              UNTIL SalesLine.NEXT = 0;
                                            SalesLine.SETRANGE("Qty. to Assemble to Order");

                                            SalesSetup.GET;
                                            IF SalesSetup."Archive Blanket Orders" THEN
                                              ArchiveManagement.ArchSalesDocumentNoConfirm("Sales Header");

                                            SalesLine.DELETEALL;

                                            SalesCommentLine.SETRANGE("Document Type","Document Type");
                                            SalesCommentLine.SETRANGE("No.","No.");
                                            SalesCommentLine.DELETEALL;

                                            ApprovalsMgmt.DeleteApprovalEntries(RECORDID);

                                            OnBeforeDeleteSalesHeader("Sales Header");
                                            DELETE;

                                            COMMIT;
                                          END;
                                        END;
                                      END;
                                    END;
                                  END;
                                END;

               ReqFilterFields=No.,Sell-to Customer No.,Bill-to Customer No. }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Processing sales orders #1##########;ESM=Procesando pedidos venta  #1##########;FRC=Traiter bordereaux de vente #1##########;ENC=Processing sales orders #1##########';
      SalesLine@1001 : Record 37;
      SalesLine2@1002 : Record 37;
      SalesCommentLine@1003 : Record 44;
      SalesSetup@1006 : Record 311;
      ArchiveManagement@1007 : Codeunit 5063;
      Window@1005 : Dialog;

    [Integration]
    LOCAL PROCEDURE OnBeforeDeleteSalesHeader@1(VAR SalesHeader@1000 : Record 36);
    BEGIN
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

