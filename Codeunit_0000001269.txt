OBJECT Codeunit 1269 Export Mapping
{
  OBJECT-PROPERTIES
  {
    Date=09/15/15;
    Time=12:00:00 PM;
    Version List=NAVW19.00;
  }
  PROPERTIES
  {
    TableNo=1220;
    Permissions=TableData 1220=rimd;
    OnRun=VAR
            DataExchMapping@1002 : Record 1224;
            PaymentExportMgt@1000 : Codeunit 1210;
            SourceRecRef@1001 : RecordRef;
            Window@1005 : Dialog;
            LineNo@1006 : Integer;
          BEGIN
            FindMapping(DataExchMapping,"Data Exch. Def Code","Data Exch. Line Def Code");

            SetTableFilters(SourceRecRef,DataExchMapping."Table ID",ReadTableFilters(Rec));

            Window.OPEN(ProgressMsg);

            REPEAT
              LineNo += 1;
              Window.UPDATE(1,LineNo);

              PaymentExportMgt.ProcessColumnMapping(Rec,SourceRecRef,LineNo,"Data Exch. Line Def Code",SourceRecRef.NUMBER);
            UNTIL SourceRecRef.NEXT = 0;

            Window.CLOSE;
          END;

  }
  CODE
  {
    VAR
      ProgressMsg@1000 : TextConst 'ENU=Processing line no. #1######.;ESM=Procesando la l¡nea n.§ #1######.;FRC=Traitement de la ligne nø #1######.;ENC=Processing line no. #1######.';
      MappingNotFoundErr@1002 : TextConst 'ENU=No mapping was found for format definition %1 and line definition %2.;ESM=No se ha encontrado ninguna asignaci¢n para la definici¢n de formato %1 y la definici¢n de l¡nea %2.;FRC=Aucune correspondance n''a ‚t‚ trouv‚e pour la d‚finition de format %1 et la d‚finition de ligne %2.;ENC=No mapping was found for format definition %1 and line definition %2.';
      RecordsNotFoundErr@1001 : TextConst 'ENU=No records were found for source table %1 using the filters: %2.;ESM=No se encontraron registros para la tabla de origen %1 usando los filtros: %2.;FRC=Aucun enregistrement n''a ‚t‚ trouv‚ pour la table source %1 … l''aide des filtresÿ: %2.;ENC=No records were found for source table %1 using the filters: %2.';

    LOCAL PROCEDURE FindMapping@3(VAR DataExchMapping@1000 : Record 1224;DataExchDefCode@1001 : Code[20];DataExchLineDefCode@1002 : Code[20]);
    BEGIN
      DataExchMapping.SETRANGE("Data Exch. Def Code",DataExchDefCode);
      DataExchMapping.SETRANGE("Data Exch. Line Def Code",DataExchLineDefCode);

      IF NOT DataExchMapping.FINDFIRST THEN
        ERROR(MappingNotFoundErr,DataExchDefCode,DataExchLineDefCode);
    END;

    LOCAL PROCEDURE SetTableFilters@5(RecRef@1000 : RecordRef;TableID@1001 : Integer;Filters@1002 : Text);
    BEGIN
      RecRef.OPEN(TableID);
      RecRef.SETVIEW(Filters);

      IF NOT RecRef.FINDSET THEN
        ERROR(RecordsNotFoundErr,TableID,RecRef.GETVIEW);
    END;

    LOCAL PROCEDURE ReadTableFilters@1(DataExch@1000 : Record 1220) TableFilters : Text;
    VAR
      TableFiltersInStream@1001 : InStream;
    BEGIN
      IF NOT DataExch."Table Filters".HASVALUE THEN
        EXIT('');

      DataExch.CALCFIELDS("Table Filters");
      DataExch."Table Filters".CREATEINSTREAM(TableFiltersInStream);
      TableFiltersInStream.READTEXT(TableFilters);
    END;

    BEGIN
    END.
  }
}

