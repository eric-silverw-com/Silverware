OBJECT Table 14103463 EasyPDF Queue
{
  OBJECT-PROPERTIES
  {
    Date=04/13/17;
    Time=[ 1:22:51 PM];
    Modified=Yes;
    Version List=EZP2.31.00;
  }
  PROPERTIES
  {
    OnInsert=VAR
               EasyPDFQueue@1000000000 : Record 14103463;
             BEGIN

               // Mark the header as not processed when a new entry is queued
               IF Rec.EntryType = Rec.EntryType::Line THEN
               BEGIN
                 EasyPDFQueue.RESET;
                 EasyPDFQueue.SETRANGE(BatchId, Rec.BatchId);
                 EasyPDFQueue.SETRANGE(EntryType, EntryType::Header);
                 IF EasyPDFQueue.FINDFIRST THEN
                 BEGIN
                   EasyPDFQueue.Processed := FALSE;
                   EasyPDFQueue.MODIFY;
                 END;
               END;
             END;

    OnDelete=VAR
               EasyPDFQueue@1000000000 : Record 14103463;
               EasyPDFAttachment@1000000001 : Record 14103466;
             BEGIN

               // Remove the lines and attachments when the header is removed
               IF Rec.EntryType = Rec.EntryType::Header THEN
               BEGIN
                 // Attachments (batch or batchline)
                 EasyPDFAttachment.RESET;
                 EasyPDFAttachment.SETFILTER(AttachmentType, '=%1|=%2', EasyPDFAttachment.AttachmentType::Batch, EasyPDFAttachment.AttachmentType::BatchLine);
                 EasyPDFAttachment.SETRANGE(BatchId, Rec.BatchId);
                 IF EasyPDFAttachment.FIND('-') THEN
                   EasyPDFAttachment.DELETEALL(TRUE);

                 // Lines
                 EasyPDFQueue.RESET;
                 EasyPDFQueue.SETRANGE(BatchId, Rec.BatchId);
                 EasyPDFQueue.SETRANGE(EntryType, EntryType::Line);
                 IF EasyPDFQueue.FIND('-') THEN
                   EasyPDFQueue.DELETEALL;
               END;

               // Remove the header when the last line is removed
               IF Rec.EntryType = Rec.EntryType::Line THEN
               BEGIN
                 EasyPDFQueue.RESET;
                 EasyPDFQueue.SETRANGE(BatchId, Rec.BatchId);
                 EasyPDFQueue.SETFILTER(EntryNo, '<>%1', Rec.EntryNo);
                 IF EasyPDFQueue.FIND('-') THEN
                   IF EasyPDFQueue.COUNT = 1 THEN
                     EasyPDFQueue.DELETE;

                 // Attachments (non-history)
                 EasyPDFAttachment.RESET;
                 EasyPDFAttachment.SETRANGE(AttachmentType, EasyPDFAttachment.AttachmentType::BatchLine);
                 EasyPDFAttachment.SETRANGE(BatchId, Rec.BatchId);
                 EasyPDFAttachment.SETRANGE(BatchEntryNo, Rec.EntryNo);
                 IF EasyPDFAttachment.FIND('-') THEN
                   EasyPDFAttachment.DELETEALL(TRUE);
               END;
             END;

    LookupPageID=Page14103466;
  }
  FIELDS
  {
    { 1   ;   ;EntryNo             ;Integer       ;AutoIncrement=Yes }
    { 2   ;   ;EntryType           ;Option        ;OptionString=Header,Line }
    { 3   ;   ;BatchId             ;Text80         }
    { 4   ;   ;DocCode             ;Code50         }
    { 5   ;   ;DocNo               ;Code20         }
    { 6   ;   ;DeliveryMethod      ;Option        ;OptionString=Print,Email,Fax,No Action }
    { 7   ;   ;CreatedDate         ;Date           }
    { 8   ;   ;CreatedBy           ;Code50         }
    { 9   ;   ;Processed           ;Boolean       ;InitValue=No }
    { 10  ;   ;ProcessedDate       ;Date           }
    { 11  ;   ;ProcessedBy         ;Code50         }
    { 12  ;   ;MailTransport       ;Option        ;OptionString=Smtp,Mapi }
    { 13  ;   ;DelayPostedDocumentDelivery;Boolean }
    { 14  ;   ;RecipientType       ;Integer        }
    { 15  ;   ;RecipientNo         ;Code20         }
    { 16  ;   ;RecipientName       ;Text50         }
    { 17  ;   ;RecipientAddress    ;Text250        }
    { 18  ;   ;FilterData          ;Text250        }
    { 19  ;   ;ParameterData       ;Text250        }
    { 20  ;   ;Value               ;Decimal        }
    { 21  ;   ;ConsolidateEmail    ;Boolean       ;InitValue=No }
    { 22  ;   ;ParentNo            ;Integer        }
    { 23  ;   ;AddressSpecified    ;Boolean        }
    { 24  ;   ;Attachments         ;Integer       ;FieldClass=FlowField;
                                                   InitValue=0;
                                                   CalcFormula=Count("EasyPDF Attachment" WHERE (AttachmentType=FILTER(Batch|BatchLine),
                                                                                                 BatchEntryNo=FIELD(EntryNo))) }
    { 25  ;   ;PrimaryKey          ;Text250        }
    { 26  ;   ;MergePdfAttachments ;Boolean        }
    { 27  ;   ;ReportParametersData;BLOB           }
    { 28  ;   ;LanguageCode        ;Code10         }
  }
  KEYS
  {
    {    ;EntryNo                                 ;Clustered=Yes }
    {    ;BatchId,DocCode,DocNo                    }
    {    ;CreatedDate                              }
    {    ;RecipientNo,EntryNo                      }
  }
  FIELDGROUPS
  {
  }
  CODE
  {

    PROCEDURE GetReportParameters@1000000000() : Text;
    VAR
      Parameters@1000000001 : Text;
      IStream@1000000000 : InStream;
    BEGIN

      Parameters := '';
      IF CALCFIELDS(ReportParametersData) THEN
      BEGIN
        IF ReportParametersData.HASVALUE THEN
        BEGIN
          ReportParametersData.CREATEINSTREAM(IStream);
          IStream.READTEXT(Parameters);
          CLEAR(IStream);
        END;
      END;
      EXIT(Parameters);
    END;

    BEGIN
    END.
  }
}

