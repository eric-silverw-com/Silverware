OBJECT Table 130415 Semi-Manual Test Wizard
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=;
  }
  PROPERTIES
  {
    CaptionML=ENU=Semi-Manual Test Wizard;
  }
  FIELDS
  {
    { 1   ;   ;Codeunit number     ;Integer       ;CaptionML=ENU=Codeunit number;
                                                   BlankZero=Yes }
    { 2   ;   ;Codeunit name       ;Text250       ;CaptionML=ENU=Codeunit name }
    { 3   ;   ;Step number         ;Integer       ;CaptionML=ENU=Step number }
    { 4   ;   ;Step heading        ;Text250       ;CaptionML=ENU=Step heading }
    { 5   ;   ;Manual detailed steps;BLOB         ;CaptionML=ENU=Manual detailed steps }
    { 6   ;   ;Total steps         ;Integer       ;CaptionML=ENU=Total steps }
    { 7   ;   ;Skip current step   ;Boolean       ;CaptionML=ENU=Skip current step }
  }
  KEYS
  {
    {    ;Codeunit name                           ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      InvalidCodeunitErr@1000 : TextConst '@@@={Locked};ENU=Codeunit %1 does not seem to be valid for a manual test.';

    PROCEDURE Initialize@1(CodeunitId@1000 : Integer;CodeunitName@1001 : Text[250]);
    VAR
      FailureCondition@1002 : Boolean;
    BEGIN
      INIT;
      "Codeunit number" := CodeunitId;
      "Codeunit name" := CodeunitName;
      FailureCondition := NOT CODEUNIT.RUN("Codeunit number",Rec);
      FailureCondition := FailureCondition OR ("Total steps" = 0);
      IF FailureCondition THEN
        ERROR(InvalidCodeunitErr,"Codeunit number");
    END;

    PROCEDURE SetManualSteps@2(Steps@1000 : Text);
    VAR
      TypeHelper@1003 : Codeunit 10;
      RecordRef@1002 : RecordRef;
      FieldRef@1001 : FieldRef;
    BEGIN
      RecordRef.GETTABLE(Rec);
      FieldRef := RecordRef.FIELD(FIELDNO("Manual detailed steps"));
      TypeHelper.WriteBlob(FieldRef,Steps);
      RecordRef.SETTABLE(Rec);
    END;

    PROCEDURE GetManualSteps@3() : Text;
    VAR
      TypeHelper@1002 : Codeunit 10;
      RecordRef@1000 : RecordRef;
      FieldRef@1001 : FieldRef;
    BEGIN
      RecordRef.GETTABLE(Rec);
      FieldRef := RecordRef.FIELD(FIELDNO("Manual detailed steps"));
      EXIT(TypeHelper.ReadBlob(FieldRef));
    END;

    BEGIN
    END.
  }
}

