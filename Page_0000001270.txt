OBJECT Page 1270 OCR Service Setup
{
  OBJECT-PROPERTIES
  {
    Date=11/27/18;
    Time=11:10:37 PM;
    Version List=;
  }
  PROPERTIES
  {
    CaptionML=ENU=OCR Service Setup;
    ApplicationArea=#Basic,#Suite;
    InsertAllowed=No;
    DeleteAllowed=No;
    LinksAllowed=No;
    SourceTable=Table1270;
    PageType=Card;
    UsageCategory=Administration;
    PromotedActionCategoriesML=ENU=New,Process,Page,Encryption;
    ShowFilter=No;
    OnOpenPage=BEGIN
                 RESET;
                 IF NOT GET THEN BEGIN
                   INIT;
                   INSERT(TRUE);
                   SetURLsToDefault;
                 END;
                 UpdateBasedOnEnable;
               END;

    OnAfterGetRecord=BEGIN
                       UpdateBasedOnEnable;
                     END;

    OnQueryClosePage=BEGIN
                       IF NOT Enabled THEN
                         IF NOT CONFIRM(STRSUBSTNO(EnableServiceQst,CurrPage.CAPTION),TRUE) THEN
                           EXIT(FALSE);
                     END;

    OnAfterGetCurrRecord=BEGIN
                           UpdateEncryptedField("Password Key",Password);
                           UpdateEncryptedField("Authorization Key",AuthorizationKey);
                         END;

    ActionList=ACTIONS
    {
      { 12      ;    ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 11      ;1   ;Action    ;
                      Name=SetURLsToDefault;
                      CaptionML=ENU=Set URLs to Default;
                      ToolTipML=ENU=Change the service and sign-up URLs to their default values. You cannot cancel this action to revert back to the current values.;
                      ApplicationArea=#Basic,#Suite;
                      Promoted=Yes;
                      Enabled=EditableByNotEnabled;
                      Image=Restore;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 SetURLsToDefault;
                               END;
                                }
      { 15      ;1   ;Action    ;
                      Name=TestConnection;
                      CaptionML=ENU=Test Connection;
                      ToolTipML=ENU=Check that the settings that you added are correct and the connection to the Data Exchange Service is working.;
                      ApplicationArea=#Basic,#Suite;
                      Promoted=Yes;
                      Image=Link;
                      PromotedCategory=Process;
                      OnAction=VAR
                                 OCRServiceMgt@1000 : Codeunit 1294;
                               BEGIN
                                 OCRServiceMgt.TestConnection(Rec);
                               END;
                                }
      { 21      ;1   ;Action    ;
                      Name=UpdateOCRDocTemplateList;
                      CaptionML=ENU=Update OCR Doc. Template List;
                      ToolTipML=ENU=Check for new document templates that the OCR service supports, and add them to the list.;
                      ApplicationArea=#Basic,#Suite;
                      Promoted=Yes;
                      Image=Template;
                      PromotedCategory=Process;
                      OnAction=VAR
                                 OCRServiceMgt@1000 : Codeunit 1294;
                               BEGIN
                                 OCRServiceMgt.UpdateOcrDocumentTemplates;
                               END;
                                }
      { 31      ;1   ;Action    ;
                      Name=ResyncMasterData;
                      CaptionML=ENU=Resync Master Data;
                      ToolTipML=ENU=Synchronize master data for vendors and vendor bank accounts with the OCR service.;
                      ApplicationArea=#Basic,#Suite;
                      Promoted=Yes;
                      Enabled=EditableBySyncEnabled;
                      Image=CopyFromChartOfAccounts;
                      PromotedCategory=Process;
                      OnAction=VAR
                                 ReadSoftOCRMasterDataSync@1000 : Codeunit 884;
                               BEGIN
                                 CLEAR("Master Data Last Sync");
                                 MODIFY;
                                 ReadSoftOCRMasterDataSync.SyncMasterData(FALSE,FALSE);
                               END;
                                }
      { 13      ;1   ;Action    ;
                      Name=JobQueueEntry;
                      CaptionML=ENU=Job Queue Entry;
                      ToolTipML=ENU=View or edit the jobs that automatically process the incoming and outgoing electronic documents.;
                      ApplicationArea=#Basic,#Suite;
                      Promoted=Yes;
                      Enabled=Enabled;
                      Image=JobListSetup;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 ShowJobQueueEntry;
                               END;
                                }
      { 10      ;0   ;ActionContainer;
                      ActionContainerType=RelatedInformation }
      { 8       ;1   ;Action    ;
                      Name=EncryptionManagement;
                      CaptionML=ENU=Encryption Management;
                      ToolTipML=ENU=Enable or disable data encryption. Data encryption helps make sure that unauthorized users cannot read business data.;
                      ApplicationArea=#Basic,#Suite;
                      RunObject=Page 9905;
                      Promoted=Yes;
                      Enabled=EditableByNotEnabled;
                      PromotedIsBig=Yes;
                      Image=EncryptionKeys;
                      PromotedCategory=Category4;
                      RunPageMode=View }
      { 26      ;1   ;Action    ;
                      Name=ActivityLog;
                      CaptionML=ENU=Activity Log;
                      ToolTipML=ENU=See the status and any errors for the electronic document or OCR file that you send through the document exchange service.;
                      ApplicationArea=#Basic,#Suite;
                      Image=Log;
                      OnAction=VAR
                                 ActivityLog@1002 : Record 710;
                               BEGIN
                                 ActivityLog.ShowEntries(Rec);
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1   ;0   ;Container ;
                ContainerType=ContentArea }

    { 2   ;1   ;Group     ;
                Name=General;
                CaptionML=ENU=General;
                GroupType=Group }

    { 23  ;2   ;Group     ;
                GroupType=Group }

    { 3   ;3   ;Field     ;
                ToolTipML=ENU=Specifies the user name that represents your company's login to the OCR service.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="User Name";
                Editable=EditableByNotEnabled;
                OnValidate=BEGIN
                             CurrPage.SAVERECORD;
                           END;

                ShowMandatory=TRUE }

    { 9   ;3   ;Field     ;
                Name=Password;
                ExtendedDatatype=Masked;
                CaptionML=ENU=Password;
                ToolTipML=ENU=Specifies the password that is used for your company's login to the OCR service.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=Password;
                Editable=EditableByNotEnabled;
                OnValidate=BEGIN
                             SavePassword("Password Key",Password);
                             IF Password <> '' THEN
                               CheckEncryption;
                           END;

                ShowMandatory=TRUE }

    { 7   ;3   ;Field     ;
                ExtendedDatatype=Masked;
                CaptionML=ENU=Authorization Key;
                ToolTipML=ENU=Specifies the authorization key that is used for your company's login to the OCR service.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=AuthorizationKey;
                Editable=EditableByNotEnabled;
                OnValidate=BEGIN
                             SavePassword("Authorization Key",AuthorizationKey);
                             IF AuthorizationKey <> '' THEN
                               CheckEncryption;
                           END;

                ShowMandatory=TRUE }

    { 16  ;3   ;Field     ;
                ToolTipML=ENU=Specifies the OCR template that must be used by default for electronic documents that are received from the OCR service. You can change the OCR template on the individual incoming document card before sending the related file to the OCR service.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Default OCR Doc. Template";
                Editable=EditableByNotEnabled;
                OnValidate=BEGIN
                             CurrPage.UPDATE
                           END;
                            }

    { 25  ;2   ;Group     ;
                GroupType=Group }

    { 32  ;3   ;Field     ;
                ToolTipML=ENU=Specifies whether or not the master data sync has been enabled.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Master Data Sync Enabled";
                Editable=EditableByNotEnabled;
                OnValidate=BEGIN
                             UpdateBasedOnSyncEnable;
                           END;
                            }

    { 28  ;3   ;Field     ;
                ToolTipML=ENU=Specifies the last time when the master data was synched.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Master Data Last Sync";
                Editable=False }

    { 22  ;3   ;Field     ;
                ToolTipML=ENU=Specifies if the service is enabled.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=Enabled;
                OnValidate=BEGIN
                             UpdateBasedOnEnable;
                             CurrPage.UPDATE
                           END;
                            }

    { 27  ;3   ;Field     ;
                AssistEdit=No;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=ShowEnableWarning;
                Enabled=NOT EditableByNotEnabled;
                Editable=False;
                OnDrillDown=BEGIN
                              DrilldownCode;
                            END;
                             }

    { 14  ;1   ;Group     ;
                Name=Service;
                CaptionML=ENU=Service;
                GroupType=Group }

    { 4   ;2   ;Field     ;
                ToolTipML=ENU=Specifies the web page where you sign up for the OCR service.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Sign-up URL";
                Editable=EditableByNotEnabled }

    { 5   ;2   ;Field     ;
                ToolTipML=ENU=Specifies the address of the OCR service. The service specified in the Service URL field is called when you send and receive files for OCR.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Service URL";
                Editable=EditableByNotEnabled;
                ShowMandatory=TRUE }

    { 6   ;2   ;Field     ;
                ToolTipML=ENU=Specifies the sign-in page for the OCR service. This is the web page where you enter your company's user name, password, and authorization key to sign in to the service.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Sign-in URL";
                Editable=EditableByNotEnabled }

    { 17  ;1   ;Group     ;
                Name=CustomerStatus;
                CaptionML=ENU=Status;
                GroupType=Group }

    { 18  ;2   ;Field     ;
                ToolTipML=ENU=Specifies your company's name at the provider of the OCR service.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Customer Name";
                Importance=Promoted;
                Editable=False }

    { 19  ;2   ;Field     ;
                ToolTipML=ENU=Specifies your company's customer ID at the provider of the OCR service.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Customer ID";
                Editable=False }

    { 20  ;2   ;Field     ;
                ToolTipML=ENU=Specifies your company's status at the provider of the OCR service.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Customer Status";
                Importance=Promoted;
                Editable=False }

  }
  CODE
  {
    VAR
      Password@1000 : Text[50];
      AuthorizationKey@1001 : Text[50];
      ShowEnableWarning@1005 : Text;
      EditableByNotEnabled@1002 : Boolean;
      EnabledWarningTok@1003 : TextConst 'ENU=You must disable the service before you can make changes.';
      DisableEnableQst@1004 : TextConst 'ENU=Do you want to disable the OCR service?';
      EnableServiceQst@1006 : TextConst '@@@="%1 = pagecaption (OCR Service Setup)";ENU=The %1 is not enabled. Are you sure you want to exit?';
      EncryptionIsNotActivatedQst@1007 : TextConst 'ENU=Data encryption is not activated. It is recommended that you encrypt data. \Do you want to open the Data Encryption Management window?';
      EditableBySyncEnabled@1009 : Boolean;
      CheckedEncryption@1008 : Boolean;

    LOCAL PROCEDURE UpdateBasedOnEnable@2();
    BEGIN
      EditableByNotEnabled := (NOT Enabled) AND CurrPage.EDITABLE;
      ShowEnableWarning := '';
      IF CurrPage.EDITABLE AND Enabled THEN
        ShowEnableWarning := EnabledWarningTok;
      UpdateBasedOnSyncEnable;
    END;

    LOCAL PROCEDURE UpdateBasedOnSyncEnable@8();
    BEGIN
      EditableBySyncEnabled := "Master Data Sync Enabled" AND Enabled;
      IF EditableBySyncEnabled THEN
        EXIT;
      IF "Master Data Last Sync" = 0DT THEN
        EXIT;
      CLEAR("Master Data Last Sync");
      MODIFY;
    END;

    LOCAL PROCEDURE DrilldownCode@1();
    BEGIN
      IF CONFIRM(DisableEnableQst,TRUE) THEN BEGIN
        Enabled := FALSE;
        UpdateBasedOnEnable;
        CurrPage.UPDATE;
      END;
    END;

    LOCAL PROCEDURE UpdateEncryptedField@3(InputGUID@1000 : GUID;VAR Text@1001 : Text[50]);
    BEGIN
      IF ISNULLGUID(InputGUID) THEN
        Text := ''
      ELSE
        Text := '*************';
    END;

    LOCAL PROCEDURE CheckEncryption@6();
    BEGIN
      IF NOT CheckedEncryption AND NOT ENCRYPTIONENABLED THEN BEGIN
        CheckedEncryption := TRUE;
        IF NOT ENCRYPTIONENABLED THEN
          IF CONFIRM(EncryptionIsNotActivatedQst) THEN
            PAGE.RUN(PAGE::"Data Encryption Management");
      END;
    END;

    BEGIN
    END.
  }
}

