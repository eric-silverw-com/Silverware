OBJECT Report 5692 Calculate Depreciation
{
  OBJECT-PROPERTIES
  {
    Date=09/19/18;
    Time=12:00:00 PM;
    Version List=NAVW113.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Calculate Depreciation;
               ESM=Calcular amortizaci¢n;
               FRC=Calculer l'amortissement;
               ENC=Calculate Depreciation];
    ApplicationArea=#FixedAssets;
    ProcessingOnly=Yes;
    OnPreReport=BEGIN
                  DeprBook.GET(DeprBookCode);
                  IF DeprUntilDate = 0D THEN
                    ERROR(Text000,FAJnlLine.FIELDCAPTION("FA Posting Date"));
                  IF PostingDate = 0D THEN
                    PostingDate := DeprUntilDate;
                  IF UseForceNoOfDays AND (DaysInPeriod = 0) THEN
                    ERROR(Text001);

                  IF DeprBook."Use Same FA+G/L Posting Dates" AND (DeprUntilDate <> PostingDate) THEN
                    ERROR(
                      Text002,
                      FAJnlLine.FIELDCAPTION("FA Posting Date"),
                      FAJnlLine.FIELDCAPTION("Posting Date"),
                      DeprBook.FIELDCAPTION("Use Same FA+G/L Posting Dates"),
                      FALSE,
                      DeprBook.TABLECAPTION,
                      DeprBook.FIELDCAPTION(Code),
                      DeprBook.Code);

                  Window.OPEN(
                    Text003 +
                    Text004 +
                    Text005);
                END;

    OnPostReport=VAR
                   PageGenJnlLine@1000 : Record 81;
                   PageFAJnlLine@1001 : Record 5621;
                 BEGIN
                   Window.CLOSE;
                   IF (FAJnlLineCreatedCount = 0) AND (GenJnlLineCreatedCount = 0) THEN BEGIN
                     MESSAGE(CompletionStatsMsg);
                     EXIT;
                   END;

                   IF FAJnlLineCreatedCount > 0 THEN
                     IF CONFIRM(CompletionStatsFAJnlQst,TRUE,FAJnlLineCreatedCount)
                     THEN BEGIN
                       PageFAJnlLine.SETRANGE("Journal Template Name",FAJnlLine."Journal Template Name");
                       PageFAJnlLine.SETRANGE("Journal Batch Name",FAJnlLine."Journal Batch Name");
                       PageFAJnlLine.FINDFIRST;
                       PAGE.RUN(PAGE::"Fixed Asset Journal",PageFAJnlLine);
                     END;

                   IF GenJnlLineCreatedCount > 0 THEN
                     IF CONFIRM(CompletionStatsGenJnlQst,TRUE,GenJnlLineCreatedCount)
                     THEN BEGIN
                       PageGenJnlLine.SETRANGE("Journal Template Name",GenJnlLine."Journal Template Name");
                       PageGenJnlLine.SETRANGE("Journal Batch Name",GenJnlLine."Journal Batch Name");
                       PageGenJnlLine.FINDFIRST;
                       PAGE.RUN(PAGE::"Fixed Asset G/L Journal",PageGenJnlLine);
                     END;
                 END;

    UsageCategory=Tasks;
  }
  DATASET
  {
    { 3794;    ;DataItem;                    ;
               DataItemTable=Table5600;
               OnAfterGetRecord=BEGIN
                                  IF Inactive OR Blocked THEN
                                    CurrReport.SKIP;

                                  CalculateDepr.Calculate(
                                    DeprAmount,Custom1Amount,NumberOfDays,Custom1NumberOfDays,
                                    "No.",DeprBookCode,DeprUntilDate,EntryAmounts,0D,DaysInPeriod);
                                  IF (DeprAmount <> 0) OR (Custom1Amount <> 0) THEN
                                    Window.UPDATE(1,"No.")
                                  ELSE
                                    Window.UPDATE(2,"No.");

                                  IF Custom1Amount <> 0 THEN
                                    IF NOT DeprBook."G/L Integration - Custom 1" OR "Budgeted Asset" THEN BEGIN
                                      FAJnlLineTmp."FA No." := "No.";
                                      FAJnlLineTmp."FA Posting Type" := FAJnlLineTmp."FA Posting Type"::"Custom 1";
                                      FAJnlLineTmp.Amount := Custom1Amount;
                                      FAJnlLineTmp."No. of Depreciation Days" := Custom1NumberOfDays;
                                      FAJnlLineTmp."FA Error Entry No." := Custom1ErrorNo;
                                      FAJnlLineTmp."Line No." := FAJnlLineTmp."Line No." + 1;
                                      FAJnlLineTmp.INSERT;
                                    END ELSE BEGIN
                                      GenJnlLineTmp."Account No." := "No.";
                                      GenJnlLineTmp."FA Posting Type" := GenJnlLineTmp."FA Posting Type"::"Custom 1";
                                      GenJnlLineTmp.Amount := Custom1Amount;
                                      GenJnlLineTmp."No. of Depreciation Days" := Custom1NumberOfDays;
                                      GenJnlLineTmp."FA Error Entry No." := Custom1ErrorNo;
                                      GenJnlLineTmp."Line No." := GenJnlLineTmp."Line No." + 1;
                                      GenJnlLineTmp.INSERT;
                                    END;

                                  IF DeprAmount <> 0 THEN
                                    IF NOT DeprBook."G/L Integration - Depreciation" OR "Budgeted Asset" THEN BEGIN
                                      FAJnlLineTmp."FA No." := "No.";
                                      FAJnlLineTmp."FA Posting Type" := FAJnlLineTmp."FA Posting Type"::Depreciation;
                                      FAJnlLineTmp.Amount := DeprAmount;
                                      FAJnlLineTmp."No. of Depreciation Days" := NumberOfDays;
                                      FAJnlLineTmp."FA Error Entry No." := ErrorNo;
                                      FAJnlLineTmp."Line No." := FAJnlLineTmp."Line No." + 1;
                                      FAJnlLineTmp.INSERT;
                                    END ELSE BEGIN
                                      GenJnlLineTmp."Account No." := "No.";
                                      GenJnlLineTmp."FA Posting Type" := GenJnlLineTmp."FA Posting Type"::Depreciation;
                                      GenJnlLineTmp.Amount := DeprAmount;
                                      GenJnlLineTmp."No. of Depreciation Days" := NumberOfDays;
                                      GenJnlLineTmp."FA Error Entry No." := ErrorNo;
                                      GenJnlLineTmp."Line No." := GenJnlLineTmp."Line No." + 1;
                                      GenJnlLineTmp.INSERT;
                                    END;
                                END;

               OnPostDataItem=BEGIN
                                WITH FAJnlLine DO BEGIN
                                  IF FAJnlLineTmp.FIND('-') THEN BEGIN
                                    LOCKTABLE;
                                    FAJnlSetup.FAJnlName(DeprBook,FAJnlLine,FAJnlNextLineNo);
                                    NoSeries := FAJnlSetup.GetFANoSeries(FAJnlLine);
                                    IF DocumentNo = '' THEN
                                      DocumentNo2 := FAJnlSetup.GetFAJnlDocumentNo(FAJnlLine,DeprUntilDate,TRUE)
                                    ELSE
                                      DocumentNo2 := DocumentNo;
                                  END;
                                  IF FAJnlLineTmp.FIND('-') THEN
                                    REPEAT
                                      INIT;
                                      "Line No." := 0;
                                      FAJnlSetup.SetFAJnlTrailCodes(FAJnlLine);
                                      LineNo := LineNo + 1;
                                      Window.UPDATE(3,LineNo);
                                      "Posting Date" := PostingDate;
                                      "FA Posting Date" := DeprUntilDate;
                                      IF "Posting Date" = "FA Posting Date" THEN
                                        "Posting Date" := 0D;
                                      "FA Posting Type" := FAJnlLineTmp."FA Posting Type";
                                      VALIDATE("FA No.",FAJnlLineTmp."FA No.");
                                      "Document No." := DocumentNo2;
                                      "Posting No. Series" := NoSeries;
                                      Description := PostingDescription;
                                      VALIDATE("Depreciation Book Code",DeprBookCode);
                                      VALIDATE(Amount,FAJnlLineTmp.Amount);
                                      "No. of Depreciation Days" := FAJnlLineTmp."No. of Depreciation Days";
                                      "FA Error Entry No." := FAJnlLineTmp."FA Error Entry No.";
                                      FAJnlNextLineNo := FAJnlNextLineNo + 10000;
                                      "Line No." := FAJnlNextLineNo;
                                      INSERT(TRUE);
                                      FAJnlLineCreatedCount += 1;
                                    UNTIL FAJnlLineTmp.NEXT = 0;
                                END;

                                WITH GenJnlLine DO BEGIN
                                  IF GenJnlLineTmp.FIND('-') THEN BEGIN
                                    LOCKTABLE;
                                    FAJnlSetup.GenJnlName(DeprBook,GenJnlLine,GenJnlNextLineNo);
                                    NoSeries := FAJnlSetup.GetGenNoSeries(GenJnlLine);
                                    IF DocumentNo = '' THEN
                                      DocumentNo2 := FAJnlSetup.GetGenJnlDocumentNo(GenJnlLine,DeprUntilDate,TRUE)
                                    ELSE
                                      DocumentNo2 := DocumentNo;
                                  END;
                                  IF GenJnlLineTmp.FIND('-') THEN
                                    REPEAT
                                      INIT;
                                      "Line No." := 0;
                                      FAJnlSetup.SetGenJnlTrailCodes(GenJnlLine);
                                      LineNo := LineNo + 1;
                                      Window.UPDATE(3,LineNo);
                                      "Posting Date" := PostingDate;
                                      "FA Posting Date" := DeprUntilDate;
                                      IF "Posting Date" = "FA Posting Date" THEN
                                        "FA Posting Date" := 0D;
                                      "FA Posting Type" := GenJnlLineTmp."FA Posting Type";
                                      "Account Type" := "Account Type"::"Fixed Asset";
                                      VALIDATE("Account No.",GenJnlLineTmp."Account No.");
                                      Description := PostingDescription;
                                      "Document No." := DocumentNo2;
                                      "Posting No. Series" := NoSeries;
                                      VALIDATE("Depreciation Book Code",DeprBookCode);
                                      VALIDATE(Amount,GenJnlLineTmp.Amount);
                                      "No. of Depreciation Days" := GenJnlLineTmp."No. of Depreciation Days";
                                      "FA Error Entry No." := GenJnlLineTmp."FA Error Entry No.";
                                      GenJnlNextLineNo := GenJnlNextLineNo + 1000;
                                      "Line No." := GenJnlNextLineNo;
                                      INSERT(TRUE);
                                      GenJnlLineCreatedCount += 1;
                                      IF BalAccount THEN
                                        FAInsertGLAcc.GetBalAcc2(GenJnlLine,GenJnlNextLineNo);
                                    UNTIL GenJnlLineTmp.NEXT = 0;
                                END;
                              END;

               ReqFilterFields=No.,FA Class Code,FA Subclass Code,Budgeted Asset }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
      OnOpenPage=BEGIN
                   BalAccount := TRUE;
                   PostingDate := WORKDATE;
                   DeprUntilDate := WORKDATE;
                   IF DeprBookCode = '' THEN BEGIN
                     FASetup.GET;
                     DeprBookCode := FASetup."Default Depr. Book";
                   END;
                 END;

    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[ENU=Options;
                             ESM=Opciones;
                             FRC=Options;
                             ENC=Options] }

      { 15  ;2   ;Field     ;
                  Name=DepreciationBook;
                  CaptionML=[ENU=Depreciation Book;
                             ESM=Libro amortizaci¢n;
                             FRC=Registre amortissement;
                             ENC=Depreciation Book];
                  ToolTipML=[ENU=Specifies the code for the depreciation book to be included in the report or batch job.;
                             ESM=Especifica el c¢digo del libro de amortizaci¢n que se va a incluir en el informe o el proceso.;
                             FRC=Sp‚cifie le code du registre amortissement … inclure dans le rapport ou le traitement en lot.;
                             ENC=Specifies the code for the depreciation book to be included in the report or batch job.];
                  ApplicationArea=#FixedAssets;
                  SourceExpr=DeprBookCode;
                  TableRelation="Depreciation Book" }

      { 9   ;2   ;Field     ;
                  Name=FAPostingDate;
                  CaptionML=[ENU=FA Posting Date;
                             ESM=A/F Fecha registro;
                             FRC=Date report immo.;
                             ENC=FA Posting Date];
                  ToolTipML=[ENU=Specifies the fixed asset posting date to be used by the batch job. The batch job includes ledger entries up to this date. This date appears in the FA Posting Date field in the resulting journal lines. If the Use Same FA+G/L Posting Dates field has been activated in the depreciation book that is used in the batch job, then this date must be the same as the posting date entered in the Posting Date field.;
                             ESM=Especifica la fecha de registro de activo fijo que se va a usar en el proceso. El proceso incluye los movimientos hasta esa fecha. Esta aparecer  en el campo A/F Fecha registro de las l¡neas del diario que se creen. Si el campo Usa fechas reg. CG+AF comunes se ha activado en el libro de amortizaci¢n que se usa en el proceso, esta fecha debe coincidir con la fecha de registro que se indica en el campo Fecha registro.;
                             FRC=Sp‚cifie la date de report immobilisation qui doit ˆtre utilis‚e par le traitement en lot. Le traitement en lot inclut les ‚critures comptables allant jusqu'… cette date. Cette date s'affiche dans le champ date de report immo. des lignes journal r‚sultantes. Si le champ Mˆmes dates de report/immo. a ‚t‚ activ‚ dans le registre amortissement utilis‚ dans le traitement en lot, cette date doit ˆtre identique … la date de report saisie dans le champ correspondant.;
                             ENC=Specifies the fixed asset posting date to be used by the batch job. The batch job includes ledger entries up to this date. This date appears in the FA Posting Date field in the resulting journal lines. If the Use Same FA+G/L Posting Dates field has been activated in the depreciation book that is used in the batch job, then this date must be the same as the posting date entered in the Posting Date field.];
                  ApplicationArea=#FixedAssets;
                  SourceExpr=DeprUntilDate;
                  Importance=Additional;
                  OnValidate=BEGIN
                               DeprUntilDateModified := TRUE;
                             END;
                              }

      { 8   ;2   ;Field     ;
                  Name=UseForceNoOfDays;
                  CaptionML=[ENU=Use Force No. of Days;
                             ESM=Utilizar forzar n§ de d¡as;
                             FRC=Utiliser nbr de jours forc‚s;
                             ENC=Use Force No. of Days];
                  ToolTipML=[ENU=Specifies if you want the program to use the number of days, as specified in the field below, in the depreciation calculation.;
                             ESM=Especifica si desea que el sistema use el n£mero de d¡as, especificado en el campo siguiente, en el c lculo de la amortizaci¢n.;
                             FRC=Indique si vous souhaitez que le programme utilise le nombre de jours sp‚cifi‚ dans le champ ci-dessous pour le calcul de l'amortissement.;
                             ENC=Specifies if you want the program to use the number of days, as specified in the field below, in the depreciation calculation.];
                  ApplicationArea=#FixedAssets;
                  SourceExpr=UseForceNoOfDays;
                  Importance=Additional;
                  OnValidate=BEGIN
                               IF NOT UseForceNoOfDays THEN
                                 DaysInPeriod := 0;
                             END;
                              }

      { 5   ;2   ;Field     ;
                  Name=ForceNoOfDays;
                  CaptionML=[ENU=Force No. of Days;
                             ESM=Forzar n§ de d¡as;
                             FRC=Forcer nbr de jours;
                             ENC=Force No. of Days];
                  ToolTipML=[ENU=Specifies if you want the program to use the number of days, as specified in the field below, in the depreciation calculation.;
                             ESM=Especifica si desea que el sistema use el n£mero de d¡as, especificado en el campo siguiente, en el c lculo de la amortizaci¢n.;
                             FRC=Indique si vous souhaitez que le programme utilise le nombre de jours sp‚cifi‚ dans le champ ci-dessous pour le calcul de l'amortissement.;
                             ENC=Specifies if you want the program to use the number of days, as specified in the field below, in the depreciation calculation.];
                  ApplicationArea=#FixedAssets;
                  BlankZero=Yes;
                  SourceExpr=DaysInPeriod;
                  MinValue=0;
                  Importance=Additional;
                  OnValidate=BEGIN
                               IF NOT UseForceNoOfDays AND (DaysInPeriod <> 0) THEN
                                 ERROR(Text006);
                             END;
                              }

      { 11  ;2   ;Field     ;
                  Name=PostingDate;
                  CaptionML=[ENU=Posting Date;
                             ESM=Fecha registro;
                             FRC=Date de report;
                             ENC=Posting Date];
                  ToolTipML=[ENU=Specifies the posting date to be used by the batch job.;
                             ESM=Especifica la fecha de registro que usar  el proceso.;
                             FRC=Sp‚cifie la date de report qui doit ˆtre utilis‚e par le traitement en lot.;
                             ENC=Specifies the posting date to be used by the batch job.];
                  ApplicationArea=#FixedAssets;
                  SourceExpr=PostingDate;
                  OnValidate=BEGIN
                               IF NOT DeprUntilDateModified THEN
                                 DeprUntilDate := PostingDate;
                             END;
                              }

      { 1   ;2   ;Field     ;
                  Name=DocumentNo;
                  CaptionML=[ENU=Document No.;
                             ESM=N§ documento;
                             FRC=Nø de document;
                             ENC=Document No.];
                  ToolTipML=[ENU=Specifies, if you leave the field empty, the next available number on the resulting journal line. If a number series is not set up, enter the document number that you want assigned to the resulting journal line.;
                             ESM=Especifica, si deja el campo vac¡o, el siguiente n£mero disponible en la l¡nea del diario resultante. Si no se configura una serie num‚rica, escriba el n£mero de documento que desea asignar a la l¡nea del diario resultante.;
                             FRC=Indique, si vous laissez le champ vide, le num‚ro suivant disponible sur la ligne journal r‚sultante. Si un num‚ro de s‚rie n'est pas configur‚, entrez le num‚ro de document que vous souhaitez affecter … la ligne journal r‚sultante.;
                             ENC=Specifies, if you leave the field empty, the next available number on the resulting journal line. If a number series is not set up, enter the document number that you want assigned to the resulting journal line.];
                  ApplicationArea=#FixedAssets;
                  SourceExpr=DocumentNo }

      { 13  ;2   ;Field     ;
                  Name=PostingDescription;
                  CaptionML=[ENU=Posting Description;
                             ESM=Texto de registro;
                             FRC=Description du report;
                             ENC=Posting Description];
                  ToolTipML=[ENU=Specifies the posting date to be used by the batch job as a filter.;
                             ESM=Especifica la fecha de registro que el proceso va a usar como filtro.;
                             FRC=Sp‚cifie la date de report qui doit ˆtre utilis‚e par le traitement en lot comme filtre.;
                             ENC=Specifies the posting date to be used by the batch job as a filter.];
                  ApplicationArea=#FixedAssets;
                  SourceExpr=PostingDescription }

      { 3   ;2   ;Field     ;
                  Name=InsertBalAccount;
                  CaptionML=[ENU=Insert Bal. Account;
                             ESM=Insertar contrapartida;
                             FRC=Ins‚rer compte contr“le;
                             ENC=Insert Bal. Account];
                  ToolTipML=[ENU=Specifies if you want the batch job to automatically insert fixed asset entries with balancing accounts.;
                             ESM=Especifica si desea que el proceso inserte autom ticamente movimientos de activos fijos con cuentas de contrapartida.;
                             FRC=Sp‚cifie si vous souhaitez que le traitement en lot insŠre automatiquement des ‚critures immobilisation avec les comptes de contrepartie.;
                             ENC=Specifies if you want the batch job to automatically insert fixed asset entries with balancing accounts.];
                  ApplicationArea=#FixedAssets;
                  SourceExpr=BalAccount;
                  Importance=Additional }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=You must specify %1.;ESM=Se debe indicar %1.;FRC=Vous devez sp‚cifier %1;ENC=You must specify %1.';
      Text001@1001 : TextConst 'ENU=Force No. of Days must be activated.;ESM=Se debe activar forzar n§ de d¡as.;FRC=Forcer nø de jours doit ˆtre activ‚e.;ENC=Force No. of Days must be activated.';
      Text002@1002 : TextConst 'ENU="%1 and %2 must be identical. %3 must be %4 in %5 %6 = %7.";ESM="%1 y %2 deben ser iguales. %3 debe ser %4 en %5 %6 = %7.";FRC="%1 et %2 doivent ˆtre identiques. %3 doit ˆtre %4 dans %5 %6 = %7.";ENC="%1 and %2 must be identical. %3 must be %4 in %5 %6 = %7."';
      Text003@1003 : TextConst 'ENU=Depreciating fixed asset      #1##########\;ESM=Amortizando activo            #1##########\;FRC=Amortissement immobilisations            #1##########\;ENC=Depreciating fixed asset      #1##########\';
      Text004@1004 : TextConst 'ENU=Not depreciating fixed asset  #2##########\;ESM=No amortizando activo         #2##########\;FRC=Pas d''amortissement d''immobilisation     #2##########\;ENC=Not depreciating fixed asset  #2##########\';
      Text005@1005 : TextConst 'ENU=Inserting journal lines       #3##########;ESM=Insertando l¡neas diario      #3##########;FRC=Ins‚rer lignes journal                   #3##########;ENC=Inserting journal lines       #3##########';
      Text006@1006 : TextConst 'ENU=Use Force No. of Days must be activated.;ESM=Se debe activar utilizar forzar n§ de d¡as.;FRC=La fonction forcer n§ de jours doit ˆtre activ‚e.;ENC=Use Force No. of Days must be activated.';
      GenJnlLine@1007 : Record 81;
      GenJnlLineTmp@1008 : TEMPORARY Record 81;
      FASetup@1009 : Record 5603;
      FAJnlLine@1010 : Record 5621;
      FAJnlLineTmp@1011 : TEMPORARY Record 5621;
      DeprBook@1012 : Record 5611;
      FAJnlSetup@1013 : Record 5605;
      CalculateDepr@1014 : Codeunit 5610;
      FAInsertGLAcc@1015 : Codeunit 5601;
      Window@1016 : Dialog;
      DeprAmount@1017 : Decimal;
      Custom1Amount@1018 : Decimal;
      NumberOfDays@1019 : Integer;
      Custom1NumberOfDays@1020 : Integer;
      DeprUntilDate@1021 : Date;
      UseForceNoOfDays@1022 : Boolean;
      DaysInPeriod@1023 : Integer;
      PostingDate@1024 : Date;
      DocumentNo@1025 : Code[20];
      DocumentNo2@1026 : Code[20];
      NoSeries@1027 : Code[20];
      PostingDescription@1028 : Text[50];
      DeprBookCode@1029 : Code[10];
      BalAccount@1030 : Boolean;
      ErrorNo@1031 : Integer;
      Custom1ErrorNo@1032 : Integer;
      FAJnlNextLineNo@1033 : Integer;
      GenJnlNextLineNo@1034 : Integer;
      EntryAmounts@1035 : ARRAY [4] OF Decimal;
      LineNo@1036 : Integer;
      CompletionStatsMsg@1037 : TextConst 'ENU=The depreciation has been calculated.\\No journal lines were created.;ESM=Se ha calculado la amortizaci¢n.\\No se crearon l¡neas del diario.;FRC=L''amortissement a ‚t‚ calcul‚.\\Aucune ligne journal n''a ‚t‚ cr‚‚e.;ENC=The depreciation has been calculated.\\No journal lines were created.';
      FAJnlLineCreatedCount@1038 : Integer;
      GenJnlLineCreatedCount@1039 : Integer;
      CompletionStatsFAJnlQst@1042 : TextConst '@@@=The depreciation has been calculated.\\5 fixed asset journal lines were created.\\Do you want to open the Fixed Asset Journal window?;ENU=The depreciation has been calculated.\\%1 fixed asset journal lines were created.\\Do you want to open the Fixed Asset Journal window?;ESM=La amortizaci¢n se ha calculado.\\Se han creado %1 l¡neas de diario de activos fijos.\\¨Desea abrir la ventana Diario activos fijos?;FRC=L''amortissement a ‚t‚ calcul‚.\\%1ÿlignes journal immobilisation ont ‚t‚ cr‚‚es.\\Souhaitez-vous ouvrir la fenˆtre Journal immobilisation?;ENC=The depreciation has been calculated.\\%1 fixed asset journal lines were created.\\Do you want to open the Fixed Asset Journal window?';
      CompletionStatsGenJnlQst@1043 : TextConst '@@@=The depreciation has been calculated.\\2 fixed asset G/L  journal lines were created.\\Do you want to open the Fixed Asset G/L Journal window?;ENU=The depreciation has been calculated.\\%1 fixed asset G/L journal lines were created.\\Do you want to open the Fixed Asset G/L Journal window?;ESM=La amortizaci¢n se ha calculado.\\Se han creado %1 l¡neas de diario general de activos fijos.\\¨Desea abrir la ventana A/F Diario general?;FRC=L''amortissement a ‚t‚ calcul‚.\\%1ÿlignes journal GL immo. ont ‚t‚ cr‚‚es.\\Souhaitez-vous ouvrir la fenˆtre Journal GL immobilisation?;ENC=The depreciation has been calculated.\\%1 fixed asset G/L journal lines were created.\\Do you want to open the Fixed Asset G/L Journal window?';
      DeprUntilDateModified@1040 : Boolean;

    PROCEDURE InitializeRequest@1(DeprBookCodeFrom@1007 : Code[10];DeprUntilDateFrom@1000 : Date;UseForceNoOfDaysFrom@1001 : Boolean;DaysInPeriodFrom@1002 : Integer;PostingDateFrom@1003 : Date;DocumentNoFrom@1004 : Code[20];PostingDescriptionFrom@1005 : Text[50];BalAccountFrom@1006 : Boolean);
    BEGIN
      DeprBookCode := DeprBookCodeFrom;
      DeprUntilDate := DeprUntilDateFrom;
      UseForceNoOfDays := UseForceNoOfDaysFrom;
      DaysInPeriod := DaysInPeriodFrom;
      PostingDate := PostingDateFrom;
      DocumentNo := DocumentNoFrom;
      PostingDescription := PostingDescriptionFrom;
      BalAccount := BalAccountFrom;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

