OBJECT Page 5512 G/L Entry Attachments Entity
{
  OBJECT-PROPERTIES
  {
    Date=09/19/18;
    Time=12:00:00 PM;
    Version List=NAVW113.00;
  }
  PROPERTIES
  {
    CaptionML=[@@@={Locked};
               ENU=generalLedgerEntryAttachments;
               ESM=generalLedgerEntryAttachments;
               FRC=generalLedgerEntryAttachments;
               ENC=generalLedgerEntryAttachments];
    SourceTable=Table5509;
    DelayedInsert=Yes;
    PageType=API;
    SourceTableTemporary=Yes;
    EntitySetName=generalLedgerEntryAttachments;
    EntityName=generalLedgerEntryAttachments;
    OnFindRecord=VAR
                   GLEntryNoFilter@1003 : Text;
                   AttachmentIdFilter@1002 : Text;
                   FilterView@1001 : Text;
                 BEGIN
                   IF NOT AttachmentsLoaded THEN BEGIN
                     FilterView := GETVIEW;
                     GLEntryNoFilter := GETFILTER("G/L Entry No.");
                     AttachmentIdFilter := GETFILTER(Id);
                     IF GLEntryNoFilter = '' THEN
                       ERROR(MissingGLEntryNoErr);

                     GraphMgtAttachmentBuffer.LoadAttachments(Rec,GLEntryNoFilter,AttachmentIdFilter);
                     SETVIEW(FilterView);
                     AttachmentsFound := FINDFIRST;
                     IF NOT AttachmentsFound THEN
                       EXIT(FALSE);
                     AttachmentsLoaded := TRUE;
                   END;
                   EXIT(AttachmentsFound);
                 END;

    OnInsertRecord=VAR
                     FileManagement@1001 : Codeunit 419;
                     TypeHelper@1004 : Codeunit 10;
                     Value@1003 : Variant;
                     GLEntryNoFilter@1000 : Text;
                     FilterView@1005 : Text;
                   BEGIN
                     IF "G/L Entry No." = 0 THEN BEGIN
                       FilterView := GETVIEW;
                       GLEntryNoFilter := GETFILTER("G/L Entry No.");
                       IF GLEntryNoFilter <> '' THEN BEGIN
                         Value := "G/L Entry No.";
                         TypeHelper.Evaluate(Value,GLEntryNoFilter,'','en-US');
                         "G/L Entry No." := Value;
                       END;
                       SETVIEW(FilterView);
                     END;
                     IF "G/L Entry No." = 0 THEN
                       ERROR(MissingGLEntryNoErr);

                     IF NOT FileManagement.IsValidFileName("File Name") THEN
                       VALIDATE("File Name",'filename.txt');

                     VALIDATE("Created Date-Time",ROUNDDATETIME(CURRENTDATETIME,1000));
                     GraphMgtAttachmentBuffer.RegisterFieldSet(FIELDNO("Created Date-Time"),TempFieldBuffer);

                     ByteSizeFromContent;

                     GraphMgtAttachmentBuffer.PropagateInsertAttachment(Rec,TempFieldBuffer);

                     EXIT(FALSE);
                   END;

    OnModifyRecord=BEGIN
                     IF xRec.Id <> Id THEN
                       ERROR(STRSUBSTNO(CannotModifyKeyFieldErr,'id'));
                     IF xRec."G/L Entry No." <> "G/L Entry No." THEN
                       ERROR(STRSUBSTNO(CannotModifyKeyFieldErr,'generalLedgerEntryNumber'));

                     GraphMgtAttachmentBuffer.PropagateModifyAttachment(Rec,TempFieldBuffer);
                     ByteSizeFromContent;
                     EXIT(FALSE);
                   END;

    OnDeleteRecord=BEGIN
                     GraphMgtAttachmentBuffer.PropagateDeleteAttachment(Rec);
                     EXIT(FALSE);
                   END;

    ODataKeyFields=G/L Entry No.,Id;
  }
  CONTROLS
  {
    { 1   ;0   ;Container ;
                ContainerType=ContentArea }

    { 2   ;1   ;Group     ;
                Name=Group;
                GroupType=Repeater }

    { 3   ;2   ;Field     ;
                Name=generalLedgerEntryNumber;
                CaptionML=[@@@={Locked};
                           ENU=generalLedgerEntryNumber;
                           ESM=generalLedgerEntryNumber;
                           FRC=generalLedgerEntryNumber;
                           ENC=generalLedgerEntryNumber];
                ApplicationArea=#All;
                SourceExpr="G/L Entry No." }

    { 4   ;2   ;Field     ;
                Name=id;
                CaptionML=[@@@={Locked};
                           ENU=id;
                           ESM=id;
                           FRC=id;
                           ENC=id];
                ApplicationArea=#All;
                SourceExpr=Id;
                OnValidate=BEGIN
                             GraphMgtAttachmentBuffer.RegisterFieldSet(FIELDNO(Id),TempFieldBuffer);
                           END;
                            }

    { 5   ;2   ;Field     ;
                Name=fileName;
                CaptionML=[@@@={Locked};
                           ENU=fileName;
                           ESM=fileName;
                           FRC=fileName;
                           ENC=fileName];
                ApplicationArea=#All;
                SourceExpr="File Name";
                OnValidate=BEGIN
                             GraphMgtAttachmentBuffer.RegisterFieldSet(FIELDNO("File Name"),TempFieldBuffer);
                           END;
                            }

    { 6   ;2   ;Field     ;
                Name=byteSize;
                CaptionML=[@@@={Locked};
                           ENU=byteSize;
                           ESM=byteSize;
                           FRC=byteSize;
                           ENC=byteSize];
                ApplicationArea=#All;
                SourceExpr="Byte Size";
                OnValidate=BEGIN
                             GraphMgtAttachmentBuffer.RegisterFieldSet(FIELDNO("Byte Size"),TempFieldBuffer);
                           END;
                            }

    { 7   ;2   ;Field     ;
                Name=content;
                CaptionML=[@@@={Locked};
                           ENU=content;
                           ESM=content;
                           FRC=content;
                           ENC=content];
                ApplicationArea=#All;
                SourceExpr=Content;
                OnValidate=BEGIN
                             IF AttachmentsLoaded THEN
                               MODIFY;
                             GraphMgtAttachmentBuffer.RegisterFieldSet(FIELDNO(Content),TempFieldBuffer);
                           END;
                            }

    { 8   ;2   ;Field     ;
                Name=createdDateTime;
                CaptionML=[@@@={Locked};
                           ENU=createdDateTime;
                           ESM=createdDateTime;
                           FRC=createdDateTime;
                           ENC=createdDateTime];
                ApplicationArea=#All;
                SourceExpr="Created Date-Time" }

  }
  CODE
  {
    VAR
      TempFieldBuffer@1003 : TEMPORARY Record 8450;
      GraphMgtAttachmentBuffer@1002 : Codeunit 5503;
      AttachmentsLoaded@1001 : Boolean;
      AttachmentsFound@1000 : Boolean;
      MissingGLEntryNoErr@1005 : TextConst '@@@={Locked};ENU=You must specify a generalLedgerEntryNumber in the request body.;ESM=You must specify a generalLedgerEntryNumber in the request body.;FRC=You must specify a generalLedgerEntryNumber in the request body.;ENC=You must specify a generalLedgerEntryNumber in the request body.';
      CannotModifyKeyFieldErr@1004 : TextConst '@@@={Locked};ENU=You cannot change the value of the key field %1.;ESM=You cannot change the value of the key field %1.;FRC=You cannot change the value of the key field %1.;ENC=You cannot change the value of the key field %1.';

    LOCAL PROCEDURE ByteSizeFromContent@15();
    VAR
      TempBlob@1001 : Record 99008535;
      GraphMgtAttachmentBuffer@1000 : Codeunit 5503;
    BEGIN
      TempBlob.Blob := Content;
      "Byte Size" := GraphMgtAttachmentBuffer.GetContentLength(TempBlob);
    END;

    BEGIN
    END.
  }
}

