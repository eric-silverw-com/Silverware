OBJECT Table 2121 O365 Brand Color
{
  OBJECT-PROPERTIES
  {
    Date=11/27/18;
    Time=11:14:36 PM;
    Version List=;
  }
  PROPERTIES
  {
    CaptionML=ENU=O365 Brand Color;
  }
  FIELDS
  {
    { 1   ;   ;Code                ;Code20        ;CaptionML=ENU=Code }
    { 2   ;   ;Name                ;Text30        ;CaptionML=ENU=Name }
    { 6   ;   ;Color Value         ;Code10        ;CaptionML=ENU=Color Value }
    { 15  ;   ;Sample Picture      ;Media         ;CaptionML=ENU=Sample Picture }
  }
  KEYS
  {
    {    ;Code                                    ;Clustered=Yes }
  }
  FIELDGROUPS
  {
    { 2   ;Brick               ;Name,Sample Picture                      }
  }
  CODE
  {
    VAR
      CustomColorCodeTxt@1000 : TextConst '@@@={Locked};ENU=Custom';
      CustomColorNameTxt@1001 : TextConst 'ENU=Custom';
      BlueCodeTok@1025 : TextConst '@@@=Blue;ENU=BLUE';
      BlueGrayCodeTok@1024 : TextConst '@@@=Blue gray;ENU=BLUE_GRAY';
      DarkBlueCodeTok@1023 : TextConst '@@@=dark blue;ENU=DARK_BLUE';
      GreenCodeTok@1022 : TextConst '@@@=Green;ENU=GREEN';
      DarkGreenCodeTok@1021 : TextConst '@@@=dark green;ENU=DARK_GREEN';
      OrangeCodeTok@1020 : TextConst '@@@=Orange;ENU=ORANGE';
      DarkOrangeTok@1019 : TextConst '@@@=dark orange;ENU=DARK_ORANGE';
      RedCodeTok@1018 : TextConst '@@@=Red;ENU=RED';
      PurpleCodeTok@1017 : TextConst '@@@=Purple;ENU=PURPLE';
      DarkPurpleCodeTok@1016 : TextConst '@@@=dark purple;ENU=DARK_PURPLE';
      YellowCodeTok@1015 : TextConst '@@@=yellow;ENU=YELLOW';
      TealCodeTok@1014 : TextConst '@@@=teal;ENU=TEAL';
      BlueTxt@1013 : TextConst 'ENU=Blue';
      BlueGrayTxt@1012 : TextConst 'ENU=Blue gray';
      DarkBlueTxt@1011 : TextConst 'ENU=Dark blue';
      GreenTxt@1010 : TextConst 'ENU=Green';
      DarkGreenTxt@1009 : TextConst 'ENU=Dark green';
      OrangeTxt@1008 : TextConst 'ENU=Orange';
      DarkOrangeTxt@1007 : TextConst 'ENU=Dark orange';
      RedTxt@1006 : TextConst 'ENU=Red';
      PurpleTxt@1005 : TextConst 'ENU=Purple';
      DarkPurpleTxt@1004 : TextConst 'ENU=Dark purple';
      YellowTxt@1003 : TextConst 'ENU=Yellow';
      TealTxt@1002 : TextConst 'ENU=Teal';

    PROCEDURE CreateOrUpdateCustomColor@2(VAR O365BrandColor@1000 : Record 2121;ColorValue@1001 : Code[10]);
    BEGIN
      IF NOT O365BrandColor.GET(CustomColorCodeTxt) THEN BEGIN
        O365BrandColor.INIT;
        O365BrandColor.Code := CustomColorCodeTxt;
        O365BrandColor.Name := CustomColorNameTxt;
        O365BrandColor.INSERT;
      END;

      O365BrandColor."Color Value" := ColorValue;
      O365BrandColor.MakePicture;
      O365BrandColor.MODIFY;
    END;

    PROCEDURE FindColor@4(VAR O365BrandColor@1001 : Record 2121;ColorValue@1000 : Code[10]);
    BEGIN
      O365BrandColor.SETRANGE("Color Value",ColorValue);
      IF O365BrandColor.FINDFIRST THEN
        EXIT;

      IF ColorValue = '' THEN
        IF O365BrandColor.GET(BlueCodeTok) THEN
          EXIT;

      // ARGB color format has additional alpha channel and has to be converted to RGB
      IF STRLEN(ColorValue) = 9 THEN
        ColorValue := ConvertARGBToRGB(ColorValue);

      O365BrandColor.SETRANGE("Color Value",ColorValue);
      IF O365BrandColor.FINDFIRST THEN
        EXIT;

      CreateOrUpdateCustomColor(O365BrandColor,ColorValue);
    END;

    PROCEDURE MakePicture@1();
    VAR
      DummyTempBlob@1012 : Record 99008535;
      Bitmap@1008 : DotNet "'System.Drawing, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b03f5f7f11d50a3a'.System.Drawing.Bitmap";
      Graphics@1007 : DotNet "'System.Drawing, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b03f5f7f11d50a3a'.System.Drawing.Graphics";
      Color@1006 : DotNet "'System.Drawing, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b03f5f7f11d50a3a'.System.Drawing.Color";
      ColorTranslator@1005 : DotNet "'System.Drawing, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b03f5f7f11d50a3a'.System.Drawing.ColorTranslator";
      SolidColorBrush@1004 : DotNet "'System.Drawing, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b03f5f7f11d50a3a'.System.Drawing.SolidBrush";
      ImageFormat@1002 : DotNet "'System.Drawing, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b03f5f7f11d50a3a'.System.Drawing.Imaging.ImageFormat";
      InStr@1010 : InStream;
    BEGIN
      Bitmap := Bitmap.Bitmap(100,100);
      Graphics := Graphics.FromImage(Bitmap);

      Color := ColorTranslator.FromHtml("Color Value");
      SolidColorBrush := SolidColorBrush.SolidBrush(Color);
      Graphics.FillEllipse(SolidColorBrush,0,0,100,100);
      Graphics.Dispose;

      DummyTempBlob.INIT;
      DummyTempBlob.Blob.CREATEINSTREAM(InStr);
      Bitmap.Save(InStr,ImageFormat.Png);

      "Sample Picture".IMPORTSTREAM(InStr,'');
      Bitmap.Dispose;
    END;

    LOCAL PROCEDURE BlendColorWithWhite@26(Value@1000 : Integer;Alpha@1001 : Integer) : Integer;
    BEGIN
      EXIT(ROUND(Value * Alpha / 255 + (1 - Alpha / 255) * 255,1));
    END;

    LOCAL PROCEDURE ConvertARGBToRGB@27(ARGBValue@1000 : Text) : Code[10];
    VAR
      Convert@1001 : DotNet "'mscorlib, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Convert";
      R@1002 : Integer;
      G@1003 : Integer;
      B@1004 : Integer;
      A@1005 : Integer;
    BEGIN
      A := Convert.ToInt32(COPYSTR(ARGBValue,2,2),16);
      R := Convert.ToInt32(COPYSTR(ARGBValue,4,2),16);
      G := Convert.ToInt32(COPYSTR(ARGBValue,6,2),16);
      B := Convert.ToInt32(COPYSTR(ARGBValue,8,2),16);

      EXIT(
        STRSUBSTNO(
          '#%1%2%3',
          IntToHex(BlendColorWithWhite(R,A)),
          IntToHex(BlendColorWithWhite(G,A)),
          IntToHex(BlendColorWithWhite(B,A))));
    END;

    LOCAL PROCEDURE IntToHex@3(IntValue@1000 : Integer) HexValue : Code[10];
    VAR
      TypeHelper@1001 : Codeunit 10;
    BEGIN
      HexValue := COPYSTR(TypeHelper.IntToHex(IntValue),1,MAXSTRLEN(HexValue));
      IF STRLEN(HexValue) = 1 THEN
        HexValue := '0' + HexValue;
    END;

    PROCEDURE CreateDefaultBrandColors@2103();
    BEGIN
      CreateBrandColor(RedCodeTok,RedTxt,'#B51725');
      CreateBrandColor(DarkOrangeTok,DarkOrangeTxt,'#DE371C');
      CreateBrandColor(OrangeCodeTok,OrangeTxt,'#FF5709');
      CreateBrandColor(YellowCodeTok,YellowTxt,'#E29D00');
      CreateBrandColor(GreenCodeTok,GreenTxt,'#25892F');
      CreateBrandColor(DarkGreenCodeTok,DarkGreenTxt,'#005C4D');
      CreateBrandColor(TealCodeTok,TealTxt,'#00A199');
      CreateBrandColor(BlueCodeTok,BlueTxt,'#008DD3');
      CreateBrandColor(DarkBlueCodeTok,DarkBlueTxt,'#003A6C');
      CreateBrandColor(BlueGrayCodeTok,BlueGrayTxt,'#536076');
      CreateBrandColor(DarkPurpleCodeTok,DarkPurpleTxt,'#3A327D');
      CreateBrandColor(PurpleCodeTok,PurpleTxt,'#8F65B6');
    END;

    LOCAL PROCEDURE CreateBrandColor@2104(BrandColorCode@1000 : Code[20];BrandColorName@1001 : Text[30];ColorValue@1002 : Code[10]);
    VAR
      O365BrandColor@1004 : Record 2121;
    BEGIN
      IF O365BrandColor.GET(Code) THEN
        EXIT;
      O365BrandColor.Code := BrandColorCode;
      O365BrandColor.Name := BrandColorName;
      O365BrandColor."Color Value" := ColorValue;
      O365BrandColor.MakePicture;
      O365BrandColor.INSERT;
    END;

    BEGIN
    END.
  }
}

