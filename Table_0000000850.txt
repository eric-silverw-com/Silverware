OBJECT Table 850 Cash Flow Manual Expense
{
  OBJECT-PROPERTIES
  {
    Date=11/27/18;
    Time=11:14:22 PM;
    Version List=;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               DimMgt.UpdateDefaultDim(
                 DATABASE::"Cash Flow Manual Expense",Code,
                 "Global Dimension 1 Code","Global Dimension 2 Code");
             END;

    OnDelete=BEGIN
               DimMgt.DeleteDefaultDim(DATABASE::"Cash Flow Manual Expense",Code);
             END;

    CaptionML=ENU=Cash Flow Manual Expense;
    LookupPageID=Page859;
    DrillDownPageID=Page859;
  }
  FIELDS
  {
    { 2   ;   ;Code                ;Code10        ;CaptionML=ENU=Code;
                                                   NotBlank=Yes }
    { 3   ;   ;Cash Flow Account No.;Code20       ;TableRelation="Cash Flow Account";
                                                   OnValidate=VAR
                                                                CFAccount@1000 : Record 841;
                                                              BEGIN
                                                                IF "Cash Flow Account No." <> '' THEN BEGIN
                                                                  CFAccount.GET("Cash Flow Account No.");
                                                                  CFAccount.TESTFIELD("Account Type",CFAccount."Account Type"::Entry);
                                                                  IF "Cash Flow Account No." <> xRec."Cash Flow Account No." THEN
                                                                    Description := CFAccount.Name;
                                                                END;
                                                              END;

                                                   CaptionML=ENU=Cash Flow Account No. }
    { 4   ;   ;Description         ;Text50        ;CaptionML=ENU=Description }
    { 5   ;   ;Starting Date       ;Date          ;CaptionML=ENU=Starting Date }
    { 6   ;   ;Ending Date         ;Date          ;CaptionML=ENU=Ending Date }
    { 7   ;   ;Recurring Frequency ;DateFormula   ;CaptionML=ENU=Recurring Frequency }
    { 8   ;   ;Amount              ;Decimal       ;CaptionML=ENU=Amount }
    { 9   ;   ;Global Dimension 1 Code;Code20     ;TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(1));
                                                   OnValidate=BEGIN
                                                                ValidateShortcutDimCode(1,"Global Dimension 1 Code");
                                                                MODIFY;
                                                              END;

                                                   CaptionML=ENU=Global Dimension 1 Code;
                                                   CaptionClass='1,1,1' }
    { 10  ;   ;Global Dimension 2 Code;Code20     ;TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(2));
                                                   OnValidate=BEGIN
                                                                ValidateShortcutDimCode(2,"Global Dimension 2 Code");
                                                                MODIFY;
                                                              END;

                                                   CaptionML=ENU=Global Dimension 2 Code;
                                                   CaptionClass='1,1,2' }
  }
  KEYS
  {
    {    ;Code                                    ;Clustered=Yes }
    {    ;Starting Date                            }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      DimMgt@1002 : Codeunit 408;
      ExpTxt@1000 : TextConst '@@@=Abbreviation of Expense, used as prefix for code (e.g. EXP000001);ENU=EXP';

    [External]
    PROCEDURE ValidateShortcutDimCode@1000(FieldNo@1000 : Integer;VAR ShortcutDimCode@1001 : Code[20]);
    BEGIN
      DimMgt.ValidateDimValueCode(FieldNo,ShortcutDimCode);
      DimMgt.SaveDefaultDim(DATABASE::"Cash Flow Manual Expense",Code,FieldNo,ShortcutDimCode);
    END;

    PROCEDURE InitNewRecord@1();
    VAR
      CashFlowManualExpense@1002 : Record 850;
      CashFlowAccount@1001 : Record 841;
      CashFlowCode@1000 : Code[10];
    BEGIN
      CashFlowManualExpense.SETFILTER(Code,'%1',ExpTxt + '0*');
      IF NOT CashFlowManualExpense.FINDLAST THEN
        CashFlowCode := PADSTR(ExpTxt,MAXSTRLEN(CashFlowManualExpense.Code),'0')
      ELSE
        CashFlowCode := CashFlowManualExpense.Code;
      CashFlowCode := INCSTR(CashFlowCode);

      CashFlowAccount.SETRANGE("Source Type",CashFlowAccount."Source Type"::"Cash Flow Manual Expense");
      IF NOT CashFlowAccount.FINDFIRST THEN
        EXIT;

      Code := CashFlowCode;
      "Cash Flow Account No." := CashFlowAccount."No.";
      "Starting Date" := WORKDATE;
      "Ending Date" := 0D;
    END;

    BEGIN
    END.
  }
}

