OBJECT Page 2861 Native - Payment Terms
{
  OBJECT-PROPERTIES
  {
    Date=11/27/18;
    Time=11:10:51 PM;
    Version List=;
  }
  PROPERTIES
  {
    CaptionML=[@@@={Locked};
               ENU=nativePaymentTerms];
    SourceTable=Table3;
    DelayedInsert=Yes;
    PageType=List;
    OnOpenPage=VAR
                 O365PaymentTerms@1001 : Record 2153;
               BEGIN
                 BINDSUBSCRIPTION(NativeAPILanguageHandler);
                 SETFILTER(Code,'<>%1&<>%2',O365PaymentTerms.ExcludedOneMonthPaymentTermCode,
                   O365PaymentTerms.ExcludedCurrentMonthPaymentTermCode);
               END;

    OnAfterGetRecord=VAR
                       O365SalesInitialSetup@1000 : Record 2110;
                     BEGIN
                       Default := O365SalesInitialSetup.IsDefaultPaymentTerms(Rec);
                       DescriptionInCurrentLanguage := GetDescriptionInCurrentLanguage;
                     END;

    OnInsertRecord=VAR
                     PaymentTerms@1000 : Record 3;
                     GraphMgtGeneralTools@1001 : Codeunit 5465;
                     RecRef@1002 : RecordRef;
                   BEGIN
                     PaymentTerms.SETRANGE(Code,Code);
                     IF NOT PaymentTerms.ISEMPTY THEN
                       INSERT;

                     INSERT(TRUE);

                     RecRef.GETTABLE(Rec);
                     GraphMgtGeneralTools.ProcessNewRecordFromAPI(RecRef,TempFieldSet,CURRENTDATETIME);
                     RecRef.SETTABLE(Rec);

                     MODIFY(TRUE);

                     EXIT(FALSE);
                   END;

    OnModifyRecord=VAR
                     GraphMgtGeneralTools@1001 : Codeunit 5465;
                   BEGIN
                     IF xRec.Id <> Id THEN
                       GraphMgtGeneralTools.ErrorIdImmutable;
                   END;

    ODataKeyFields=Id;
  }
  CONTROLS
  {
    { 1   ;0   ;Container ;
                ContainerType=ContentArea }

    { 2   ;1   ;Group     ;
                Name=Group;
                GroupType=Repeater }

    { 10  ;2   ;Field     ;
                Name=id;
                CaptionML=[@@@={Locked};
                           ENU=Id];
                ApplicationArea=#All;
                SourceExpr=Id }

    { 3   ;2   ;Field     ;
                Name=code;
                CaptionML=[@@@={Locked};
                           ENU=Code];
                ApplicationArea=#All;
                SourceExpr=Code;
                OnValidate=VAR
                             O365SalesInitialSetup@1000 : Record 2110;
                           BEGIN
                             RegisterFieldSet(FIELDNO(Code));

                             IF Default THEN
                               O365SalesInitialSetup.UpdateDefaultPaymentTerms(Code);
                           END;
                            }

    { 7   ;2   ;Field     ;
                Name=displayName;
                CaptionML=[@@@={Locked};
                           ENU=DisplayName];
                ToolTipML=ENU=Specifies the displayName.;
                ApplicationArea=#All;
                SourceExpr=DescriptionInCurrentLanguage;
                OnValidate=BEGIN
                             IF DescriptionInCurrentLanguage <> GetDescriptionInCurrentLanguage THEN BEGIN
                               IF STRLEN(DescriptionInCurrentLanguage) > MAXSTRLEN(Description) THEN
                                 ERROR(STRSUBSTNO(DisplayNameTooLongErr,MAXSTRLEN(Description)));
                               VALIDATE(Description,COPYSTR(DescriptionInCurrentLanguage,1,MAXSTRLEN(Description)));
                               RegisterFieldSet(FIELDNO(Description));
                             END;
                           END;
                            }

    { 4   ;2   ;Field     ;
                Name=dueDateCalculation;
                CaptionML=[@@@={Locked};
                           ENU=DueDateCalculation];
                ApplicationArea=#All;
                SourceExpr="Due Date Calculation";
                OnValidate=BEGIN
                             RegisterFieldSet(FIELDNO("Due Date Calculation"));
                           END;
                            }

    { 5   ;2   ;Field     ;
                Name=discountDateCalculation;
                CaptionML=[@@@={Locked};
                           ENU=DiscountDateCalculation];
                ApplicationArea=#All;
                SourceExpr="Discount Date Calculation";
                OnValidate=BEGIN
                             RegisterFieldSet(FIELDNO("Discount Date Calculation"));
                           END;
                            }

    { 6   ;2   ;Field     ;
                Name=discountPercent;
                CaptionML=[@@@={Locked};
                           ENU=DiscountPercent];
                ApplicationArea=#All;
                SourceExpr="Discount %";
                OnValidate=BEGIN
                             RegisterFieldSet(FIELDNO("Discount %"));
                           END;
                            }

    { 8   ;2   ;Field     ;
                Name=calculateDiscountOnCreditMemos;
                CaptionML=[@@@={Locked};
                           ENU=CalcPmtDiscOnCreditMemos];
                ApplicationArea=#All;
                SourceExpr="Calc. Pmt. Disc. on Cr. Memos";
                OnValidate=BEGIN
                             RegisterFieldSet(FIELDNO("Calc. Pmt. Disc. on Cr. Memos"));
                           END;
                            }

    { 9   ;2   ;Field     ;
                Name=lastModifiedDateTime;
                CaptionML=[@@@={Locked};
                           ENU=LastModifiedDateTime];
                ApplicationArea=#All;
                SourceExpr="Last Modified Date Time" }

    { 11  ;2   ;Field     ;
                Name=default;
                CaptionML=ENU=default;
                ToolTipML=ENU=Specifies that the payment terms are the default.;
                ApplicationArea=#All;
                SourceExpr=Default;
                OnValidate=VAR
                             O365SalesInitialSetup@1000 : Record 2110;
                           BEGIN
                             IF Default = FALSE THEN
                               ERROR(CannotSetDefaultToFalseErr);

                             O365SalesInitialSetup.UpdateDefaultPaymentTerms(Code);
                           END;
                            }

  }
  CODE
  {
    VAR
      TempFieldSet@1000 : TEMPORARY Record 2000000041;
      NativeAPILanguageHandler@1004 : Codeunit 2850;
      Default@1001 : Boolean;
      CannotSetDefaultToFalseErr@1002 : TextConst 'ENU=It is not possible to set the default to false. Select a different Payment Term as a default.';
      DescriptionInCurrentLanguage@1005 : Text;
      DisplayNameTooLongErr@1003 : TextConst '@@@=%1 - Max length of display name;ENU=The display name can be at most %1 characters long.';

    LOCAL PROCEDURE RegisterFieldSet@11(FieldNo@1000 : Integer);
    BEGIN
      IF TempFieldSet.GET(DATABASE::"Payment Terms",FieldNo) THEN
        EXIT;

      TempFieldSet.INIT;
      TempFieldSet.TableNo := DATABASE::"Payment Terms";
      TempFieldSet.VALIDATE("No.",FieldNo);
      TempFieldSet.INSERT(TRUE);
    END;

    BEGIN
    END.
  }
}

