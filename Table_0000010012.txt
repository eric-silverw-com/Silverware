OBJECT Table 10012 Sales Tax Amount Difference
{
  OBJECT-PROPERTIES
  {
    Date=11/27/18;
    Time=11:15:50 PM;
    Version List=;
  }
  PROPERTIES
  {
    CaptionML=ENU=Sales Tax Amount Difference;
  }
  FIELDS
  {
    { 1   ;   ;Document Type       ;Option        ;CaptionML=ENU=Document Type;
                                                   OptionCaptionML=ENU=Quote,Order,Invoice,Credit Memo,Blanket Order,Return Order;
                                                   OptionString=Quote,Order,Invoice,Credit Memo,Blanket Order,Return Order }
    { 2   ;   ;Document Product Area;Option       ;CaptionML=ENU=Document Product Area;
                                                   OptionCaptionML=ENU=Sales,Purchase,Service,,,,Posted Sale,Posted Purchase,Posted Service;
                                                   OptionString=Sales,Purchase,Service,,,,Posted Sale,Posted Purchase,Posted Service }
    { 3   ;   ;Document No.        ;Code20        ;TableRelation=IF (Document Product Area=CONST(Sales)) "Sales Header".No. WHERE (Document Type=FIELD(Document Type))
                                                                 ELSE IF (Document Product Area=CONST(Purchase)) "Purchase Header".No. WHERE (Document Type=FIELD(Document Type))
                                                                 ELSE IF (Document Product Area=CONST(Service)) "Service Header".No. WHERE (Document Type=FIELD(Document Type))
                                                                 ELSE IF (Document Type=CONST(Invoice),
                                                                          Document Product Area=CONST(Posted Sale)) "Sales Invoice Header"
                                                                          ELSE IF (Document Type=CONST(Credit Memo),
                                                                                   Document Product Area=CONST(Posted Sale)) "Sales Cr.Memo Header"
                                                                                   ELSE IF (Document Type=CONST(Invoice),
                                                                                            Document Product Area=CONST(Posted Purchase)) "Purch. Inv. Header"
                                                                                            ELSE IF (Document Type=CONST(Credit Memo),
                                                                                                     Document Product Area=CONST(Posted Purchase)) "Purch. Cr. Memo Hdr."
                                                                                                     ELSE IF (Document Type=CONST(Invoice),
                                                                                                              Document Product Area=CONST(Posted Service)) "Service Invoice Header"
                                                                                                              ELSE IF (Document Type=CONST(Credit Memo),
                                                                                                                       Document Product Area=CONST(Posted Service)) "Service Cr.Memo Header";
                                                   CaptionML=ENU=Document No. }
    { 5   ;   ;Tax Area Code       ;Code20        ;TableRelation="Tax Area";
                                                   CaptionML=ENU=Tax Area Code }
    { 6   ;   ;Tax Jurisdiction Code;Code10       ;TableRelation="Tax Jurisdiction";
                                                   CaptionML=ENU=Tax Jurisdiction Code }
    { 7   ;   ;Tax Group Code      ;Code20        ;TableRelation="Tax Group";
                                                   CaptionML=ENU=Tax Group Code }
    { 8   ;   ;Tax %               ;Decimal       ;CaptionML=ENU=Tax % }
    { 9   ;   ;Expense/Capitalize  ;Boolean       ;CaptionML=ENU=Expense/Capitalize }
    { 10  ;   ;Tax Type            ;Option        ;CaptionML=ENU=Tax Type;
                                                   OptionCaptionML=ENU=Sales and Use Tax,Excise Tax,Sales Tax Only,Use Tax Only;
                                                   OptionString=Sales and Use Tax,Excise Tax,Sales Tax Only,Use Tax Only }
    { 11  ;   ;Use Tax             ;Boolean       ;CaptionML=ENU=Use Tax }
    { 15  ;   ;Tax Difference      ;Decimal       ;CaptionML=ENU=Tax Difference;
                                                   Editable=No;
                                                   AutoFormatType=1 }
  }
  KEYS
  {
    {    ;Document Product Area,Document Type,Document No.,Tax Area Code,Tax Jurisdiction Code,Tax %,Tax Group Code,Expense/Capitalize,Tax Type,Use Tax;
                                                   Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {

    [External]
    PROCEDURE ClearDocDifference@1020001(ProductArea@1020001 : 'Sales,Purchase';DocType@1020002 : 'Quote,Order,Invoice,Credit Memo,Blanket Order,Return Order';DocNo@1020003 : Code[20]);
    VAR
      TaxAmountDifference@1020000 : Record 10012;
    BEGIN
      TaxAmountDifference.RESET;
      TaxAmountDifference.SETRANGE("Document Product Area",ProductArea);
      TaxAmountDifference.SETRANGE("Document Type",DocType);
      TaxAmountDifference.SETRANGE("Document No.",DocNo);
      IF TaxAmountDifference.FINDFIRST THEN
        TaxAmountDifference.DELETEALL;
    END;

    [External]
    PROCEDURE AnyTaxDifferenceRecords@1020002(ProductArea@1020001 : 'Sales,Purchase';DocType@1020002 : 'Quote,Order,Invoice,Credit Memo,Blanket Order,Return Order';DocNo@1020003 : Code[20]) : Boolean;
    VAR
      TaxAmountDifference@1020000 : Record 10012;
    BEGIN
      TaxAmountDifference.RESET;
      TaxAmountDifference.SETRANGE("Document Product Area",ProductArea);
      TaxAmountDifference.SETRANGE("Document Type",DocType);
      TaxAmountDifference.SETRANGE("Document No.",DocNo);
      EXIT(TaxAmountDifference.FINDFIRST);
    END;

    [External]
    PROCEDURE CopyTaxDifferenceRecords@1020003(FromProductArea@1020001 : 'Sales,Purchase';FromDocType@1020002 : 'Quote,Order,Invoice,Credit Memo,Blanket Order,Return Order';FromDocNo@1020003 : Code[20];ToProductArea@1020006 : ',,,,,,Posted Sale,Posted Purchase';ToDocType@1020005 : ',,Invoice,Credit Memo';ToDocNo@1020004 : Code[20]);
    VAR
      FromTaxAmountDifference@1020000 : Record 10012;
      ToTaxAmountDifference@1020007 : Record 10012;
    BEGIN
      FromTaxAmountDifference.RESET;
      FromTaxAmountDifference.SETRANGE("Document Product Area",FromProductArea);
      FromTaxAmountDifference.SETRANGE("Document Type",FromDocType);
      FromTaxAmountDifference.SETRANGE("Document No.",FromDocNo);
      IF FromTaxAmountDifference.FIND('-') THEN BEGIN
        ToTaxAmountDifference.INIT;
        ToTaxAmountDifference."Document Product Area" := ToProductArea;
        ToTaxAmountDifference."Document Type" := ToDocType;
        ToTaxAmountDifference."Document No." := ToDocNo;
        REPEAT
          ToTaxAmountDifference."Tax Area Code" := FromTaxAmountDifference."Tax Area Code";
          ToTaxAmountDifference."Tax Jurisdiction Code" := FromTaxAmountDifference."Tax Jurisdiction Code";
          ToTaxAmountDifference."Tax %" := FromTaxAmountDifference."Tax %";
          ToTaxAmountDifference."Tax Group Code" := FromTaxAmountDifference."Tax Group Code";
          ToTaxAmountDifference."Expense/Capitalize" := FromTaxAmountDifference."Expense/Capitalize";
          ToTaxAmountDifference."Tax Type" := FromTaxAmountDifference."Tax Type";
          ToTaxAmountDifference."Use Tax" := FromTaxAmountDifference."Use Tax";
          ToTaxAmountDifference."Tax Difference" := FromTaxAmountDifference."Tax Difference";
          ToTaxAmountDifference.INSERT;
        UNTIL FromTaxAmountDifference.NEXT = 0;
      END;
    END;

    BEGIN
    END.
  }
}

