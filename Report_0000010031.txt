OBJECT Report 10031 Budget from History
{
  OBJECT-PROPERTIES
  {
    Date=11/22/17;
    Time=12:00:00 PM;
    Version List=NAVNA11.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Budget from History;
               ESM=Ppto. de historial;
               FRC=Budget du registre;
               ENC=Budget from History];
    ProcessingOnly=Yes;
    OnPreReport=BEGIN
                  GLSetup.GET;
                  GlobalDim1Code := GLSetup."Global Dimension 1 Code";
                  GlobalDim2Code := GLSetup."Global Dimension 2 Code";
                END;

  }
  DATASET
  {
    { 6710;    ;DataItem;                    ;
               DataItemTable=Table15;
               DataItemTableView=SORTING(No.);
               OnPreDataItem=BEGIN
                               IF HistoryDate = 0D THEN
                                 ERROR(Text001);
                               IF BudgetDate = 0D THEN
                                 ERROR(Text002);
                               IF NoPeriods = 0 THEN
                                 ERROR(Text003);
                               PercentChg := PercentChg / 100;

                               IF PercentChg < 1 THEN
                                 PercentChg := 1 + PercentChg;
                               CASE Rounding OF
                                 Rounding::Pennies:
                                   Precision := 0.05;
                                 Rounding::Dollars:
                                   Precision := 1;
                                 Rounding::Hundreds:
                                   Precision := 100;
                                 Rounding::Thousands:
                                   Precision := 1000;
                                 Rounding::Millions:
                                   Precision := 1000000;
                               END;
                             END;

               ReqFilterFields=No.,Global Dimension 1 Filter,Global Dimension 2 Filter,Budget Filter }

    { 5444;1   ;DataItem;                    ;
               DataItemTable=Table2000000026;
               DataItemTableView=SORTING(Number);
               OnPreDataItem=BEGIN
                               GLBudgetEntry.RESET;
                               IF GLBudgetEntry.FINDLAST THEN
                                 EntryNo := GLBudgetEntry."Entry No.";
                               CurrentPeriod := 0;
                               RunHistoryDate := HistoryDate;
                               RunBudgetDate := BudgetDate;
                             END;

               OnAfterGetRecord=VAR
                                  TempDimSetEntry@1020002 : TEMPORARY Record 480;
                                  DimensionValue@1020001 : Record 349;
                                  DimMgt@1020000 : Codeunit 408;
                                BEGIN
                                  CurrentPeriod := CurrentPeriod + 1;
                                  IF CurrentPeriod > NoPeriods THEN
                                    CurrReport.BREAK;

                                  "G/L Account".SETRANGE("Date Filter",RunHistoryDate,NextDate(RunHistoryDate) - 1);
                                  "G/L Account".SETRANGE("Account Type","G/L Account"."Account Type"::Posting);
                                  "G/L Account".CALCFIELDS("Net Change");
                                  RunHistoryDate := NextDate(RunHistoryDate);

                                  EntryNo := EntryNo + 10000;
                                  GLBudgetEntry.INIT;
                                  GLBudgetEntry."Entry No." := EntryNo;
                                  GLBudgetEntry."Budget Name" := "G/L Account".GETFILTER("Budget Filter");
                                  GLBudgetEntry."G/L Account No." := "G/L Account"."No.";
                                  GLBudgetEntry.Date := RunBudgetDate;
                                  IF "G/L Account".GETFILTER("Global Dimension 1 Filter") <> '' THEN BEGIN
                                    GLBudgetEntry."Global Dimension 1 Code" := "G/L Account".GETRANGEMIN("Global Dimension 1 Filter");
                                    IF DimensionValue.GET(GlobalDim1Code,GLBudgetEntry."Global Dimension 1 Code") THEN BEGIN
                                      TempDimSetEntry.INIT;
                                      TempDimSetEntry."Dimension Code" := DimensionValue."Dimension Code";
                                      TempDimSetEntry."Dimension Value Code" := DimensionValue.Code;
                                      TempDimSetEntry."Dimension Value ID" := DimensionValue."Dimension Value ID";
                                      TempDimSetEntry.INSERT;
                                    END;
                                  END;
                                  IF "G/L Account".GETFILTER("Global Dimension 2 Filter") <> '' THEN BEGIN
                                    GLBudgetEntry."Global Dimension 2 Code" := "G/L Account".GETRANGEMIN("Global Dimension 2 Filter");
                                    IF DimensionValue.GET(GlobalDim2Code,GLBudgetEntry."Global Dimension 2 Code") THEN BEGIN
                                      TempDimSetEntry.INIT;
                                      TempDimSetEntry."Dimension Code" := DimensionValue."Dimension Code";
                                      TempDimSetEntry."Dimension Value Code" := DimensionValue.Code;
                                      TempDimSetEntry."Dimension Value ID" := DimensionValue."Dimension Value ID";
                                      TempDimSetEntry.INSERT;
                                    END;
                                  END;
                                  GLBudgetEntry.Amount := ROUND("G/L Account"."Net Change" * PercentChg,Precision);
                                  GLBudgetEntry."Dimension Set ID" := DimMgt.GetDimensionSetID(TempDimSetEntry);
                                  GLBudgetEntry.INSERT;
                                  RunBudgetDate := NextDate(RunBudgetDate);
                                END;
                                 }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
      OnOpenPage=BEGIN
                   IF PeriodLength = 0 THEN
                     PeriodLength := PeriodLength::Month;
                   IF Rounding = 0 THEN
                     Rounding := Rounding::Dollars;
                   IF NoPeriods = 0 THEN
                     NoPeriods := 12;
                 END;

    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[ENU=Options;
                             ESM=Opciones;
                             FRC=Options;
                             ENC=Options] }

      { 3   ;2   ;Field     ;
                  Name=HistoryBeginningDate;
                  CaptionML=[ENU=History Beginning Date;
                             ESM=Fecha inicial historial;
                             FRC=Date de d‚but registre;
                             ENC=History Beginning Date];
                  ToolTipML=[ENU=Specifies the starting date of the first historical period to be analyzed for the creation of the budget. Usually this would be one fiscal year before the budget beginning date.;
                             ESM=Especifica la fecha inicial del primer periodo hist¢rico que se debe analizar para la creaci¢n del presupuesto. Normalmente, corresponder  a un ejercicio anterior a la fecha inicial de presupuesto.;
                             FRC=Sp‚cifie la date de d‚but de la premiŠre p‚riode historique … analyser pour la cr‚ation du budget. G‚n‚ralement, ce sera un exercice financier avant la date de d‚but du budget.;
                             ENC=Specifies the starting date of the first historical period to be analysed for the creation of the budget. Usually this would be one fiscal year before the budget beginning date.];
                  ApplicationArea=#Suite;
                  SourceExpr=HistoryDate }

      { 7   ;2   ;Field     ;
                  Name=BudgetBeginningDate;
                  CaptionML=[ENU=Budget Beginning Date;
                             ESM=Fecha inicial ppto.;
                             FRC=Date de d‚but budget;
                             ENC=Budget Beginning Date];
                  ToolTipML=[ENU=Specifies the starting date of the first budget period to be created.;
                             ESM=Especifica la fecha de inicio del periodo del primer presupuesto que se debe crear.;
                             FRC=Sp‚cifie la date de d‚but de la premiŠre p‚riode budg‚taire … cr‚er.;
                             ENC=Specifies the starting date of the first budget period to be created.];
                  ApplicationArea=#Suite;
                  SourceExpr=BudgetDate }

      { 5   ;2   ;Field     ;
                  Name=NoOfPeriods;
                  CaptionML=[ENU=No. of Periods;
                             ESM=N§ de periodos;
                             FRC=Nombre de p‚riodes;
                             ENC=No. of Periods];
                  ToolTipML=[ENU=Specifies the number of budget periods to be created.;
                             ESM=Especifica n£mero de periodos de presupuesto que se deben crear.;
                             FRC=Sp‚cifie le nombre de p‚riodes budg‚taires … cr‚er.;
                             ENC=Specifies the number of budget periods to be created.];
                  ApplicationArea=#Suite;
                  SourceExpr=NoPeriods }

      { 10  ;2   ;Field     ;
                  CaptionML=[ENU=Period Length;
                             ESM=Longitud periodo;
                             FRC=Dur‚e de la p‚riode;
                             ENC=Period Length];
                  ToolTipML=[ENU=Specifies the length of each period, for example, enter "1M" for one month.;
                             ESM=Especifica la longitud de cada periodo. Por ejemplo, escriba "1M" para un mes.;
                             FRC=Sp‚cifie la longueur de chaque p‚riode, par exemple, entrez ®ÿ1Mÿ¯ pour un mois.;
                             ENC=Specifies the length of each period, for example, enter "1M" for one month.];
                  OptionCaptionML=[ENU=,Day,Week,Month,Quarter,Year,Accounting Period;
                                   ESM=,D¡a,Semana,Mes,Trimestre,A¤o,Periodo contable;
                                   FRC=,Jour,Semaine,Mois,Trimestre,Ann‚e,P‚riode comptable;
                                   ENC=,Day,Week,Month,Quarter,Year,Accounting Period];
                  ApplicationArea=#Suite;
                  SourceExpr=PeriodLength }

      { 8   ;2   ;Field     ;
                  CaptionML=[ENU=Percent Change;
                             ESM=Cambio porcen.;
                             FRC=Pourcentage de changement;
                             ENC=Percent Change];
                  ToolTipML=[ENU=Specifies if you want the budget to increase in relation to the corresponding historical period. The budget created will be that percentage over (under if negative) the actual amounts existing in the corresponding historical period. For example, if the historical amount for a period is $100, and the percent change is 10, then the budget for the corresponding period will be $110.;
                             ESM=Especifica si desea que el presupuesto se aumente en relaci¢n con el periodo hist¢rico correspondiente. El presupuesto creado ser  ese porcentaje por encima (por debajo si es negativo) de los importes reales que existen en el periodo hist¢rico correspondiente. Por ejemplo, si el importe hist¢rico de un periodo es 100 USD y el cambio de porcentaje es 10, el presupuesto del periodo correspondiente ser  de 110 USD.;
                             FRC=Sp‚cifie si vous souhaitez que le budget augmente par rapport … la p‚riode historique pr‚c‚dente. Le budget cr‚‚ correspondra au pourcentage en plus (ou en moins, si c'est n‚gatif) par rapport aux montants r‚els existants dans la p‚riode historique correspondante. Par exemple, si le montant historique pour une p‚riode correspond … 100ÿ$ et que le pourcentage de changement est 10, le budget pour la p‚riode correspondante sera de 110ÿ$.;
                             ENC=Specifies if you want the budget to increase in relation to the corresponding historical period. The budget created will be that percentage over (under if negative) the actual amounts existing in the corresponding historical period. For example, if the historical amount for a period is $100, and the percent change is 10, then the budget for the corresponding period will be $110.];
                  ApplicationArea=#Suite;
                  SourceExpr=PercentChg }

      { 11  ;2   ;Field     ;
                  Name=RoundTo;
                  CaptionML=[ENU=Round To;
                             ESM=Redondear a;
                             FRC=Arrondir …;
                             ENC=Round To];
                  ToolTipML=[ENU=Specifies if you want the results in the report to be rounded to the nearest penny (hundredth of a unit), dollar (unit), or thousand dollars (units). The results are in US dollars, unless you use an additional reporting currency.;
                             ESM=Especifica si desea que los resultados del informe se redondeen al centavo (c‚ntimo de unidad), d¢lar (unidad) o millar de d¢lares (unidades) m s cercano. Los resultados se muestran en USD, a menos que use una divisa de notificaci¢n adicional.;
                             FRC=Sp‚cifie si vous souhaitez que les r‚sultats du rapport soient arrondis au penny le plus proche (centiŠme d'une unit‚), au dollar (unit‚) ou au millier de dollars (unit‚s). Les r‚sultats sont en dollars US, sauf si vous utilisez une devise de report additionnelle.;
                             ENC=Specifies if you want the results in the report to be rounded to the nearest penny (hundredth of a unit), dollar (unit), or thousand dollars (units). The results are in CA Dollars, unless you use an additional reporting currency.];
                  OptionCaptionML=[ENU=,Pennies,Dollars,Hundreds,Thousands,Millions;
                                   ESM=,Centavos,Pesos,Cientos,Miles,Millones;
                                   FRC=,Cents,Dollars,Centaines,Milliers,Millions;
                                   ENC=,Pennies,Dollars,Hundreds,Thousands,Millions];
                  ApplicationArea=#Suite;
                  SourceExpr=Rounding }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      GLBudgetEntry@1000 : Record 96;
      AcctPeriod@1001 : Record 50;
      GLSetup@1020001 : Record 98;
      PercentChg@1002 : Decimal;
      Precision@1003 : Decimal;
      BudgetDate@1004 : Date;
      RunBudgetDate@1005 : Date;
      HistoryDate@1006 : Date;
      RunHistoryDate@1007 : Date;
      PeriodLength@1008 : ',Day,Week,Month,Quarter,Year,Accounting Period';
      Rounding@1009 : ',Pennies,Dollars,Hundreds,Thousands,Millions';
      NoPeriods@1010 : Integer;
      CurrentPeriod@1011 : Integer;
      EntryNo@1012 : Integer;
      Text000@1016 : TextConst 'ENU=There are not enough accounting periods set up to complete this budget.;ESM=No hay suficientes per¡odos contables para completar este presupuesto.;FRC=Il n''y a pas assez de p‚riodes comptables configur‚es pour compl‚ter ce budget.;ENC=There are not enough accounting periods set up to complete this budget.';
      Text001@1015 : TextConst 'ENU=Please enter a beginning date for the history.;ESM=Introduzca una fecha inicial para el historial.;FRC=Entrez une date de d‚but pour l''historique.;ENC=Please enter a beginning date for the history.';
      Text002@1014 : TextConst 'ENU=Please enter a beginning date for the budget.;ESM=Introduzca una fecha inicial para el presupuesto.;FRC=Entrez une date de d‚but pour le budget.;ENC=Please enter a beginning date for the budget.';
      Text003@1013 : TextConst 'ENU=Please enter the number of periods to budget.;ESM=Inserte el n§ de per¡odos para presupuestar.;FRC=Entrez un nombre de p‚riodes au budget.;ENC=Please enter the number of periods to budget.';
      GlobalDim1Code@1020002 : Code[20];
      GlobalDim2Code@1020003 : Code[20];

    [External]
    PROCEDURE NextDate@1(CurrentDate@1000 : Date) : Date;
    VAR
      CalculatedDate@1001 : Date;
    BEGIN
      CASE PeriodLength OF
        PeriodLength::Day:
          CalculatedDate := CALCDATE('<+1D>',CurrentDate);
        PeriodLength::Week:
          CalculatedDate := CALCDATE('<+1W>',CurrentDate);
        PeriodLength::Month:
          CalculatedDate := CALCDATE('<+1M>',CurrentDate);
        PeriodLength::Quarter:
          CalculatedDate := CALCDATE('<+1Q>',CurrentDate);
        PeriodLength::Year:
          CalculatedDate := CALCDATE('<+1Y>',CurrentDate);
        PeriodLength::"Accounting Period":
          BEGIN
            AcctPeriod.SETFILTER("Starting Date",'>%1',CurrentDate);
            IF NOT AcctPeriod.FINDFIRST THEN
              ERROR(Text000);
            CalculatedDate := AcctPeriod."Starting Date";
          END;
      END;
      EXIT(CalculatedDate);
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

