OBJECT Page 664 Sales Prepayment Percentages
{
  OBJECT-PROPERTIES
  {
    Date=09/19/18;
    Time=12:00:00 PM;
    Version List=NAVW113.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Sales Prepayment Percentages;
               ESM=Porcentajes anticipo ventas;
               FRC=Pourcentages de paiement anticip‚ pour vente;
               ENC=Sales Prepayment Percentages];
    SourceTable=Table459;
    DataCaptionExpr=Caption;
    DelayedInsert=Yes;
    PageType=Worksheet;
    OnInit=BEGIN
             CodeFilterCtrlEnable := TRUE;
             SalesCodeFilterCtrlEnable := TRUE;
             SalesCodeEditable := TRUE;
           END;

    OnOpenPage=BEGIN
                 GetRecFilters;
                 SetRecFilters;
               END;

    OnAfterGetCurrRecord=BEGIN
                           SetEditable;
                         END;

  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 17  ;1   ;Group     ;
                CaptionML=[ENU=General;
                           ESM=General;
                           FRC=G‚n‚ral;
                           ENC=General] }

    { 18  ;2   ;Field     ;
                CaptionML=[ENU=Sales Type Filter;
                           ESM=Filtro tipo ventas;
                           FRC=Filtre type vente;
                           ENC=Sales Type Filter];
                ToolTipML=[ENU=Specifies if the sales prepayments apply to customers or price groups.;
                           ESM=Especifica si los anticipos de ventas se aplican a los clientes o grupos de precios.;
                           FRC=Sp‚cifie si les paiements anticip‚s ventes s'appliquent aux clients ou aux groupes de prix.;
                           ENC=Specifies if the sales prepayments apply to customers or price groups.];
                OptionCaptionML=[ENU=Customer,Customer Price Group,All Customers,None;
                                 ESM=Cliente,Grupo precio cliente,Todos clientes,Ninguno;
                                 FRC=Client,Groupe de prix du client,Tous les clients,Aucun;
                                 ENC=Customer,Customer Price Group,All Customers,None];
                ApplicationArea=#Basic,#Suite;
                SourceExpr=SalesTypeFilter;
                OnValidate=BEGIN
                             SalesTypeFilterOnAfterValidate;
                           END;
                            }

    { 19  ;2   ;Field     ;
                Name=SalesCodeFilterCtrl;
                CaptionML=[ENU=Sales Code Filter;
                           ESM=Filtro c¢d. ventas;
                           FRC=Filtre code vente;
                           ENC=Sales Code Filter];
                ToolTipML=[ENU=Specifies a filter for the customer or price group that the prepayment percentages apply to.;
                           ESM=Especifica un filtro para el cliente o grupo de precios al que se aplican los porcentajes de anticipo.;
                           FRC=Sp‚cifie un filtre pour le client ou le groupe de prix auquel s'appliquent les pourcentages paiement anticip‚.;
                           ENC=Specifies a filter for the customer or price group that the prepayment percentages apply to.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr=SalesCodeFilter;
                Enabled=SalesCodeFilterCtrlEnable;
                OnValidate=BEGIN
                             SalesCodeFilterOnAfterValidate;
                           END;

                OnLookup=VAR
                           CustList@1005 : Page 22;
                           CustPriceGrList@1004 : Page 7;
                         BEGIN
                           IF SalesTypeFilter = SalesTypeFilter::"All Customers" THEN
                             EXIT;

                           CASE SalesTypeFilter OF
                             SalesTypeFilter::Customer:
                               BEGIN
                                 CustList.LOOKUPMODE := TRUE;
                                 IF CustList.RUNMODAL = ACTION::LookupOK THEN BEGIN
                                   Text := CustList.GetSelectionFilter;
                                   EXIT(TRUE);
                                 END;
                               END;
                             SalesTypeFilter::"Customer Price Group":
                               BEGIN
                                 CustPriceGrList.LOOKUPMODE := TRUE;
                                 IF CustPriceGrList.RUNMODAL = ACTION::LookupOK THEN BEGIN
                                   Text := CustPriceGrList.GetSelectionFilter;
                                   EXIT(TRUE);
                                 END;
                               END;
                           END;
                         END;
                          }

    { 24  ;2   ;Field     ;
                Name=CodeFilterCtrl;
                CaptionML=[ENU=Item No. Filter;
                           ESM=Filtro n§ prod.;
                           FRC=Filtre nø article;
                           ENC=Item No. Filter];
                ToolTipML=[ENU=Specifies a filter for the items that the prepayment percentages apply to.;
                           ESM=Especifica un filtro para los productos a los que se aplican los porcentajes de anticipo.;
                           FRC=Sp‚cifie un filtre pour les articles auxquels s'appliquent les pourcentages paiement anticip‚.;
                           ENC=Specifies a filter for the items that the prepayment percentages apply to.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr=ItemNoFilter;
                Enabled=CodeFilterCtrlEnable;
                OnValidate=BEGIN
                             ItemNoFilterOnAfterValidate;
                           END;

                OnLookup=VAR
                           ItemList@1000 : Page 31;
                         BEGIN
                           ItemList.LOOKUPMODE := TRUE;
                           IF ItemList.RUNMODAL = ACTION::LookupOK THEN BEGIN
                             Text := ItemList.GetSelectionFilter;
                             EXIT(TRUE);
                           END;
                         END;
                          }

    { 20  ;2   ;Field     ;
                CaptionML=[ENU=Starting Date Filter;
                           ESM=Filtro fecha inicio;
                           FRC=Filtre date d‚but;
                           ENC=Starting Date Filter];
                ToolTipML=[ENU=Specifies a filter for the starting date of the prepayment percentages.;
                           ESM=Especifica un filtro para la fecha inicial de los porcentajes de anticipo.;
                           FRC=Sp‚cifie un filtre pour la date d‚but … laquelle s'appliquent les pourcentages paiement anticip‚.;
                           ENC=Specifies a filter for the starting date of the prepayment percentages.];
                ApplicationArea=#Prepayments;
                SourceExpr=StartingDateFilter;
                OnValidate=VAR
                             TextManagement@1000 : Codeunit 41;
                           BEGIN
                             TextManagement.MakeDateFilter(StartingDateFilter);
                             StartingDateFilterOnAfterValid;
                           END;
                            }

    { 1   ;1   ;Group     ;
                GroupType=Repeater }

    { 2   ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the sales type of the prepayment percentage.;
                           ESM=Especifica el tipo de venta del porcentaje de anticipo.;
                           FRC=Sp‚cifie le type vente du pourcentage de paiement anticip‚.;
                           ENC=Specifies the sales type of the prepayment percentage.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Sales Type";
                OnValidate=BEGIN
                             SalesTypeOnAfterValidate;
                           END;
                            }

    { 4   ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the code that belongs to the sales type.;
                           ESM=Especifica el c¢digo que pertenece al tipo de venta.;
                           FRC=Sp‚cifie le code correspondant au type vente.;
                           ENC=Specifies the code that belongs to the sales type.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Sales Code";
                Editable=SalesCodeEditable }

    { 6   ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the number of the item for which the prepayment percentage is valid.;
                           ESM=Especifica el n£mero del producto para el que es v lido el porcentaje de anticipo.;
                           FRC=Sp‚cifie le num‚ro de l'article pour lequel le pourcentage de paiement anticip‚ est valable.;
                           ENC=Specifies the number of the item for which the prepayment percentage is valid.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Item No." }

    { 8   ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the date from which the prepayment percentage is valid.;
                           ESM=Especifica la fecha a partir de la cual es v lido el porcentaje de anticipo.;
                           FRC=Sp‚cifie la date de d‚but de validit‚ du pourcentage de paiement anticip‚.;
                           ENC=Specifies the date from which the prepayment percentage is valid.];
                ApplicationArea=#Prepayments;
                SourceExpr="Starting Date" }

    { 10  ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the date to which the prepayment percentage is valid.;
                           ESM=Especifica la fecha hasta la que es v lido el porcentaje de anticipo.;
                           FRC=Sp‚cifie la date de fin de validit‚ du pourcentage de paiement anticip‚.;
                           ENC=Specifies the date to which the prepayment percentage is valid.];
                ApplicationArea=#Prepayments;
                SourceExpr="Ending Date" }

    { 12  ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the prepayment percentage to use to calculate the prepayment for sales.;
                           ESM=Especifica el porcentaje de anticipo que se debe usar para calcular el anticipo de las ventas.;
                           FRC=Sp‚cifie le pourcentage de paiement anticip‚ … utiliser pour calculer le paiement anticip‚ pour les ventes.;
                           ENC=Specifies the prepayment percentage to use to calculate the prepayment for sales.];
                ApplicationArea=#Prepayments;
                SourceExpr="Prepayment %" }

    { 1900000007;0;Container;
                ContainerType=FactBoxArea }

    { 1900383207;1;Part   ;
                Visible=FALSE;
                PartType=System;
                SystemPartID=RecordLinks }

    { 1905767507;1;Part   ;
                Visible=FALSE;
                PartType=System;
                SystemPartID=Notes }

  }
  CODE
  {
    VAR
      SalesTypeFilter@1003 : 'Customer,Customer Price Group,All Customers,None';
      SalesCodeFilter@1002 : Text;
      ItemNoFilter@1001 : Text;
      StartingDateFilter@1000 : Text[30];
      SalesCodeEditable@19031640 : Boolean INDATASET;
      SalesCodeFilterCtrlEnable@19013164 : Boolean INDATASET;
      CodeFilterCtrlEnable@19026169 : Boolean INDATASET;

    LOCAL PROCEDURE SetEditable@3();
    BEGIN
      SalesCodeEditable := "Sales Type" <> "Sales Type"::"All Customers";
    END;

    LOCAL PROCEDURE GetRecFilters@4();
    BEGIN
      IF HASFILTER THEN BEGIN
        IF GETFILTER("Sales Type") <> '' THEN
          SalesTypeFilter := "Sales Type"
        ELSE
          SalesTypeFilter := SalesTypeFilter::None;

        SalesCodeFilter := GETFILTER("Sales Code");
        ItemNoFilter := GETFILTER("Item No.");
        EVALUATE(StartingDateFilter,GETFILTER("Starting Date"));
      END;
    END;

    [External]
    PROCEDURE SetRecFilters@5();
    BEGIN
      SalesCodeFilterCtrlEnable := TRUE;
      CodeFilterCtrlEnable := TRUE;

      IF SalesTypeFilter <> SalesTypeFilter::None THEN
        SETRANGE("Sales Type",SalesTypeFilter)
      ELSE
        SETRANGE("Sales Type");

      IF SalesTypeFilter IN [SalesTypeFilter::"All Customers",SalesTypeFilter::None] THEN BEGIN
        SalesCodeFilterCtrlEnable := FALSE;
        SalesCodeFilter := '';
      END;

      IF SalesCodeFilter <> '' THEN
        SETFILTER("Sales Code",SalesCodeFilter)
      ELSE
        SETRANGE("Sales Code");

      IF ItemNoFilter <> '' THEN
        SETFILTER("Item No.",ItemNoFilter)
      ELSE
        SETRANGE("Item No.");

      IF StartingDateFilter <> '' THEN
        SETFILTER("Starting Date",StartingDateFilter)
      ELSE
        SETRANGE("Starting Date");

      CurrPage.UPDATE(FALSE);
    END;

    LOCAL PROCEDURE Caption@6() : Text;
    VAR
      Item@1005 : Record 27;
      Cust@1004 : Record 18;
      CustPriceGr@1003 : Record 6;
      ItemNoCaption@1001 : Text;
      SalesCodeCaption@1000 : Text;
    BEGIN
      IF ItemNoFilter <> '' THEN BEGIN
        ItemNoCaption := STRSUBSTNO('%1 %2',Item.TABLECAPTION,ItemNoFilter);
        IF Item.GET(COPYSTR(ItemNoFilter,1,MAXSTRLEN(Item."No."))) THEN
          ItemNoCaption := ItemNoCaption + ' - ' + Item.Description;
      END;

      CASE SalesTypeFilter OF
        SalesTypeFilter::Customer:
          BEGIN
            SalesCodeCaption := STRSUBSTNO('%1 %2',Cust.TABLECAPTION,SalesCodeFilter);
            IF Cust.GET(COPYSTR(SalesCodeFilter,1,MAXSTRLEN(Cust."No."))) THEN
              SalesCodeCaption := SalesCodeCaption + ' - ' + Cust.Name;
          END;
        SalesTypeFilter::"Customer Price Group":
          BEGIN
            SalesCodeCaption := STRSUBSTNO('%1 %2',CustPriceGr.TABLECAPTION,SalesCodeFilter);
            IF CustPriceGr.GET(COPYSTR(SalesCodeFilter,1,MAXSTRLEN(CustPriceGr.Code))) THEN
              SalesCodeCaption := SalesCodeCaption + ' - ' + CustPriceGr.Description;
          END;
        SalesTypeFilter::"All Customers":
          SalesCodeCaption := FORMAT(SalesTypeFilter);
      END;

      EXIT(DELCHR(ItemNoCaption + ' ' + SalesCodeCaption,'<>'))
    END;

    LOCAL PROCEDURE SalesTypeOnAfterValidate@19046339();
    BEGIN
      SetEditable;
    END;

    LOCAL PROCEDURE SalesTypeFilterOnAfterValidate@19024044();
    BEGIN
      CurrPage.SAVERECORD;
      SalesCodeFilter := '';
      SetRecFilters;
    END;

    LOCAL PROCEDURE SalesCodeFilterOnAfterValidate@19067727();
    BEGIN
      CurrPage.SAVERECORD;
      SetRecFilters;
    END;

    LOCAL PROCEDURE StartingDateFilterOnAfterValid@19055286();
    BEGIN
      CurrPage.SAVERECORD;
      SetRecFilters;
    END;

    LOCAL PROCEDURE ItemNoFilterOnAfterValidate@19009808();
    BEGIN
      CurrPage.SAVERECORD;
      SetRecFilters;
    END;

    BEGIN
    END.
  }
}

