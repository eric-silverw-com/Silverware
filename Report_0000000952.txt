OBJECT Report 952 Suggest Job Jnl. Lines
{
  OBJECT-PROPERTIES
  {
    Date=09/19/18;
    Time=12:00:00 PM;
    Version List=NAVW113.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Suggest Job Jnl. Lines;
               ESM=Proponer l¡neas diario proyectos;
               FRC=Proposer lignes feuille projet;
               ENC=Suggest Job Jnl. Lines];
    ProcessingOnly=Yes;
    OnPostReport=VAR
                   NoSeriesMgt@1002 : Codeunit 396;
                   TimeSheetMgt@1003 : Codeunit 950;
                   NextDocNo@1001 : Code[20];
                   LineNo@1000 : Integer;
                   QtyToPost@1004 : Decimal;
                 BEGIN
                   DateFilter := TimeSheetMgt.GetDateFilter(StartingDate,EndingDate);
                   FillTimeSheetLineBuffer;

                   IF TempTimeSheetLine.FINDSET THEN BEGIN
                     JobJnlLine.LOCKTABLE;
                     JobJnlTemplate.GET(JobJnlLine."Journal Template Name");
                     JobJnlBatch.GET(JobJnlLine."Journal Template Name",JobJnlLine."Journal Batch Name");
                     IF JobJnlBatch."No. Series" = '' THEN
                       NextDocNo := ''
                     ELSE
                       NextDocNo := NoSeriesMgt.GetNextNo(JobJnlBatch."No. Series",TempTimeSheetLine."Time Sheet Starting Date",FALSE);

                     JobJnlLine.SETRANGE("Journal Template Name",JobJnlLine."Journal Template Name");
                     JobJnlLine.SETRANGE("Journal Batch Name",JobJnlLine."Journal Batch Name");
                     IF JobJnlLine.FINDLAST THEN;
                     LineNo := JobJnlLine."Line No.";

                     REPEAT
                       TimeSheetHeader.GET(TempTimeSheetLine."Time Sheet No.");
                       TimeSheetDetail.SETRANGE("Time Sheet No.",TempTimeSheetLine."Time Sheet No.");
                       TimeSheetDetail.SETRANGE("Time Sheet Line No.",TempTimeSheetLine."Line No.");
                       IF DateFilter <> '' THEN
                         TimeSheetDetail.SETFILTER(Date,DateFilter);
                       TimeSheetDetail.SETFILTER(Quantity,'<>0');
                       TimeSheetDetail.SETRANGE(Posted,FALSE);
                       IF TimeSheetDetail.FINDSET THEN
                         REPEAT
                           QtyToPost := TimeSheetDetail.GetMaxQtyToPost;
                           IF QtyToPost <> 0 THEN BEGIN
                             JobJnlLine.INIT;
                             LineNo := LineNo + 10000;
                             JobJnlLine."Line No." := LineNo;
                             JobJnlLine."Time Sheet No." := TimeSheetDetail."Time Sheet No.";
                             JobJnlLine."Time Sheet Line No." := TimeSheetDetail."Time Sheet Line No.";
                             JobJnlLine."Time Sheet Date" := TimeSheetDetail.Date;
                             JobJnlLine.VALIDATE("Job No.",TimeSheetDetail."Job No.");
                             JobJnlLine."Source Code" := JobJnlTemplate."Source Code";
                             IF TimeSheetDetail."Job Task No." <> '' THEN
                               JobJnlLine.VALIDATE("Job Task No.",TimeSheetDetail."Job Task No.");
                             JobJnlLine.VALIDATE(Type,JobJnlLine.Type::Resource);
                             JobJnlLine.VALIDATE("No.",TimeSheetHeader."Resource No.");
                             IF TempTimeSheetLine."Work Type Code" <> '' THEN
                               JobJnlLine.VALIDATE("Work Type Code",TempTimeSheetLine."Work Type Code");
                             JobJnlLine.VALIDATE("Posting Date",TimeSheetDetail.Date);
                             JobJnlLine."Document No." := NextDocNo;
                             NextDocNo := INCSTR(NextDocNo);
                             JobJnlLine."Posting No. Series" := JobJnlBatch."Posting No. Series";
                             JobJnlLine.Description := TempTimeSheetLine.Description;
                             JobJnlLine.VALIDATE(Quantity,QtyToPost);
                             JobJnlLine.VALIDATE(Chargeable,TempTimeSheetLine.Chargeable);
                             JobJnlLine."Reason Code" := JobJnlBatch."Reason Code";
                             OnAfterTransferTimeSheetDetailToJobJnlLine(JobJnlLine,JobJnlTemplate,TempTimeSheetLine,TimeSheetDetail);
                             JobJnlLine.INSERT;
                           END;
                         UNTIL TimeSheetDetail.NEXT = 0;
                     UNTIL TempTimeSheetLine.NEXT = 0;
                   END;
                 END;

  }
  DATASET
  {
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[ENU=Options;
                             ESM=Opciones;
                             FRC=Options;
                             ENC=Options] }

      { 2   ;2   ;Field     ;
                  Name=StartingDate;
                  CaptionML=[ENU=Starting Date;
                             ESM=Fecha inicial;
                             FRC=Date d‚but;
                             ENC=Starting Date];
                  ToolTipML=[ENU=Specifies the date from which the report or batch job processes information.;
                             ESM=Especifica la fecha a partir de la cual el informe o trabajo por lotes procesa la informaci¢n.;
                             FRC=Sp‚cifie la date … partir de laquelle le rapport ou le traitement en lot traite les informations.;
                             ENC=Specifies the date from which the report or batch job processes information.];
                  ApplicationArea=#Jobs;
                  SourceExpr=StartingDate }

      { 1   ;2   ;Field     ;
                  Name=EndingDate;
                  CaptionML=[ENU=Ending Date;
                             ESM=Fecha final;
                             FRC=Date fin;
                             ENC=Ending Date];
                  ToolTipML=[ENU=Specifies the date to which the report or batch job processes information.;
                             ESM=Especifica la fecha hasta la cual el informe o trabajo por lotes procesa la informaci¢n.;
                             FRC=Sp‚cifie la date jusqu'… laquelle le rapport ou le traitement en lot traite les informations.;
                             ENC=Specifies the date to which the report or batch job processes information.];
                  ApplicationArea=#Jobs;
                  SourceExpr=EndingDate }

      { 3   ;2   ;Field     ;
                  Name=ResourceNoFilter;
                  CaptionML=[ENU=Resource No. Filter;
                             ESM=Filtro n§ recurso;
                             FRC=Filtre nø ressource;
                             ENC=Resource No. Filter];
                  ToolTipML=[ENU=Specifies the resource number that the batch job will suggest job lines for.;
                             ESM=Especifica el n£mero del recurso para el que el proceso sugerir  l¡neas de proyecto.;
                             FRC=Indique le num‚ro de la ressource pour laquelle le traitement par lot suggŠre des lignes projet.;
                             ENC=Specifies the resource number that the batch job will suggest job lines for.];
                  ApplicationArea=#Jobs;
                  SourceExpr=ResourceNoFilter;
                  TableRelation=Resource }

      { 4   ;2   ;Field     ;
                  Name=JobNoFilter;
                  CaptionML=[ENU=Job No. Filter;
                             ESM=Filtro n§ proyecto;
                             FRC=Filtre nø projet;
                             ENC=Job No. Filter];
                  ToolTipML=[ENU=Specifies a filter for the job numbers that will be included in the report.;
                             ESM=Especifica un filtro para los n£meros de trabajo que se incluir n en el informe.;
                             FRC=Sp‚cifie un filtre pour les num‚ros de projet qui sont inclus dans le rapport.;
                             ENC=Specifies a filter for the job numbers that will be included in the report.];
                  ApplicationArea=#Jobs;
                  SourceExpr=JobNoFilter;
                  TableRelation=Job }

      { 5   ;2   ;Field     ;
                  Name=JobTaskNoFilter;
                  CaptionML=[ENU=Job Task No. Filter;
                             ESM=Filtro n§ tarea proyecto;
                             FRC=Filtre nø tƒche projet;
                             ENC=Job Task No. Filter];
                  ToolTipML=[ENU=Specifies a filter for the job task numbers that will be included in the report.;
                             ESM=Especifica un filtro para los n£meros de tarea de trabajo que se incluir n en el informe.;
                             FRC=Sp‚cifie un filtre pour les num‚ros de tƒche projet qui sont inclus dans le rapport.;
                             ENC=Specifies a filter for the job task numbers that will be included in the report.];
                  ApplicationArea=#Jobs;
                  SourceExpr=JobTaskNoFilter;
                  OnLookup=VAR
                             JobTask@1000 : Record 1001;
                           BEGIN
                             JobTask.FILTERGROUP(2);
                             IF JobNoFilter <> '' THEN
                               JobTask.SETFILTER("Job No.",JobNoFilter);
                             JobTask.FILTERGROUP(0);
                             IF PAGE.RUNMODAL(PAGE::"Job Task List",JobTask) = ACTION::LookupOK THEN
                               JobTask.TESTFIELD("Job Task Type",JobTask."Job Task Type"::Posting);
                             JobTaskNoFilter := JobTask."Job Task No.";
                           END;
                            }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      JobJnlLine@1012 : Record 210;
      JobJnlBatch@1011 : Record 237;
      JobJnlTemplate@1010 : Record 209;
      TimeSheetHeader@1009 : Record 950;
      TimeSheetLine@1008 : Record 951;
      TempTimeSheetLine@1007 : TEMPORARY Record 951;
      TimeSheetDetail@1006 : Record 952;
      ResourceNoFilter@1005 : Code[1024];
      JobNoFilter@1013 : Code[1024];
      JobTaskNoFilter@1015 : Code[1024];
      StartingDate@1001 : Date;
      EndingDate@1000 : Date;
      DateFilter@1002 : Text[30];

    [External]
    PROCEDURE SetJobJnlLine@1(NewJobJnlLine@1000 : Record 210);
    BEGIN
      JobJnlLine := NewJobJnlLine;
    END;

    [External]
    PROCEDURE InitParameters@2(NewJobJnlLine@1000 : Record 210;NewResourceNoFilter@1003 : Code[1024];NewJobNoFilter@1005 : Code[1024];NewJobTaskNoFilter@1004 : Code[1024];NewStartingDate@1001 : Date;NewEndingDate@1002 : Date);
    BEGIN
      JobJnlLine := NewJobJnlLine;
      ResourceNoFilter := NewResourceNoFilter;
      JobNoFilter := NewJobNoFilter;
      JobTaskNoFilter := NewJobTaskNoFilter;
      StartingDate := NewStartingDate;
      EndingDate := NewEndingDate;
    END;

    LOCAL PROCEDURE FillTimeSheetLineBuffer@3();
    VAR
      SkipLine@1000 : Boolean;
    BEGIN
      IF ResourceNoFilter <> '' THEN
        TimeSheetHeader.SETFILTER("Resource No.",ResourceNoFilter);
      IF DateFilter <> '' THEN BEGIN
        TimeSheetHeader.SETFILTER("Starting Date",DateFilter);
        TimeSheetHeader.SETFILTER("Starting Date",'..%1',TimeSheetHeader.GETRANGEMAX("Starting Date"));
        TimeSheetHeader.SETFILTER("Ending Date",DateFilter);
        TimeSheetHeader.SETFILTER("Ending Date",'%1..',TimeSheetHeader.GETRANGEMIN("Ending Date"));
      END;

      IF TimeSheetHeader.FINDSET THEN
        REPEAT
          TimeSheetLine.SETRANGE("Time Sheet No.",TimeSheetHeader."No.");
          TimeSheetLine.SETRANGE(Type,TimeSheetLine.Type::Job);
          TimeSheetLine.SETRANGE(Status,TimeSheetLine.Status::Approved);
          IF JobNoFilter <> '' THEN
            TimeSheetLine.SETFILTER("Job No.",JobNoFilter);
          IF JobTaskNoFilter <> '' THEN
            TimeSheetLine.SETFILTER("Job Task No.",JobTaskNoFilter);
          TimeSheetLine.SETRANGE(Posted,FALSE);
          IF TimeSheetLine.FINDSET THEN
            REPEAT
              TempTimeSheetLine := TimeSheetLine;
              OnBeforeInsertTempTimeSheetLine(JobJnlLine,TimeSheetHeader,TempTimeSheetLine,SkipLine);
              IF NOT SkipLine THEN
                TempTimeSheetLine.INSERT;
            UNTIL TimeSheetLine.NEXT = 0;
        UNTIL TimeSheetHeader.NEXT = 0;
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeInsertTempTimeSheetLine@4(JobJournalLine@1000 : Record 210;TimeSheetHeader@1001 : Record 950;VAR TempTimeSheetLine@1002 : TEMPORARY Record 951;VAR SkipLine@1003 : Boolean);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterTransferTimeSheetDetailToJobJnlLine@5(VAR JobJournalLine@1000 : Record 210;JobJournalTemplate@1001 : Record 209;VAR TempTimeSheetLine@1002 : TEMPORARY Record 951;TimeSheetDetail@1003 : Record 952);
    BEGIN
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

