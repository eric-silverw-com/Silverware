OBJECT Page 234 Apply Employee Entries
{
  OBJECT-PROPERTIES
  {
    Date=11/27/18;
    Time=11:10:11 PM;
    Version List=;
  }
  PROPERTIES
  {
    Permissions=TableData 5222=m;
    CaptionML=ENU=Apply Employee Entries;
    InsertAllowed=No;
    DeleteAllowed=No;
    LinksAllowed=No;
    SourceTable=Table5222;
    DataCaptionFields=Employee No.;
    PageType=Worksheet;
    OnInit=BEGIN
             AppliesToIDVisible := TRUE;
           END;

    OnOpenPage=VAR
                 OfficeMgt@1000 : Codeunit 1630;
               BEGIN
                 IF CalcType = CalcType::Direct THEN BEGIN
                   Empl.GET("Employee No.");
                   ApplnCurrencyCode := '';
                   FindApplyingEntry;
                 END;

                 AppliesToIDVisible := ApplnType <> ApplnType::"Applies-to Doc. No.";

                 GLSetup.GET;

                 IF CalcType = CalcType::GenJnlLine THEN
                   CalcApplnAmount;
                 PostingDone := FALSE;
                 IsOfficeAddin := OfficeMgt.IsAvailable;
               END;

    OnModifyRecord=BEGIN
                     CODEUNIT.RUN(CODEUNIT::"Empl. Entry-Edit",Rec);
                     IF "Applies-to ID" <> xRec."Applies-to ID" THEN
                       CalcApplnAmount;
                     EXIT(FALSE);
                   END;

    OnQueryClosePage=BEGIN
                       IF CloseAction = ACTION::LookupOK THEN
                         LookupOKOnPush;
                       IF ApplnType = ApplnType::"Applies-to Doc. No." THEN BEGIN
                         IF OK AND (TempApplyingEmplLedgEntry."Posting Date" < "Posting Date") THEN BEGIN
                           OK := FALSE;
                           ERROR(
                             EarlierPostingDateErr,TempApplyingEmplLedgEntry."Document Type",TempApplyingEmplLedgEntry."Document No.",
                             "Document Type","Document No.");
                         END;
                         IF OK THEN BEGIN
                           IF "Amount to Apply" = 0 THEN
                             "Amount to Apply" := "Remaining Amount";
                           CODEUNIT.RUN(CODEUNIT::"Empl. Entry-Edit",Rec);
                         END;
                       END;

                       IF CheckActionPerformed THEN BEGIN
                         Rec := TempApplyingEmplLedgEntry;
                         "Applying Entry" := FALSE;
                         IF AppliesToID = '' THEN BEGIN
                           "Applies-to ID" := '';
                           "Amount to Apply" := 0;
                         END;
                         CODEUNIT.RUN(CODEUNIT::"Empl. Entry-Edit",Rec);
                       END;
                     END;

    OnAfterGetCurrRecord=BEGIN
                           IF ApplnType = ApplnType::"Applies-to Doc. No." THEN
                             CalcApplnAmount;
                         END;

    ActionList=ACTIONS
    {
      { 1900000003;0 ;ActionContainer;
                      ActionContainerType=RelatedInformation }
      { 37      ;1   ;ActionGroup;
                      CaptionML=ENU=Ent&ry;
                      Image=Entry }
      { 31      ;2   ;Action    ;
                      CaptionML=ENU=Applied E&ntries;
                      ToolTipML=ENU=View the ledger entries that have been applied to this record.;
                      ApplicationArea=#BasicHR;
                      RunObject=Page 63;
                      RunPageOnRec=Yes;
                      Image=Approve }
      { 57      ;2   ;Action    ;
                      AccessByPermission=TableData 348=R;
                      ShortCutKey=Shift+Ctrl+D;
                      CaptionML=ENU=Dimensions;
                      ToolTipML=ENU=View or edit dimensions, such as area, project, or department, that you can assign to sales and purchase documents to distribute costs and analyze transaction history.;
                      ApplicationArea=#Dimensions;
                      Image=Dimensions;
                      OnAction=BEGIN
                                 ShowDimensions;
                               END;
                                }
      { 63      ;2   ;Action    ;
                      ShortCutKey=Ctrl+F7;
                      CaptionML=ENU=Detailed &Ledger Entries;
                      ToolTipML=ENU=View a summary of all the posted entries and adjustments related to a specific employee ledger entry.;
                      ApplicationArea=#BasicHR;
                      RunObject=Page 5238;
                      RunPageView=SORTING(Employee Ledger Entry No.,Posting Date);
                      RunPageLink=Employee Ledger Entry No.=FIELD(Entry No.);
                      Image=View }
      { 17      ;2   ;Action    ;
                      Name=Navigate;
                      CaptionML=ENU=&Navigate;
                      ToolTipML=ENU=Find all entries and documents that exist for the document number and posting date on the selected entry or document.;
                      ApplicationArea=#BasicHR;
                      Visible=NOT IsOfficeAddin;
                      Image=Navigate;
                      OnAction=BEGIN
                                 Navigate.SetDoc("Posting Date","Document No.");
                                 Navigate.RUN;
                               END;
                                }
      { 1900000004;0 ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 15      ;1   ;ActionGroup;
                      CaptionML=ENU=&Application;
                      Image=Apply }
      { 13      ;2   ;Action    ;
                      Name=ActionSetAppliesToID;
                      ShortCutKey=Shift+F11;
                      CaptionML=ENU=Set Applies-to ID;
                      ToolTipML=ENU=Set the Applies-to ID field on the posted entry to automatically be filled in with the document number of the entry in the journal.;
                      ApplicationArea=#BasicHR;
                      Image=SelectLineToApply;
                      OnAction=BEGIN
                                 IF (CalcType = CalcType::GenJnlLine) AND (ApplnType = ApplnType::"Applies-to Doc. No.") THEN
                                   ERROR(CannotSetAppliesToIDErr);

                                 SetEmplApplId;
                               END;
                                }
      { 11      ;2   ;Action    ;
                      Name=ActionPostApplication;
                      ShortCutKey=F9;
                      Ellipsis=Yes;
                      CaptionML=ENU=Post Application;
                      ToolTipML=ENU=Define the document number of the ledger entry to use to perform the application. In addition, you specify the Posting Date for the application.;
                      ApplicationArea=#BasicHR;
                      Image=PostApplication;
                      OnAction=BEGIN
                                 PostDirectApplication(FALSE);
                               END;
                                }
      { 9       ;2   ;Action    ;
                      Name=Preview;
                      CaptionML=ENU=Preview Posting;
                      ToolTipML=ENU=Review the different types of entries that will be created when you post the document or journal.;
                      ApplicationArea=#BasicHR;
                      Image=ViewPostedOrder;
                      OnAction=BEGIN
                                 PostDirectApplication(TRUE);
                               END;
                                }
      { 7       ;2   ;Separator ;
                      CaptionML=ENU=- }
      { 5       ;2   ;Action    ;
                      CaptionML=ENU=Show Only Selected Entries to Be Applied;
                      ToolTipML=ENU=View the selected ledger entries that will be applied to the specified record.;
                      ApplicationArea=#BasicHR;
                      Image=ShowSelected;
                      OnAction=BEGIN
                                 ShowAppliedEntries := NOT ShowAppliedEntries;
                                 IF ShowAppliedEntries THEN
                                   IF CalcType = CalcType::GenJnlLine THEN
                                     SETRANGE("Applies-to ID",GenJnlLine."Applies-to ID")
                                   ELSE BEGIN
                                     EmplEntryApplID := USERID;
                                     IF EmplEntryApplID = '' THEN
                                       EmplEntryApplID := '***';
                                     SETRANGE("Applies-to ID",EmplEntryApplID);
                                   END
                                 ELSE
                                   SETRANGE("Applies-to ID");
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 70  ;1   ;Group     ;
                CaptionML=ENU=General }

    { 73  ;2   ;Field     ;
                CaptionML=ENU=Posting Date;
                ToolTipML=ENU=Specifies the posting date of the entry to be applied.;
                ApplicationArea=#BasicHR;
                SourceExpr=TempApplyingEmplLedgEntry."Posting Date";
                Editable=FALSE }

    { 4   ;2   ;Field     ;
                CaptionML=ENU=Document Type;
                ToolTipML=ENU=Specifies the document type of the entry to be applied.;
                ApplicationArea=#BasicHR;
                SourceExpr=TempApplyingEmplLedgEntry."Document Type";
                Editable=FALSE }

    { 77  ;2   ;Field     ;
                CaptionML=ENU=Document No.;
                ToolTipML=ENU=Specifies the document number of the entry to be applied.;
                ApplicationArea=#BasicHR;
                SourceExpr=TempApplyingEmplLedgEntry."Document No.";
                Editable=FALSE }

    { 71  ;2   ;Field     ;
                Name=ApplyingEmployeeNo;
                CaptionML=ENU=Employee No.;
                ToolTipML=ENU=Specifies the employee number of the entry to be applied.;
                ApplicationArea=#BasicHR;
                SourceExpr=TempApplyingEmplLedgEntry."Employee No.";
                Visible=FALSE;
                Editable=FALSE }

    { 85  ;2   ;Field     ;
                Name=ApplyingDescription;
                CaptionML=ENU=Description;
                ToolTipML=ENU=Specifies the description of the entry to be applied.;
                ApplicationArea=#BasicHR;
                SourceExpr=TempApplyingEmplLedgEntry.Description;
                Visible=FALSE;
                Editable=FALSE }

    { 79  ;2   ;Field     ;
                CaptionML=ENU=Currency Code;
                ToolTipML=ENU=Specifies the code for the currency that amounts are shown in.;
                ApplicationArea=#BasicHR;
                SourceExpr=TempApplyingEmplLedgEntry."Currency Code";
                Editable=FALSE }

    { 81  ;2   ;Field     ;
                CaptionML=ENU=Amount;
                ToolTipML=ENU=Specifies the amount on the entry to be applied.;
                ApplicationArea=#BasicHR;
                SourceExpr=TempApplyingEmplLedgEntry.Amount;
                Editable=FALSE }

    { 83  ;2   ;Field     ;
                CaptionML=ENU=Remaining Amount;
                ToolTipML=ENU=Specifies the amount on the entry to be applied.;
                ApplicationArea=#BasicHR;
                SourceExpr=TempApplyingEmplLedgEntry."Remaining Amount";
                Editable=FALSE }

    { 1   ;1   ;Group     ;
                GroupType=Repeater }

    { 22  ;2   ;Field     ;
                ToolTipML=ENU=Specifies the ID of entries that will be applied to when you choose the Apply Entries action.;
                ApplicationArea=#All;
                SourceExpr="Applies-to ID";
                Visible=AppliesToIDVisible }

    { 2   ;2   ;Field     ;
                ToolTipML=ENU=Specifies the employee entry's posting date.;
                ApplicationArea=#BasicHR;
                SourceExpr="Posting Date";
                Editable=FALSE }

    { 16  ;2   ;Field     ;
                ToolTipML=ENU=Specifies the employee entry's document type.;
                ApplicationArea=#BasicHR;
                SourceExpr="Document Type";
                Editable=FALSE }

    { 6   ;2   ;Field     ;
                ToolTipML=ENU=Specifies the employee entry's document number.;
                ApplicationArea=#BasicHR;
                SourceExpr="Document No.";
                Editable=FALSE }

    { 8   ;2   ;Field     ;
                ToolTipML=ENU=Specifies the number of the employee account that the entry is linked to.;
                ApplicationArea=#BasicHR;
                SourceExpr="Employee No.";
                Editable=FALSE }

    { 10  ;2   ;Field     ;
                ToolTipML=ENU=Specifies a description of the employee entry.;
                ApplicationArea=#BasicHR;
                SourceExpr=Description;
                Editable=FALSE }

    { 39  ;2   ;Field     ;
                ToolTipML=ENU=Specifies the currency code for the amount on the line.;
                ApplicationArea=#BasicHR;
                SourceExpr="Currency Code" }

    { 61  ;2   ;Field     ;
                ToolTipML=ENU=Specifies the amount of the original entry.;
                ApplicationArea=#BasicHR;
                SourceExpr="Original Amount";
                Visible=FALSE;
                Editable=FALSE }

    { 12  ;2   ;Field     ;
                ToolTipML=ENU=Specifies the amount of the entry.;
                ApplicationArea=#BasicHR;
                SourceExpr=Amount;
                Visible=FALSE;
                Editable=FALSE }

    { 19  ;2   ;Field     ;
                ToolTipML=ENU=Specifies the total of the ledger entries that represent debits.;
                ApplicationArea=#BasicHR;
                SourceExpr="Debit Amount";
                Visible=FALSE }

    { 21  ;2   ;Field     ;
                ToolTipML=ENU=Specifies the total of the ledger entries that represent credits.;
                ApplicationArea=#BasicHR;
                SourceExpr="Credit Amount";
                Visible=FALSE }

    { 14  ;2   ;Field     ;
                ToolTipML=ENU=Specifies the amount that remains to be applied to before the entry is totally applied to.;
                ApplicationArea=#BasicHR;
                SourceExpr="Remaining Amount";
                Editable=FALSE }

    { 33  ;2   ;Field     ;
                CaptionML=ENU=Appln. Remaining Amount;
                ToolTipML=ENU=Specifies the amount that remains to be applied to before the entry is totally applied to.;
                ApplicationArea=#BasicHR;
                SourceExpr=CalcApplnRemainingAmount("Remaining Amount");
                AutoFormatType=1;
                AutoFormatExpr=ApplnCurrencyCode }

    { 90  ;2   ;Field     ;
                ToolTipML=ENU=Specifies the amount to apply.;
                ApplicationArea=#BasicHR;
                SourceExpr="Amount to Apply";
                OnValidate=BEGIN
                             CODEUNIT.RUN(CODEUNIT::"Empl. Entry-Edit",Rec);

                             IF (xRec."Amount to Apply" = 0) OR ("Amount to Apply" = 0) AND
                                ((ApplnType = ApplnType::"Applies-to ID") OR (CalcType = CalcType::Direct))
                             THEN
                               SetEmplApplId;
                             GET("Entry No.");
                             AmounttoApplyOnAfterValidate;
                           END;
                            }

    { 92  ;2   ;Field     ;
                CaptionML=ENU=Appln. Amount to Apply;
                ToolTipML=ENU=Specifies the amount to apply.;
                ApplicationArea=#BasicHR;
                SourceExpr=CalcApplnAmounttoApply("Amount to Apply");
                AutoFormatType=1;
                AutoFormatExpr=ApplnCurrencyCode }

    { 3   ;2   ;Field     ;
                ToolTipML=ENU=Specifies the payment to the employee.;
                ApplicationArea=#BasicHR;
                SourceExpr="Payment Reference" }

    { 24  ;2   ;Field     ;
                ToolTipML=ENU=Specifies whether the amount on the entry has been fully paid or there is still a remaining amount that must be applied to.;
                ApplicationArea=#BasicHR;
                SourceExpr=Open;
                Editable=FALSE }

    { 26  ;2   ;Field     ;
                ToolTipML=ENU=Specifies if the entry to be applied is positive.;
                ApplicationArea=#BasicHR;
                SourceExpr=Positive;
                Editable=FALSE }

    { 56  ;2   ;Field     ;
                ToolTipML=ENU=Specifies the code for the global dimension that is linked to the record or entry for analysis purposes. Two global dimensions, typically for the company's most important activities, are available on all cards, documents, reports, and lists.;
                ApplicationArea=#Dimensions;
                SourceExpr="Global Dimension 1 Code" }

    { 59  ;2   ;Field     ;
                ToolTipML=ENU=Specifies the code for the global dimension that is linked to the record or entry for analysis purposes. Two global dimensions, typically for the company's most important activities, are available on all cards, documents, reports, and lists.;
                ApplicationArea=#Dimensions;
                SourceExpr="Global Dimension 2 Code" }

    { 41  ;1   ;Group      }

    { 1903222401;2;Group  ;
                GroupType=FixedLayout }

    { 1903866701;3;Group  ;
                CaptionML=ENU=Appln. Currency }

    { 49  ;4   ;Field     ;
                ToolTipML=ENU=Specifies the currency code that the amount will be applied in, in case of different currencies.;
                ApplicationArea=#BasicHR;
                SourceExpr=ApplnCurrencyCode;
                TableRelation=Currency;
                Editable=FALSE;
                ShowCaption=No }

    { 1900545201;3;Group  ;
                CaptionML=ENU=Amount to Apply }

    { 45  ;4   ;Field     ;
                Name=AmountToApply;
                CaptionML=ENU=Amount to Apply;
                ToolTipML=ENU=Specifies the sum of the amounts on all the selected employee ledger entries that will be applied by the entry shown in the Available Amount field. The amount is in the currency represented by the code in the Currency Code field.;
                ApplicationArea=#BasicHR;
                SourceExpr=AppliedAmount;
                AutoFormatType=1;
                AutoFormatExpr=ApplnCurrencyCode;
                Editable=FALSE }

    { 1901652301;3;Group  ;
                CaptionML=ENU=Rounding }

    { 53  ;4   ;Field     ;
                CaptionML=ENU=Rounding;
                ToolTipML=ENU=Specifies the rounding difference when you apply entries in different currencies to one another. The amount is in the currency represented by the code in the Currency Code field.;
                ApplicationArea=#BasicHR;
                SourceExpr=ApplnRounding;
                AutoFormatType=1;
                AutoFormatExpr=ApplnCurrencyCode;
                Editable=FALSE }

    { 1901992701;3;Group  ;
                CaptionML=ENU=Applied Amount }

    { 98  ;4   ;Field     ;
                Name=AppliedAmount;
                CaptionML=ENU=Applied Amount;
                ToolTipML=ENU=Specifies the sum of the amounts in the Amount to Apply field, Pmt. Disc. Amount field, and the Rounding. The amount is in the currency represented by the code in the Currency Code field.;
                ApplicationArea=#BasicHR;
                SourceExpr=AppliedAmount + (-PmtDiscAmount) + ApplnRounding;
                AutoFormatType=1;
                AutoFormatExpr=ApplnCurrencyCode;
                Editable=FALSE }

    { 1900295601;3;Group  ;
                CaptionML=ENU=Available Amount }

    { 47  ;4   ;Field     ;
                CaptionML=ENU=Available Amount;
                ToolTipML=ENU=Specifies the amount of the journal entry, purchase credit memo, or current employee ledger entry that you have selected as the applying entry.;
                ApplicationArea=#BasicHR;
                SourceExpr=ApplyingAmount;
                AutoFormatType=1;
                AutoFormatExpr=ApplnCurrencyCode;
                Editable=FALSE }

    { 1901652401;3;Group  ;
                CaptionML=ENU=Balance }

    { 43  ;4   ;Field     ;
                Name=ControlBalance;
                CaptionML=ENU=Balance;
                ToolTipML=ENU=Specifies any extra amount that will remain after the application.;
                ApplicationArea=#BasicHR;
                SourceExpr=AppliedAmount + (-PmtDiscAmount) + ApplyingAmount + ApplnRounding;
                AutoFormatType=1;
                AutoFormatExpr=ApplnCurrencyCode;
                Editable=FALSE }

  }
  CODE
  {
    VAR
      TempApplyingEmplLedgEntry@1033 : TEMPORARY Record 5222;
      AppliedEmplLedgEntry@1001 : Record 5222;
      Currency@1002 : Record 4;
      CurrExchRate@1003 : Record 330;
      GenJnlLine@1004 : Record 81;
      GenJnlLine2@1005 : Record 81;
      Empl@1007 : Record 5200;
      EmplLedgEntry@1008 : Record 5222;
      GLSetup@1009 : Record 98;
      EmplEntrySetApplID@1013 : Codeunit 112;
      GenJnlApply@1014 : Codeunit 225;
      Navigate@1012 : Page 344;
      GenJnlLineApply@1016 : Boolean;
      AppliedAmount@1017 : Decimal;
      ApplyingAmount@1018 : Decimal;
      PmtDiscAmount@1040 : Decimal;
      ApplnDate@1019 : Date;
      ApplnCurrencyCode@1020 : Code[10];
      ApplnRoundingPrecision@1021 : Decimal;
      ApplnRounding@1022 : Decimal;
      ApplnType@1023 : ' ,Applies-to Doc. No.,Applies-to ID';
      AmountRoundingPrecision@1024 : Decimal;
      CalcType@1027 : 'Direct,GenJnlLine,PurchHeader';
      EmplEntryApplID@1029 : Code[50];
      AppliesToID@1042 : Code[50];
      ValidExchRate@1030 : Boolean;
      DifferentCurrenciesInAppln@1032 : Boolean;
      MustSelectEntryErr@1036 : TextConst 'ENU=You must select an applying entry before you can post the application.';
      PostingInWrongContextErr@1035 : TextConst 'ENU=You must post the application from the window where you entered the applying entry.';
      CannotSetAppliesToIDErr@1038 : TextConst 'ENU=You cannot set Applies-to ID field while selecting Applies-to Doc. No field.';
      ShowAppliedEntries@1039 : Boolean;
      OK@1028 : Boolean;
      EarlierPostingDateErr@1034 : TextConst '@@@=%1 - document type, %2 - document number,%3 - document type,%4 - document number;ENU=You cannot apply and post an entry to an entry with an earlier posting date.\\Instead, post the document of type %1 with the number %2 and then apply it to the document of type %3 with the number %4.';
      PostingDone@1000 : Boolean;
      AppliesToIDVisible@19043506 : Boolean INDATASET;
      ActionPerformed@1104 : Boolean;
      ApplicationPostedMsg@1043 : TextConst 'ENU=The application was successfully posted.';
      ApplicationDateErr@1044 : TextConst 'ENU=The posting date entered must not be before the posting date on the employee ledger entry.';
      ApplicationProcessCanceledErr@1045 : TextConst 'ENU=Post application process has been canceled.';
      IsOfficeAddin@1041 : Boolean;

    [External]
    PROCEDURE SetGenJnlLine@1(NewGenJnlLine@1000 : Record 81;ApplnTypeSelect@1001 : Integer);
    BEGIN
      GenJnlLine := NewGenJnlLine;
      GenJnlLineApply := TRUE;

      IF GenJnlLine."Account Type" = GenJnlLine."Account Type"::Employee THEN
        ApplyingAmount := GenJnlLine.Amount;
      IF GenJnlLine."Bal. Account Type" = GenJnlLine."Bal. Account Type"::Employee THEN
        ApplyingAmount := -GenJnlLine.Amount;
      ApplnDate := GenJnlLine."Posting Date";
      ApplnCurrencyCode := GenJnlLine."Currency Code";
      CalcType := CalcType::GenJnlLine;

      CASE ApplnTypeSelect OF
        GenJnlLine.FIELDNO("Applies-to Doc. No."):
          ApplnType := ApplnType::"Applies-to Doc. No.";
        GenJnlLine.FIELDNO("Applies-to ID"):
          ApplnType := ApplnType::"Applies-to ID";
      END;

      SetApplyingEmplLedgEntry;
    END;

    [External]
    PROCEDURE SetEmplLedgEntry@13(NewEmplLedgEntry@1000 : Record 5222);
    BEGIN
      Rec := NewEmplLedgEntry;
    END;

    PROCEDURE SetApplyingEmplLedgEntry@9();
    VAR
      Employee@1001 : Record 5200;
    BEGIN
      CASE CalcType OF
        CalcType::Direct:
          BEGIN
            IF "Applying Entry" THEN BEGIN
              IF TempApplyingEmplLedgEntry."Entry No." <> 0 THEN
                EmplLedgEntry := TempApplyingEmplLedgEntry;
              CODEUNIT.RUN(CODEUNIT::"Empl. Entry-Edit",Rec);
              IF "Applies-to ID" = '' THEN
                SetEmplApplId;
              CALCFIELDS(Amount);
              TempApplyingEmplLedgEntry := Rec;
              IF EmplLedgEntry."Entry No." <> 0 THEN BEGIN
                Rec := EmplLedgEntry;
                "Applying Entry" := FALSE;
                SetEmplApplId;
              END;
              SETFILTER("Entry No.",'<> %1',TempApplyingEmplLedgEntry."Entry No.");
              ApplyingAmount := TempApplyingEmplLedgEntry."Remaining Amount";
              ApplnDate := TempApplyingEmplLedgEntry."Posting Date";
              ApplnCurrencyCode := TempApplyingEmplLedgEntry."Currency Code";
            END;
            CalcApplnAmount;
          END;
        CalcType::GenJnlLine:
          BEGIN
            TempApplyingEmplLedgEntry."Posting Date" := GenJnlLine."Posting Date";
            TempApplyingEmplLedgEntry."Document Type" := GenJnlLine."Document Type";
            TempApplyingEmplLedgEntry."Document No." := GenJnlLine."Document No.";
            IF GenJnlLine."Bal. Account Type" = GenJnlLine."Bal. Account Type"::Employee THEN BEGIN
              TempApplyingEmplLedgEntry."Employee No." := GenJnlLine."Bal. Account No.";
              Employee.GET(TempApplyingEmplLedgEntry."Employee No.");
              TempApplyingEmplLedgEntry.Description := COPYSTR(Employee.FullName,1,MAXSTRLEN(TempApplyingEmplLedgEntry.Description));
            END ELSE BEGIN
              TempApplyingEmplLedgEntry."Employee No." := GenJnlLine."Account No.";
              TempApplyingEmplLedgEntry.Description := GenJnlLine.Description;
            END;
            TempApplyingEmplLedgEntry."Currency Code" := GenJnlLine."Currency Code";
            TempApplyingEmplLedgEntry.Amount := GenJnlLine.Amount;
            TempApplyingEmplLedgEntry."Remaining Amount" := GenJnlLine.Amount;
            CalcApplnAmount;
          END;
      END;
    END;

    PROCEDURE SetEmplApplId@10();
    BEGIN
      IF (CalcType = CalcType::GenJnlLine) AND (TempApplyingEmplLedgEntry."Posting Date" < "Posting Date") THEN
        ERROR(
          EarlierPostingDateErr,TempApplyingEmplLedgEntry."Document Type",TempApplyingEmplLedgEntry."Document No.",
          "Document Type","Document No.");

      IF TempApplyingEmplLedgEntry."Entry No." <> 0 THEN
        GenJnlApply.CheckAgainstApplnCurrency(
          ApplnCurrencyCode,"Currency Code",GenJnlLine."Account Type"::Employee,TRUE);

      EmplLedgEntry.COPY(Rec);
      CurrPage.SETSELECTIONFILTER(EmplLedgEntry);

      IF GenJnlLineApply THEN
        EmplEntrySetApplID.SetApplId(EmplLedgEntry,TempApplyingEmplLedgEntry,GenJnlLine."Applies-to ID")
      ELSE
        EmplEntrySetApplID.SetApplId(EmplLedgEntry,TempApplyingEmplLedgEntry,'');

      ActionPerformed := EmplLedgEntry."Applies-to ID" <> '';
      CalcApplnAmount;
    END;

    LOCAL PROCEDURE CalcApplnAmount@7();
    BEGIN
      AppliedAmount := 0;
      PmtDiscAmount := 0;
      DifferentCurrenciesInAppln := FALSE;

      CASE CalcType OF
        CalcType::Direct:
          BEGIN
            FindAmountRounding;
            EmplEntryApplID := USERID;
            IF EmplEntryApplID = '' THEN
              EmplEntryApplID := '***';

            EmplLedgEntry := TempApplyingEmplLedgEntry;

            AppliedEmplLedgEntry.SETCURRENTKEY("Employee No.",Open,Positive);
            AppliedEmplLedgEntry.SETRANGE("Employee No.","Employee No.");
            AppliedEmplLedgEntry.SETRANGE(Open,TRUE);
            IF AppliesToID = '' THEN
              AppliedEmplLedgEntry.SETRANGE("Applies-to ID",EmplEntryApplID)
            ELSE
              AppliedEmplLedgEntry.SETRANGE("Applies-to ID",AppliesToID);

            IF TempApplyingEmplLedgEntry."Entry No." <> 0 THEN BEGIN
              EmplLedgEntry.CALCFIELDS("Remaining Amount");
              AppliedEmplLedgEntry.SETFILTER("Entry No.",'<>%1',EmplLedgEntry."Entry No.");
            END;

            HandlChosenEntries(0,EmplLedgEntry."Remaining Amount");
          END;
        CalcType::GenJnlLine:
          BEGIN
            FindAmountRounding;
            IF GenJnlLine."Bal. Account Type" = GenJnlLine."Bal. Account Type"::Employee THEN
              CODEUNIT.RUN(CODEUNIT::"Exchange Acc. G/L Journal Line",GenJnlLine);

            CASE ApplnType OF
              ApplnType::"Applies-to Doc. No.":
                BEGIN
                  AppliedEmplLedgEntry := Rec;
                  WITH AppliedEmplLedgEntry DO BEGIN
                    CALCFIELDS("Remaining Amount");
                    IF "Currency Code" <> ApplnCurrencyCode THEN BEGIN
                      "Remaining Amount" :=
                        CurrExchRate.ExchangeAmtFCYToFCY(
                          ApplnDate,"Currency Code",ApplnCurrencyCode,"Remaining Amount");
                      "Amount to Apply" :=
                        CurrExchRate.ExchangeAmtFCYToFCY(
                          ApplnDate,"Currency Code",ApplnCurrencyCode,"Amount to Apply");
                    END;

                    IF "Amount to Apply" <> 0 THEN
                      AppliedAmount := ROUND("Amount to Apply",AmountRoundingPrecision)
                    ELSE
                      AppliedAmount := ROUND("Remaining Amount",AmountRoundingPrecision);

                    IF NOT DifferentCurrenciesInAppln THEN
                      DifferentCurrenciesInAppln := ApplnCurrencyCode <> "Currency Code";
                  END;
                  CheckRounding;
                END;
              ApplnType::"Applies-to ID":
                BEGIN
                  GenJnlLine2 := GenJnlLine;
                  AppliedEmplLedgEntry.SETCURRENTKEY("Employee No.",Open,Positive);
                  AppliedEmplLedgEntry.SETRANGE("Employee No.",GenJnlLine."Account No.");
                  AppliedEmplLedgEntry.SETRANGE(Open,TRUE);
                  AppliedEmplLedgEntry.SETRANGE("Applies-to ID",GenJnlLine."Applies-to ID");

                  HandlChosenEntries(1,GenJnlLine2.Amount);
                END;
            END;
          END;
      END;
    END;

    LOCAL PROCEDURE CalcApplnRemainingAmount@4(Amt@1000 : Decimal) : Decimal;
    VAR
      ApplnRemainingAmount@1001 : Decimal;
    BEGIN
      ValidExchRate := TRUE;
      IF ApplnCurrencyCode = "Currency Code" THEN
        EXIT(Amt);

      IF ApplnDate = 0D THEN
        ApplnDate := "Posting Date";
      ApplnRemainingAmount :=
        CurrExchRate.ApplnExchangeAmtFCYToFCY(
          ApplnDate,"Currency Code",ApplnCurrencyCode,Amt,ValidExchRate);
      EXIT(ApplnRemainingAmount);
    END;

    LOCAL PROCEDURE CalcApplnAmounttoApply@11(AmounttoApply@1000 : Decimal) : Decimal;
    VAR
      ApplnAmountToApply@1001 : Decimal;
    BEGIN
      ValidExchRate := TRUE;

      IF ApplnCurrencyCode = "Currency Code" THEN
        EXIT(AmounttoApply);

      IF ApplnDate = 0D THEN
        ApplnDate := "Posting Date";
      ApplnAmountToApply :=
        CurrExchRate.ApplnExchangeAmtFCYToFCY(
          ApplnDate,"Currency Code",ApplnCurrencyCode,AmounttoApply,ValidExchRate);
      EXIT(ApplnAmountToApply);
    END;

    LOCAL PROCEDURE FindAmountRounding@6();
    BEGIN
      IF ApplnCurrencyCode = '' THEN BEGIN
        Currency.INIT;
        Currency.Code := '';
        Currency.InitRoundingPrecision;
      END ELSE
        IF ApplnCurrencyCode <> Currency.Code THEN
          Currency.GET(ApplnCurrencyCode);

      AmountRoundingPrecision := Currency."Amount Rounding Precision";
    END;

    LOCAL PROCEDURE CheckRounding@3();
    BEGIN
      ApplnRounding := 0;

      CASE CalcType OF
        CalcType::PurchHeader:
          EXIT;
        CalcType::GenJnlLine:
          IF (GenJnlLine."Document Type" <> GenJnlLine."Document Type"::Payment) AND
             (GenJnlLine."Document Type" <> GenJnlLine."Document Type"::Refund)
          THEN
            EXIT;
      END;

      IF ApplnCurrencyCode = '' THEN
        ApplnRoundingPrecision := GLSetup."Appln. Rounding Precision"
      ELSE BEGIN
        IF ApplnCurrencyCode <> "Currency Code" THEN
          Currency.GET(ApplnCurrencyCode);
        ApplnRoundingPrecision := Currency."Appln. Rounding Precision";
      END;

      IF (ABS((AppliedAmount - PmtDiscAmount) + ApplyingAmount) <= ApplnRoundingPrecision) AND DifferentCurrenciesInAppln THEN
        ApplnRounding := -((AppliedAmount - PmtDiscAmount) + ApplyingAmount);
    END;

    [External]
    PROCEDURE GetEmplLedgEntry@5(VAR EmplLedgEntry@1000 : Record 5222);
    BEGIN
      EmplLedgEntry := Rec;
    END;

    LOCAL PROCEDURE FindApplyingEntry@12();
    BEGIN
      IF CalcType = CalcType::Direct THEN BEGIN
        EmplEntryApplID := USERID;
        IF EmplEntryApplID = '' THEN
          EmplEntryApplID := '***';

        EmplLedgEntry.SETCURRENTKEY("Employee No.","Applies-to ID",Open);
        EmplLedgEntry.SETRANGE("Employee No.","Employee No.");
        IF AppliesToID = '' THEN
          EmplLedgEntry.SETRANGE("Applies-to ID",EmplEntryApplID)
        ELSE
          EmplLedgEntry.SETRANGE("Applies-to ID",AppliesToID);
        EmplLedgEntry.SETRANGE(Open,TRUE);
        EmplLedgEntry.SETRANGE("Applying Entry",TRUE);
        IF EmplLedgEntry.FINDFIRST THEN BEGIN
          EmplLedgEntry.CALCFIELDS(Amount,"Remaining Amount");
          TempApplyingEmplLedgEntry := EmplLedgEntry;
          SETFILTER("Entry No.",'<>%1',EmplLedgEntry."Entry No.");
          ApplyingAmount := EmplLedgEntry."Remaining Amount";
          ApplnDate := EmplLedgEntry."Posting Date";
          ApplnCurrencyCode := EmplLedgEntry."Currency Code";
        END;
        CalcApplnAmount;
      END;
    END;

    LOCAL PROCEDURE AmounttoApplyOnAfterValidate@19038179();
    BEGIN
      IF ApplnType <> ApplnType::"Applies-to Doc. No." THEN BEGIN
        CalcApplnAmount;
        CurrPage.UPDATE(FALSE);
      END;
    END;

    LOCAL PROCEDURE LookupOKOnPush@19031339();
    BEGIN
      OK := TRUE;
    END;

    LOCAL PROCEDURE PostDirectApplication@15(PreviewMode@1005 : Boolean);
    VAR
      EmplEntryApplyPostedEntries@1000 : Codeunit 224;
      PostApplication@1002 : Page 579;
      ApplicationDate@1001 : Date;
      NewApplicationDate@1003 : Date;
      NewDocumentNo@1004 : Code[20];
    BEGIN
      IF CalcType = CalcType::Direct THEN BEGIN
        IF TempApplyingEmplLedgEntry."Entry No." <> 0 THEN BEGIN
          Rec := TempApplyingEmplLedgEntry;
          ApplicationDate := EmplEntryApplyPostedEntries.GetApplicationDate(Rec);

          PostApplication.SetValues("Document No.",ApplicationDate);
          IF ACTION::OK = PostApplication.RUNMODAL THEN BEGIN
            PostApplication.GetValues(NewDocumentNo,NewApplicationDate);
            IF NewApplicationDate < ApplicationDate THEN
              ERROR(ApplicationDateErr);
          END ELSE
            ERROR(ApplicationProcessCanceledErr);

          IF PreviewMode THEN
            EmplEntryApplyPostedEntries.PreviewApply(Rec,NewDocumentNo,NewApplicationDate)
          ELSE
            EmplEntryApplyPostedEntries.Apply(Rec,NewDocumentNo,NewApplicationDate);

          IF NOT PreviewMode THEN BEGIN
            MESSAGE(ApplicationPostedMsg);
            PostingDone := TRUE;
            CurrPage.CLOSE;
          END;
        END ELSE
          ERROR(MustSelectEntryErr);
      END ELSE
        ERROR(PostingInWrongContextErr);
    END;

    LOCAL PROCEDURE CheckActionPerformed@1011() : Boolean;
    BEGIN
      IF ActionPerformed THEN
        EXIT(FALSE);
      IF (NOT (CalcType = CalcType::Direct) AND NOT OK AND NOT PostingDone) OR
         (ApplnType = ApplnType::"Applies-to Doc. No.")
      THEN
        EXIT(FALSE);
      EXIT(CalcType = CalcType::Direct);
    END;

    [External]
    PROCEDURE SetAppliesToID@1031(AppliesToID2@1043 : Code[50]);
    BEGIN
      AppliesToID := AppliesToID2;
    END;

    LOCAL PROCEDURE HandlChosenEntries@8(Type@1000 : 'Direct,GenJnlLine';CurrentAmount@1001 : Decimal);
    VAR
      TempAppliedEmplLedgEntry@1004 : TEMPORARY Record 5222;
      CorrectionAmount@1009 : Decimal;
      FromZeroGenJnl@1010 : Boolean;
    BEGIN
      CorrectionAmount := 0;
      IF AppliedEmplLedgEntry.FINDSET(FALSE,FALSE) THEN BEGIN
        REPEAT
          TempAppliedEmplLedgEntry := AppliedEmplLedgEntry;
          TempAppliedEmplLedgEntry.INSERT;
        UNTIL AppliedEmplLedgEntry.NEXT = 0;
      END ELSE
        EXIT;

      FromZeroGenJnl := (CurrentAmount = 0) AND (Type = Type::GenJnlLine);

      REPEAT
        IF NOT FromZeroGenJnl THEN
          TempAppliedEmplLedgEntry.SETRANGE(Positive,CurrentAmount < 0);
        IF TempAppliedEmplLedgEntry.FINDFIRST THEN BEGIN
          IF ((CurrentAmount + TempAppliedEmplLedgEntry."Amount to Apply") * CurrentAmount) >= 0 THEN
            AppliedAmount := AppliedAmount + CorrectionAmount;
          CurrentAmount := CurrentAmount + TempAppliedEmplLedgEntry."Amount to Apply";
        END ELSE BEGIN
          TempAppliedEmplLedgEntry.SETRANGE(Positive);
          TempAppliedEmplLedgEntry.FINDFIRST;
        END;

        AppliedAmount := AppliedAmount + TempAppliedEmplLedgEntry."Amount to Apply";

        TempAppliedEmplLedgEntry.DELETE;
        TempAppliedEmplLedgEntry.SETRANGE(Positive);

      UNTIL NOT TempAppliedEmplLedgEntry.FINDFIRST;
      CheckRounding;
    END;

    BEGIN
    END.
  }
}

