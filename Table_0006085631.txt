OBJECT Table 6085631 Approval User Group Member
{
  OBJECT-PROPERTIES
  {
    Date=01/23/17;
    Time=[ 1:00:00 AM];
    Version List=DCW13.70.00.4.07;
  }
  PROPERTIES
  {
    OnInsert=VAR
               ApprovalUserGroupPermission@1160040001 : Record 6085632;
               AppUserGroupPmsSelection@1160040002 : Record 6085633;
               ContiniaUserPermission@1160040000 : Record 6085634;
               ContiniaUserPmsSelection@1160040003 : Record 6085635;
             BEGIN
               TESTFIELD("Approval User Group Code");
               TESTFIELD("Continia User ID");

               GetContiniaUserSetup(GetCurrCompanyNameTbl);

               ApprovalUserGroupPermission.CHANGECOMPANY(GetCurrCompanyNameTbl);
               ApprovalUserGroupPermission.SETRANGE("Approval User Group Code","Approval User Group Code");
               IF ApprovalUserGroupPermission.FINDSET THEN
                 REPEAT
                   ContiniaUserPermission.CHANGECOMPANY(GetCurrCompanyNameTbl);
                   ContiniaUserPermission.TRANSFERFIELDS(ApprovalUserGroupPermission);
                   ContiniaUserPermission."Continia User ID" := "Continia User ID";
                   ContiniaUserPermission.INSERT;

                   AppUserGroupPmsSelection.CHANGECOMPANY(GetCurrCompanyNameTbl);
                   AppUserGroupPmsSelection.SETRANGE("Approval User Group Code",ApprovalUserGroupPermission."Approval User Group Code");
                   AppUserGroupPmsSelection.SETRANGE(Type,ApprovalUserGroupPermission.Type);
                   AppUserGroupPmsSelection.SETRANGE("Dimension Code",ApprovalUserGroupPermission."Dimension Code");
                   IF AppUserGroupPmsSelection.FINDSET THEN
                     REPEAT
                       ContiniaUserPmsSelection.CHANGECOMPANY(GetCurrCompanyNameTbl);
                       ContiniaUserPmsSelection.TRANSFERFIELDS(AppUserGroupPmsSelection);
                       ContiniaUserPmsSelection."Continia User ID" := "Continia User ID";
                       ContiniaUserPmsSelection.INSERT;
                     UNTIL AppUserGroupPmsSelection.NEXT = 0;
                 UNTIL ApprovalUserGroupPermission.NEXT = 0;
             END;

    OnDelete=VAR
               ContiniaUserPermission@1160040001 : Record 6085634;
               ContiniaUserPmsSelection@1160040000 : Record 6085635;
             BEGIN
               TESTFIELD("Approval User Group Code");
               TESTFIELD("Continia User ID");

               ContiniaUserPermission.CHANGECOMPANY(GetCurrCompanyNameTbl);
               ContiniaUserPermission.SETRANGE("Continia User ID","Continia User ID");
               ContiniaUserPermission.SETRANGE("Approval User Group Code","Approval User Group Code");
               ContiniaUserPermission.DELETEALL;

               ContiniaUserPmsSelection.CHANGECOMPANY(GetCurrCompanyNameTbl);
               ContiniaUserPmsSelection.SETRANGE("Continia User ID","Continia User ID");
               ContiniaUserPmsSelection.SETRANGE("Approval User Group Code","Approval User Group Code");
               ContiniaUserPmsSelection.DELETEALL;
             END;

    OnRename=BEGIN
               ERROR(RenameErr,TABLECAPTION);
             END;

    CaptionML=ENU=Approval User Group Member;
    LookupPageID=Page6085661;
    DrillDownPageID=Page6085661;
  }
  FIELDS
  {
    { 1   ;   ;Approval User Group Code;Code20    ;TableRelation="Approval User Group";
                                                   CaptionML=ENU=Approval User Group Code;
                                                   NotBlank=Yes }
    { 2   ;   ;Continia User ID    ;Code50        ;TableRelation="Continia User Setup";
                                                   CaptionML=ENU=Continia User ID;
                                                   NotBlank=Yes }
    { 3   ;   ;Continia User Name  ;Text80        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Continia User".Name WHERE (User ID=FIELD(Continia User ID)));
                                                   CaptionML=ENU=Continia User Name;
                                                   Editable=No }
    { 4   ;   ;Approval User Group Name;Text50    ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Approval User Group".Name WHERE (Code=FIELD(Approval User Group Code)));
                                                   CaptionML=ENU=User Group Name;
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Approval User Group Code,Continia User ID;
                                                   Clustered=Yes }
    {    ;Continia User ID                         }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      RenameErr@1160040001 : TextConst 'ENU=You cannot rename a %1.;ENC=You cannot rename a %1.';
      CurrCompanyName@1160040004 : Text[30];
      ContiniaUserSetupMissingErr@1160040003 : TextConst 'ENU=%1 %2 ''%3'' does not exist in %4.';

    PROCEDURE SetCurrCompanyNameTbl@1160040019(NewCompanyName@1160040000 : Text[30]);
    BEGIN
      CurrCompanyName := NewCompanyName;
    END;

    PROCEDURE GetCurrCompanyNameTbl@1160040024() : Text[30];
    BEGIN
      IF CurrCompanyName = '' THEN
        EXIT(COMPANYNAME)
      ELSE
        EXIT(CurrCompanyName);
    END;

    PROCEDURE GetContiniaUserSetup@1160040000(NewCompanyName@1160040000 : Text[30]);
    VAR
      ContiniaUserSetup@1160040001 : Record 6086002;
    BEGIN
      TESTFIELD("Continia User ID");

      SetCurrCompanyNameTbl(NewCompanyName);
      ContiniaUserSetup.CHANGECOMPANY(GetCurrCompanyNameTbl);
      IF GetCurrCompanyNameTbl <> COMPANYNAME THEN BEGIN
        IF NOT ContiniaUserSetup.GET("Continia User ID") THEN
          ERROR(ContiniaUserSetupMissingErr,ContiniaUserSetup.TABLECAPTION,ContiniaUserSetup.FIELDCAPTION("Continia User ID"),
            "Continia User ID",GetCurrCompanyNameTbl);
      END ELSE
        ContiniaUserSetup.GET("Continia User ID");
    END;

    BEGIN
    END.
  }
}

