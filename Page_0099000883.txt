OBJECT Page 99000883 Sales Order Planning
{
  OBJECT-PROPERTIES
  {
    Date=11/27/18;
    Time=11:12:20 PM;
    Version List=;
  }
  PROPERTIES
  {
    Editable=No;
    CaptionML=ENU=Sales Order Planning;
    SourceTable=Table99000800;
    DataCaptionExpr=Caption;
    DataCaptionFields=Sales Order No.;
    PageType=List;
    SourceTableTemporary=Yes;
    OnOpenPage=BEGIN
                 BuildForm;
               END;

    ActionList=ACTIONS
    {
      { 1900000003;0 ;ActionContainer;
                      ActionContainerType=RelatedInformation }
      { 4       ;1   ;ActionGroup;
                      CaptionML=ENU=&Item;
                      Image=Item }
      { 23      ;2   ;Action    ;
                      ShortCutKey=Shift+F7;
                      CaptionML=ENU=Card;
                      ToolTipML=ENU=View or change detailed information about the record on the document or journal line.;
                      ApplicationArea=#Planning;
                      RunObject=Page 30;
                      RunPageLink=No.=FIELD(Item No.);
                      Image=EditLines }
      { 27      ;2   ;Action    ;
                      ShortCutKey=Ctrl+F7;
                      CaptionML=ENU=Ledger E&ntries;
                      ToolTipML=ENU=View the history of transactions that have been posted for the selected record.;
                      ApplicationArea=#Planning;
                      RunObject=Page 38;
                      RunPageView=SORTING(Item No.,Open,Variant Code);
                      RunPageLink=Item No.=FIELD(Item No.),
                                  Variant Code=FIELD(Variant Code);
                      Image=CustomerLedger }
      { 6       ;2   ;ActionGroup;
                      Name=<Action8>;
                      CaptionML=ENU=&Item Availability by;
                      Image=ItemAvailability }
      { 8       ;3   ;Action    ;
                      Name=<Action6>;
                      CaptionML=ENU=Event;
                      ToolTipML=ENU=View how the actual and the projected available balance of an item will develop over time according to supply and demand events.;
                      ApplicationArea=#Planning;
                      Promoted=Yes;
                      Image=Event;
                      PromotedCategory=Process;
                      OnAction=VAR
                                 Item@1001 : Record 27;
                               BEGIN
                                 IF Item.GET("Item No.") THEN
                                   ItemAvailFormsMgt.ShowItemAvailFromItem(Item,ItemAvailFormsMgt.ByEvent);
                               END;
                                }
      { 31      ;3   ;Action    ;
                      Name=<Action31>;
                      CaptionML=ENU=Period;
                      ToolTipML=ENU=View the projected quantity of the item over time according to time periods, such as day, week, or month.;
                      ApplicationArea=#Planning;
                      RunObject=Page 157;
                      RunPageLink=No.=FIELD(Item No.);
                      Image=Period }
      { 10      ;3   ;Action    ;
                      CaptionML=ENU=BOM Level;
                      ToolTipML=ENU=View availability figures for items on bills of materials that show how many units of a parent item you can make based on the availability of child items.;
                      ApplicationArea=#Planning;
                      Image=BOMLevel;
                      OnAction=VAR
                                 Item@1001 : Record 27;
                               BEGIN
                                 IF Item.GET("Item No.") THEN
                                   ItemAvailFormsMgt.ShowItemAvailFromItem(Item,ItemAvailFormsMgt.ByBOM);
                               END;
                                }
      { 30      ;2   ;Separator  }
      { 3       ;2   ;Action    ;
                      ShortCutKey=F7;
                      CaptionML=ENU=Statistics;
                      ToolTipML=ENU=View statistical information, such as the value of posted entries, for the record.;
                      ApplicationArea=#Planning;
                      Promoted=Yes;
                      Image=Statistics;
                      PromotedCategory=Process;
                      OnAction=VAR
                                 Item@1001 : Record 27;
                                 ItemStatistics@1002 : Page 5827;
                               BEGIN
                                 IF Item.GET("Item No.") THEN;
                                 ItemStatistics.SetItem(Item);
                                 ItemStatistics.RUNMODAL;
                               END;
                                }
      { 1900000004;0 ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 19      ;1   ;ActionGroup;
                      CaptionML=ENU=F&unctions;
                      Image=Action }
      { 24      ;2   ;Action    ;
                      Ellipsis=Yes;
                      CaptionML=ENU=Update &Shipment Dates;
                      ToolTipML=ENU=Update the Shipment Date field on lines with any changes that were made since you opened the Sales Order Planning window.;
                      ApplicationArea=#Planning;
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=UpdateShipment;
                      PromotedCategory=Process;
                      OnAction=VAR
                                 SalesLine@1001 : Record 37;
                                 Choice@1002 : Integer;
                                 LastShipmentDate@1003 : Date;
                               BEGIN
                                 Choice := STRMENU(Text000);

                                 IF Choice = 0 THEN
                                   EXIT;

                                 LastShipmentDate := WORKDATE;

                                 SalesHeader.LOCKTABLE;
                                 SalesHeader.GET(SalesHeader."Document Type"::Order,SalesHeader."No.");

                                 IF Choice = 1 THEN BEGIN
                                   IF FIND('-') THEN
                                     REPEAT
                                       IF "Expected Delivery Date" > LastShipmentDate THEN
                                         LastShipmentDate := "Expected Delivery Date";
                                     UNTIL NEXT = 0;
                                   SalesHeader.VALIDATE("Shipment Date",LastShipmentDate);
                                   SalesHeader.MODIFY;
                                 END
                                 ELSE BEGIN
                                   SalesLine.LOCKTABLE;
                                   IF FIND('-') THEN
                                     REPEAT
                                       SalesLine.GET(
                                         SalesLine."Document Type"::Order,
                                         "Sales Order No.",
                                         "Sales Order Line No.");
                                       SalesLine."Shipment Date" := "Expected Delivery Date";
                                       SalesLine.MODIFY;
                                     UNTIL NEXT = 0;
                                 END;
                                 BuildForm;
                               END;
                                }
      { 25      ;2   ;Action    ;
                      AccessByPermission=TableData 5405=R;
                      CaptionML=ENU=&Create Prod. Order;
                      ToolTipML=ENU=Prepare to create a production order to fulfill the sales demand.;
                      ApplicationArea=#Manufacturing;
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=CreateDocument;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 CreateProdOrder;
                               END;
                                }
      { 32      ;2   ;Separator  }
      { 20      ;2   ;Action    ;
                      CaptionML=ENU=Order &Tracking;
                      ToolTipML=ENU=Tracks the connection of a supply to its corresponding demand. This can help you find the original demand that created a specific production order or purchase order.;
                      ApplicationArea=#Planning;
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=OrderTracking;
                      PromotedCategory=Process;
                      OnAction=VAR
                                 SalesOrderLine@1001 : Record 37;
                                 TrackingForm@1002 : Page 99000822;
                               BEGIN
                                 SalesOrderLine.GET(
                                   SalesOrderLine."Document Type"::Order,
                                   "Sales Order No.",
                                   "Sales Order Line No.");

                                 TrackingForm.SetSalesLine(SalesOrderLine);
                                 TrackingForm.RUNMODAL;
                                 BuildForm;
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 2   ;1   ;Group     ;
                GroupType=Repeater }

    { 5   ;2   ;Field     ;
                ToolTipML=ENU=Specifies the item number of the sales order line.;
                ApplicationArea=#Planning;
                SourceExpr="Item No." }

    { 21  ;2   ;Field     ;
                ToolTipML=ENU=Specifies the variant of the item on the line.;
                ApplicationArea=#Planning;
                SourceExpr="Variant Code";
                Visible=FALSE }

    { 1   ;2   ;Field     ;
                ToolTipML=ENU=Specifies the planning status of the production order, depending on the actual sales order.;
                ApplicationArea=#Planning;
                SourceExpr="Planning Status" }

    { 7   ;2   ;Field     ;
                ToolTipML=ENU=Specifies the description of the item in the sales order line.;
                ApplicationArea=#Planning;
                SourceExpr=Description }

    { 9   ;2   ;Field     ;
                ToolTipML=ENU=Specifies when items on the document are shipped or were shipped. A shipment date is usually calculated from a requested delivery date plus lead time.;
                ApplicationArea=#Planning;
                SourceExpr="Shipment Date" }

    { 26  ;2   ;Field     ;
                ToolTipML=ENU=Specifies the quantity planned in this line.;
                ApplicationArea=#Planning;
                SourceExpr="Planned Quantity";
                Visible=FALSE;
                OnLookup=VAR
                           SalesLine@1002 : Record 37;
                         BEGIN
                           SalesLine.GET(
                             SalesLine."Document Type"::Order,
                             "Sales Order No.","Sales Order Line No.");
                           SalesLine.ShowReservationEntries(TRUE);
                         END;
                          }

    { 11  ;2   ;Field     ;
                ToolTipML=ENU=Specifies how many of the actual items are available.;
                ApplicationArea=#Planning;
                DecimalPlaces=0:5;
                SourceExpr=Available }

    { 13  ;2   ;Field     ;
                ToolTipML=ENU=Specifies the next planning date.;
                ApplicationArea=#Planning;
                SourceExpr="Next Planning Date" }

    { 15  ;2   ;Field     ;
                ToolTipML=ENU=Specifies the expected delivery date.;
                ApplicationArea=#Planning;
                SourceExpr="Expected Delivery Date" }

    { 28  ;2   ;Field     ;
                ToolTipML=ENU=Specifies if it is necessary or not to reschedule this line.;
                ApplicationArea=#Planning;
                SourceExpr="Needs Replanning";
                Visible=FALSE }

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=All Lines to last Shipment Date,Each line own Shipment Date';
      Text001@1001 : TextConst 'ENU=There is nothing to plan.';
      SalesHeader@1002 : Record 36;
      ReservEntry@1004 : Record 337;
      ReservEngineMgt@1005 : Codeunit 99000831;
      ReserveSalesline@1006 : Codeunit 99000832;
      ItemAvailFormsMgt@1003 : Codeunit 353;
      NewStatus@1007 : 'Simulated,Planned,Firm Planned,Released,Finished';
      NewOrderType@1008 : 'ItemOrder,ProjectOrder';

    [External]
    PROCEDURE SetSalesOrder@2(SalesOrderNo@1000 : Code[20]);
    BEGIN
      SalesHeader.GET(SalesHeader."Document Type"::Order,SalesOrderNo);
    END;

    [External]
    PROCEDURE BuildForm@3();
    BEGIN
      RESET;
      DELETEALL;
      MakeLines;
      SETRANGE("Sales Order No.",SalesHeader."No.");
    END;

    LOCAL PROCEDURE MakeLines@1();
    VAR
      SalesLine@1000 : Record 37;
      ProdOrderLine@1001 : Record 5406;
      PurchLine@1002 : Record 39;
      ReqLine@1003 : Record 246;
      ReservEntry2@1004 : Record 337;
    BEGIN
      SalesLine.SETRANGE("Document Type",SalesLine."Document Type"::Order);
      SalesLine.SETRANGE("Document No.",SalesHeader."No.");
      SalesLine.SETRANGE(Type,SalesLine.Type::Item);
      OnMakeLinesSetOnAfterSetFilters(SalesLine);
      IF SalesLine.FIND('-') THEN
        REPEAT
          INIT;
          "Sales Order No." := SalesLine."Document No.";
          "Sales Order Line No." := SalesLine."Line No.";
          "Item No." := SalesLine."No.";

          "Variant Code" := SalesLine."Variant Code";
          Description := SalesLine.Description;
          "Shipment Date" := SalesLine."Shipment Date";
          "Planning Status" := "Planning Status"::None;
          SalesLine.CALCFIELDS("Reserved Qty. (Base)");
          "Planned Quantity" := SalesLine."Reserved Qty. (Base)";
          ReservEngineMgt.InitFilterAndSortingLookupFor(ReservEntry,FALSE);
          ReserveSalesline.FilterReservFor(ReservEntry,SalesLine);
          ReservEntry.SETRANGE("Reservation Status",ReservEntry."Reservation Status"::Reservation);
          IF ReservEntry.FIND('-') THEN
            REPEAT
              IF ReservEntry2.GET(ReservEntry."Entry No.",NOT ReservEntry.Positive) THEN
                CASE ReservEntry2."Source Type" OF
                  DATABASE::"Item Ledger Entry":
                    "Planning Status" := "Planning Status"::Inventory;
                  DATABASE::"Requisition Line":
                    BEGIN
                      ReqLine.GET(
                        ReservEntry2."Source ID",ReservEntry2."Source Batch Name",ReservEntry2."Source Ref. No.");
                      "Planning Status" := "Planning Status"::Planned;
                      "Expected Delivery Date" := ReqLine."Due Date";
                    END;
                  DATABASE::"Purchase Line":
                    BEGIN
                      PurchLine.GET(
                        ReservEntry2."Source Subtype",ReservEntry2."Source ID",ReservEntry2."Source Ref. No.");
                      "Planning Status" := "Planning Status"::"Firm Planned";
                      "Expected Delivery Date" := PurchLine."Expected Receipt Date";
                    END;
                  DATABASE::"Prod. Order Line":
                    BEGIN
                      ProdOrderLine.GET(
                        ReservEntry2."Source Subtype",ReservEntry2."Source ID",ReservEntry2."Source Prod. Order Line");
                      IF ProdOrderLine."Due Date" > "Expected Delivery Date" THEN
                        "Expected Delivery Date" := ProdOrderLine."Ending Date";
                      IF ((ProdOrderLine.Status + 1) < "Planning Status") OR
                         ("Planning Status" = "Planning Status"::None)
                      THEN
                        "Planning Status" := ProdOrderLine.Status + 1;
                    END;
                END;
            UNTIL ReservEntry.NEXT = 0;
          "Needs Replanning" :=
            ("Planned Quantity" <> SalesLine."Outstanding Qty. (Base)") OR
            ("Expected Delivery Date" > "Shipment Date");
          CalculateDisposalPlan(SalesLine."Variant Code",SalesLine."Location Code");
          INSERT;
        UNTIL SalesLine.NEXT = 0;
    END;

    LOCAL PROCEDURE CalculateDisposalPlan@4(VariantCode@1000 : Code[20];LocationCode@1001 : Code[10]);
    VAR
      Item@1003 : Record 27;
    BEGIN
      IF NOT "Needs Replanning" THEN
        EXIT;

      Item.GET("Item No.");
      Item.SETRANGE("Variant Filter",VariantCode);
      Item.SETRANGE("Location Filter",LocationCode);
      Item.CALCFIELDS(
        Inventory,
        "Qty. on Purch. Order",
        "Qty. on Sales Order",
        "Scheduled Receipt (Qty.)",
        "Planned Order Receipt (Qty.)",
        "Scheduled Need (Qty.)");

      Available :=
        Item.Inventory -
        Item."Qty. on Sales Order" +
        Item."Qty. on Purch. Order" -
        Item."Scheduled Need (Qty.)" +
        Item."Scheduled Receipt (Qty.)" +
        Item."Planned Order Receipt (Qty.)";

      "Next Planning Date" := WORKDATE;

      CalculatePlanAndDelivDates(Item,"Next Planning Date","Expected Delivery Date");
    END;

    LOCAL PROCEDURE CalculatePlanAndDelivDates@6(Item@1000 : Record 27;VAR NextPlanningDate@1001 : Date;VAR ExpectedDeliveryDate@1002 : Date);
    BEGIN
      NextPlanningDate := CALCDATE(Item."Lot Accumulation Period",NextPlanningDate);

      IF (Available > 0) OR ("Planning Status" <> "Planning Status"::None) THEN
        ExpectedDeliveryDate := CALCDATE(Item."Safety Lead Time",WORKDATE)
      ELSE
        ExpectedDeliveryDate :=
          CALCDATE(Item."Safety Lead Time",
            CALCDATE(Item."Lead Time Calculation",NextPlanningDate))
    END;

    LOCAL PROCEDURE CreateOrders@7() OrdersCreated@1000 : Boolean;
    VAR
      xSalesPlanLine@1005 : Record 99000800;
      Item@1001 : Record 27;
      SalesLine@1002 : Record 37;
      SKU@1006 : Record 5700;
      ProdOrderFromSale@1003 : Codeunit 99000792;
      CreateProdOrder@1007 : Boolean;
      EndLoop@1004 : Boolean;
    BEGIN
      xSalesPlanLine := Rec;

      IF NOT FINDSET THEN
        EXIT;

      REPEAT
        SalesLine.GET(SalesLine."Document Type"::Order,"Sales Order No.","Sales Order Line No.");
        SalesLine.TESTFIELD("Shipment Date");
        SalesLine.CALCFIELDS("Reserved Qty. (Base)");
        IF SalesLine."Outstanding Qty. (Base)" > SalesLine."Reserved Qty. (Base)" THEN BEGIN
          IF SKU.GET(SalesLine."Location Code",SalesLine."No.",SalesLine."Variant Code") THEN
            CreateProdOrder := SKU."Replenishment System" = SKU."Replenishment System"::"Prod. Order"
          ELSE BEGIN
            Item.GET(SalesLine."No.");
            CreateProdOrder := Item."Replenishment System" = Item."Replenishment System"::"Prod. Order";
          END;

          IF CreateProdOrder THEN BEGIN
            OrdersCreated := TRUE;
            ProdOrderFromSale.CreateProdOrder(SalesLine,NewStatus,NewOrderType);
            IF NewOrderType = NewOrderType::ProjectOrder THEN
              EndLoop := TRUE;
          END;
        END;
      UNTIL (NEXT = 0) OR EndLoop;

      Rec := xSalesPlanLine;
    END;

    LOCAL PROCEDURE Caption@5() : Text[250];
    BEGIN
      EXIT(STRSUBSTNO('%1 %2',SalesHeader."No.",SalesHeader."Bill-to Name"));
    END;

    [External]
    PROCEDURE CreateProdOrder@8();
    VAR
      CreateOrderForm@1000 : Page 99000884;
    BEGIN
      IF CreateOrderForm.RUNMODAL <> ACTION::Yes THEN
        EXIT;

      CreateOrderForm.ReturnPostingInfo(NewStatus,NewOrderType);

      IF NOT CreateOrders THEN
        MESSAGE(Text001);

      SETRANGE("Planning Status");

      BuildForm;

      CurrPage.UPDATE(FALSE);
    END;

    [Integration]
    LOCAL PROCEDURE OnMakeLinesSetOnAfterSetFilters@9(VAR SalesLine@1000 : Record 37);
    BEGIN
    END;

    BEGIN
    END.
  }
}

