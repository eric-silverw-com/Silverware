OBJECT Table 6085772 Temp. Display Document
{
  OBJECT-PROPERTIES
  {
    Date=04/06/18;
    Time=[ 1:00:00 AM];
    Version List=DCW13.70.00.5.00;
  }
  PROPERTIES
  {
    OnDelete=VAR
               CODC@1160040002 : Codeunit 6085617;
               FullFileName@1160040000 : Text[1024];
             BEGIN
               IF "Scanned File" THEN BEGIN
                 IF NOT CONFIRM(STRSUBSTNO(Text001,"File Name" + '.pdf')) THEN
                   ERROR('');

                 IF NOT "Is Cloud OCR" THEN BEGIN
                   FullFileName := "File Path" + '\' + "File Name";
                   FileSysMgt.DeleteFile(FullFileName + '.pdf',FALSE);

                   IF Status = Status::Import THEN BEGIN
                     FileSysMgt.DeleteFile(FullFileName + '.tiff',TRUE);
                     FileSysMgt.DeleteFile(FullFileName + '.xml',TRUE);
                     FileSysMgt.DeleteFile(FullFileName + '.eml',TRUE);
                   END;
                 END ELSE
                   CODC.DeleteDocument("Document Category Code","Document ID",Status);
               END ELSE BEGIN
                 IF NOT CONFIRM(STRSUBSTNO(Text001,"File Name with Extension")) THEN
                   ERROR('');

                 FileSysMgt.DeleteFile("File Path" + '\' + "File Name with Extension",TRUE);
               END;
             END;

    CaptionML=ENU=Document;
  }
  FIELDS
  {
    { 1   ;   ;Entry No.           ;Integer       ;CaptionML=ENU=Entry No. }
    { 2   ;   ;Document ID         ;Text250       ;CaptionML=ENU=Document ID }
    { 3   ;   ;File Name           ;Text250       ;CaptionML=ENU=Filename }
    { 4   ;   ;File Path           ;Text250       ;CaptionML=ENU=File Path }
    { 5   ;   ;Date/Time           ;DateTime      ;CaptionML=ENU=Date/Time }
    { 6   ;   ;Is Cloud OCR        ;Boolean       ;CaptionML=ENU=Is Cloud OCR }
    { 7   ;   ;Status              ;Option        ;CaptionML=ENU=Status;
                                                   OptionCaptionML=ENU=OCR,Import,Error;
                                                   OptionString=OCR,Import,Error }
    { 8   ;   ;Document Category Code;Code20      ;CaptionML=ENU=Document Category Code }
    { 9   ;   ;Description         ;Text250       ;CaptionML=ENU=Description }
    { 10  ;   ;From E-Mail Address ;Text200       ;CaptionML=ENU=From E-Mail Address }
    { 11  ;   ;E-Mail Subject      ;Text200       ;CaptionML=ENU=E-Mail Subject }
    { 12  ;   ;E-Mail Received     ;DateTime      ;CaptionML=ENU=E-Mail Received }
    { 13  ;   ;OCR Processed       ;DateTime      ;CaptionML=ENU=OCR Processed }
    { 14  ;   ;File Name with Extension;Text250   ;CaptionML=ENU=File Name with Extension }
    { 15  ;   ;Scanned File        ;Boolean       ;CaptionML=ENU=Scanned File }
  }
  KEYS
  {
    {    ;Entry No.                               ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text001@1160040001 : TextConst 'ENU=Are you sure you want to delete the file %1?';
      FileSysMgt@1160040002 : Codeunit 6085584;
      CanOnlyImportDCFilesErr@1160040003 : TextConst 'ENU=It is only possible to import Document Capture scanned files one by one.';

    PROCEDURE ShowFile@1160040001();
    VAR
      OnlineMgt@1160040001 : Codeunit 6192772;
      TempFile@1000 : TEMPORARY Record 6085608;
      Filename@1160040000 : Text[1024];
    BEGIN
      IF "Scanned File" THEN BEGIN
        IF "Is Cloud OCR" THEN BEGIN
          TempFile.CreateTemp('pdf');
          OnlineMgt.DownloadFile("File Path",'',TempFile,FALSE);
          Filename := TempFile.GetClientFilePath;
        END ELSE
          Filename := "File Path" + '\' + "File Name" + '.pdf';
      END ELSE
        Filename := "File Path" + '\' + "File Name with Extension";

      FileSysMgt.LocalShell(Filename);
    END;

    PROCEDURE Import@1160040000();
    VAR
      DocCat@1160040003 : Record 6085575;
      CODC@1160040001 : Codeunit 6085617;
      DocumentImporter@1160040000 : Codeunit 6085577;
      DocName@1160040002 : Text[100];
      DocPath@1160040004 : Text[1024];
    BEGIN
      TESTFIELD(Status,Status::Import);

      IF "Scanned File" THEN BEGIN
        IF "Is Cloud OCR" THEN BEGIN
          DocName := CODC.GetNewDocument(Rec);

          DocCat.GET("Document Category Code");
          DocPath := DocCat.GetCategoryPath(2);

          DocumentImporter.ImportDocument("Document Category Code",DocPath,DocName);
        END ELSE
          DocumentImporter.ImportDocument("Document Category Code","File Path","File Name");

        DELETE;
      END ELSE
        ERROR(CanOnlyImportDCFilesErr);
    END;

    BEGIN
    END.
  }
}

