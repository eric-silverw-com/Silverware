OBJECT Table 5600 Fixed Asset
{
  OBJECT-PROPERTIES
  {
    Date=09/19/18;
    Time=12:00:00 PM;
    Version List=NAVW113.00;
  }
  PROPERTIES
  {
    Permissions=TableData 5629=r;
    DataCaptionFields=No.,Description;
    OnInsert=BEGIN
               IF "No." = '' THEN BEGIN
                 FASetup.GET;
                 FASetup.TESTFIELD("Fixed Asset Nos.");
                 NoSeriesMgt.InitSeries(FASetup."Fixed Asset Nos.",xRec."No. Series",0D,"No.","No. Series");
               END;

               "Main Asset/Component" := "Main Asset/Component"::" ";
               "Component of Main Asset" := '';

               DimMgt.UpdateDefaultDim(
                 DATABASE::"Fixed Asset","No.",
                 "Global Dimension 1 Code","Global Dimension 2 Code");
             END;

    OnModify=BEGIN
               "Last Date Modified" := TODAY;
             END;

    OnDelete=VAR
               FADeprBook@1000 : Record 5612;
             BEGIN
               LOCKTABLE;
               MainAssetComp.LOCKTABLE;
               InsCoverageLedgEntry.LOCKTABLE;
               IF "Main Asset/Component" = "Main Asset/Component"::"Main Asset" THEN
                 ERROR(Text000);
               FAMoveEntries.MoveFAInsuranceEntries("No.");
               FADeprBook.SETRANGE("FA No.","No.");
               FADeprBook.DELETEALL(TRUE);
               IF NOT FADeprBook.ISEMPTY THEN
                 ERROR(Text001,TABLECAPTION,"No.");

               MainAssetComp.SETCURRENTKEY("FA No.");
               MainAssetComp.SETRANGE("FA No.","No.");
               MainAssetComp.DELETEALL;
               IF "Main Asset/Component" = "Main Asset/Component"::Component THEN BEGIN
                 MainAssetComp.RESET;
                 MainAssetComp.SETRANGE("Main Asset No.","Component of Main Asset");
                 MainAssetComp.SETRANGE("FA No.",'');
                 MainAssetComp.DELETEALL;
                 MainAssetComp.SETRANGE("FA No.");
                 IF NOT MainAssetComp.FINDFIRST THEN BEGIN
                   FA.GET("Component of Main Asset");
                   FA."Main Asset/Component" := FA."Main Asset/Component"::" ";
                   FA."Component of Main Asset" := '';
                   FA.MODIFY;
                 END;
               END;

               MaintenanceRegistration.SETRANGE("FA No.","No.");
               MaintenanceRegistration.DELETEALL;

               CommentLine.SETRANGE("Table Name",CommentLine."Table Name"::"Fixed Asset");
               CommentLine.SETRANGE("No.","No.");
               CommentLine.DELETEALL;

               DimMgt.DeleteDefaultDim(DATABASE::"Fixed Asset","No.");
             END;

    OnRename=VAR
               SalesLine@1000 : Record 37;
               PurchaseLine@1001 : Record 39;
             BEGIN
               SalesLine.RenameNo(SalesLine.Type::"Fixed Asset",xRec."No.","No.");
               PurchaseLine.RenameNo(PurchaseLine.Type::"Fixed Asset",xRec."No.","No.");

               "Last Date Modified" := TODAY;
             END;

    CaptionML=[ENU=Fixed Asset;
               ESM=Activo fijo;
               FRC=Immobilisation;
               ENC=Fixed Asset];
    LookupPageID=Page5601;
    DrillDownPageID=Page5601;
  }
  FIELDS
  {
    { 1   ;   ;No.                 ;Code20        ;OnValidate=BEGIN
                                                                IF "No." <> xRec."No." THEN BEGIN
                                                                  FASetup.GET;
                                                                  NoSeriesMgt.TestManual(FASetup."Fixed Asset Nos.");
                                                                  "No. Series" := '';
                                                                END;
                                                              END;

                                                   AltSearchField=Search Description;
                                                   CaptionML=[ENU=No.;
                                                              ESM=N§;
                                                              FRC=Nø;
                                                              ENC=No.] }
    { 2   ;   ;Description         ;Text50        ;OnValidate=VAR
                                                                FADeprBook@1000 : Record 5612;
                                                              BEGIN
                                                                IF ("Search Description" = UPPERCASE(xRec.Description)) OR ("Search Description" = '') THEN
                                                                  "Search Description" := Description;
                                                                IF Description <> xRec.Description THEN BEGIN
                                                                  FADeprBook.SETCURRENTKEY("FA No.");
                                                                  FADeprBook.SETRANGE("FA No.","No.");
                                                                  FADeprBook.MODIFYALL(Description,Description);
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Description;
                                                              ESM=Descripci¢n;
                                                              FRC=Description;
                                                              ENC=Description] }
    { 3   ;   ;Search Description  ;Code50        ;CaptionML=[ENU=Search Description;
                                                              ESM=Descripci¢n alias;
                                                              FRC=Description de recherche;
                                                              ENC=Search Description] }
    { 4   ;   ;Description 2       ;Text50        ;CaptionML=[ENU=Description 2;
                                                              ESM=Descripci¢n 2;
                                                              FRC=Description 2;
                                                              ENC=Description 2] }
    { 5   ;   ;FA Class Code       ;Code10        ;TableRelation="FA Class";
                                                   OnValidate=VAR
                                                                FASubclass@1000 : Record 5608;
                                                              BEGIN
                                                                IF "FA Subclass Code" = '' THEN
                                                                  EXIT;

                                                                FASubclass.GET("FA Subclass Code");
                                                                IF NOT (FASubclass."FA Class Code" IN ['',"FA Class Code"]) THEN
                                                                  "FA Subclass Code" := '';
                                                              END;

                                                   CaptionML=[ENU=FA Class Code;
                                                              ESM=A/F C¢d. clase;
                                                              FRC=Code classe immo.;
                                                              ENC=FA Class Code] }
    { 6   ;   ;FA Subclass Code    ;Code10        ;TableRelation="FA Subclass";
                                                   OnValidate=VAR
                                                                FASubclass@1000 : Record 5608;
                                                              BEGIN
                                                                IF "FA Subclass Code" = '' THEN
                                                                  EXIT;

                                                                FASubclass.GET("FA Subclass Code");
                                                                IF "FA Class Code" <> '' THEN BEGIN
                                                                  IF FASubclass."FA Class Code" IN ['',"FA Class Code"] THEN
                                                                    EXIT;

                                                                  ERROR(UnexpctedSubclassErr);
                                                                END;

                                                                VALIDATE("FA Class Code",FASubclass."FA Class Code");
                                                              END;

                                                   CaptionML=[ENU=FA Subclass Code;
                                                              ESM=A/F C¢d. subclase;
                                                              FRC=Code sous-classe immo.;
                                                              ENC=FA Subclass Code] }
    { 7   ;   ;Global Dimension 1 Code;Code20     ;TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(1));
                                                   OnValidate=BEGIN
                                                                ValidateShortcutDimCode(1,"Global Dimension 1 Code");
                                                              END;

                                                   CaptionML=[ENU=Global Dimension 1 Code;
                                                              ESM=C¢d. dimensi¢n global 1;
                                                              FRC=Code de dimension principal 1;
                                                              ENC=Global Dimension 1 Code];
                                                   CaptionClass='1,1,1' }
    { 8   ;   ;Global Dimension 2 Code;Code20     ;TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(2));
                                                   OnValidate=BEGIN
                                                                ValidateShortcutDimCode(2,"Global Dimension 2 Code");
                                                              END;

                                                   CaptionML=[ENU=Global Dimension 2 Code;
                                                              ESM=C¢d. dimensi¢n global 2;
                                                              FRC=Code de dimension principal 2;
                                                              ENC=Global Dimension 2 Code];
                                                   CaptionClass='1,1,2' }
    { 9   ;   ;Location Code       ;Code10        ;TableRelation=Location WHERE (Use As In-Transit=CONST(No));
                                                   CaptionML=[ENU=Location Code;
                                                              ESM=C¢d. almac‚n;
                                                              FRC=Code d'emplacement;
                                                              ENC=Location Code] }
    { 10  ;   ;FA Location Code    ;Code10        ;TableRelation="FA Location";
                                                   CaptionML=[ENU=FA Location Code;
                                                              ESM=A/F C¢d. almac‚n;
                                                              FRC=Code d'emplacement immo.;
                                                              ENC=FA Location Code] }
    { 11  ;   ;Vendor No.          ;Code20        ;TableRelation=Vendor;
                                                   CaptionML=[ENU=Vendor No.;
                                                              ESM=N§ proveedor;
                                                              FRC=Nø fournisseur;
                                                              ENC=Vendor No.] }
    { 12  ;   ;Main Asset/Component;Option        ;CaptionML=[ENU=Main Asset/Component;
                                                              ESM=Principal/Componente;
                                                              FRC=Immo./composante principale;
                                                              ENC=Main Asset/Component];
                                                   OptionCaptionML=[ENU=" ,Main Asset,Component";
                                                                    ESM=" ,Principal,Componente";
                                                                    FRC=" ,Immo. principale,Composante";
                                                                    ENC=" ,Main Asset,Component"];
                                                   OptionString=[ ,Main Asset,Component];
                                                   Editable=No }
    { 13  ;   ;Component of Main Asset;Code20     ;TableRelation="Fixed Asset";
                                                   CaptionML=[ENU=Component of Main Asset;
                                                              ESM=Componente de activo ppal.;
                                                              FRC=Composante immo. princ.;
                                                              ENC=Component of Main Asset];
                                                   Editable=No }
    { 14  ;   ;Budgeted Asset      ;Boolean       ;OnValidate=BEGIN
                                                                FAMoveEntries.ChangeBudget(Rec);
                                                              END;

                                                   CaptionML=[ENU=Budgeted Asset;
                                                              ESM=Activo presupuestado;
                                                              FRC=Actif budg‚t‚;
                                                              ENC=Budgeted Asset] }
    { 15  ;   ;Warranty Date       ;Date          ;CaptionML=[ENU=Warranty Date;
                                                              ESM=Fecha garant¡a;
                                                              FRC=Date de garantie;
                                                              ENC=Warranty Date] }
    { 16  ;   ;Responsible Employee;Code20        ;TableRelation=Employee;
                                                   CaptionML=[ENU=Responsible Employee;
                                                              ESM=Empleado responsable;
                                                              FRC=Employ‚ responsable;
                                                              ENC=Responsible Employee] }
    { 17  ;   ;Serial No.          ;Text50        ;CaptionML=[ENU=Serial No.;
                                                              ESM=N§ serie;
                                                              FRC=Nø de s‚rie;
                                                              ENC=Serial No.] }
    { 18  ;   ;Last Date Modified  ;Date          ;CaptionML=[ENU=Last Date Modified;
                                                              ESM=Fecha £lt. modificaci¢n;
                                                              FRC=Date derniŠre modification;
                                                              ENC=Last Date Modified];
                                                   Editable=No }
    { 19  ;   ;Insured             ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Exist("Ins. Coverage Ledger Entry" WHERE (FA No.=FIELD(No.),
                                                                                                         Disposed FA=CONST(No)));
                                                   CaptionML=[ENU=Insured;
                                                              ESM=Asegurado;
                                                              FRC=Assur‚;
                                                              ENC=Insured];
                                                   Editable=No }
    { 20  ;   ;Comment             ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Exist("Comment Line" WHERE (Table Name=CONST(Fixed Asset),
                                                                                           No.=FIELD(No.)));
                                                   CaptionML=[ENU=Comment;
                                                              ESM=Comentario;
                                                              FRC=Commentaire;
                                                              ENC=Comment];
                                                   Editable=No }
    { 21  ;   ;Blocked             ;Boolean       ;CaptionML=[ENU=Blocked;
                                                              ESM=Bloqueado;
                                                              FRC=Bloqu‚;
                                                              ENC=Blocked] }
    { 22  ;   ;Picture             ;BLOB          ;CaptionML=[ENU=Picture;
                                                              ESM=Foto;
                                                              FRC=illustration;
                                                              ENC=Picture];
                                                   SubType=Bitmap }
    { 23  ;   ;Maintenance Vendor No.;Code20      ;TableRelation=Vendor;
                                                   CaptionML=[ENU=Maintenance Vendor No.;
                                                              ESM=N§ proveedor mtto.;
                                                              FRC=Nø fournisseur maintenance;
                                                              ENC=Maintenance Vendor No.] }
    { 24  ;   ;Under Maintenance   ;Boolean       ;CaptionML=[ENU=Under Maintenance;
                                                              ESM=Bajo mantenimiento;
                                                              FRC=Maintenance en cours;
                                                              ENC=Under Maintenance] }
    { 25  ;   ;Next Service Date   ;Date          ;CaptionML=[ENU=Next Service Date;
                                                              ESM=Pr¢xima fecha servicio;
                                                              FRC=Prochaine date de service;
                                                              ENC=Next Service Date] }
    { 26  ;   ;Inactive            ;Boolean       ;CaptionML=[ENU=Inactive;
                                                              ESM=Inactivo;
                                                              FRC=Inactif;
                                                              ENC=Inactive] }
    { 27  ;   ;FA Posting Date Filter;Date        ;FieldClass=FlowFilter;
                                                   CaptionML=[ENU=FA Posting Date Filter;
                                                              ESM=A/F Filtro fecha registro;
                                                              FRC=Filtre date report immo.;
                                                              ENC=FA Posting Date Filter] }
    { 28  ;   ;No. Series          ;Code20        ;TableRelation="No. Series";
                                                   CaptionML=[ENU=No. Series;
                                                              ESM=Nos. serie;
                                                              FRC=S‚ries de nø;
                                                              ENC=No. Series];
                                                   Editable=No }
    { 29  ;   ;FA Posting Group    ;Code20        ;TableRelation="FA Posting Group";
                                                   CaptionML=[ENU=FA Posting Group;
                                                              ESM=Grupo registro A/F;
                                                              FRC=Groupe report immobilisation;
                                                              ENC=FA Posting Group] }
    { 30  ;   ;Acquired            ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Exist("FA Depreciation Book" WHERE (FA No.=FIELD(No.),
                                                                                                   Acquisition Date=FILTER(<>'')));
                                                   CaptionML=[ENU=Acquired;
                                                              ESM=Adquirido;
                                                              FRC=Acquises;
                                                              ENC=Acquired] }
    { 140 ;   ;Image               ;Media         ;CaptionML=[ENU=Image;
                                                              ESM=Imagen;
                                                              FRC=Image;
                                                              ENC=Image] }
  }
  KEYS
  {
    {    ;No.                                     ;Clustered=Yes }
    {    ;Search Description                       }
    {    ;FA Class Code                            }
    {    ;FA Subclass Code                         }
    {    ;Component of Main Asset,Main Asset/Component }
    {    ;FA Location Code                         }
    {    ;Global Dimension 1 Code                  }
    {    ;Global Dimension 2 Code                  }
    {    ;FA Posting Group                         }
    {    ;Description                              }
  }
  FIELDGROUPS
  {
    { 1   ;DropDown            ;No.,Description,FA Class Code            }
    { 2   ;Brick               ;No.,Description,FA Class Code,Image      }
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=A main asset cannot be deleted.;ESM=Un activo principal no se puede eliminar.;FRC=Un actif principal ne peut ˆtre supprim‚.;ENC=A main asset cannot be deleted.';
      Text001@1001 : TextConst 'ENU=You cannot delete %1 %2 because it has associated depreciation books.;ESM=No se puede eliminar %1 %2 ya que tiene asociados libros de amortizaci¢n.;FRC=Vous ne pouvez supprimer %1 %2 car il existe des registres d''amortissement li‚s.;ENC=You cannot delete %1 %2 because it has associated depreciation books.';
      CommentLine@1002 : Record 97;
      FA@1003 : Record 5600;
      FASetup@1004 : Record 5603;
      MaintenanceRegistration@1005 : Record 5616;
      MainAssetComp@1007 : Record 5640;
      InsCoverageLedgEntry@1008 : Record 5629;
      FAMoveEntries@1009 : Codeunit 5623;
      NoSeriesMgt@1010 : Codeunit 396;
      DimMgt@1011 : Codeunit 408;
      UnexpctedSubclassErr@1006 : TextConst 'ENU=This fixed asset subclass belongs to a different fixed asset class.;ESM=Esta subclase de activo fijo pertenece a una clase de activo fijo diferente.;FRC=Cette sous-classe d''immobilisations appartient … une classe d''immobilisations diff‚rente.;ENC=This fixed asset subclass belongs to a different fixed asset class.';
      DontAskAgainActionTxt@1018 : TextConst 'ENU=Don''t ask again;ESM=No volver a preguntar;FRC=Ne plus demander;ENC=Don''t ask again';
      NotificationNameTxt@1016 : TextConst '@@@={Locked};ENU=Fixed Asset Acquisition Wizard;ESM=Fixed Asset Acquisition Wizard;FRC=Fixed Asset Acquisition Wizard;ENC=Fixed Asset Acquisition Wizard';
      NotificationDescriptionTxt@1015 : TextConst '@@@={Locked};ENU=Notify when ready to acquire the fixed asset.;ESM=Notify when ready to acquire the fixed asset.;FRC=Notify when ready to acquire the fixed asset.;ENC=Notify when ready to acquire the fixed asset.';
      ReadyToAcquireMsg@1014 : TextConst 'ENU=You are ready to acquire the fixed asset.;ESM=Est  listo para comprar el activo fijo.;FRC=Vous ˆtes prˆt … acqu‚rir l''immobilisation.;ENC=You are ready to acquire the fixed asset.';
      AcquireActionTxt@1013 : TextConst 'ENU=Acquire;ESM=Adquirir;FRC=Acqu‚rir;ENC=Acquire';

    [External]
    PROCEDURE AssistEdit@2(OldFA@1000 : Record 5600) : Boolean;
    BEGIN
      WITH FA DO BEGIN
        FA := Rec;
        FASetup.GET;
        FASetup.TESTFIELD("Fixed Asset Nos.");
        IF NoSeriesMgt.SelectSeries(FASetup."Fixed Asset Nos.",OldFA."No. Series","No. Series") THEN BEGIN
          NoSeriesMgt.SetSeries("No.");
          Rec := FA;
          EXIT(TRUE);
        END;
      END;
    END;

    [External]
    PROCEDURE ValidateShortcutDimCode@29(FieldNumber@1000 : Integer;VAR ShortcutDimCode@1001 : Code[20]);
    BEGIN
      DimMgt.ValidateDimValueCode(FieldNumber,ShortcutDimCode);
      DimMgt.SaveDefaultDim(DATABASE::"Fixed Asset","No.",FieldNumber,ShortcutDimCode);
      MODIFY(TRUE);
    END;

    [External]
    PROCEDURE FieldsForAcquitionInGeneralGroupAreCompleted@30() : Boolean;
    BEGIN
      EXIT(("No." <> '') AND (Description <> '') AND ("FA Subclass Code" <> ''));
    END;

    [External]
    PROCEDURE ShowAcquireWizardNotification@3();
    VAR
      NotificationLifecycleMgt@1002 : Codeunit 1511;
      FixedAssetAcquisitionWizard@1001 : Codeunit 5550;
      FAAcquireWizardNotification@1000 : Notification;
    BEGIN
      IF IsNotificationEnabledForCurrentUser THEN BEGIN
        FAAcquireWizardNotification.ID(GetNotificationID);
        FAAcquireWizardNotification.MESSAGE(ReadyToAcquireMsg);
        FAAcquireWizardNotification.SCOPE(NOTIFICATIONSCOPE::LocalScope);
        FAAcquireWizardNotification.ADDACTION(
          AcquireActionTxt,CODEUNIT::"Fixed Asset Acquisition Wizard",'RunAcquisitionWizardFromNotification');
        FAAcquireWizardNotification.ADDACTION(
          DontAskAgainActionTxt,CODEUNIT::"Fixed Asset Acquisition Wizard",'HideNotificationForCurrentUser');
        FAAcquireWizardNotification.SETDATA(FixedAssetAcquisitionWizard.GetNotificationFANoDataItemID,"No.");
        NotificationLifecycleMgt.SendNotification(FAAcquireWizardNotification,RECORDID);
      END
    END;

    PROCEDURE GetNotificationID@4() : GUID;
    BEGIN
      EXIT('3d5c2f86-cfb9-4407-97c3-9df74c7696c9');
    END;

    PROCEDURE SetNotificationDefaultState@10();
    VAR
      MyNotifications@1000 : Record 1518;
    BEGIN
      MyNotifications.InsertDefault(GetNotificationID,NotificationNameTxt,NotificationDescriptionTxt,TRUE);
    END;

    LOCAL PROCEDURE IsNotificationEnabledForCurrentUser@1() : Boolean;
    VAR
      MyNotifications@1000 : Record 1518;
    BEGIN
      EXIT(MyNotifications.IsEnabled(GetNotificationID));
    END;

    PROCEDURE DontNotifyCurrentUserAgain@6();
    VAR
      MyNotifications@1000 : Record 1518;
    BEGIN
      IF NOT MyNotifications.Disable(GetNotificationID) THEN
        MyNotifications.InsertDefault(GetNotificationID,NotificationNameTxt,NotificationDescriptionTxt,FALSE);
    END;

    PROCEDURE RecallNotificationForCurrentUser@9();
    VAR
      NotificationLifecycleMgt@1000 : Codeunit 1511;
    BEGIN
      NotificationLifecycleMgt.RecallNotificationsForRecord(RECORDID,FALSE);
    END;

    BEGIN
    END.
  }
}

