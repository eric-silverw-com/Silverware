OBJECT Report 5756 Create Warehouse Location
{
  OBJECT-PROPERTIES
  {
    Date=09/19/18;
    Time=12:00:00 PM;
    Version List=NAVW113.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Create Warehouse Location;
               ESM=Crear un almac‚n;
               FRC=Cr‚er un emplacement d'entrep“t;
               ENC=Create Warehouse Location];
    ApplicationArea=#Warehouse;
    ProcessingOnly=Yes;
    OnPreReport=VAR
                  WhseEntry@1000 : Record 7312;
                BEGIN
                  IF LocCode = '' THEN
                    ERROR(Text001);
                  IF AdjBinCode = '' THEN
                    ERROR(Text002);

                  ItemLedgEntry.RESET;
                  ItemLedgEntry.SETCURRENTKEY("Item No.",Open);
                  IF ItemLedgEntry.FIND('-') THEN
                    REPEAT
                      ItemLedgEntry.SETRANGE("Item No.",ItemLedgEntry."Item No.");
                      ItemLedgEntry.SETRANGE(Open,TRUE);
                      ItemLedgEntry.SETRANGE("Location Code",LocCode);
                      Found := ItemLedgEntry.FIND('-');
                      IF Found AND NOT HideValidationDialog THEN
                        IF NOT CONFIRM(STRSUBSTNO('%1 %2 %3 %4 %5 %6 %7 %8',
                               Text010,Text011,Text012,Text013,Text014,
                               Text015,STRSUBSTNO(Text016,LocCode),Text017),FALSE)
                        THEN
                          CurrReport.QUIT;
                      ItemLedgEntry.SETRANGE("Location Code");
                      ItemLedgEntry.SETRANGE(Open);
                      ItemLedgEntry.FIND('+');
                      ItemLedgEntry.SETRANGE("Item No.");
                    UNTIL (ItemLedgEntry.NEXT = 0) OR Found;

                  IF NOT Found THEN
                    ERROR(Text018,Location.TABLECAPTION,Location.FIELDCAPTION(Code),LocCode);
                  CLEAR(ItemLedgEntry);

                  WhseEntry.SETRANGE("Location Code",LocCode);
                  IF NOT WhseEntry.ISEMPTY THEN
                    ERROR(
                      Text019,LocCode,WhseEntry.TABLECAPTION);

                  TempWhseJnlLine.RESET;
                  TempWhseJnlLine.DELETEALL;

                  LastLineNo := 0;

                  WITH ItemLedgEntry DO BEGIN
                    SETCURRENTKEY(
                      "Item No.","Location Code",Open,"Variant Code","Unit of Measure Code","Lot No.","Serial No.");

                    Location.GET(LocCode);
                    Location.TESTFIELD("Adjustment Bin Code",'');
                    CheckWhseDocs;

                    Bin.GET(LocCode,AdjBinCode);

                    IF FIND('-') THEN BEGIN
                      IF NOT HideValidationDialog THEN BEGIN
                        Window.OPEN(STRSUBSTNO(Text020,"Location Code") + Text003);
                        i := 1;
                        CountItemLedgEntries := COUNT;
                      END;

                      REPEAT
                        IF NOT HideValidationDialog THEN BEGIN
                          Window.UPDATE(100,i);
                          Window.UPDATE(102,ROUND(i / CountItemLedgEntries * 10000,1));
                        END;

                        SETRANGE("Item No.","Item No.");
                        IF FIND('-') THEN BEGIN
                          SETRANGE("Location Code",LocCode);
                          SETRANGE(Open,TRUE);
                          IF FIND('-') THEN
                            REPEAT
                              SETRANGE("Variant Code","Variant Code");
                              IF FIND('-') THEN
                                REPEAT
                                  SETRANGE("Unit of Measure Code","Unit of Measure Code");
                                  IF FIND('-') THEN
                                    REPEAT
                                      SETRANGE("Lot No.","Lot No.");
                                      IF FIND('-') THEN
                                        REPEAT
                                          SETRANGE("Serial No.","Serial No.");

                                          CALCSUMS("Remaining Quantity");

                                          IF "Remaining Quantity" < 0 THEN
                                            ERROR(
                                              STRSUBSTNO(Text005,BuildErrorText) +
                                              STRSUBSTNO(Text009,ItemsWithNegativeInventory.OBJECTID));

                                          IF "Remaining Quantity" > 0 THEN
                                            CreateWhseJnlLine;
                                          FIND('+');
                                          SETRANGE("Serial No.");
                                        UNTIL NEXT = 0;

                                      FIND('+');
                                      SETRANGE("Lot No.");
                                    UNTIL NEXT = 0;

                                  FIND('+');
                                  SETRANGE("Unit of Measure Code")
                                UNTIL NEXT = 0;

                              FIND('+');
                              SETRANGE("Variant Code");
                            UNTIL NEXT = 0;
                        END;

                        SETRANGE(Open);
                        SETRANGE("Location Code");
                        FIND('+');
                        IF NOT HideValidationDialog THEN
                          i := i + COUNT;
                        SETRANGE("Item No.");
                      UNTIL NEXT = 0;
                    END;
                  END;
                END;

    OnPostReport=VAR
                   WhseJnlRegisterLine@1000 : Codeunit 7301;
                 BEGIN
                   Location."Bin Mandatory" := TRUE;
                   Location.VALIDATE("Directed Put-away and Pick",TRUE);
                   Location.VALIDATE("Adjustment Bin Code",AdjBinCode);
                   Location.MODIFY;

                   IF TempWhseJnlLine.FIND('-') THEN
                     REPEAT
                       WhseJnlRegisterLine.RegisterWhseJnlLine(TempWhseJnlLine);
                     UNTIL TempWhseJnlLine.NEXT = 0;

                   IF NOT HideValidationDialog THEN BEGIN
                     Window.CLOSE;
                     MESSAGE(Text004);
                   END;
                 END;

    UsageCategory=Tasks;
  }
  DATASET
  {
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[ENU=Options;
                             ESM=Opciones;
                             FRC=Options;
                             ENC=Options] }

      { 1   ;2   ;Field     ;
                  CaptionML=[ENU=Location Code;
                             ESM=C¢d. almac‚n;
                             FRC=Code d'emplacement;
                             ENC=Location Code];
                  ToolTipML=[ENU="Specifies the location where the warehouse activity takes place. ";
                             ESM="Especifica la ubicaci¢n en la que tiene lugar la actividad de almac‚n. ";
                             FRC="Sp‚cifie l'emplacement o— l'activit‚ entrep“t est effectu‚e. ";
                             ENC="Specifies the location where the warehouse activity takes place. "];
                  ApplicationArea=#Warehouse;
                  SourceExpr=LocCode;
                  OnValidate=BEGIN
                               Location.GET(LocCode);
                               Location.TESTFIELD("Bin Mandatory",FALSE);
                               Location.TESTFIELD("Use As In-Transit",FALSE);
                               Location.TESTFIELD("Directed Put-away and Pick",FALSE);
                             END;

                  OnLookup=BEGIN
                             CLEAR(Location);
                             IF LocCode <> '' THEN
                               Location.Code := LocCode;
                             IF PAGE.RUNMODAL(0,Location) = ACTION::LookupOK THEN BEGIN
                               Location.TESTFIELD("Bin Mandatory",FALSE);
                               Location.TESTFIELD("Use As In-Transit",FALSE);
                               Location.TESTFIELD("Directed Put-away and Pick",FALSE);
                               LocCode := Location.Code;
                             END;
                           END;
                            }

      { 3   ;2   ;Field     ;
                  CaptionML=[ENU=Adjustment Bin Code;
                             ESM=C¢d. ubicaci¢n ajuste;
                             FRC=Code de zone ajustement;
                             ENC=Adjustment Bin Code];
                  ToolTipML=[ENU=Specifies the code of the item on the bin list.;
                             ESM=Especifica el c¢digo del producto en la lista ubicaci¢n.;
                             FRC=Sp‚cifie le code de l'article sur la liste de zones.;
                             ENC=Specifies the code of the item on the bin list.];
                  ApplicationArea=#Warehouse;
                  SourceExpr=AdjBinCode;
                  OnValidate=BEGIN
                               IF AdjBinCode <> '' THEN
                                 IF LocCode <> '' THEN
                                   Bin.GET(LocCode,AdjBinCode)
                                 ELSE BEGIN
                                   Bin.SETRANGE(Code,AdjBinCode);
                                   Bin.FINDFIRST;
                                   LocCode := Bin."Location Code";
                                 END;
                             END;

                  OnLookup=BEGIN
                             Bin.RESET;
                             IF LocCode <> '' THEN
                               Bin.SETRANGE("Location Code",LocCode);

                             IF PAGE.RUNMODAL(0,Bin) = ACTION::LookupOK THEN BEGIN
                               IF LocCode = '' THEN
                                 LocCode := Bin."Location Code";
                               AdjBinCode := Bin.Code;
                             END;
                           END;
                            }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      TempWhseJnlLine@1007 : TEMPORARY Record 7311;
      Item@1020 : Record 27;
      ItemLedgEntry@1011 : Record 32;
      ItemUnitOfMeasure@1001 : Record 5404;
      Location@1002 : Record 14;
      Bin@1003 : Record 7354;
      ItemsWithNegativeInventory@1010 : Report 5757;
      WMSMgt@1019 : Codeunit 7302;
      Window@1013 : Dialog;
      LocCode@1000 : Code[10];
      Text001@1042 : TextConst 'ENU=Enter a location code.;ESM=Introduzca un c¢digo de almac‚n.;FRC=Veuillez entrer un code d''emplacement.;ENC=Enter a location code.';
      Text002@1012 : TextConst 'ENU=Enter an adjustment bin code.;ESM=Introduzca un c¢digo de ubicaci¢n de ajuste.;FRC=Entrez un code de zone d''ajustement.;ENC=Enter an adjustment bin code.';
      Text003@1004 : TextConst 'ENU=Count #100##### @102@@@@@@@@;ESM=Cuenta #100##### @102@@@@@@@@;FRC=D‚nombrement #100##### @102@@@@@@@@;ENC=Count #100##### @102@@@@@@@@';
      AdjBinCode@1009 : Code[20];
      i@1005 : Integer;
      CountItemLedgEntries@1029 : Integer;
      LastLineNo@1008 : Integer;
      Text004@1015 : TextConst 'ENU=The conversion was successfully completed.;ESM=La conversi¢n se realiz¢ correctamente.;FRC=La conversion a ‚t‚ r‚ussie.;ENC=The conversion was successfully completed.';
      Text005@1016 : TextConst 'ENU=Negative inventory was found in the location. You must clear this negative inventory in the program before you can proceed with the conversion.\\%1.\\;ESM=Hay inventario negativo en el almac‚n. Debe corregir este inventario negativo en el programa antes de lanzar la conversi¢n.\\%1.\\;FRC=Un inventaire n‚gatif a ‚t‚ trouv‚ dans l''emplacement. Vous devez enlever cet inventaire n‚gatif du programme avant de poursuivre la conversion.\\%1.\\;ENC=Negative inventory was found in the location. You must clear this negative inventory in the program before you can proceed with the conversion.\\%1.\\';
      HideValidationDialog@1018 : Boolean;
      Text006@1022 : TextConst 'ENU=Location %1 cannot be converted because at least one %2 is not completely posted yet.\\Post or delete all of them before restarting the conversion batch job.;ESM=La ubicaci¢n %1 no se puede convertir porque todav¡a no se ha registrado al menos un %2.\\Registre o elimine todos antes de reiniciar el trabajo por lotes de conversi¢n.;FRC=L''emplacement %1 ne peut pas ˆtre converti parce qu''au moins un %2 n''est pas encore report‚.\\Reportez ou supprimez complŠtement les lignes signal‚es avant de reprendre le traitement de conversion en lot.;ENC=Location %1 cannot be converted because at least one %2 is not completely posted yet.\\Post or delete all of them before restarting the conversion batch job.';
      Text007@1023 : TextConst 'ENU=Location %1 cannot be converted because at least one %2 is not completely registered yet.\\Register or delete all of them before restarting the conversion batch job.;ESM=La ubicaci¢n %1 no se puede convertir porque al menos un %2 no est  completamente registrado.\\Registre o elimine todos antes de reiniciar el trabajo por lotes de conversi¢n.;FRC=L''emplacement %1 ne peut pas ˆtre converti parce qu''au moins un %2 n''est pas entiŠrement enregistr‚.\\Enregistrez ou supprimez complŠtement toutes les lignes avant de reprendre le traitement de conversion en lot.;ENC=Location %1 cannot be converted because at least one %2 is not completely registered yet.\\Register or delete all of them before restarting the conversion batch job.';
      Text008@1024 : TextConst 'ENU=Location %1 cannot be converted because at least one %2 exists.\\Delete all of them before restarting the conversion batch job.;ESM=La ubicaci¢n %1 no se puede convertir porque existe al menos un %2.\\Elimine todos antes de reiniciar el trabajo por lotes de conversi¢n.;FRC=L''emplacement %1 ne peut pas ˆtre converti parce qu''au moins un %2 existe.\\Supprimez toutes les lignes avant de reprendre le traitement de conversion en lot.;ENC=Location %1 cannot be converted because at least one %2 exists.\\Delete all of them before restarting the conversion batch job.';
      Text009@1030 : TextConst 'ENU=Run %1 for a report of all negative inventory in the location.;ESM=Ejecute %1 para un informe de todo el inventario negativo en el almac‚n.;FRC=Ex‚cutez %1 pour obtenir un rapport de tous les inventaires n‚gatifs de l''emplacement.;ENC=Run %1 for a report of all negative inventory in the location.';

      Text010@1039 : TextConst
        'ENU=Inventory exists on this location. By choosing Yes from this warning, you are confirming that you want to enable this location to use Warehouse Management Systems by running a batch job to create warehouse entries for the inventory in this location.\\',
        'ESM=Hay inventario en este almac‚n. Si selecciona S¡ en la advertencia, confirmar  que quiere permitir habilitar este almac‚n para usar el Sistema de Gesti¢n de almacenes para lanzar un trabajo por lotes que crea movimientos de almac‚n para el inventario de este almac‚n.\\',
        'FRC=Cet emplacement contient un inventaire. En choisissant Oui sur cet avertissement, vous confirmez que vous souhaitez bien g‚rer cet emplacement en tant qu''emplacement de distribution avanc‚ en lan‡ant le traitement en lot qui va cr‚er des Saisies d''inventaire Entrep“t pour chaque article en stock.\\',
        'ENC=Inventory exists on this location. By choosing Yes from this warning, you are confirming that you want to enable this location to use Warehouse Management Systems by running a batch job to create warehouse entries for the inventory in this location.\\';
      Text011@1038 : TextConst 'ENU="If you want to proceed, you must first ensure that no negative inventory exists in the location. Negative inventory is not allowed in a location that uses warehouse management logic and must be cleared by posting a suitable quantity to inventory. ";ESM="Para continuar, aseg£rese primero de que no haya inventario negativo en el almac‚n. No est  permitido tener inventario negativo en un almac‚n que use una l¢gica de gesti¢n de almac‚n y que quedar  ajustado registr ndose una cantidad adecuada de inventario. ";FRC="Si vous souhaitez continuer, vous devez d''abord vous assurer qu''aucun inventaire n‚gatif n''existe dans l''emplacement car les inventaires n‚gatifs ne sont pas autoris‚s dans un emplacement qui emploie la logique de gestion d''entrep“t. ";ENC="If you want to proceed, you must first ensure that no negative inventory exists in the location. Negative inventory is not allowed in a location that uses warehouse management logic and must be cleared by posting a suitable quantity to inventory. "';
      Text012@1037 : TextConst 'ENU=You can perform a check for negative inventory by using the Items with Negative Inventory report.\\;ESM=Puede comprobar el inventario negativo mediante el informe Productos con inventario negativo.\\;FRC=Vous pouvez contr“ler les inventaire n‚gatifs en utilisant le rapport Articles avec inventaire n‚gatif.\\;ENC=You can perform a check for negative inventory by using the Items with Negative Inventory report.\\';
      Text013@1036 : TextConst 'ENU="If you can confirm that no negative inventory exists in the location, proceed with the conversion batch job. If negative inventory is found, the batch job will stop with an error message. ";ESM="Si est  seguro de que no hay inventario negativo en el almac‚n, ejecute el trabajo por lotes de conversi¢n. Si el programa encuentra inventario negativo, detendr  el trabajo por lotes con un mensaje de error. ";FRC="Si vous pouvez confirmer qu''aucun inventaire n‚gatif n''existe dans l''emplacement, proc‚dez au traitement de conversion en lot. Si le programme trouve un inventaire n‚gatif, il arrˆte le traitement en lot avec un message d''erreur. ";ENC="If you can confirm that no negative inventory exists in the location, proceed with the conversion batch job. If negative inventory is found, the batch job will stop with an error message. "';

      Text014@1035 : TextConst
        'ENU=The result of this batch job is that initial warehouse entries will be created. You must balance these initial warehouse entries on the adjustment bin by posting a warehouse physical inventory journal or a warehouse item journal to assign zones and bins to items.\',
        'ESM=El resultado de este proceso es la creaci¢n de movs. iniciales de almac‚n. Debe ajustar estos movs. iniciales de almac‚n en la ubic. de ajuste registrando un diario de inventario f¡sico o un diario producto almac‚n para asignar zonas y ubicaciones a los productos.\',
        'FRC=Le r‚sultat de ce travail par lots est que des ‚critures d''entrep“t initiales seront cr‚‚es. Vous devez ‚quilibrer ces ‚critures d''entrep“t initiales dans la zone d''ajustement en effectuant un report de journal d''inventaire physique d''entrep“t ou de journal d''article d''entrep“t pour attribuer des zones aux articles.\',
        'ENC=The result of this batch job is that initial warehouse entries will be created. You must balance these initial warehouse entries on the adjustment bin by posting a warehouse physical inventory journal or a warehouse item journal to assign zones and bins to items.\';
      Text015@1034 : TextConst 'ENU=You must create zones and bins before posting a warehouse physical inventory.\\;ESM=Debe crear zonas y ubicaciones antes de registrar un inventario f¡sico en almac‚n.\\;FRC=Vous devez cr‚er des zones avant de reporter un inventaire physique d''entrep“t.\\;ENC=You must create zones and bins before posting a warehouse physical inventory.\\';
      Text016@1033 : TextConst 'ENU=Location %1 will be a warehouse management location after the batch job has run successfully. This conversion cannot be reversed or undone after it has run.;ESM=La ubicaci¢n %1 ser  un almac‚n despu‚s de que se haya ejecutado correctamente el trabajo por lotes. Esta conversi¢n no es reversible o no puede deshacerse despu‚s ejecutarse.;FRC=L''emplacement %1 sera un emplacement de distribution aprŠs l''ex‚cution r‚ussie du traitement en lot. Cette conversion ne peut pas ˆtre invers‚e ou d‚faite aprŠs avoir ‚t‚ ex‚cut‚e.;ENC=Location %1 will be a warehouse management location after the batch job has run successfully. This conversion cannot be reversed or undone after it has run.';
      Text017@1032 : TextConst 'ENU=\\Do you really want to proceed?;ESM=\\¨Est  seguro de querer continuar?;FRC=\\Voulez-vous vraiment continuer?;ENC=\\Do you really want to proceed?';
      Text018@1031 : TextConst 'ENU=There is nothing to convert for %1 %2 ''%3''.;ESM=No hay nada que convertir para %1 %2 ''%3''.;FRC=Il n''y a rien … convertir pour %1 %2 ''%3 ''.;ENC=There is nothing to convert for %1 %2 ''%3''.';
      Text019@1041 : TextConst 'ENU=Location %1 cannot be converted because at least one %2 exists for this location.;ESM=La ubicaci¢n %1 no se puede convertir porque existe al menos un %2 para esta ubicaci¢n.;FRC=L''emplacement %1 ne peut pas ˆtre converti parce qu''au moins un %2 existe pour cet emplacement.;ENC=Location %1 cannot be converted because at least one %2 exists for this location.';
      Text020@1006 : TextConst 'ENU=Location %1 will be converted to a WMS location.\\This might take some time so please be patient.;ESM=La ubicaci¢n %1 se convertir  en un almac‚n WMS.\\Esto puede llevar algo de tiempo.;FRC=L''emplacement %1 sera converti en emplacement WMS.\\Cela peut prendre un certain temps. Merci de patienter.;ENC=Location %1 will be converted to a WMS location.\\This might take some time so please be patient.';
      Found@1017 : Boolean;

    LOCAL PROCEDURE CheckWhseDocs@5();
    VAR
      WhseRcptHeader@1000 : Record 7316;
      WarehouseShipmentHeader@1001 : Record 7320;
      WhseActivHeader@1002 : Record 5766;
      WhseWkshLine@1003 : Record 7326;
    BEGIN
      WhseRcptHeader.SETRANGE("Location Code",Location.Code);
      IF NOT WhseRcptHeader.ISEMPTY THEN
        ERROR(
          Text006,
          Location.Code,
          WhseRcptHeader.TABLECAPTION);

      WarehouseShipmentHeader.SETRANGE("Location Code",Location.Code);
      IF NOT WarehouseShipmentHeader.ISEMPTY THEN
        ERROR(
          Text006,
          Location.Code,
          WarehouseShipmentHeader.TABLECAPTION);

      WhseActivHeader.SETCURRENTKEY("Location Code");
      WhseActivHeader.SETRANGE("Location Code",Location.Code);
      IF WhseActivHeader.FINDFIRST THEN
        ERROR(
          Text007,
          Location.Code,
          WhseActivHeader.Type);

      WhseWkshLine.SETRANGE("Location Code",Location.Code);
      IF NOT WhseWkshLine.ISEMPTY THEN
        ERROR(
          Text008,
          Location.Code,
          WhseWkshLine.TABLECAPTION);
    END;

    LOCAL PROCEDURE CreateWhseJnlLine@4();
    BEGIN
      LastLineNo := LastLineNo + 10000;

      WITH ItemLedgEntry DO BEGIN
        TempWhseJnlLine.INIT;
        TempWhseJnlLine."Entry Type" := TempWhseJnlLine."Entry Type"::"Positive Adjmt.";
        TempWhseJnlLine."Line No." := LastLineNo;
        TempWhseJnlLine."Location Code" := "Location Code";
        TempWhseJnlLine."Registering Date" := TODAY;
        TempWhseJnlLine."Item No." := "Item No.";
        TempWhseJnlLine.Quantity := ROUND("Remaining Quantity" / "Qty. per Unit of Measure",0.00001);
        TempWhseJnlLine."Qty. (Base)" := "Remaining Quantity";
        TempWhseJnlLine."Qty. (Absolute)" := ROUND(ABS("Remaining Quantity") / "Qty. per Unit of Measure",0.00001);
        TempWhseJnlLine."Qty. (Absolute, Base)" := ABS("Remaining Quantity");
        TempWhseJnlLine."User ID" := USERID;
        TempWhseJnlLine."Variant Code" := "Variant Code";
        IF "Unit of Measure Code" = '' THEN BEGIN
          Item.GET("Item No.");
          "Unit of Measure Code" := Item."Base Unit of Measure";
        END;
        TempWhseJnlLine."Unit of Measure Code" := "Unit of Measure Code";
        TempWhseJnlLine."Qty. per Unit of Measure" := "Qty. per Unit of Measure";
        TempWhseJnlLine."Lot No." := "Lot No.";
        TempWhseJnlLine."Serial No." := "Serial No.";
        TempWhseJnlLine.VALIDATE("Zone Code",Bin."Zone Code");
        TempWhseJnlLine."Bin Code" := AdjBinCode;
        TempWhseJnlLine."To Bin Code" := AdjBinCode;
        GetItemUnitOfMeasure("Item No.","Unit of Measure Code");
        TempWhseJnlLine.Cubage :=
          TempWhseJnlLine."Qty. (Absolute)" * ItemUnitOfMeasure.Cubage;
        TempWhseJnlLine.Weight :=
          TempWhseJnlLine."Qty. (Absolute)" * ItemUnitOfMeasure.Weight;

        WMSMgt.CheckWhseJnlLine(TempWhseJnlLine,0,0,FALSE);
        TempWhseJnlLine.INSERT;
      END;
    END;

    LOCAL PROCEDURE GetItemUnitOfMeasure@15(ItemNo@1000 : Code[20];UOMCode@1001 : Code[10]);
    BEGIN
      IF (ItemUnitOfMeasure."Item No." <> ItemNo) OR
         (ItemUnitOfMeasure.Code <> UOMCode)
      THEN
        IF NOT ItemUnitOfMeasure.GET(ItemNo,UOMCode) THEN
          ItemUnitOfMeasure.INIT;
    END;

    LOCAL PROCEDURE BuildErrorText@7() : Text[250];
    VAR
      Text@1000 : Text[250];
    BEGIN
      WITH ItemLedgEntry DO BEGIN
        Text :=
          STRSUBSTNO(
            '%1: %2, %3: %4',
            FIELDCAPTION("Location Code"),"Location Code",
            FIELDCAPTION("Item No."),"Item No.");
        IF "Variant Code" <> '' THEN
          Text :=
            STRSUBSTNO('%1, %2: %3',Text,
              FIELDCAPTION("Variant Code"),"Variant Code");
        IF "Unit of Measure Code" <> '' THEN
          Text :=
            STRSUBSTNO('%1, %2: %3',Text,
              FIELDCAPTION("Unit of Measure Code"),
              "Unit of Measure Code");
        IF "Lot No." <> '' THEN
          Text :=
            STRSUBSTNO('%1, %2: %3',Text,
              FIELDCAPTION("Lot No."),"Lot No.");
        IF "Serial No." <> '' THEN
          Text :=
            STRSUBSTNO('%1, %2: %3',Text,
              FIELDCAPTION("Serial No."),"Serial No.");
      END;
      EXIT(Text);
    END;

    PROCEDURE InitializeRequest@2(LocationCode@1000 : Code[10];AdjustmentBinCode@1001 : Code[20]);
    BEGIN
      LocCode := LocationCode;
      AdjBinCode := AdjustmentBinCode;
    END;

    PROCEDURE SetHideValidationDialog@7380(NewHideValidationDialog@1000 : Boolean);
    BEGIN
      HideValidationDialog := NewHideValidationDialog;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

