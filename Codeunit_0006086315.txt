OBJECT Codeunit 6086315 Expense Send Welcome E-Mail
{
  OBJECT-PROPERTIES
  {
    Date=06/29/17;
    Time=[ 1:00:00 AM];
    Version List=EMW16.00.10.2.60;
  }
  PROPERTIES
  {
    OnRun=BEGIN
            SendWelcomeEmail;
          END;

  }
  CODE
  {
    VAR
      Text000@1160040005 : TextConst 'ENU=Welcome to Continia Expense Management';
      Text001@1160040004 : TextConst 'ENU=Dear %1,';
      Text002@1160040003 : TextConst 'ENU=Welcome to Continia Expense Management! You have been set up as a Continia Expense Management user with the details below';
      Text003@1160040010 : TextConst 'ENU=Account Details';
      Text004@1160040009 : TextConst 'ENU=Username: %1';
      Text005@1160040001 : TextConst 'ENU=Click here to specify and activate your account';
      Text006@1160040012 : TextConst 'ENU=Password:';
      Text020@1160040006 : TextConst 'ENU=Sending Welcome E-mails...\@1@@@@@@@@@@@@@@@@@@@@@@@@@@';
      Text058@1160040023 : TextConst 'ENU=Useful links';
      Text059@1160040022 : TextConst 'ENU=When your account has been activated you can use Continia Expense Management the following ways:';
      TextApp@1160040013 : TextConst 'ENU=- Mobile App: %1, %2 and %3';
      Text060@1160040017 : TextConst 'ENU=Windows';
      Text062@1160040018 : TextConst 'ENU=Android';
      Text064@1160040019 : TextConst 'ENU=iPhone';
      Text068@1160040020 : TextConst 'ENU="- Web Portal: "';
      Text070@1160040000 : TextConst 'ENU=Best Regards,';
      SmtpMailMgt@1160040008 : Codeunit 6085598;

    PROCEDURE SendWelcomeEmail@1160040002();
    VAR
      EMSetup@1160040009 : Record 6086300;
      ContCompSetup@1160040002 : Record 6192773;
      ContiniaUser@1160040001 : Record 6086001;
      ContiniaUser2@1160040004 : Record 6086001;
      ContiniaUserSetup@1160040000 : Record 6086002;
      ContiniaUserMgt@1160040007 : Codeunit 6085781;
      EMOnlineMgt@1160040008 : Codeunit 6086305;
      Window@1160040005 : Dialog;
      RecCount@1160040003 : Integer;
      i@1160040006 : Integer;
    BEGIN
      ContCompSetup.GET;
      EMSetup.GET;

      ContiniaUser.SETCURRENTKEY("Send Welcome E-mail (EM)");
      ContiniaUser.SETRANGE("Send Welcome E-mail (EM)",TRUE);
      IF ContiniaUser.FINDSET THEN BEGIN
        IF GUIALLOWED THEN BEGIN
          Window.OPEN(Text020);
          RecCount := ContiniaUser.COUNT;
        END;

        REPEAT
          IF ContiniaUserSetup.GET(ContiniaUser."User ID") THEN
            IF ContiniaUserSetup."Expense Management User" THEN BEGIN
              IF GUIALLOWED THEN BEGIN
                i := i + 1;
                Window.UPDATE(1,CalcProgress(RecCount,i));
              END;

              SmtpMailMgt.AddTo(ContiniaUser.Name,ContiniaUser."E-Mail");
              SmtpMailMgt.SetSubject(Text000);

              SmtpMailMgt.SetFromName := EMSetup."Sender Name";
              SmtpMailMgt.SetFromAddress := EMSetup."Sender E-mail";

              SmtpMailMgt.AppendToBody(Bold(STRSUBSTNO(Text001,ContiniaUser.Name)),TRUE);
              SmtpMailMgt.AppendLineBreakToBody;

              AppendToBody(Text002);
              SmtpMailMgt.AppendToBody(Bold(Text003),TRUE);
              SmtpMailMgt.AppendToBody(STRSUBSTNO(Text004,ContiniaUser."E-Mail"),TRUE);

              AppendToBody(STRSUBSTNO('%1 <a href=%3>%2</a>',Text006,Text005,ContiniaUser."Activation Link (EM)"));
              AppendToBody(Bold(Text058));
              AppendToBodyNobreak(Text059);

              AppendToBodyNobreak(
                STRSUBSTNO(TextApp,
                  Link(Text060,'http://www.windowsphone.com/s?appid=d025eec9-93d5-425e-8703-7ee287ee50ba'),
                  Link(Text062,'http://play.google.com/store/apps/details?id=continiaexpensemanagement.droid'),
                  Link(Text064,'https://itunes.apple.com/app/id983970143')));
              AppendToBody(Text068 + Link(EMOnlineMgt.GetCEMOnlineUrl,EMOnlineMgt.GetCEMOnlineUrl));
              SmtpMailMgt.AppendLineBreakToBody;

              SmtpMailMgt.AppendToBody(Text070,TRUE);
              AppendToBody('Continia Software A/S');

              IF NOT SmtpMailMgt.Send THEN
                SmtpMailMgt.DisplayLastError;

              CLEAR(SmtpMailMgt);

              ContiniaUser2.GET(ContiniaUser."User ID");
              ContiniaUser2."Send Welcome E-mail (EM)" := FALSE;
              ContiniaUser2.MODIFY(TRUE);
              COMMIT;
            END;
        UNTIL ContiniaUser.NEXT = 0;

        IF GUIALLOWED THEN
          Window.CLOSE;
      END;
    END;

    PROCEDURE CalcProgress@161024017(VAR TotalCount@161024012 : Integer;VAR Index@161024013 : Integer) : Integer;
    BEGIN
      EXIT(ROUND(Index / TotalCount * 10000,1,'>'));
    END;

    PROCEDURE Bold@1160040000(text@1160040000 : Text[1024]) : Text[1024];
    BEGIN
      EXIT(STRSUBSTNO('<b>%1</b>',text));
    END;

    PROCEDURE Link@1160040001(text@1160040000 : Text[1024];link@1160040001 : Text[1024]) : Text[1024];
    BEGIN
      EXIT(STRSUBSTNO('<a href=%2>%1</a>',text,link));
    END;

    PROCEDURE AppendToBody@1160040005(text@1160040000 : Text[1024]);
    BEGIN
      SmtpMailMgt.AppendToBody(text,TRUE);
      SmtpMailMgt.AppendLineBreakToBody;
    END;

    PROCEDURE AppendToBodyNobreak@1160040003(text@1160040000 : Text[1024]);
    BEGIN
      SmtpMailMgt.AppendToBody(text,TRUE);
    END;

    BEGIN
    END.
  }
}

