OBJECT Table 6085575 DC Document Category
{
  OBJECT-PROPERTIES
  {
    Date=09/05/18;
    Time=[ 1:00:00 AM];
    Version List=DCW13.70.00.4.50.07;
  }
  PROPERTIES
  {
    DataCaptionFields=Code,Description;
    OnInsert=BEGIN
               "Export PDF File" := "Export PDF File"::"Yes - Keep Existing Or Create New";
               "Document Category GUID" := CREATEGUID;
             END;

    OnDelete=VAR
               Document@1160040004 : Record 6085590;
               Template@1000000001 : Record 6085579;
               SourceExcl@1160040002 : Record 6085598;
               IdentField@1160040001 : Record 6085759;
               GroupLang@1000000000 : Record 6085576;
               RecIDMgt@1160040000 : Codeunit 6085604;
               CODC@1160040003 : Codeunit 6085617;
             BEGIN
               Document.SETRANGE("Document Category Code",Code);
               IF NOT Document.ISEMPTY THEN
                 ERROR(Text008,TABLECAPTION,Code);

               Template.SETRANGE("Category Code",Code);
               IF NOT Template.ISEMPTY THEN
                 ERROR(Text007,TABLECAPTION,Code);

               GroupLang.SETRANGE("Document Category Code",Code);
               GroupLang.DELETEALL(TRUE);

               SourceExcl.SETRANGE("Document Category Code",Code);
               SourceExcl.DELETEALL(TRUE);

               IdentField.SETRANGE("Document Category Code",Code);
               IdentField.DELETEALL(TRUE);

               RecIDMgt.DeleteTableFilter("Document Category GUID");

               CODC.DeleteCategory(Rec);
             END;

    OnRename=BEGIN
               ERROR(Text003,TABLECAPTION);
             END;

    CaptionML=ENU=Document Category;
    LookupPageID=Page6085582;
    DrillDownPageID=Page6085582;
  }
  FIELDS
  {
    { 1   ;   ;Code                ;Code10        ;CaptionML=ENU=Code;
                                                   NotBlank=Yes }
    { 2   ;   ;Description         ;Text50        ;CaptionML=ENU=Description }
    { 3   ;   ;Source Table No.    ;Integer       ;TableRelation=Object.ID WHERE (Type=CONST(Table));
                                                   OnValidate=BEGIN
                                                                CheckSourceTable;
                                                                IF "Split on Sep. Fields" AND ("Source Table No." > 0) THEN
                                                                  "Split on Source ID" := TRUE;
                                                              END;

                                                   CaptionML=ENU=Source Table No.;
                                                   BlankZero=Yes }
    { 4   ;   ;Source Table Name   ;Text30        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObjWithCaption."Object Caption" WHERE (Object Type=CONST(Table),
                                                                                                                Object ID=FIELD(Source Table No.)));
                                                   CaptionML=ENU=Source Table Name;
                                                   Editable=No }
    { 5   ;   ;Source Type         ;Option        ;CaptionML=ENU=Source Type;
                                                   OptionCaptionML=ENU=" ,Vendor,Contact,Customer,Job,Item,Fixed Asset,Employee";
                                                   OptionString=[ ,Vendor,Contact,Customer,Job,Item,Fixed Asset,Employee] }
    { 6   ;   ;Open Documents      ;Integer       ;CaptionML=ENU=Open Documents;
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 7   ;   ;Process New Files Automatic;Boolean;InitValue=Yes;
                                                   CaptionML=ENU=Process New Files Automatically }
    { 8   ;   ;No. of Templates    ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count(Template WHERE (Category Code=FIELD(Code)));
                                                   CaptionML=ENU=No. of Templates;
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 9   ;   ;No. of Documents    ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("DC Document" WHERE (Document Category Code=FIELD(Code)));
                                                   CaptionML=ENU=No. of Documents;
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 10  ;   ;Registered Documents;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("DC Document" WHERE (Document Category Code=FIELD(Code),
                                                                                          Status=CONST(Registered)));
                                                   CaptionML=ENU=Registered Documents;
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 11  ;   ;Rejected Documents  ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("DC Document" WHERE (Document Category Code=FIELD(Code),
                                                                                          Status=CONST(Rejected)));
                                                   CaptionML=ENU=Rejected Documents;
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 12  ;   ;Export PDF File     ;Option        ;InitValue=Yes - Keep Existing Or Create New;
                                                   CaptionML=ENU=Export PDF File;
                                                   OptionCaptionML=ENU=No,Yes - Keep Existing Or Create New,Yes - Always Create New;
                                                   OptionString=No,Yes - Keep Existing Or Create New,Yes - Always Create New }
    { 13  ;   ;Process Scanned Files;Boolean      ;InitValue=Yes;
                                                   CaptionML=ENU=Process Scanned Files }
    { 14  ;   ;Process PDF Files   ;Boolean       ;InitValue=Yes;
                                                   CaptionML=ENU=Process PDF Files }
    { 15  ;   ;PDF Format          ;Option        ;InitValue=PDF/A;
                                                   CaptionML=ENU=PDF Format;
                                                   OptionCaptionML=ENU=PDF,PDF/A;
                                                   OptionString=PDF,PDF/A }
    { 16  ;   ;TIFF Image Resolution;Integer      ;InitValue=300;
                                                   CaptionML=ENU=TIFF Image Resolution;
                                                   MinValue=0 }
    { 18  ;   ;Allow Deleting Documents;Boolean   ;CaptionML=ENU=Allow Deleting Documents }
    { 19  ;   ;Scanning Allowed    ;Boolean       ;CaptionML=ENU=Scanning Allowed }
    { 20  ;   ;E-Mail Connection Endpoint;Code10  ;TableRelation="Connection Endpoint";
                                                   OnValidate=VAR
                                                                ConnEndpoint@161024012 : Record 6085620;
                                                                DocCat@1160040000 : Record 6085575;
                                                              BEGIN
                                                                IF "E-Mail Connection Endpoint" = '' THEN
                                                                  EXIT;

                                                                DocCat.SETRANGE("E-Mail Connection Endpoint","E-Mail Connection Endpoint");
                                                                DocCat.SETFILTER(Code,'<>%1',Code);
                                                                IF NOT DocCat.ISEMPTY THEN
                                                                  ERROR(Text016,"E-Mail Connection Endpoint",FIELDCAPTION("E-Mail Connection Endpoint"),TABLECAPTION);

                                                                ConnEndpoint.GET("E-Mail Connection Endpoint");
                                                                IF ConnEndpoint.Protocol = ConnEndpoint.Protocol::IMAP THEN BEGIN
                                                                  ConnEndpoint.TESTFIELD("Protocol Port");
                                                                  ConnEndpoint.TESTFIELD("Server Address");
                                                                  ConnEndpoint.TESTFIELD(Username);
                                                                  ConnEndpoint.TESTFIELD(Password);
                                                                END;
                                                              END;

                                                   CaptionML=ENU=E-Mail Connection Endpoint }
    { 21  ;   ;Document with UIC   ;Option        ;OnValidate=BEGIN
                                                                TESTFIELD("Auto Move to Company",TRUE);
                                                              END;

                                                   CaptionML=ENU=Doc. with Unidentified Company;
                                                   OptionCaptionML=ENU=Import in active Company,Import as UIC document;
                                                   OptionString=Import in active Company,Import as UIC document }
    { 22  ;   ;No. of Documents with UIC;Integer  ;FieldClass=FlowField;
                                                   CalcFormula=Count("DC Document (UIC)" WHERE (Document Category Code=FIELD(Code),
                                                                                                Status=CONST(Open)));
                                                   CaptionML=ENU=Open UIC Documents;
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 24  ;   ;No. of Excluded Sources;Integer    ;FieldClass=FlowField;
                                                   CalcFormula=Count("Document Category Source Excl." WHERE (Document Category Code=FIELD(Code)));
                                                   CaptionML=ENU=No. of Excluded Sources;
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 25  ;   ;Allow Drag-and-Drop ;Boolean       ;CaptionML=ENU=Allow Drag-and-Drop }
    { 26  ;   ;Source Field No.    ;Integer       ;TableRelation=Field.No. WHERE (TableNo=FIELD(Source Table No.));
                                                   OnValidate=VAR
                                                                RecIDMgt@1160040000 : Codeunit 6085604;
                                                              BEGIN
                                                                IF "Source Field No." = xRec."Source Field No." THEN
                                                                  EXIT;

                                                                RecIDMgt.CheckDocument(Code,FIELDCAPTION("Source Field No."));

                                                                IF NOT RecIDMgt.PartOfKey("Source Table No.","Source Field No.") THEN
                                                                  ERROR(Text013,FIELDCAPTION("Source Field No."),FIELDCAPTION("Source Table No."));
                                                              END;

                                                   CaptionML=ENU=Source Field No.;
                                                   BlankZero=Yes }
    { 27  ;   ;Source Field No. (Name);Integer    ;TableRelation=Field.No. WHERE (TableNo=FIELD(Source Table No.));
                                                   OnValidate=VAR
                                                                RecIDMgt@1160040000 : Codeunit 6085604;
                                                              BEGIN
                                                                IF "Source Field No. (Name)" = 0 THEN
                                                                  "Source Field No. (Name)" := "Source Field No.";
                                                              END;

                                                   CaptionML=ENU=Source Field No. (Name);
                                                   BlankZero=Yes }
    { 28  ;   ;No. of Source Table Filters;Integer;FieldClass=FlowField;
                                                   CalcFormula=Count("Table Filter Field" WHERE (Table Filter GUID=FIELD(Document Category GUID)));
                                                   CaptionML=ENU=No. of Source Table Filters;
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 29  ;   ;Destination Header Table No.;Integer;
                                                   TableRelation=Object.ID WHERE (Type=CONST(Table));
                                                   OnValidate=VAR
                                                                Template@161024013 : Record 6085579;
                                                                TemplField@161024012 : Record 6085580;
                                                                RecIDMgt@1160040000 : Codeunit 6085604;
                                                              BEGIN
                                                                IF "Destination Header Table No." = xRec."Destination Header Table No." THEN
                                                                  EXIT;

                                                                Template.SETRANGE("Category Code",Code);
                                                                IF Template.FINDSET THEN
                                                                  REPEAT
                                                                    TemplField.SETRANGE("Template No.",Template."No.");
                                                                    TemplField.SETFILTER("Destination Header Field No.",'<>%1',0);
                                                                    IF NOT TemplField.ISEMPTY THEN
                                                                      ERROR(Text015,FIELDCAPTION("Destination Header Table No."),
                                                                        TemplField.TABLECAPTION,TemplField.FIELDCAPTION(TemplField."Destination Header Field No."));
                                                                  UNTIL Template.NEXT = 0;
                                                              END;

                                                   CaptionML=ENU=Destination Header Table No.;
                                                   BlankZero=Yes }
    { 30  ;   ;Destination Line Table No.;Integer ;TableRelation=Object.ID WHERE (Type=CONST(Table));
                                                   OnValidate=VAR
                                                                Template@161024013 : Record 6085579;
                                                                TemplField@161024012 : Record 6085580;
                                                                RecIDMgt@1160040000 : Codeunit 6085604;
                                                              BEGIN
                                                                IF "Destination Line Table No." = xRec."Destination Line Table No." THEN
                                                                  EXIT;

                                                                Template.SETRANGE("Category Code",Code);
                                                                IF Template.FINDSET THEN
                                                                  REPEAT
                                                                    TemplField.SETRANGE("Template No.",Template."No.");
                                                                    TemplField.SETFILTER("Destination Line Field No.",'<>%1',0);
                                                                    IF NOT TemplField.ISEMPTY THEN
                                                                      ERROR(Text015,FIELDCAPTION("Destination Line Table No."),
                                                                        TemplField.TABLECAPTION,TemplField.FIELDCAPTION(TemplField."Destination Line Field No."));
                                                                  UNTIL Template.NEXT = 0;
                                                              END;

                                                   CaptionML=ENU=Destination Line Table No.;
                                                   BlankZero=Yes }
    { 31  ;   ;Archive E-mails on Document;Boolean;CaptionML=ENU=Archive E-mails on Document }
    { 32  ;   ;Source Search Field No.;Integer    ;TableRelation=Field.No. WHERE (TableNo=FIELD(Source Table No.));
                                                   OnValidate=VAR
                                                                RecIDMgt@1160040000 : Codeunit 6085604;
                                                              BEGIN
                                                                IF "Source Field No." = xRec."Source Field No." THEN
                                                                  EXIT;
                                                              END;

                                                   CaptionML=ENU=Source Search Field No.;
                                                   BlankZero=Yes }
    { 33  ;   ;TIFF Image Colour Mode;Option      ;CaptionML=ENU=TIFF Image Colour Mode;
                                                   OptionCaptionML=ENU=Black & White,Gray,Colour;
                                                   OptionString=Black & White,Gray,Colour }
    { 50  ;   ;Codeunit ID: Reopen ;Integer       ;TableRelation=Object.ID WHERE (Type=CONST(Codeunit));
                                                   CaptionML=ENU=Codeunit ID: Reopen;
                                                   BlankZero=Yes }
    { 52  ;   ;No. of Identification Fields;Integer;
                                                   FieldClass=FlowField;
                                                   CalcFormula=Count("Document Category Ident. Field" WHERE (Document Category Code=FIELD(Code)));
                                                   CaptionML=ENU=No. of Identification Fields;
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 53  ;   ;Document Category GUID;GUID        ;CaptionML=ENU=Document Category GUID }
    { 54  ;   ;Register without Template;Boolean  ;CaptionML=ENU=Register without Template }
    { 55  ;   ;Register Documents Automatic.;Boolean;
                                                   CaptionML=ENU=Register Documents Automatically }
    { 56  ;   ;Codeunit ID: Show Reg. Doc.;Integer;TableRelation=Object.ID WHERE (Type=CONST(Codeunit));
                                                   CaptionML=ENU=Codeunit ID: Show Reg. Doc.;
                                                   BlankZero=Yes }
    { 58  ;   ;Codeunit ID: Get Doc. Status;Integer;
                                                   TableRelation=Object.ID WHERE (Type=CONST(Codeunit));
                                                   CaptionML=ENU=Codeunit ID: Get Doc. Status;
                                                   BlankZero=Yes }
    { 60  ;   ;Documents for OCR   ;Integer       ;CaptionML=ENU=Documents for OCR;
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 61  ;   ;Documents for Import;Integer       ;CaptionML=ENU=Documents for Import;
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 62  ;   ;Documents with Error;Integer       ;CaptionML=ENU=Documents with Error;
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 64  ;   ;Split on Barcode    ;Boolean       ;CaptionML=ENU=Split on Barcode }
    { 65  ;   ;Split Barcode Text  ;Text250       ;CaptionML=ENU=Split on Barcode Text }
    { 66  ;   ;Split on Blank Page ;Boolean       ;CaptionML=ENU=Split on Blank Page }
    { 67  ;   ;Split on Source ID  ;Boolean       ;OnValidate=BEGIN
                                                                IF "Split on Sep. Fields" AND ("Source Table No." > 0) THEN
                                                                  TESTFIELD("Split on Source ID",TRUE);
                                                              END;

                                                   CaptionML=ENU=Split on Source ID }
    { 68  ;   ;Auto Move to Company;Boolean       ;CaptionML=ENU=Auto Move to Company }
    { 70  ;   ;Codeunit ID: Get File Count;Integer;TableRelation=Object.ID WHERE (Type=CONST(Codeunit));
                                                   CaptionML=ENU=Codeunit ID: Get File Count;
                                                   BlankZero=Yes }
    { 72  ;   ;Codeunit ID: Doc. or File;Integer  ;TableRelation=Object.ID WHERE (Type=CONST(Codeunit));
                                                   CaptionML=ENU=Codeunit ID: Show Document or File;
                                                   BlankZero=Yes }
    { 74  ;   ;Codeunit ID: Import Files;Integer  ;TableRelation=Object.ID WHERE (Type=CONST(Codeunit));
                                                   CaptionML=ENU=Codeunit ID: Import Files;
                                                   BlankZero=Yes }
    { 76  ;   ;Split on Sep. Fields;Boolean       ;OnValidate=BEGIN
                                                                IF "Source Table No." > 0 THEN
                                                                  "Split on Source ID" := TRUE;
                                                              END;

                                                   CaptionML=ENU=Split on Separator Fields }
    { 77  ;   ;Archive Documents in Cloud OCR;Option;
                                                   CaptionML=ENU=Archive Documents in Cloud OCR;
                                                   OptionCaptionML=ENU=No,One Day,One Week,One Month,One Year,Permanent;
                                                   OptionString=No,One Day,One Week,One Month,One Year,Permanent }
    { 78  ;   ;Max no. of Pages to Process;Integer;InitValue=200;
                                                   CaptionML=ENU=Max. number of pages to process per file }
    { 79  ;   ;OCR Export Codepage ;Integer       ;InitValue=1252;
                                                   CaptionML=ENU=OCR Export Codepage }
  }
  KEYS
  {
    {    ;Code                                    ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text002@1000000001 : TextConst 'ENU=There are no open documents in this group.';
      Text003@1160040005 : TextConst 'ENU=You cannot rename a %1.;ENC=You cannot rename a %1.';
      Text006@1000000005 : TextConst 'ENU=No Document Separators can be created on a %1.\\Documents placed in a %1 will automatically be distributed to the correct categories, based on the Document Separator patterns of the individual categories.';
      Text007@1000000006 : TextConst 'ENU=You cannot delete %1 %2 because there is at least one template for this category.';
      Text008@1000000007 : TextConst 'ENU=You cannot delete %1 %2 because there is at least one document for this category.';
      Text010@161024013 : TextConst 'ENU=%1 - %2';
      Text012@161024015 : TextConst 'ENU=Please delete all identification fields before you change %1.';
      Text013@1160040001 : TextConst 'ENU=The %1 is not part of the primary key of the %2.';
      Text014@1160040002 : TextConst 'ENU=The %1 cannot be changed because there is at least one template with a value for this category.';
      Text015@161024016 : TextConst 'ENU=The %1 cannot be changed because it has one or more %2 with %3 assigned.';
      Text016@1160040003 : TextConst 'ENU=%1 is already used as %2 in another %3.';
      ForceUpdate@1160040004 : Boolean;
      Status@1160040000 : 'Files for OCR,Files for Import,Files with Error,Open Documents,Registered Documents,Rejected Documents,UIC Documents';

    PROCEDURE RegisterOpenDoc@1000000002();
    VAR
      Document@1000000000 : Record 6085590;
    BEGIN
      Document.SETCURRENTKEY(Status);
      Document.SETRANGE(Status,Document.Status::Open);
      Document.SETRANGE("Document Category Code",Code);
      IF Document.ISEMPTY THEN
        ERROR(Text002);

      IF Document.FINDSET THEN
        REPEAT
          Document.MARK(TRUE);
        UNTIL Document.NEXT = 0;

      Document.FINDFIRST;
      Document.SETRANGE("Document Category Code");
      Document.MARKEDONLY(TRUE);
      PAGE.RUN(PAGE::"Document Card",Document);
    END;

    PROCEDURE CheckSourceTable@1160040000();
    VAR
      Template@1160040004 : Record 6085579;
      IdentField@1160040006 : Record 6085759;
      TableFilterField@1160040001 : Record 6085762;
      DCMgt@1160040003 : Codeunit 6085603;
      RecIDMgt@1160040000 : Codeunit 6085604;
    BEGIN
      IF "Source Table No." > 0 THEN
        DCMgt.IsLogActive("Source Table No.",TRUE);

      IF "Source Table No." = xRec."Source Table No." THEN
        EXIT;

      RecIDMgt.CheckDocument(Code,FIELDCAPTION("Source Table No."));

      Template.SETRANGE("Category Code",Code);
      Template.SETFILTER("Source Record ID Tree ID",'<>%1',0);
      IF NOT Template.ISEMPTY THEN
        ERROR(Text014,FIELDCAPTION("Source Table No."));

      IdentField.SETRANGE("Document Category Code",Code);
      IF NOT IdentField.ISEMPTY THEN
        ERROR(Text012);

      RecIDMgt.DeleteTableFilter("Document Category GUID");
      CLEAR("Document Category GUID");
      "Source Field No." := RecIDMgt.GetFirstKeyField("Source Table No.");
      "Source Field No. (Name)" := "Source Field No.";
      MODIFY;
    END;

    PROCEDURE UpdateCounters@1160040001();
    BEGIN
      IF "Codeunit ID: Get File Count" = 0 THEN
        CODEUNIT.RUN(CODEUNIT::"Update DC Doc. Counters",Rec)
      ELSE
        CODEUNIT.RUN("Codeunit ID: Get File Count",Rec)
    END;

    PROCEDURE GetCategoryPath@1160040003(FileType@1160040003 : 'Obsolete,PDFForOCR,ImportFiles,ErrorFiles') Path : Text[1024];
    VAR
      ContiniaCompanySetup@6085573 : Record 6192773;
      DCSetup@1160040000 : Record 6085573;
      FileSysMgt@6085574 : Codeunit 6085584;
    BEGIN
      ContiniaCompanySetup.GET;
      DCSetup.GET;

      IF ContiniaCompanySetup."Company Code" <> '' THEN
        Path := ContiniaCompanySetup."Company Code" + ' - ' + Code
      ELSE
        Path := Code;

      CASE FileType OF
        FileType::Obsolete:
          ERROR('This option is obsolete');
        FileType::PDFForOCR:
          Path :=  DCSetup."PDF File Path for OCR" + Path;
        FileType::ImportFiles:
          IF DCSetup."Use Cloud OCR" THEN
            Path := TEMPORARYPATH + Path
          ELSE
            Path := DCSetup."File Path for OCR-proc. files" + Path;
        FileType::ErrorFiles:
          Path := DCSetup."PDF File Path for OCR" + Path + '\ERROR';
      END;

      IF (NOT FileSysMgt.DirectoryExists(Path)) THEN
        FileSysMgt.CreateDirectory(Path);
    END;

    PROCEDURE UploadFilesForOCR@1160040004();
    VAR
      CODC@1160040000 : Codeunit 6085617;
      DocCat@1160040001 : Record 6085575;
    BEGIN
      IF CODC.IsCloudActive(FALSE) THEN BEGIN
        DocCat := Rec;
        CODC.UploadPdfDocsInCategory(DocCat);
      END;
    END;

    PROCEDURE UploadFilesInFolderForOCR@6085574(FolderPath@6085573 : Text[1024]);
    VAR
      CODC@1160040000 : Codeunit 6085617;
      DocCat@1160040001 : Record 6085575;
    BEGIN
      IF CODC.IsCloudActive(FALSE) THEN BEGIN
        DocCat := Rec;
        CODC.UploadPdfDocsInFolder(DocCat,FolderPath);
      END;
    END;

    LOCAL PROCEDURE OpenDocumentOrFile@1160040006(_Status@1160040000 : 'Files for OCR,Files for Import,Files with Error,Open Documents,Registered Documents,Rejected Documents,UIC Documents');
    VAR
      ShowDocAndFilesArgsTemp@1160040001 : Record 6085636;
    BEGIN
      ShowDocAndFilesArgsTemp."Document Category Code" := Code;
      ShowDocAndFilesArgsTemp.Status := _Status;

      IF "Codeunit ID: Doc. or File" = 0 THEN
        CODEUNIT.RUN(CODEUNIT::"Open Document or File",ShowDocAndFilesArgsTemp)
      ELSE
        CODEUNIT.RUN("Codeunit ID: Doc. or File",ShowDocAndFilesArgsTemp);
    END;

    PROCEDURE OpenFilesForOCR@1160040002();
    BEGIN
      OpenDocumentOrFile(Status::"Files for OCR");
    END;

    PROCEDURE OpenFilesForImport@1160040005();
    BEGIN
      OpenDocumentOrFile(Status::"Files for Import");
    END;

    PROCEDURE OpenFilesWithError@1160040007();
    BEGIN
      OpenDocumentOrFile(Status::"Files with Error");
    END;

    PROCEDURE OpenOpenDocuments@1160040008();
    BEGIN
      OpenDocumentOrFile(Status::"Open Documents");
    END;

    PROCEDURE OpenRegisteredDocuments@1160040009();
    BEGIN
      OpenDocumentOrFile(Status::"Registered Documents");
    END;

    PROCEDURE OpenRejectedDocuments@1160040010();
    BEGIN
      OpenDocumentOrFile(Status::"Rejected Documents");
    END;

    PROCEDURE OpenUICDocuments@1160040011();
    BEGIN
      OpenDocumentOrFile(Status::"UIC Documents");
    END;

    BEGIN
    END.
  }
}

