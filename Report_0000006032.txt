OBJECT Report 6032 Post Prepaid Contract Entries
{
  OBJECT-PROPERTIES
  {
    Date=09/19/18;
    Time=12:00:00 PM;
    Version List=NAVW113.00;
  }
  PROPERTIES
  {
    Permissions=TableData 5907=rm;
    CaptionML=[ENU=Post Prepaid Service Contract Entries;
               ESM=Registrar movimientos de contrato de servicio de prepago;
               FRC=Reporter les Çcritures de contrat de service prÇpayÇes;
               ENC=Post Prepaid Service Contract Entries];
    ApplicationArea=#Service;
    ProcessingOnly=Yes;
    OnInitReport=BEGIN
                   PostingDate := WORKDATE;
                   CLEAR(GenJnlPostLine);
                 END;

    OnPostReport=BEGIN
                   IF PostPrepaidContracts = PostPrepaidContracts::"Post Prepaid Transactions" THEN
                     Window.CLOSE;
                 END;

    UsageCategory=Tasks;
  }
  DATASET
  {
    { 1141;    ;DataItem;                    ;
               DataItemTable=Table5907;
               DataItemTableView=SORTING(Service Contract No.)
                                 WHERE(Type=CONST(Service Contract),
                                       Moved from Prepaid Acc.=CONST(No),
                                       Open=CONST(No));
               OnPreDataItem=BEGIN
                               IF PostPrepaidContracts = PostPrepaidContracts::"Print Only" THEN BEGIN
                                 CLEAR(PrepaidContractEntriesTest);
                                 PrepaidContractEntriesTest.InitVariables(UntilDate,PostingDate);
                                 PrepaidContractEntriesTest.SETTABLEVIEW("Service Ledger Entry");
                                 PrepaidContractEntriesTest.RUNMODAL;
                                 CurrReport.BREAK;
                               END;

                               IF PostPrepaidContracts = PostPrepaidContracts::"Post Prepaid Transactions" THEN BEGIN
                                 ServContractHdr.SETFILTER("Contract No.",GETFILTER("Service Contract No."));
                                 IF ServContractHdr.FIND('-') THEN BEGIN
                                   REPEAT
                                     ServContractHdr.CALCFIELDS("No. of Unposted Credit Memos");
                                     IF ServContractHdr."No. of Unposted Credit Memos" <> 0 THEN
                                       ERROR(Text005,Text007,Text008,ServContractHdr."Contract No.",Text006);
                                   UNTIL ServContractHdr.NEXT = 0;
                                 END;
                               END;

                               LastContract := '';
                               IF UntilDate = 0D THEN
                                 ERROR(Text000);
                               IF PostingDate = 0D THEN
                                 ERROR(Text001);

                               SETRANGE("Posting Date",0D,UntilDate);

                               NoOfContracts := COUNT;

                               Window.OPEN(
                                 Text002 +
                                 Text003 +
                                 '@2@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@');

                               ServMgtSetup.GET;
                               ServMgtSetup.TESTFIELD("Prepaid Posting Document Nos.");
                               SourceCodeSetup.GET;
                               SourceCodeSetup.TESTFIELD("Service Management");
                               SalesSetup.GET;
                             END;

               OnAfterGetRecord=BEGIN
                                  Counter := Counter + 1;
                                  Window.UPDATE(1,"Service Contract No.");
                                  Window.UPDATE(2,ROUND(Counter / NoOfContracts * 10000,1));

                                  ServLedgEntry.GET("Entry No.");
                                  ServLedgEntry."Moved from Prepaid Acc." := TRUE;
                                  ServLedgEntry.MODIFY;

                                  IF NOT (LastContract IN ['',"Service Contract No."]) THEN BEGIN
                                    TempServLedgEntry.RESET;
                                    TempServLedgEntry.SETRANGE("Service Contract No.",LastContract);
                                    TempServLedgEntry.CALCSUMS("Amount (LCY)");
                                    IF TempServLedgEntry."Amount (LCY)" <> 0 THEN
                                      PostGenJnlLine
                                    ELSE
                                      TempServLedgEntry.DELETEALL;
                                    TempServLedgEntry.SETRANGE("Service Contract No.","Service Contract No.");
                                  END;

                                  LastContract := "Service Contract No.";

                                  IF SalesSetup."Discount Posting" IN
                                     [SalesSetup."Discount Posting"::"Line Discounts",SalesSetup."Discount Posting"::"All Discounts"]
                                  THEN
                                    AmtInclDisc := ROUND(("Amount (LCY)" / (1 - ("Discount %" / 100))))
                                  ELSE
                                    AmtInclDisc := "Amount (LCY)";

                                  TempServLedgEntry.SETRANGE("Dimension Set ID","Dimension Set ID");
                                  IF TempServLedgEntry.FINDFIRST THEN BEGIN
                                    TempServLedgEntry."Amount (LCY)" += AmtInclDisc;
                                    TempServLedgEntry.MODIFY;
                                  END ELSE BEGIN
                                    TempServLedgEntry := "Service Ledger Entry";
                                    TempServLedgEntry."Amount (LCY)" := AmtInclDisc;
                                    TempServLedgEntry.INSERT;
                                  END;
                                END;

               OnPostDataItem=VAR
                                UpdateAnalysisView@1000 : Codeunit 410;
                              BEGIN
                                IF PostPrepaidContracts = PostPrepaidContracts::"Post Prepaid Transactions" THEN BEGIN
                                  TempServLedgEntry.SETRANGE("Dimension Set ID");
                                  TempServLedgEntry.CALCSUMS("Amount (LCY)");
                                  IF TempServLedgEntry."Amount (LCY)" <> 0 THEN BEGIN
                                    PostGenJnlLine;
                                    UpdateAnalysisView.UpdateAll(0,TRUE);
                                  END;
                                END;
                              END;

               ReqFilterFields=Service Contract No. }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[ENU=Options;
                             ESM=Opciones;
                             FRC=Options;
                             ENC=Options] }

      { 2   ;2   ;Field     ;
                  CaptionML=[ENU=Post until Date;
                             ESM=Registra hasta fecha;
                             FRC=Derniäre date de report;
                             ENC=Post until Date];
                  ToolTipML=[ENU=Specifies the date up to which you want to post prepaid entries. The batch job includes service ledger entries with posting dates on or before this date.;
                             ESM=Especifica la fecha hasta la que desea registrar movimientos de prepago. El proceso incluye movimientos de servicio registrados con esta fecha o una anterior.;
                             FRC=SpÇcifie la date jusqu'Ö laquelle vous souhaitez reporter les Çcritures prÇpayÇes. Le traitement en lot inclut les Çcritures service dont les dates de report sont Çgales ou antÇrieures Ö cette date.;
                             ENC=Specifies the date up to which you want to post prepaid entries. The batch job includes service ledger entries with posting dates on or before this date.];
                  ApplicationArea=#Service;
                  SourceExpr=UntilDate;
                  MultiLine=Yes }

      { 4   ;2   ;Field     ;
                  CaptionML=[ENU=Posting Date;
                             ESM=Fecha registro;
                             FRC=Date de report;
                             ENC=Posting Date];
                  ToolTipML=[ENU=Specifies the date that you want to use as the posting date on the service ledger entries.;
                             ESM=Especifica la fecha que desea utilizar como fecha de registro de los movimientos de servicio.;
                             FRC=SpÇcifie la date Ö utiliser comme date de report dans les Çcritures service.;
                             ENC=Specifies the date that you want to use as the posting date on the service ledger entries.];
                  ApplicationArea=#Service;
                  SourceExpr=PostingDate;
                  MultiLine=Yes }

      { 5   ;2   ;Field     ;
                  CaptionML=[ENU=Action;
                             ESM=Acci¢n;
                             FRC=Action;
                             ENC=Action];
                  ToolTipML=[ENU=Specifies the desired action relating to prepaid contract entries.;
                             ESM=Especifica la acci¢n deseada relacionada con los movimientos de contrato de prepago.;
                             FRC=SpÇcifie l'action souhaitÇe associÇe aux Çcritures contrat prÇpayÇ.;
                             ENC=Specifies the desired action relating to prepaid contract entries.];
                  OptionCaptionML=[ENU=Post Prepaid Transactions,Print Only;
                                   ESM=Regis. transacciones prepag.,S¢lo imprimir;
                                   FRC=Reporter transactions prÇpayÇes,Imprimer seulement;
                                   ENC=Post Prepaid Transactions,Print Only];
                  ApplicationArea=#Service;
                  SourceExpr=PostPrepaidContracts }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=You must fill in the Post Until Date field.;ESM=Debe rellenar el campo Registra hasta fecha.;FRC=Vous devez remplir le champ Dern. date compta.;ENC=You must fill in the Post Until Date field.';
      Text001@1001 : TextConst 'ENU=You must fill in the Posting Date field.;ESM=Debe rellenar el campo Fecha registro.;FRC=Vous devez remplir le champ Date comptabilisation.;ENC=You must fill in the Posting Date field.';
      Text002@1002 : TextConst 'ENU=Posting prepaid contract entries...\;ESM=Registrando movs. contrato prepag....\;FRC=Report des Çcritures contrat prÇpayÇes...\;ENC=Posting prepaid contract entries...\';
      Text003@1003 : TextConst 'ENU=Service Contract: #1###############\\;ESM=Contrato de Servicio: #1###############\\;FRC=Contrat de service : #1###############\\;ENC=Service Contract: #1###############\\';
      Text004@1004 : TextConst 'ENU=Service Contract;ESM=Contrato de Servicio;FRC=Contrat de service;ENC=Service Contract';
      GenJnlLine@1005 : Record 81;
      ServLedgEntry@1006 : Record 5907;
      TempServLedgEntry@1011 : TEMPORARY Record 5907;
      ServContractAccGr@1028 : Record 5973;
      ServMgtSetup@1008 : Record 5911;
      SourceCodeSetup@1009 : Record 242;
      ServContractHdr@1029 : Record 5965;
      SalesSetup@1010 : Record 311;
      PrepaidContractEntriesTest@1007 : Report 5986;
      NoSeriesMgt@1012 : Codeunit 396;
      GenJnlPostLine@1014 : Codeunit 12;
      Window@1015 : Dialog;
      PostPrepaidContracts@1016 : 'Post Prepaid Transactions,Print Only';
      DocNo@1017 : Code[20];
      UntilDate@1019 : Date;
      PostingDate@1020 : Date;
      NoOfContracts@1021 : Integer;
      Counter@1022 : Integer;
      AmtInclDisc@1023 : Decimal;
      LastContract@1024 : Code[20];
      Text005@1030 : TextConst 'ENU=You cannot post %1 because %2 %3 has at least one %4 linked to it.;ESM=No puede registrar %1 porque %2 %3 tiene al menos un %4 vinculado.;FRC=Vous ne pouvez pas reporter %1, car au moins une %4 est associÇe Ö %2 %3.;ENC=You cannot post %1 because %2 %3 has at least one %4 linked to it.';
      Text006@1102601000 : TextConst 'ENU=Unposted Credit Memo;ESM=Nota crÇdito no registrada;FRC=Note de crÇdit non reportÇe;ENC=Unposted Credit Memo';
      Text007@1102601001 : TextConst 'ENU=Prepaid Contract Entries;ESM=Mov. contrato anticipo;FRC=êcritures de contrat prÇpayÇes;ENC=Prepaid Contract Entries';
      Text008@1102601002 : TextConst 'ENU=Service Contract;ESM=Contrato de Servicio;FRC=Contrat de service;ENC=Service Contract';

    LOCAL PROCEDURE PostGenJnlLine@1();
    BEGIN
      TempServLedgEntry.RESET;
      IF NOT TempServLedgEntry.FINDSET THEN
        EXIT;

      DocNo := NoSeriesMgt.GetNextNo(ServMgtSetup."Prepaid Posting Document Nos.",WORKDATE,TRUE);

      REPEAT
        GenJnlLine.RESET;
        GenJnlLine.INIT;
        GenJnlLine."Document No." := DocNo;
        GenJnlLine."Account Type" := GenJnlLine."Account Type"::"G/L Account";
        GenJnlLine."VAT Posting" := GenJnlLine."VAT Posting"::"Manual VAT Entry";
        ServContractAccGr.GET(TempServLedgEntry."Serv. Contract Acc. Gr. Code");
        ServContractAccGr.TESTFIELD("Non-Prepaid Contract Acc.");
        GenJnlLine.VALIDATE("Account No.",ServContractAccGr."Non-Prepaid Contract Acc.");
        GenJnlLine."Posting Date" := PostingDate;
        GenJnlLine.Description := Text004;
        GenJnlLine."External Document No." := TempServLedgEntry."Service Contract No.";
        GenJnlLine.VALIDATE(Amount,TempServLedgEntry."Amount (LCY)");
        GenJnlLine."Shortcut Dimension 1 Code" := TempServLedgEntry."Global Dimension 1 Code";
        GenJnlLine."Shortcut Dimension 2 Code" := TempServLedgEntry."Global Dimension 2 Code";
        GenJnlLine."Dimension Set ID" := TempServLedgEntry."Dimension Set ID";
        GenJnlLine."Source Code" := SourceCodeSetup."Service Management";
        GenJnlLine."System-Created Entry" := TRUE;
        GenJnlLine."Gen. Posting Type" := GenJnlLine."Gen. Posting Type"::" ";
        GenJnlLine."Gen. Bus. Posting Group" := '';
        GenJnlLine."Gen. Prod. Posting Group" := '';
        GenJnlLine."VAT Bus. Posting Group" := '';
        GenJnlLine."VAT Prod. Posting Group" := '';
        GenJnlPostLine.RUN(GenJnlLine);

        ServContractAccGr.GET(TempServLedgEntry."Serv. Contract Acc. Gr. Code");
        ServContractAccGr.TESTFIELD("Prepaid Contract Acc.");
        GenJnlLine.VALIDATE("Account No.",ServContractAccGr."Prepaid Contract Acc.");
        GenJnlLine.VALIDATE(Amount,-TempServLedgEntry."Amount (LCY)");
        GenJnlLine."Shortcut Dimension 1 Code" := TempServLedgEntry."Global Dimension 1 Code";
        GenJnlLine."Shortcut Dimension 2 Code" := TempServLedgEntry."Global Dimension 2 Code";
        GenJnlLine."Dimension Set ID" := TempServLedgEntry."Dimension Set ID";
        GenJnlLine."Gen. Posting Type" := GenJnlLine."Gen. Posting Type"::" ";
        GenJnlLine."Gen. Bus. Posting Group" := '';
        GenJnlLine."Gen. Prod. Posting Group" := '';
        GenJnlLine."VAT Bus. Posting Group" := '';
        GenJnlLine."VAT Prod. Posting Group" := '';
        GenJnlPostLine.RUN(GenJnlLine);
      UNTIL TempServLedgEntry.NEXT = 0;

      TempServLedgEntry.RESET;
      TempServLedgEntry.DELETEALL;
    END;

    PROCEDURE InitializeRequest@2(UntilDateFrom@1000 : Date;PostingDateFrom@1001 : Date;PostPrepaidContractsFrom@1002 : 'Post Prepaid Transactions,Print Only');
    BEGIN
      UntilDate := UntilDateFrom;
      PostingDate := PostingDateFrom;
      PostPrepaidContracts := PostPrepaidContractsFrom;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

