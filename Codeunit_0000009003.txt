OBJECT Codeunit 9003 Team Member Action Manager
{
  OBJECT-PROPERTIES
  {
    Date=09/19/18;
    Time=12:00:00 PM;
    Version List=NAVW113.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      TeamMemberErr@1000 : TextConst 'ENU=You are logged in as a Team Member role, so you cannot complete this task.;ESM=Ha iniciado sesi¢n con el rol de Miembro de equipo, por lo que no puede completar esta tarea.;FRC=Vous ˆtes connect‚ en tant que Membre de l''‚quipe, et par cons‚quent vous ne pouvez pas ex‚cuter cette tƒche.;ENC=You are logged in as a Team Member role, so you cannot complete this task.';
      TeamMemberLicenseTok@1001 : TextConst '@@@={Locked};ENU=d9a6391b-8970-4976-bd94-5f205007c8d8;ESM=d9a6391b-8970-4976-bd94-5f205007c8d8;FRC=d9a6391b-8970-4976-bd94-5f205007c8d8;ENC=d9a6391b-8970-4976-bd94-5f205007c8d8';
      UserPlanErr@1002 : TextConst 'ENU=You cannot have more than one user plan assigned to each user.;ESM=No puede tener m s de un plan de usuario asignado a cada usuario.;FRC=Vous ne pouvez pas avoir plusieurs plans utilisateur affect‚s … chaque utilisateur.;ENC=You cannot have more than one user plan assigned to each user.';

    [EventSubscriber(Table,36,OnBeforeInsertEvent)]
    LOCAL PROCEDURE OnBeforeSalesHeaderInsert@2(VAR Rec@1000 : Record 36;RunTrigger@1001 : Boolean);
    BEGIN
      CheckTeamMemberPermissionOnSalesHeaderTable(Rec);
    END;

    [EventSubscriber(Codeunit,80,OnBeforePostSalesDoc)]
    LOCAL PROCEDURE OnBeforeSalesDocPost@6(VAR Sender@1001 : Codeunit 80;VAR SalesHeader@1002 : Record 36;CommitIsSuppressed@1003 : Boolean;PreviewMode@1004 : Boolean);
    BEGIN
      // Team member is not allowed to invoice a sales document.
      IF IsCurrentUserAssignedTeamMemberPlan AND SalesHeader.Invoice THEN
        ERROR(TeamMemberErr);
    END;

    [EventSubscriber(Codeunit,90,OnBeforePostPurchaseDoc)]
    LOCAL PROCEDURE OnBeforePurchaseDocPost@7(VAR Sender@1001 : Codeunit 90;VAR PurchaseHeader@1002 : Record 38;PreviewMode@1003 : Boolean;CommitIsSupressed@1004 : Boolean);
    BEGIN
      // Team member is not allowed to invoice a purchase document.
      IF IsCurrentUserAssignedTeamMemberPlan AND PurchaseHeader.Invoice THEN
        ERROR(TeamMemberErr);
    END;

    LOCAL PROCEDURE CheckTeamMemberPermissionOnSalesHeaderTable@12(VAR SalesHeader@1001 : Record 36);
    BEGIN
      IF IsCurrentUserAssignedTeamMemberPlan AND (SalesHeader."Document Type" <> SalesHeader."Document Type"::Quote) THEN
        ERROR(TeamMemberErr);
    END;

    [EventSubscriber(Table,38,OnBeforeInsertEvent)]
    LOCAL PROCEDURE OnBeforePurchaseHeaderInsert@3(VAR Rec@1000 : Record 38;RunTrigger@1001 : Boolean);
    BEGIN
      CheckTeamMemberPermissionOnPurchaseHeaderTable(Rec);
    END;

    LOCAL PROCEDURE CheckTeamMemberPermissionOnPurchaseHeaderTable@4(VAR PurchaseHeader@1001 : Record 38);
    BEGIN
      IF IsCurrentUserAssignedTeamMemberPlan AND (PurchaseHeader."Document Type" <> PurchaseHeader."Document Type"::Quote) THEN
        ERROR(TeamMemberErr);
    END;

    LOCAL PROCEDURE IsCurrentUserAssignedTeamMemberPlan@5() : Boolean;
    VAR
      UserPlan@1001 : Record 9005;
    BEGIN
      UserPlan.SETRANGE("User Security ID",USERSECURITYID);
      IF UserPlan.COUNT > 1 THEN
        ERROR(UserPlanErr);

      UserPlan.SETRANGE("Plan ID",TeamMemberLicenseTok);
      EXIT(NOT UserPlan.ISEMPTY);
    END;

    BEGIN
    END.
  }
}

