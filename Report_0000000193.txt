OBJECT Report 193 Issue Finance Charge Memos
{
  OBJECT-PROPERTIES
  {
    Date=11/27/18;
    Time=11:12:51 PM;
    Version List=;
  }
  PROPERTIES
  {
    CaptionML=ENU=Issue Finance Charge Memos;
    ProcessingOnly=Yes;
  }
  DATASET
  {
    { 8733;    ;DataItem;                    ;
               DataItemTable=Table302;
               DataItemTableView=SORTING(No.);
               ReqFilterHeadingML=ENU=Finance Charge Memo;
               OnPreDataItem=BEGIN
                               IF ReplacePostingDate AND (PostingDateReq = 0D) THEN
                                 ERROR(Text000);
                               NoOfRecords := COUNT;
                               IF NoOfRecords = 1 THEN
                                 Window.OPEN(Text001)
                               ELSE BEGIN
                                 Window.OPEN(Text002);
                                 OldDateTime := CURRENTDATETIME;
                               END;
                             END;

               OnAfterGetRecord=BEGIN
                                  RecordNo := RecordNo + 1;
                                  CLEAR(FinChrgMemoIssue);
                                  FinChrgMemoIssue.Set("Finance Charge Memo Header",ReplacePostingDate,PostingDateReq);
                                  IF NoOfRecords = 1 THEN BEGIN
                                    FinChrgMemoIssue.RUN;
                                    MARK := FALSE;
                                  END ELSE BEGIN
                                    NewDateTime := CURRENTDATETIME;
                                    IF (NewDateTime - OldDateTime > 100) OR (NewDateTime < OldDateTime) THEN BEGIN
                                      NewProgress := ROUND(RecordNo / NoOfRecords * 100,1);
                                      IF NewProgress <> OldProgress THEN BEGIN
                                        Window.UPDATE(1,NewProgress * 100);
                                        OldProgress := NewProgress;
                                      END;
                                      OldDateTime := CURRENTDATETIME;
                                    END;
                                    MARK := NOT FinChrgMemoIssue.RUN;
                                  END;

                                  IF (PrintDoc <> PrintDoc::" ") AND NOT MARK THEN BEGIN
                                    FinChrgMemoIssue.GetIssuedFinChrgMemo(IssuedFinChrgMemoHeader);
                                    TempIssuedFinChrgMemoHeader := IssuedFinChrgMemoHeader;
                                    TempIssuedFinChrgMemoHeader.INSERT;
                                  END;
                                END;

               OnPostDataItem=VAR
                                IsHandled@1000 : Boolean;
                              BEGIN
                                Window.CLOSE;
                                COMMIT;
                                IF PrintDoc <> PrintDoc::" " THEN
                                  IF TempIssuedFinChrgMemoHeader.FINDSET THEN
                                    REPEAT
                                      IssuedFinChrgMemoHeader := TempIssuedFinChrgMemoHeader;
                                      IsHandled := FALSE;
                                      OnBeforePrintRecords(IssuedFinChrgMemoHeader,IsHandled);
                                      IF NOT IsHandled THEN BEGIN
                                        IssuedFinChrgMemoHeader.SETRECFILTER;
                                        IssuedFinChrgMemoHeader.PrintRecords(FALSE,PrintDoc = PrintDoc::Email,HideDialog);
                                      END;
                                    UNTIL TempIssuedFinChrgMemoHeader.NEXT = 0;
                                MARKEDONLY := TRUE;
                                IF FIND('-') THEN
                                  IF CONFIRM(Text003,TRUE) THEN
                                    PAGE.RUNMODAL(0,"Finance Charge Memo Header");
                              END;

               ReqFilterFields=No. }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=ENU=Options }

      { 3   ;2   ;Field     ;
                  Name=PrintDoc;
                  CaptionML=ENU=Print;
                  ToolTipML=ENU=Specifies if you want the program to print the finance charge memos when they are issued.;
                  ApplicationArea=#Suite;
                  SourceExpr=PrintDoc }

      { 2   ;2   ;Field     ;
                  CaptionML=ENU=Replace Posting Date;
                  ToolTipML=ENU=Specifies if you want to replace the finance charge memos' posting date with the date entered in the field below.;
                  ApplicationArea=#Suite;
                  SourceExpr=ReplacePostingDate }

      { 1   ;2   ;Field     ;
                  CaptionML=ENU=Posting Date;
                  ToolTipML=ENU=Specifies the posting date. If you place a check mark in the check box above, the program will use this date on all finance charge memos when you post.;
                  ApplicationArea=#Suite;
                  SourceExpr=PostingDateReq }

      { 4   ;2   ;Field     ;
                  Name=HideEmailDialog;
                  CaptionML=ENU=Hide Email Dialog;
                  ToolTipML=ENU=Specifies if you want to hide email dialog.;
                  ApplicationArea=#Suite;
                  SourceExpr=HideDialog }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Enter the posting date.';
      Text001@1001 : TextConst 'ENU=Issuing finance charge memo...';
      Text002@1002 : TextConst 'ENU=Issuing finance charge memos @1@@@@@@@@@@@@@';
      Text003@1003 : TextConst 'ENU=It was not possible to issue some of the selected finance charge memos.\Do you want to see these finance charge memos?';
      IssuedFinChrgMemoHeader@1005 : Record 304;
      TempIssuedFinChrgMemoHeader@1004 : TEMPORARY Record 304;
      FinChrgMemoIssue@1006 : Codeunit 395;
      Window@1007 : Dialog;
      NoOfRecords@1008 : Integer;
      RecordNo@1009 : Integer;
      NewProgress@1010 : Integer;
      OldProgress@1011 : Integer;
      NewDateTime@1012 : DateTime;
      OldDateTime@1013 : DateTime;
      PostingDateReq@1014 : Date;
      ReplacePostingDate@1015 : Boolean;
      PrintDoc@1016 : ' ,Print,Email';
      HideDialog@1017 : Boolean;

    [Integration]
    LOCAL PROCEDURE OnBeforePrintRecords@1(VAR IssuedFinChargeMemoHeader@1000 : Record 304;VAR IsHandled@1001 : Boolean);
    BEGIN
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

