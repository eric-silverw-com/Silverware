OBJECT Table 6086364 EM Reminder
{
  OBJECT-PROPERTIES
  {
    Date=04/27/17;
    Time=[ 1:00:00 AM];
    Version List=EMW16.00.10.2.60;
  }
  PROPERTIES
  {
    Permissions=TableData 6086002=r;
    CaptionML=ENU=EM Reminder;
  }
  FIELDS
  {
    { 1   ;   ;Table ID            ;Integer       ;CaptionML=ENU=Table ID }
    { 2   ;   ;Document Type       ;Option        ;CaptionML=ENU=Document Type;
                                                   OptionCaptionML=ENU=Budget,Settlement;
                                                   OptionString=Budget,Settlement }
    { 3   ;   ;Document No.        ;Code20        ;CaptionML=ENU=Document No. }
    { 4   ;   ;Doc. Ref. No.       ;Integer       ;CaptionML=ENU=Doc. Ref. No. }
    { 10  ;   ;Reminder Terms Code ;Code10        ;TableRelation="EM Reminder Terms";
                                                   CaptionML=ENU=Reminder Terms Code;
                                                   NotBlank=Yes }
    { 11  ;   ;No.                 ;Integer       ;CaptionML=ENU=No.;
                                                   MinValue=1;
                                                   NotBlank=Yes }
    { 12  ;   ;Reminder Sent       ;Date          ;CaptionML=ENU=Reminder Sent }
  }
  KEYS
  {
    {    ;Table ID,Document Type,Document No.,Doc. Ref. No.,Reminder Terms Code,No.;
                                                   Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {

    PROCEDURE CreateReminder@1160040000(TableID@1160040003 : Integer;DocumentType@1160040004 : Integer;DocumentNo@1160040005 : Code[10];DocRefNo@1160040000 : Integer;ContiniaUserID@1160040006 : Code[50]);
    VAR
      ContiniaUserSetup@1160040001 : Record 6086002;
      ReminderLevel@1160040002 : Record 6086318;
      EMReminder@1160040007 : Record 6086364;
    BEGIN
      IF NOT ContiniaUserSetup.GET(ContiniaUserID) THEN
        EXIT;

      EMReminder.SETCURRENTKEY("Table ID","Document Type","Document No.","Doc. Ref. No.");
      EMReminder.SETRANGE("Table ID",TableID);
      EMReminder.SETRANGE("Document Type",DocumentType);
      EMReminder.SETRANGE("Document No.",DocumentNo);
      EMReminder.SETRANGE("Doc. Ref. No.",DocRefNo);
      EMReminder.SETRANGE("Reminder Terms Code",ContiniaUserSetup."Expense Reminder Code");
      IF EMReminder.FINDLAST THEN;

      "Table ID" := TableID;
      "Document Type" := DocumentType;
      "Document No." := DocumentNo;
      "Doc. Ref. No." := DocRefNo;
      "Reminder Terms Code" := ContiniaUserSetup."Expense Reminder Code";
      "No." := EMReminder."No." + 1;
      "Reminder Sent" := TODAY;
      INSERT;

      ReminderLevel.SETRANGE("Reminder Terms Code",ContiniaUserSetup."Expense Reminder Code");
      ReminderLevel.SETRANGE("No.","No.");
      IF NOT ReminderLevel.FINDLAST THEN BEGIN
        ReminderLevel.SETRANGE("No.");
        IF NOT ReminderLevel.FINDLAST THEN;
      END;
    END;

    PROCEDURE ResetReminder@1160040001(TableID@1160040006 : Integer;DocumentType@1160040005 : Integer;DocumentNo@1160040004 : Code[10];DocRefNo@1160040003 : Integer;ContiniaUserID@1160040001 : Code[50]);
    VAR
      ContiniaUserSetup@1160040002 : Record 6086002;
    BEGIN
      IF NOT ContiniaUserSetup.GET(ContiniaUserID) THEN
        EXIT;

      SETRANGE("Table ID",TableID);
      SETRANGE("Document Type",DocumentType);
      SETRANGE("Document No.",DocumentNo);
      SETRANGE("Doc. Ref. No.",DocRefNo);
      SETRANGE("Reminder Terms Code",ContiniaUserSetup."Expense Reminder Code");
      IF FINDLAST THEN BEGIN
        "Reminder Sent" := TODAY;
        MODIFY(TRUE);
      END;
    END;

    PROCEDURE NextReminderDate@1160040003(ContiniaUserID@1160040004 : Code[50];TableID@1160040005 : Integer;DocumentType@1160040006 : Integer;DocumentNo@1160040007 : Code[10];DocRefNo@1160040008 : Integer;DateCreated@1160040009 : Date) : Date;
    VAR
      ContiniaUserSetup@1160040003 : Record 6086002;
      ReminderCode@1160040002 : Record 6086317;
      ReminderLevel@1160040001 : Record 6086318;
      EMReminder@1160040000 : Record 6086364;
    BEGIN
      IF NOT ContiniaUserSetup.GET(ContiniaUserID) THEN
        EXIT(0D);

      IF NOT ReminderCode.GET(ContiniaUserSetup."Expense Reminder Code") THEN
        EXIT(0D);

      IF DateCreated = 0D THEN
        EXIT(TODAY);

      EMReminder.SETRANGE("Table ID",TableID);
      EMReminder.SETRANGE("Document Type",DocumentType);
      EMReminder.SETRANGE("Document No.",DocumentNo);
      EMReminder.SETRANGE("Doc. Ref. No.",DocRefNo);
      EMReminder.SETRANGE("Reminder Terms Code",ReminderCode.Code);
      IF NOT EMReminder.FINDLAST THEN BEGIN
        EMReminder."Reminder Terms Code" := ReminderCode.Code;
        EMReminder."Reminder Sent" := DateCreated;
      END;

      IF ReminderLevel.GET(EMReminder."Reminder Terms Code",EMReminder."No." + 1) THEN
        EXIT(CALCDATE(ReminderLevel."Grace Period",EMReminder."Reminder Sent"))
      ELSE BEGIN
        ReminderLevel.SETRANGE("Reminder Terms Code",ContiniaUserSetup."Expense Reminder Code");
        IF ReminderLevel.FINDLAST THEN
          EXIT(CALCDATE(ReminderLevel."Grace Period",EMReminder."Reminder Sent"));
      END;
    END;

    BEGIN
    END.
  }
}

