OBJECT Codeunit 5055 CustVendBank-Update
{
  OBJECT-PROPERTIES
  {
    Date=09/19/18;
    Time=12:00:00 PM;
    Version List=NAVW113.00;
  }
  PROPERTIES
  {
    TableNo=5050;
    OnRun=VAR
            ContBusRel@1000 : Record 5054;
          BEGIN
            ContBusRel.SETRANGE("Contact No.","No.");
            ContBusRel.SETFILTER("Link to Table",'<>''''');

            IF ContBusRel.FIND('-') THEN
              REPEAT
                CASE ContBusRel."Link to Table" OF
                  ContBusRel."Link to Table"::Customer:
                    UpdateCustomer(Rec,ContBusRel);
                  ContBusRel."Link to Table"::Vendor:
                    UpdateVendor(Rec,ContBusRel);
                  ContBusRel."Link to Table"::"Bank Account":
                    UpdateBankAccount(Rec,ContBusRel);
                END;
              UNTIL ContBusRel.NEXT = 0;
          END;

  }
  CODE
  {
    VAR
      Cust@1001 : Record 18;
      Vend@1002 : Record 23;
      BankAcc@1003 : Record 270;
      NoSeries@1004 : Code[20];
      PurchaserCode@1005 : Code[20];
      OurContactCode@1006 : Code[20];

    [External]
    PROCEDURE UpdateCustomer@1(VAR Cont@1000 : Record 5050;VAR ContBusRel@1001 : Record 5054);
    VAR
      VATRegistrationLogMgt@1002 : Codeunit 249;
      VATRegNo@1003 : Text[20];
    BEGIN
      WITH Cust DO BEGIN
        GET(ContBusRel."No.");
        NoSeries := "No. Series";
        VATRegNo := "VAT Registration No.";
        TRANSFERFIELDS(Cont);
        "No." := ContBusRel."No.";
        "No. Series" := NoSeries;
        OnAfterUpdateCustomer(Cust,Cont);
        MODIFY;
        IF ("VAT Registration No." <> '') AND ("VAT Registration No." <> VATRegNo) THEN
          VATRegistrationLogMgt.LogCustomer(Cust);
      END;
    END;

    [External]
    PROCEDURE UpdateVendor@2(VAR Cont@1000 : Record 5050;VAR ContBusRel@1001 : Record 5054);
    VAR
      VATRegistrationLogMgt@1002 : Codeunit 249;
      VATRegNo@1003 : Text[20];
    BEGIN
      WITH Vend DO BEGIN
        GET(ContBusRel."No.");
        NoSeries := "No. Series";
        PurchaserCode := "Purchaser Code";
        VATRegNo := "VAT Registration No.";
        TRANSFERFIELDS(Cont);
        "No." := ContBusRel."No.";
        "No. Series" := NoSeries;
        "Purchaser Code" := PurchaserCode;
        OnAfterUpdateVendor(Vend,Cont);
        MODIFY;
        IF ("VAT Registration No." <> '') AND ("VAT Registration No." <> VATRegNo) THEN
          VATRegistrationLogMgt.LogVendor(Vend);
      END;
    END;

    [External]
    PROCEDURE UpdateBankAccount@3(VAR Cont@1000 : Record 5050;VAR ContBusRel@1001 : Record 5054);
    BEGIN
      WITH BankAcc DO BEGIN
        GET(ContBusRel."No.");
        NoSeries := "No. Series";
        OurContactCode := "Our Contact Code";
        VALIDATE("Currency Code",Cont."Currency Code");
        TRANSFERFIELDS(Cont);
        "No." := ContBusRel."No.";
        "No. Series" := NoSeries;
        "Our Contact Code" := OurContactCode;
        OnAfterUpdateBankAccount(BankAcc,Cont);
        MODIFY;
      END;
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterUpdateCustomer@4(VAR Customer@1000 : Record 18;Contact@1001 : Record 5050);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterUpdateVendor@5(VAR Vendor@1000 : Record 23;Contact@1001 : Record 5050);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterUpdateBankAccount@6(VAR BankAccount@1000 : Record 270;Contact@1001 : Record 5050);
    BEGIN
    END;

    BEGIN
    END.
  }
}

