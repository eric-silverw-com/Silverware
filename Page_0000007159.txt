OBJECT Page 7159 Invt. Analysis by Dimensions
{
  OBJECT-PROPERTIES
  {
    Date=09/19/18;
    Time=12:00:00 PM;
    Version List=NAVW113.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Invt. Analysis by Dimensions;
               ESM=An lisis de inventario por dimensiones;
               FRC=Analyse des stocks par dimensions;
               ENC=Invt. Analysis by Dimensions];
    SaveValues=Yes;
    InsertAllowed=No;
    DeleteAllowed=No;
    ModifyAllowed=No;
    LinksAllowed=No;
    SourceTable=Table367;
    DataCaptionExpr=CurrentItemAnalysisViewCode;
    PageType=Card;
    OnInit=BEGIN
             Dim3FilterEnable := TRUE;
             Dim2FilterEnable := TRUE;
             Dim1FilterEnable := TRUE;
           END;

    OnOpenPage=BEGIN
                 CurrentAnalysisArea := CurrentAnalysisArea::Inventory;

                 GLSetup.GET;

                 ItemAnalysisMgt.AnalysisViewSelection(
                   CurrentAnalysisArea,CurrentItemAnalysisViewCode,ItemAnalysisView,ItemStatisticsBuffer,
                   Dim1Filter,Dim2Filter,Dim3Filter);

                 IF (NewItemAnalysisCode <> '') AND (NewItemAnalysisCode <> CurrentItemAnalysisViewCode) THEN BEGIN
                   CurrentItemAnalysisViewCode := NewItemAnalysisCode;
                   ItemAnalysisMgt.CheckAnalysisView(CurrentAnalysisArea,CurrentItemAnalysisViewCode,ItemAnalysisView);
                   ItemAnalysisMgt.SetItemAnalysisView(
                     CurrentAnalysisArea,CurrentItemAnalysisViewCode,ItemAnalysisView,ItemStatisticsBuffer,
                     Dim1Filter,Dim2Filter,Dim3Filter);
                 END;

                 ItemAnalysisMgt.SetLineAndColDim(
                   ItemAnalysisView,LineDimCode,LineDimOption,ColumnDimCode,ColumnDimOption);
                 UpdateFilterFields;

                 FindPeriod('');
                 MATRIX_GenerateColumnCaptions(MATRIX_Step::Initial);
               END;

    ActionList=ACTIONS
    {
      { 1900000003;0 ;ActionContainer;
                      ActionContainerType=RelatedInformation }
      { 58      ;1   ;ActionGroup;
                      CaptionML=[ENU=&Actions;
                                 ESM=&Acciones;
                                 FRC=&Actions;
                                 ENC=&Actions];
                      Image=Action }
      { 59      ;2   ;Action    ;
                      Name=ReverseLinesAndColumns;
                      CaptionML=[ENU=Reverse Lines and Columns;
                                 ESM=Invertir l¡neas y columnas;
                                 FRC=Inverser lignes et colonnes;
                                 ENC=Reverse Lines and Columns];
                      ToolTipML=[ENU=Change the display of the matrix by inverting the values in the Show as Lines and Show as Columns fields.;
                                 ESM=Cambia la visualizaci¢n de la matriz invirtiendo los valores de los campos Muestra como l¡neas y Muestra como columnas.;
                                 FRC=Modifiez l'affichage de la matrice en contrepassant les valeurs dans les champs Afficher lignes et Afficher colonnes.;
                                 ENC=Change the display of the matrix by inverting the values in the Show as Lines and Show as Columns fields.];
                      ApplicationArea=#Dimensions;
                      Image=Undo;
                      OnAction=VAR
                                 TempDimCode@1001 : Text[30];
                               BEGIN
                                 TempDimCode := ColumnDimCode;
                                 ColumnDimCode := LineDimCode;
                                 LineDimCode := TempDimCode;
                                 ItemAnalysisMgt.ValidateLineDimCode(
                                   ItemAnalysisView,LineDimCode,LineDimOption,ColumnDimOption,
                                   InternalDateFilter,DateFilter,ItemStatisticsBuffer,PeriodInitialized);
                                 ItemAnalysisMgt.ValidateColumnDimCode(
                                   ItemAnalysisView,ColumnDimCode,ColumnDimOption,LineDimOption,
                                   InternalDateFilter,DateFilter,ItemStatisticsBuffer,PeriodInitialized);
                               END;
                                }
      { 56      ;2   ;Action    ;
                      Name=ExportToExcel;
                      CaptionML=[ENU=Export to Excel;
                                 ESM=Exportar a Excel;
                                 FRC=Exporter dans Excel;
                                 ENC=Export to Excel];
                      ToolTipML=[ENU=Export the information in the analysis report to Excel.;
                                 ESM=Exporta la informaci¢n del informe de an lisis a Excel.;
                                 FRC=Exportez les informations du rapport d'analyse vers Excel.;
                                 ENC=Export the information in the analysis report to Excel.];
                      ApplicationArea=#Dimensions;
                      Image=ExportToExcel;
                      OnAction=VAR
                                 ItemAnalysisViewEntry@1000 : Record 7154;
                                 ItemAnalysisViewToExcel@1001 : Codeunit 7152;
                               BEGIN
                                 ItemAnalysisViewToExcel.SetCommonFilters(
                                   CurrentAnalysisArea,CurrentItemAnalysisViewCode,
                                   ItemAnalysisViewEntry,DateFilter,ItemFilter,Dim1Filter,Dim2Filter,Dim3Filter,LocationFilter);
                                 ItemAnalysisViewEntry.FINDFIRST;
                                 ItemAnalysisViewToExcel.ExportData(
                                   ItemAnalysisViewEntry,ShowColumnName,DateFilter,ItemFilter,BudgetFilter,
                                   Dim1Filter,Dim2Filter,Dim3Filter,ShowActualBudget,LocationFilter,ShowOppositeSign);
                               END;
                                }
      { 1900000004;0 ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 19      ;1   ;Action    ;
                      Name=ShowMatrix;
                      CaptionML=[ENU=&Show Matrix;
                                 ESM=&Mostrar matriz;
                                 FRC=&Afficher matrice;
                                 ENC=&Show Matrix];
                      ToolTipML=[ENU=View the actual analysis report according to the selected filters and options.;
                                 ESM=Permite ver el informe de an lisis real en funci¢n de los filtros y las opciones seleccionados.;
                                 FRC=Affichez le rapport d'analyse r‚el en fonction des filtres et options s‚lectionn‚s.;
                                 ENC=View the actual analysis report according to the selected filters and options.];
                      ApplicationArea=#Dimensions;
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=ShowMatrix;
                      PromotedCategory=Process;
                      OnAction=VAR
                                 MatrixForm@1129 : Page 9209;
                               BEGIN
                                 CLEAR(MatrixForm);
                                 MatrixForm.Load(MATRIX_CaptionSet,MATRIX_MatrixRecords,MATRIX_CurrentNoOfColumns,
                                   LineDimOption,ColumnDimOption,RoundingFactor,DateFilter,
                                   ValueType,ItemAnalysisView,CurrentItemAnalysisViewCode,
                                   ItemFilter,LocationFilter,BudgetFilter,Dim1Filter,Dim2Filter,Dim3Filter,ShowOppositeSign);
                                 MatrixForm.RUNMODAL;
                               END;
                                }
      { 1141    ;1   ;Action    ;
                      CaptionML=[ENU=Previous Set;
                                 ESM=Conjunto anterior;
                                 FRC=Jeu pr‚c‚dent;
                                 ENC=Previous Set];
                      ToolTipML=[ENU=Go to the previous set of data.;
                                 ESM=Va al conjunto de datos anterior.;
                                 FRC=Allez … l'ensemble de donn‚es pr‚c‚dent.;
                                 ENC=Go to the previous set of data.];
                      ApplicationArea=#Dimensions;
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=PreviousSet;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 MATRIX_GenerateColumnCaptions(MATRIX_Step::Previous);
                               END;
                                }
      { 1143    ;1   ;Action    ;
                      CaptionML=[ENU=Next Set;
                                 ESM=Conjunto siguiente;
                                 FRC=Jeu suivant;
                                 ENC=Next Set];
                      ToolTipML=[ENU=Go to the next set of data.;
                                 ESM=Va al siguiente conjunto de datos.;
                                 FRC=Allez … l'ensemble de donn‚es suivant.;
                                 ENC=Go to the next set of data.];
                      ApplicationArea=#Dimensions;
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=NextSet;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 MATRIX_GenerateColumnCaptions(MATRIX_Step::Next);
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 1   ;1   ;Group     ;
                CaptionML=[ENU=General;
                           ESM=General;
                           FRC=G‚n‚ral;
                           ENC=General] }

    { 5   ;2   ;Field     ;
                CaptionML=[ENU=Analysis View Code;
                           ESM=C¢digo de vista de an lisis;
                           FRC=Code de vue d'analyse;
                           ENC=Analysis View Code];
                ToolTipML=[ENU=Specifies the code for the analysis view that the filter belongs to.;
                           ESM=Especifica el c¢digo de la vista de an lisis a la que pertenece el filtro.;
                           FRC=Sp‚cifie le code de la vue d'analyse … laquelle le filtre appartient.;
                           ENC=Specifies the code for the analysis view that the filter belongs to.];
                ApplicationArea=#Dimensions;
                SourceExpr=CurrentItemAnalysisViewCode;
                OnValidate=BEGIN
                             ItemAnalysisMgt.CheckAnalysisView(CurrentAnalysisArea,CurrentItemAnalysisViewCode,ItemAnalysisView);
                             ItemAnalysisMgt.SetItemAnalysisView(
                               CurrentAnalysisArea,CurrentItemAnalysisViewCode,ItemAnalysisView,ItemStatisticsBuffer,
                               Dim1Filter,Dim2Filter,Dim3Filter);
                             ItemAnalysisMgt.SetLineAndColDim(
                               ItemAnalysisView,LineDimCode,LineDimOption,ColumnDimCode,ColumnDimOption);
                             UpdateFilterFields;
                             CurrPage.UPDATE(FALSE);
                           END;

                OnLookup=BEGIN
                           ItemAnalysisMgt.LookupItemAnalysisView(
                             CurrentAnalysisArea,CurrentItemAnalysisViewCode,ItemAnalysisView,ItemStatisticsBuffer,
                             Dim1Filter,Dim2Filter,Dim3Filter);
                           ItemAnalysisMgt.SetLineAndColDim(
                             ItemAnalysisView,LineDimCode,LineDimOption,ColumnDimCode,ColumnDimOption);
                           UpdateFilterFields;
                           CurrPage.UPDATE(FALSE);
                         END;
                          }

    { 6   ;2   ;Field     ;
                CaptionML=[ENU=Show as Lines;
                           ESM=Muestra como l¡neas;
                           FRC=Afficher lignes;
                           ENC=Show as Lines];
                ToolTipML=[ENU=Specifies which values you want to show as lines in the window. This allows you to see the same matrix window from various perspectives, especially when you use both the Show as Lines field and the Show as Columns field.;
                           ESM=Especifica los valores que desea mostrar como l¡neas en la ventana. Esto le permite ver la misma ventana de la matriz desde distintas perspectivas, especialmente cuando usa el campo Muestra como l¡neas y el campo Muestra como columnas.;
                           FRC=Sp‚cifie les valeurs … afficher en tant que lignes dans la fenˆtre. Cela vous permet d'afficher la mˆme fenˆtre de matrice de diff‚rentes fa‡ons, en particulier si vous utilisez … la fois le champ Afficher lignes et le champ Afficher colonnes.;
                           ENC=Specifies which values you want to show as lines in the window. This allows you to see the same matrix window from various perspectives, especially when you use both the Show as Lines field and the Show as Columns field.];
                ApplicationArea=#Dimensions;
                SourceExpr=LineDimCode;
                OnValidate=BEGIN
                             IF (UPPERCASE(LineDimCode) = UPPERCASE(ColumnDimCode)) AND (LineDimCode <> '') THEN BEGIN
                               ColumnDimCode := '';
                               ItemAnalysisMgt.ValidateColumnDimCode(
                                 ItemAnalysisView,ColumnDimCode,ColumnDimOption,LineDimOption,
                                 InternalDateFilter,DateFilter,ItemStatisticsBuffer,PeriodInitialized);
                             END;
                             ItemAnalysisMgt.ValidateLineDimCode(
                               ItemAnalysisView,LineDimCode,LineDimOption,ColumnDimOption,
                               InternalDateFilter,DateFilter,ItemStatisticsBuffer,PeriodInitialized);
                             IF LineDimOption = LineDimOption::Period THEN
                               SETCURRENTKEY("Period Start")
                             ELSE
                               SETCURRENTKEY(Code);
                             CurrPage.UPDATE(FALSE);
                           END;

                OnLookup=VAR
                           NewCode@1002 : Text[30];
                         BEGIN
                           NewCode := ItemAnalysisMgt.GetDimSelection(LineDimCode,ItemAnalysisView);
                           IF NewCode <> LineDimCode THEN BEGIN
                             Text := NewCode;
                             EXIT(TRUE);
                           END;
                         END;
                          }

    { 9   ;2   ;Field     ;
                CaptionML=[ENU=Show as Columns;
                           ESM=Muestra como columnas;
                           FRC=Afficher colonnes;
                           ENC=Show as Columns];
                ToolTipML=[ENU=Specifies which values you want to show as columns in the window. This allows you to see the same matrix window from various perspectives, especially when you use both the Show as Lines field and the Show as Columns field.;
                           ESM=Especifica los valores que desea mostrar como columnas en la ventana. Esto le permite ver la misma ventana de la matriz desde distintas perspectivas, especialmente cuando use el campo Muestra como l¡neas y el campo Muestra como columnas.;
                           FRC=Sp‚cifie les valeurs … afficher en tant que colonnes dans la fenˆtre. Cela vous permet d'afficher la mˆme fenˆtre de matrice de diff‚rentes fa‡ons, en particulier si vous utilisez … la fois le champ Afficher lignes et le champ Afficher colonnes.;
                           ENC=Specifies which values you want to show as columns in the window. This allows you to see the same matrix window from various perspectives, especially when you use both the Show as Lines field and the Show as Columns field.];
                ApplicationArea=#Dimensions;
                SourceExpr=ColumnDimCode;
                OnValidate=BEGIN
                             IF (UPPERCASE(LineDimCode) = UPPERCASE(ColumnDimCode)) AND (LineDimCode <> '') THEN BEGIN
                               LineDimCode := '';
                               ItemAnalysisMgt.ValidateLineDimCode(
                                 ItemAnalysisView,LineDimCode,LineDimOption,ColumnDimOption,
                                 InternalDateFilter,DateFilter,ItemStatisticsBuffer,PeriodInitialized);
                             END;
                             ItemAnalysisMgt.ValidateColumnDimCode(
                               ItemAnalysisView,ColumnDimCode,ColumnDimOption,LineDimOption,
                               InternalDateFilter,DateFilter,ItemStatisticsBuffer,PeriodInitialized);

                             MATRIX_GenerateColumnCaptions(MATRIX_Step::Initial);
                             CurrPage.UPDATE(FALSE);
                           END;

                OnLookup=VAR
                           NewCode@1002 : Text[30];
                         BEGIN
                           NewCode := ItemAnalysisMgt.GetDimSelection(ColumnDimCode,ItemAnalysisView);
                           IF NewCode <> ColumnDimCode THEN BEGIN
                             Text := NewCode;
                             MATRIX_GenerateColumnCaptions(MATRIX_Step::Initial);
                             EXIT(TRUE);
                           END;
                         END;
                          }

    { 47  ;2   ;Field     ;
                CaptionML=[ENU=Show Value As;
                           ESM=Mostrar valor como;
                           FRC=Afficher la valeur en tant que;
                           ENC=Show Value As];
                ToolTipML=[ENU=Specifies how data is shown in the analysis view.;
                           ESM=Especifica c¢mo se mostrar n los datos en la vista de an lisis.;
                           FRC=Sp‚cifie la fa‡on dont les donn‚es sont affich‚es dans la vue d'analyse.;
                           ENC=Specifies how data is shown in the analysis view.];
                OptionCaptionML=[ENU=Sales Amount,Inventory Value,Quantity;
                                 ESM=Importe venta,Valor inventario,Cantidad;
                                 FRC=Montant de vente,valeur en stock,Quantit‚;
                                 ENC=Sales Amount,Inventory Value,Quantity];
                ApplicationArea=#Dimensions;
                SourceExpr=ValueType }

    { 1907524401;1;Group  ;
                CaptionML=[ENU=Filters;
                           ESM=Filtros;
                           FRC=Filtres;
                           ENC=Filters] }

    { 36  ;2   ;Field     ;
                CaptionML=[ENU=Date Filter;
                           ESM=Filtro fecha;
                           FRC=Filtre date;
                           ENC=Date Filter];
                ToolTipML=[ENU=Specifies the dates that will be used to filter the amounts in the window.;
                           ESM=Especifica las fechas que se usar n para filtrar los importes en la ventana.;
                           FRC=Indique les dates qui sont utilis‚es pour filtrer les montants dans la fenˆtre.;
                           ENC=Specifies the dates that will be used to filter the amounts in the window.];
                ApplicationArea=#Dimensions;
                SourceExpr=DateFilter;
                OnValidate=VAR
                             TextManagement@1000 : Codeunit 41;
                           BEGIN
                             TextManagement.MakeDateFilter(DateFilter);
                             ItemStatisticsBuffer.SETFILTER("Date Filter",DateFilter);
                             DateFilter := ItemStatisticsBuffer.GETFILTER("Date Filter");
                             InternalDateFilter := DateFilter;

                             MATRIX_GenerateColumnCaptions(MATRIX_Step::Initial);
                             CurrPage.UPDATE;
                           END;
                            }

    { 38  ;2   ;Field     ;
                CaptionML=[ENU=Item Filter;
                           ESM=Filtro producto;
                           FRC=Filtre article;
                           ENC=Item Filter];
                ToolTipML=[ENU=Specifies a filter to specify the items for which values will be shown in the matrix window.;
                           ESM=Especifica un filtro para determinar los productos para los que se mostrar n valores en la ventana de la matriz.;
                           FRC=Sp‚cifie un filtre pour sp‚cifier les articles pour lesquels des valeurs seront affich‚es dans la fenˆtre de matrice.;
                           ENC=Specifies a filter to specify the items for which values will be shown in the matrix window.];
                ApplicationArea=#Dimensions;
                SourceExpr=ItemFilter;
                OnValidate=BEGIN
                             ItemStatisticsBuffer.SETFILTER("Item Filter",ItemFilter);
                             IF ColumnDimOption = ColumnDimOption::Item THEN
                               MATRIX_GenerateColumnCaptions(MATRIX_Step::Initial);
                             CurrPage.UPDATE(FALSE);
                           END;

                OnLookup=VAR
                           ItemList@1002 : Page 31;
                         BEGIN
                           ItemList.LOOKUPMODE(TRUE);
                           IF ItemList.RUNMODAL = ACTION::LookupOK THEN BEGIN
                             Text := ItemList.GetSelectionFilter;
                             EXIT(TRUE);
                           END;
                         END;
                          }

    { 13  ;2   ;Field     ;
                CaptionML=[ENU=Location Filter;
                           ESM=Filtro almac‚n;
                           FRC=Filtre emplacement;
                           ENC=Location Filter];
                ToolTipML=[ENU=Specifies the filter through which you want to analyze inventory entries.;
                           ESM=Especifica el filtro a trav‚s del cual desea analizar los movimientos de inventario.;
                           FRC=Sp‚cifie le filtre via lequel vous souhaitez analyser les ‚critures inventaire.;
                           ENC=Specifies the filter through which you want to analyse inventory entries.];
                ApplicationArea=#Dimensions;
                SourceExpr=LocationFilter;
                OnValidate=BEGIN
                             ItemStatisticsBuffer.SETFILTER("Location Filter",LocationFilter);
                             IF ColumnDimOption = ColumnDimOption::Location THEN
                               MATRIX_GenerateColumnCaptions(MATRIX_Step::Initial);
                             CurrPage.UPDATE(FALSE);
                           END;

                OnLookup=VAR
                           LocationList@1002 : Page 15;
                         BEGIN
                           LocationList.LOOKUPMODE(TRUE);
                           IF LocationList.RUNMODAL = ACTION::LookupOK THEN BEGIN
                             Text := LocationList.GetSelectionFilter;
                             EXIT(TRUE);
                           END;
                         END;
                          }

    { 17  ;2   ;Field     ;
                CaptionML=[ENU=Budget Filter;
                           ESM=Filtro presupuesto;
                           FRC=Filtre budget;
                           ENC=Budget Filter];
                ToolTipML=[ENU=Specifies the budget that information in the matrix is shown for.;
                           ESM=Especifica el presupuesto para el que se muestra la informaci¢n en la matriz.;
                           FRC=Sp‚cifie le budget pour lequel des informations sont affich‚es dans la matrice.;
                           ENC=Specifies the budget that information in the matrix is shown for.];
                ApplicationArea=#Dimensions;
                SourceExpr=BudgetFilter;
                OnValidate=BEGIN
                             ItemStatisticsBuffer.SETFILTER("Budget Filter",BudgetFilter);
                             CurrPage.UPDATE(FALSE);
                           END;

                OnLookup=VAR
                           ItemBudgetName@1000 : Record 7132;
                         BEGIN
                           ItemBudgetName.FILTERGROUP := 2;
                           ItemBudgetName.SETRANGE("Analysis Area",CurrentAnalysisArea);
                           ItemBudgetName.FILTERGROUP := 0;
                           IF PAGE.RUNMODAL(0,ItemBudgetName) = ACTION::LookupOK THEN BEGIN
                             Text := ItemBudgetName.Name;
                             EXIT(TRUE);
                           END;
                         END;
                          }

    { 34  ;2   ;Field     ;
                Name=Dim1Filter;
                CaptionML=[ENU=Dimension 1 Filter;
                           ESM=Filtro dimensi¢n 1;
                           FRC=Filtre de dimension 1;
                           ENC=Dimension 1 Filter];
                ToolTipML=[ENU=Specifies a filter for the Dimension 1 for which entries will be shown in the matrix window.;
                           ESM=Especifica un filtro para la Dimensi¢n 1 para la que se mostrar n movimientos en la ventana de la matriz.;
                           FRC=Sp‚cifie un filtre pour la dimensionÿ1 pour laquelle des entr‚es seront affich‚es dans la fenˆtre de matrice.;
                           ENC=Specifies a filter for the Dimension 1 for which entries will be shown in the matrix window.];
                ApplicationArea=#Dimensions;
                SourceExpr=Dim1Filter;
                CaptionClass=ItemAnalysisMgt.GetCaptionClass(1,ItemAnalysisView);
                Enabled=Dim1FilterEnable;
                OnValidate=BEGIN
                             ItemStatisticsBuffer.SETFILTER("Dimension 1 Filter",Dim1Filter);
                             IF ColumnDimOption = ColumnDimOption::"Dimension 1" THEN
                               MATRIX_GenerateColumnCaptions(MATRIX_Step::Initial);
                             CurrPage.UPDATE;
                           END;

                OnLookup=BEGIN
                           EXIT(ItemAnalysisMgt.LookUpDimFilter(ItemAnalysisView."Dimension 1 Code",Text));
                         END;
                          }

    { 30  ;2   ;Field     ;
                Name=Dim2Filter;
                CaptionML=[ENU=Dimension 2 Filter;
                           ESM=Filtro dimensi¢n 2;
                           FRC=Filtre de dimension 2;
                           ENC=Dimension 2 Filter];
                ToolTipML=[ENU=Specifies a filter for the Dimension 2 for which entries will be shown in the matrix window.;
                           ESM=Especifica un filtro para la Dimensi¢n 2 para la que se mostrar n movimientos en la ventana de la matriz.;
                           FRC=Sp‚cifie un filtre pour la dimensionÿ2 pour laquelle des entr‚es seront affich‚es dans la fenˆtre de matrice.;
                           ENC=Specifies a filter for the Dimension 2 for which entries will be shown in the matrix window.];
                ApplicationArea=#Dimensions;
                SourceExpr=Dim2Filter;
                CaptionClass=ItemAnalysisMgt.GetCaptionClass(2,ItemAnalysisView);
                Enabled=Dim2FilterEnable;
                OnValidate=BEGIN
                             ItemStatisticsBuffer.SETFILTER("Dimension 2 Filter",Dim2Filter);
                             IF ColumnDimOption = ColumnDimOption::"Dimension 2" THEN
                               MATRIX_GenerateColumnCaptions(MATRIX_Step::Initial);
                             CurrPage.UPDATE;
                           END;

                OnLookup=BEGIN
                           EXIT(ItemAnalysisMgt.LookUpDimFilter(ItemAnalysisView."Dimension 2 Code",Text));
                         END;
                          }

    { 46  ;2   ;Field     ;
                Name=Dim3Filter;
                CaptionML=[ENU=Dimension 3 Filter;
                           ESM=Filtro dimensi¢n 3;
                           FRC=Filtre de dimension 3;
                           ENC=Dimension 3 Filter];
                ToolTipML=[ENU=Specifies a filter for the Dimension 3 for which entries will be shown in the matrix window.;
                           ESM=Especifica un filtro para la Dimensi¢n 3 para la que se mostrar n movimientos en la ventana de la matriz.;
                           FRC=Sp‚cifie un filtre pour la dimensionÿ3 pour laquelle des entr‚es seront affich‚es dans la fenˆtre de matrice.;
                           ENC=Specifies a filter for the Dimension 3 for which entries will be shown in the matrix window.];
                ApplicationArea=#Dimensions;
                SourceExpr=Dim3Filter;
                CaptionClass=ItemAnalysisMgt.GetCaptionClass(3,ItemAnalysisView);
                Enabled=Dim3FilterEnable;
                OnValidate=BEGIN
                             ItemStatisticsBuffer.SETFILTER("Dimension 3 Filter",Dim3Filter);
                             IF ColumnDimOption = ColumnDimOption::"Dimension 3" THEN
                               MATRIX_GenerateColumnCaptions(MATRIX_Step::Initial);
                             CurrPage.UPDATE;
                           END;

                OnLookup=BEGIN
                           EXIT(ItemAnalysisMgt.LookUpDimFilter(ItemAnalysisView."Dimension 3 Code",Text));
                         END;
                          }

    { 1903121901;1;Group  ;
                CaptionML=[ENU=Options;
                           ESM=Opciones;
                           FRC=Options;
                           ENC=Options] }

    { 32  ;2   ;Field     ;
                CaptionML=[ENU=Show;
                           ESM=Mostrar;
                           FRC=Afficher;
                           ENC=Show];
                ToolTipML=[ENU=Specifies if the selected value is shown in the window.;
                           ESM=Especifica si el valor seleccionado se muestra en la ventana.;
                           FRC=Sp‚cifie si la valeur s‚lectionn‚e est affich‚e dans la fenˆtre.;
                           ENC=Specifies if the selected value is shown in the window.];
                OptionCaptionML=[ENU=Actual Amounts,Budgeted Amounts,Variance,Variance%,Index%;
                                 ESM=Importes reales,Importes pptados.,Desviaci¢n,% Desviaci¢n,% Öndice;
                                 FRC=R‚alis‚,Budg‚t‚,Variance,% ‚cart,% indice;
                                 ENC=Actual Amounts,Budgeted Amounts,Variance,Variance%,Index%];
                ApplicationArea=#Dimensions;
                SourceExpr=ShowActualBudget }

    { 18  ;2   ;Field     ;
                CaptionML=[ENU=Rounding Factor;
                           ESM=Factor redondeo;
                           FRC=Facteur d'arrondissement;
                           ENC=Rounding Factor];
                ToolTipML=[ENU=Specifies the factor that is used to round the amounts.;
                           ESM=Especifica el factor que se usa para redondear los importes.;
                           FRC=Sp‚cifie le facteur qui est utilis‚ pour arrondir les montants.;
                           ENC=Specifies the factor that is used to round the amounts.];
                OptionCaptionML=[ENU=None,1,1000,1000000;
                                 ESM=No,1,1000,1000000;
                                 FRC=Aucun,1,1000,1000000;
                                 ENC=None,1,1000,1000000];
                ApplicationArea=#Dimensions;
                SourceExpr=RoundingFactor }

    { 4   ;2   ;Field     ;
                CaptionML=[ENU=Show Column Name;
                           ESM=Muestra nombre columna;
                           FRC=Afficher nom de colonne;
                           ENC=Show Column Name];
                ToolTipML=[ENU=Specifies that the names of columns are shown in the matrix window.;
                           ESM=Especifica que los nombres de las columnas se muestran en la ventana de la matriz.;
                           FRC=Sp‚cifie que les noms de colonnes sont affich‚s dans la fenˆtre de la matrice.;
                           ENC=Specifies that the names of columns are shown in the matrix window.];
                ApplicationArea=#Dimensions;
                SourceExpr=ShowColumnName;
                OnValidate=BEGIN
                             MATRIX_GenerateColumnCaptions(MATRIX_Step::Same);
                           END;
                            }

    { 50  ;2   ;Field     ;
                CaptionML=[ENU=Show Opposite Sign;
                           ESM=Muestra signo opuesto;
                           FRC=Afficher le signe oppos‚;
                           ENC=Show Opposite Sign];
                ToolTipML=[ENU=Specifies whether to show debits as negative amounts (with minus signs) and credits as positive amounts in the matrix window.;
                           ESM=Especifica si se muestran los d‚bitos como importes negativos (es decir, con signo negativo) y los cr‚ditos como importes positivos en la ventana de la matriz.;
                           FRC=Indique si les d‚bits doivent ˆtre affich‚s comme des montants n‚gatifs (avec un signe moins) et les cr‚dits comme des montants positifs dans la fenˆtre de la matrice.;
                           ENC=Specifies whether to show debits as negative amounts (with minus signs) and credits as positive amounts in the matrix window.];
                ApplicationArea=#Dimensions;
                SourceExpr=ShowOppositeSign;
                MultiLine=Yes }

    { 1906098301;1;Group  ;
                CaptionML=[ENU=Matrix Options;
                           ESM=Opciones matriz;
                           FRC=Options de matrice;
                           ENC=Matrix Options] }

    { 1138;2   ;Field     ;
                CaptionML=[ENU=View by;
                           ESM=Ver por;
                           FRC=Afficher par;
                           ENC=View by];
                ToolTipML=[ENU=Specifies by which period amounts are displayed.;
                           ESM=Especifica para qu‚ periodos se muestran los importes.;
                           FRC=Indique selon quelle p‚riodicit‚ les montants sont affich‚s.;
                           ENC=Specifies by which period amounts are displayed.];
                OptionCaptionML=[ENU=Day,Week,Month,Quarter,Year,Accounting Period;
                                 ESM=D¡a,Semana,Mes,Trimestre,A¤o,Periodo contable;
                                 FRC=Jour,Semaine,Mois,Trimestre,Ann‚e,P‚riode comptable;
                                 ENC=Day,Week,Month,Quarter,Year,Accounting Period];
                ApplicationArea=#Dimensions;
                SourceExpr=PeriodType;
                OnValidate=BEGIN
                             FindPeriod('');
                             IF ColumnDimOption = ColumnDimOption::Period THEN
                               MATRIX_GenerateColumnCaptions(MATRIX_Step::Initial);
                           END;
                            }

    { 1140;2   ;Field     ;
                CaptionML=[ENU=Column Set;
                           ESM=Conjunto de columnas;
                           FRC=Jeu de colonnes;
                           ENC=Column Set];
                ToolTipML=[ENU=Specifies the range of values that are displayed in the matrix window, for example, the total period. To change the contents of the field, choose Next Set or Previous Set.;
                           ESM=Especifica el rango de valores que se muestran en la ventana de la matriz, por ejemplo, el periodo total. Para modificar el contenido de este campo, elija la opci¢n Conjunto siguiente o Conjunto anterior.;
                           FRC=Sp‚cifie la plage de valeurs affich‚e dans la fenˆtre de matrice (par exemple, total de la p‚riode). Pour modifier le contenu de ce champ, choisissez Jeu pr‚c‚dent ou Jeu suivant.;
                           ENC=Specifies the range of values that are displayed in the matrix window, for example, the total period. To change the contents of the field, choose Next Set or Previous Set.];
                ApplicationArea=#Dimensions;
                SourceExpr=MATRIX_CaptionRange;
                Editable=FALSE }

  }
  CODE
  {
    VAR
      MATRIX_MatrixRecords@1130 : ARRAY [32] OF Record 367;
      GLSetup@1024 : Record 98;
      ItemAnalysisView@1023 : Record 7152;
      ItemStatisticsBuffer@1011 : Record 5821;
      ItemAnalysisMgt@1002 : Codeunit 7153;
      MATRIX_CaptionSet@1131 : ARRAY [32] OF Text[80];
      MATRIX_CaptionRange@1132 : Text;
      FirstColumn@1018 : Text;
      LastColumn@1022 : Text;
      MATRIX_PrimKeyFirstCaption@1136 : Text;
      MATRIX_CurrentNoOfColumns@1137 : Integer;
      MATRIX_Step@1017 : 'Initial,Previous,Same,Next,PreviousColumn,NextColumn';
      CurrentAnalysisArea@1001 : 'Sales,Purchase,Inventory';
      CurrentItemAnalysisViewCode@1016 : Code[10];
      ItemFilter@1003 : Code[250];
      LocationFilter@1021 : Code[250];
      BudgetFilter@1026 : Code[250];
      ValueType@1004 : 'Sales Amount,Inventory Value,Sales Quantity';
      ShowActualBudget@1027 : 'Actual Amounts,Budgeted Amounts,Variance,Variance%,Index%';
      RoundingFactor@1005 : 'None,1,1000,1000000';
      LineDimOption@1007 : 'Item,Period,Location,Dimension 1,Dimension 2,Dimension 3';
      ColumnDimOption@1006 : 'Item,Period,Location,Dimension 1,Dimension 2,Dimension 3';
      PeriodType@1008 : 'Day,Week,Month,Quarter,Year,Accounting Period';
      Dim1Filter@1012 : Code[250];
      Dim2Filter@1010 : Code[250];
      Dim3Filter@1009 : Code[250];
      LineDimCode@1020 : Text[30];
      ColumnDimCode@1019 : Text[30];
      DateFilter@1013 : Text[30];
      InternalDateFilter@1014 : Text[30];
      PeriodInitialized@1015 : Boolean;
      ShowColumnName@1029 : Boolean;
      ShowOppositeSign@1030 : Boolean;
      Text100@1000 : TextConst 'ENU=Period;ESM=Periodo;FRC=P‚riode;ENC=Period';
      NewItemAnalysisCode@1028 : Code[10];
      Dim1FilterEnable@19054192 : Boolean INDATASET;
      Dim2FilterEnable@19062512 : Boolean INDATASET;
      Dim3FilterEnable@19011605 : Boolean INDATASET;

    LOCAL PROCEDURE MATRIX_GenerateColumnCaptions@1145(MATRIX_SetWanted@1000 : 'Initial,Previous,Same,Next,PreviousColumn,NextColumn');
    VAR
      MATRIX_PeriodRecords@1008 : ARRAY [32] OF Record 2000000007;
      Location@1006 : Record 14;
      Item@1004 : Record 27;
      Customer@1003 : Record 18;
      Vendor@1002 : Record 23;
      MatrixMgt@1001 : Codeunit 9200;
      RecRef@1005 : RecordRef;
      i@1007 : Integer;
    BEGIN
      CLEAR(MATRIX_CaptionSet);
      CLEAR(MATRIX_MatrixRecords);
      FirstColumn := '';
      LastColumn := '';
      MATRIX_CurrentNoOfColumns := ARRAYLEN(MATRIX_CaptionSet);

      CASE ColumnDimCode OF
        Text100: // Period
          BEGIN
            MatrixMgt.GeneratePeriodMatrixData(MATRIX_SetWanted,ARRAYLEN(MATRIX_CaptionSet),ShowColumnName,
              PeriodType,DateFilter,MATRIX_PrimKeyFirstCaption,
              MATRIX_CaptionSet,MATRIX_CaptionRange,MATRIX_CurrentNoOfColumns,MATRIX_PeriodRecords);
            FOR i := 1 TO ARRAYLEN(MATRIX_CaptionSet) DO BEGIN
              MATRIX_MatrixRecords[i]."Period Start" := MATRIX_PeriodRecords[i]."Period Start";
              MATRIX_MatrixRecords[i]."Period End" := MATRIX_PeriodRecords[i]."Period End";
            END;
            FirstColumn := FORMAT(MATRIX_PeriodRecords[1]."Period Start");
            LastColumn := FORMAT(MATRIX_PeriodRecords[MATRIX_CurrentNoOfColumns]."Period End");
          END;
        Location.TABLECAPTION:
          BEGIN
            CLEAR(MATRIX_CaptionSet);
            Location.SETFILTER(Code,LocationFilter);
            RecRef.GETTABLE(Location);
            RecRef.SETTABLE(Location);
            MatrixMgt.GenerateMatrixData(RecRef,MATRIX_SetWanted,ARRAYLEN(MATRIX_CaptionSet),1,
              MATRIX_PrimKeyFirstCaption,MATRIX_CaptionSet,MATRIX_CaptionRange,MATRIX_CurrentNoOfColumns);
            FOR i := 1 TO MATRIX_CurrentNoOfColumns DO
              MATRIX_MatrixRecords[i].Code := MATRIX_CaptionSet[i];
            IF ShowColumnName THEN
              MatrixMgt.GenerateMatrixData(RecRef,MATRIX_SetWanted::Same,ARRAYLEN(MATRIX_CaptionSet),2,
                MATRIX_PrimKeyFirstCaption,MATRIX_CaptionSet,MATRIX_CaptionRange,MATRIX_CurrentNoOfColumns);
          END;
        Item.TABLECAPTION:
          BEGIN
            CLEAR(MATRIX_CaptionSet);
            Item.SETFILTER("No.",ItemFilter);
            RecRef.GETTABLE(Item);
            RecRef.SETTABLE(Item);

            MatrixMgt.GenerateMatrixData(RecRef,MATRIX_SetWanted,ARRAYLEN(MATRIX_CaptionSet),1,
              MATRIX_PrimKeyFirstCaption,MATRIX_CaptionSet,MATRIX_CaptionRange,MATRIX_CurrentNoOfColumns);
            FOR i := 1 TO MATRIX_CurrentNoOfColumns DO
              MATRIX_MatrixRecords[i].Code := MATRIX_CaptionSet[i];
            IF ShowColumnName THEN
              MatrixMgt.GenerateMatrixData(RecRef,MATRIX_SetWanted::Same,ARRAYLEN(MATRIX_CaptionSet),3,
                MATRIX_PrimKeyFirstCaption,MATRIX_CaptionSet,MATRIX_CaptionRange,MATRIX_CurrentNoOfColumns);
          END;
        Customer.TABLECAPTION:
          BEGIN
            CLEAR(MATRIX_CaptionSet);
            RecRef.GETTABLE(Customer);
            RecRef.SETTABLE(Customer);
            MatrixMgt.GenerateMatrixData(RecRef,MATRIX_SetWanted,ARRAYLEN(MATRIX_CaptionSet),1,
              MATRIX_PrimKeyFirstCaption,MATRIX_CaptionSet,MATRIX_CaptionRange,MATRIX_CurrentNoOfColumns);
            FOR i := 1 TO MATRIX_CurrentNoOfColumns DO
              MATRIX_MatrixRecords[i].Code := MATRIX_CaptionSet[i];
            IF ShowColumnName THEN
              MatrixMgt.GenerateMatrixData(RecRef,MATRIX_SetWanted::Same,ARRAYLEN(MATRIX_CaptionSet),2,
                MATRIX_PrimKeyFirstCaption,MATRIX_CaptionSet,MATRIX_CaptionRange,MATRIX_CurrentNoOfColumns);
          END;
        Vendor.TABLECAPTION:
          BEGIN
            CLEAR(MATRIX_CaptionSet);
            RecRef.GETTABLE(Vendor);
            RecRef.SETTABLE(Vendor);
            MatrixMgt.GenerateMatrixData(RecRef,MATRIX_SetWanted,ARRAYLEN(MATRIX_CaptionSet),1,
              MATRIX_PrimKeyFirstCaption,MATRIX_CaptionSet,MATRIX_CaptionRange,MATRIX_CurrentNoOfColumns);
            FOR i := 1 TO MATRIX_CurrentNoOfColumns DO
              MATRIX_MatrixRecords[i].Code := MATRIX_CaptionSet[i];
            IF ShowColumnName THEN
              MatrixMgt.GenerateMatrixData(RecRef,MATRIX_SetWanted::Same,ARRAYLEN(MATRIX_CaptionSet),2,
                MATRIX_PrimKeyFirstCaption,MATRIX_CaptionSet,MATRIX_CaptionRange,MATRIX_CurrentNoOfColumns);
          END;
        ItemAnalysisView."Dimension 1 Code":
          MatrixMgt.GenerateDimColumnCaption(
            ItemAnalysisView."Dimension 1 Code",
            Dim1Filter,MATRIX_SetWanted,MATRIX_PrimKeyFirstCaption,FirstColumn,LastColumn,
            MATRIX_CaptionSet,MATRIX_MatrixRecords,MATRIX_CurrentNoOfColumns,ShowColumnName,MATRIX_CaptionRange);
        ItemAnalysisView."Dimension 2 Code":
          MatrixMgt.GenerateDimColumnCaption(
            ItemAnalysisView."Dimension 2 Code",
            Dim2Filter,MATRIX_SetWanted,MATRIX_PrimKeyFirstCaption,FirstColumn,LastColumn,
            MATRIX_CaptionSet,MATRIX_MatrixRecords,MATRIX_CurrentNoOfColumns,ShowColumnName,MATRIX_CaptionRange);
        ItemAnalysisView."Dimension 3 Code":
          MatrixMgt.GenerateDimColumnCaption(
            ItemAnalysisView."Dimension 3 Code",
            Dim3Filter,MATRIX_SetWanted,MATRIX_PrimKeyFirstCaption,FirstColumn,LastColumn,
            MATRIX_CaptionSet,MATRIX_MatrixRecords,MATRIX_CurrentNoOfColumns,ShowColumnName,MATRIX_CaptionRange);
      END;
    END;

    LOCAL PROCEDURE FindPeriod@2(SearchText@1000 : Code[3]);
    VAR
      PeriodFormMgt@1002 : Codeunit 359;
    BEGIN
      PeriodFormMgt.FindPeriodOnMatrixPage(
        DateFilter,InternalDateFilter,SearchText,PeriodType,
        (LineDimOption <> LineDimOption::Period) AND (ColumnDimOption <> ColumnDimOption::Period));
    END;

    LOCAL PROCEDURE UpdateFilterFields@4();
    VAR
      ItemAnalysisViewFilter@1000 : Record 7153;
    BEGIN
      ItemFilter := ItemAnalysisView."Item Filter";
      Dim1Filter := '';
      Dim2Filter := '';
      Dim3Filter := '';

      Dim1FilterEnable := ItemAnalysisView."Dimension 1 Code" <> '';
      Dim2FilterEnable := ItemAnalysisView."Dimension 2 Code" <> '';
      Dim3FilterEnable := ItemAnalysisView."Dimension 3 Code" <> '';

      IF Dim1FilterEnable THEN
        IF ItemAnalysisViewFilter.GET(
             ItemAnalysisView."Analysis Area",ItemAnalysisView.Code,ItemAnalysisView."Dimension 1 Code")
        THEN
          Dim1Filter := ItemAnalysisViewFilter."Dimension Value Filter";

      IF Dim2FilterEnable THEN
        IF ItemAnalysisViewFilter.GET(
             ItemAnalysisView."Analysis Area",ItemAnalysisView.Code,ItemAnalysisView."Dimension 2 Code")
        THEN
          Dim2Filter := ItemAnalysisViewFilter."Dimension Value Filter";

      IF Dim3FilterEnable THEN
        IF ItemAnalysisViewFilter.GET(
             ItemAnalysisView."Analysis Area",ItemAnalysisView.Code,ItemAnalysisView."Dimension 3 Code")
        THEN
          Dim3Filter := ItemAnalysisViewFilter."Dimension Value Filter";
    END;

    [External]
    PROCEDURE SetCurrentAnalysisViewCode@1(NewAnalysisViewCode@1000 : Code[10]);
    BEGIN
      NewItemAnalysisCode := NewAnalysisViewCode;
    END;

    BEGIN
    END.
  }
}

