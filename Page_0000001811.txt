OBJECT Page 1811 Setup Email Logging
{
  OBJECT-PROPERTIES
  {
    Date=11/22/17;
    Time=12:00:00 PM;
    Version List=NAVW111.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Email Logging Setup;
               ESM=Configuraci¢n de registro de correo electr¢nico;
               FRC=Configuration de la connexion … la messagerie;
               ENC=Email Logging Setup];
    InsertAllowed=No;
    DeleteAllowed=No;
    LinksAllowed=No;
    PageType=NavigatePage;
    ShowFilter=No;
    OnInit=BEGIN
             LoadTopBanners;
           END;

    OnOpenPage=BEGIN
                 PublicMailBoxName := PublicMailBoxTxt;
                 RootQueueStorageFolder := EmailLoggingFolderTxt;
                 QueueFolderName := QueueFolderTxt;
                 StorageFolderName := StorageFolderTxt;
                 IncomingEmailRuleName := IncomingEmailRuleTxt;
                 OutgoingEmailRuleName := OutgoingEmailRuleTxt;
                 DefaultFolderSetup := TRUE;
                 CreateIncomingEmailRule := TRUE;
                 CreateOutgoingEmailRule := TRUE;
                 ShowIntroStep;
               END;

    OnQueryClosePage=VAR
                       AssistedSetup@1000 : Record 1803;
                     BEGIN
                       SetupEmailLogging.ClosePSConnection;
                       IF CloseAction = ACTION::OK THEN
                         IF AssistedSetup.GetStatus(PAGE::"Setup Email Logging") = AssistedSetup.Status::"Not Completed" THEN
                           IF NOT CONFIRM(NAVNotSetUpQst,FALSE) THEN
                             ERROR('');
                     END;

    ActionList=ACTIONS
    {
      { 9       ;    ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 12      ;1   ;Action    ;
                      Name=ActionBack;
                      CaptionML=[ENU=Back;
                                 ESM=Atr s;
                                 FRC=Pr‚c‚dent;
                                 ENC=Back];
                      ApplicationArea=#RelationshipMgmt;
                      Enabled=BackEnabled;
                      InFooterBar=Yes;
                      Image=PreviousRecord;
                      OnAction=BEGIN
                                 NextStep(TRUE);
                               END;
                                }
      { 11      ;1   ;Action    ;
                      Name=ActionNext;
                      CaptionML=[ENU=Next;
                                 ESM=Siguiente;
                                 FRC=Suivant;
                                 ENC=Next];
                      ApplicationArea=#RelationshipMgmt;
                      Enabled=NextEnabled;
                      InFooterBar=Yes;
                      Image=NextRecord;
                      OnAction=VAR
                                 ExchangeAddinSetup@1000 : Codeunit 5323;
                               BEGIN
                                 IF (Step = Step::LoginPass) AND (NOT SkipDeployment) THEN
                                   ExchangeAddinSetup.InitializeServiceWithCredentials(Email,Password);
                                 NextStep(FALSE);
                               END;
                                }
      { 10      ;1   ;Action    ;
                      Name=ActionFinish;
                      CaptionML=[ENU=Finish;
                                 ESM=Terminar;
                                 FRC=Terminer;
                                 ENC=Finish];
                      ApplicationArea=#RelationshipMgmt;
                      Enabled=FinishEnabled;
                      InFooterBar=Yes;
                      Image=Approve;
                      OnAction=VAR
                                 AssistedSetup@1004 : Record 1803;
                                 MarketingSetup@1000 : Record 5079;
                               BEGIN
                                 Window.OPEN(SetupEmailLogDialogMsg);
                                 IF NOT SkipDeployment THEN BEGIN
                                   IF MarketingSetup.GET THEN
                                     SetupEmailLogging.ClearEmailLoggingSetup(MarketingSetup);

                                   IF NOT CreateExchangePublicFolders THEN BEGIN
                                     Window.CLOSE;
                                     ERROR(GETLASTERRORTEXT);
                                   END;
                                   UpdateMarketingSetup;

                                   CreateMailLoggingRules;
                                   IF CreateEmailLoggingJobQueue THEN BEGIN
                                     UpdateWindow(CreatingEmailLoggingJobQueueTxt,9000);
                                     SetupEmailLogging.CreateEmailLoggingJobQueueSetup;
                                   END;
                                   SetupEmailLogging.SetupEmailLoggingFolderMarketingSetup(RootQueueStorageFolder,QueueFolderName,StorageFolderName);
                                 END;
                                 AssistedSetup.SetStatus(PAGE::"Setup Email Logging",AssistedSetup.Status::Completed);
                                 Window.CLOSE;
                                 CurrPage.CLOSE;
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1   ;    ;Container ;
                Name=Control 1;
                CaptionML=[ENU="";
                           ESM="";
                           FRC="";
                           ENC=""];
                ContainerType=ContentArea }

    { 96  ;1   ;Group     ;
                Visible=TopBannerVisible AND NOT DoneVisible;
                Editable=FALSE;
                GroupType=Group }

    { 97  ;2   ;Field     ;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=MediaResourcesStandard."Media Reference";
                Editable=FALSE;
                ShowCaption=No }

    { 98  ;1   ;Group     ;
                Visible=TopBannerVisible AND DoneVisible;
                Editable=FALSE;
                GroupType=Group }

    { 99  ;2   ;Field     ;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=MediaResourcesDone."Media Reference";
                Editable=FALSE;
                ShowCaption=No }

    { 2   ;1   ;Group     ;
                Name=Step1;
                CaptionML=[ENU="";
                           ESM="";
                           FRC="";
                           ENC=""];
                Visible=IntroVisible;
                GroupType=Group }

    { 3   ;2   ;Group     ;
                CaptionML=[ENU=Welcome to Email Logging Setup;
                           ESM=Configuraci¢n de registro de correo electr¢nico;
                           FRC=Bienvenue dans la configuration de la connexion … la messagerie;
                           ENC=Welcome to Email Logging Setup];
                GroupType=Group }

    { 4   ;3   ;Group     ;
                CaptionML=[ENU="";
                           ESM="";
                           FRC="";
                           ENC=""];
                GroupType=Group;
                InstructionalTextML=[ENU=You can set up Exchange public folders and rules, so that incoming and outgoing email can log Interactions. Organization customization will be enabled for Exchange. Choose Next so you can set up public folders and email rules.;
                                     ESM=Puede configurar carpetas p£blicas y reglas de Exchange de modo que el correo electr¢nico entrante y saliente pueda registrar interacciones. La personalizaci¢n de la organizaci¢n estar  habilitada para Exchange. Seleccione Siguiente para poder configurar carpetas p£blicas y reglas de correo electr¢nico.;
                                     FRC=Vous pouvez configurer les dossiers publics et les rŠgles Exchange, de sorte que les courriels entrants et sortants puissent consigner les interactions. La personnalisation de l'organisation sera activ‚e pour Exchange. S‚lectionnez Suivant pour configurer les dossiers publics et les rŠgles de messagerie.;
                                     ENC=You can set up Exchange public folders and rules, so that incoming and outgoing email can log Interactions. Organization customization will be enabled for Exchange. Choose Next so you can set up public folders and email rules.] }

    { 5   ;1   ;Group     ;
                Name=Step2;
                CaptionML=[ENU="";
                           ESM="";
                           FRC="";
                           ENC=""];
                Visible=LoginPassVisible;
                GroupType=Group }

    { 6   ;2   ;Group     ;
                Name=ExchangeCredentialsDesc;
                CaptionML=[ENU=Provide your Exchange Online administrator credentials.;
                           ESM=Proporcione las credenciales de administrador de Exchange Online.;
                           FRC=Fournissez vos informations d'identification d'administrateur ExchangeÿOnline.;
                           ENC=Provide your Exchange Online administrator credentials.];
                GroupType=Group }

    { 7   ;3   ;Field     ;
                Name=Email;
                ExtendedDatatype=E-Mail;
                CaptionML=[ENU=Email;
                           ESM=Correo electr¢nico;
                           FRC=Courriel;
                           ENC=Email];
                ApplicationArea=#RelationshipMgmt;
                SourceExpr=Email;
                OnValidate=BEGIN
                             NextEnabled := EmailPasswordNotEmpty;
                           END;
                            }

    { 8   ;3   ;Field     ;
                Name=Password;
                ExtendedDatatype=Masked;
                CaptionML=[ENU=Password;
                           ESM=Contrase¤a;
                           FRC=Mot de passe;
                           ENC=Password];
                ApplicationArea=#RelationshipMgmt;
                SourceExpr=Password;
                OnValidate=BEGIN
                             NextEnabled := EmailPasswordNotEmpty;
                           END;
                            }

    { 16  ;1   ;Group     ;
                Name=Public Folders Creation;
                Visible=PublicFoldersVisible;
                GroupType=Group;
                InstructionalTextML=[ENU=The following public mailbox and public folders will be created. (You can rename folders later):;
                                     ESM=Se crear n las siguientes carpetas p£blicas y buz¢n p£blico. (Puede cambiar el nombre de las carpetas m s adelante):;
                                     FRC=La boŒte aux lettres publique et les dossiers publics suivants seront cr‚‚s. (Vous pourrez les renommer ult‚rieurement)ÿ:;
                                     ENC=The following public mailbox and public folders will be created. (You can rename folders later):] }

    { 22  ;2   ;Field     ;
                Name=DefaultFolderSetup;
                CaptionML=[ENU=Default Folder Setup;
                           ESM=Configuraci¢n de carpetas predeterminada;
                           FRC=Configuration par d‚faut des dossiers;
                           ENC=Default Folder Setup];
                ApplicationArea=#RelationshipMgmt;
                SourceExpr=DefaultFolderSetup }

    { 21  ;2   ;Field     ;
                Name=PublicMailBoxName;
                CaptionML=[ENU=Public Mail Box Name;
                           ESM=Nombre del buz¢n p£blico;
                           FRC=Nom de la boŒte aux lettres publique;
                           ENC=Public Mail Box Name];
                ApplicationArea=#RelationshipMgmt;
                SourceExpr=PublicMailBoxName;
                Editable=NOT DefaultFolderSetup }

    { 20  ;2   ;Field     ;
                Name=RootQueueStorageFolder;
                CaptionML=[ENU=Root Folder;
                           ESM=Carpeta ra¡z;
                           FRC=Dossier racine;
                           ENC=Root Folder];
                ApplicationArea=#RelationshipMgmt;
                SourceExpr=RootQueueStorageFolder;
                Editable=NOT DefaultFolderSetup }

    { 17  ;2   ;Field     ;
                Name=QueueFolderName;
                CaptionML=[ENU=Queue Folder Name;
                           ESM=Nombre de la carpeta de colas;
                           FRC=Nom du dossier de file d'attente;
                           ENC=Queue Folder Name];
                ApplicationArea=#RelationshipMgmt;
                SourceExpr=QueueFolderName;
                Editable=NOT DefaultFolderSetup }

    { 18  ;2   ;Field     ;
                Name=StorageFolderName;
                CaptionML=[ENU=Storage Folder Name;
                           ESM=Nombre de la carpeta de almacenamiento;
                           FRC=Nom du dossier de stockage;
                           ENC=Storage Folder Name];
                ApplicationArea=#RelationshipMgmt;
                SourceExpr=StorageFolderName;
                Editable=NOT DefaultFolderSetup }

    { 24  ;1   ;Group     ;
                Name=Email Rules;
                Visible=EmailRulesVisible;
                GroupType=Group;
                InstructionalTextML=[ENU=The following Exchange transport rules will be created, so that incoming email from outside organizations and outgoing mail to outside organization will be copied to queue public folder for later NAV processing. You can disable creation or give specific names for the rules:;
                                     ESM=Se crear n las siguientes reglas de transporte de Exchange de modo que el correo electr¢nico entrante desde organizaciones externas y el correo saliente a organizaciones externas se copiar n a la carpeta p£blica de colas para su procesamiento posterior en NAV. Puede deshabilitar la creaci¢n o asignar nombres espec¡ficos a las reglas:;
                                     FRC=Les rŠgles de transport Exchange suivantes seront cr‚‚es, de sorte que les courriels entrants en provenance d'organisations externes et les courriels sortants … destination d'organisations externes seront copi‚s dans le dossier public de file d'attente en vue d'un traitement NAV ult‚rieur. Vous pouvez d‚sactiver cette cr‚ation ou indiquer des noms sp‚cifiques pour les rŠglesÿ:;
                                     ENC=The following Exchange transport rules will be created, so that incoming email from outside organizations and outgoing mail to outside organization will be copied to queue public folder for later NAV processing. You can disable creation or give specific names for the rules:] }

    { 27  ;2   ;Field     ;
                Name=CreateIncomingEmailRule;
                CaptionML=[ENU=Create Incoming Email Rule;
                           ESM=Crear regla de correo electr¢nico entrante;
                           FRC=Cr‚er une rŠgle pour les courriels entrants;
                           ENC=Create Incoming Email Rule];
                ApplicationArea=#RelationshipMgmt;
                SourceExpr=CreateIncomingEmailRule;
                OnValidate=BEGIN
                             IF NOT CreateIncomingEmailRule THEN
                               CLEAR(IncomingEmailRuleName);
                           END;
                            }

    { 25  ;2   ;Field     ;
                Name=IncomingEmailRuleName;
                CaptionML=[ENU=Incoming Email Rule Name;
                           ESM=Nombre de la regla de correo electr¢nico entrante;
                           FRC=Nom de la rŠgle pour les courriels entrants;
                           ENC=Incoming Email Rule Name];
                ApplicationArea=#RelationshipMgmt;
                SourceExpr=IncomingEmailRuleName;
                Editable=CreateIncomingEmailRule }

    { 28  ;2   ;Field     ;
                Name=CreateOutgoingEmailRule;
                CaptionML=[ENU=Create Outgoing Email Rule;
                           ESM=Crear regla de correo electr¢nico saliente;
                           FRC=Cr‚er une rŠgle pour les courriels sortants;
                           ENC=Create Outgoing Email Rule];
                ApplicationArea=#RelationshipMgmt;
                SourceExpr=CreateOutgoingEmailRule;
                OnValidate=BEGIN
                             IF NOT CreateOutgoingEmailRule THEN
                               CLEAR(OutgoingEmailRuleName);
                           END;
                            }

    { 26  ;2   ;Field     ;
                Name=OutgoingEmailRuleName;
                CaptionML=[ENU=Outgoing Email Rule Name;
                           ESM=Nombre de la regla de correo electr¢nico saliente;
                           FRC=Nom de la rŠgle pour les courriels sortants;
                           ENC=Outgoing Email Rule Name];
                ApplicationArea=#RelationshipMgmt;
                SourceExpr=OutgoingEmailRuleName;
                Editable=CreateOutgoingEmailRule }

    { 14  ;1   ;Group     ;
                Name=Step4;
                CaptionML=[ENU="";
                           ESM="";
                           FRC="";
                           ENC=""];
                Visible=DoneVisible;
                GroupType=Group }

    { 15  ;2   ;Group     ;
                Name=FinalStepDesc;
                CaptionML=[ENU=That's it!;
                           ESM=Eso es todo.;
                           FRC=Voil…ÿ!;
                           ENC=That's it!];
                GroupType=Group;
                InstructionalTextML=[ENU=When you choose Finish, the following will be created:;
                                     ESM=Al seleccionar Terminar, se crear n los elementos siguientes:;
                                     FRC=Quand vous s‚lectionnerez Terminer, les ‚l‚ments suivants seront cr‚‚sÿ:;
                                     ENC=When you choose Finish, the following will be created:] }

    { 33  ;3   ;Group     ;
                GroupType=Group }

    { 19  ;4   ;Field     ;
                Name=QueueFolderNameFinal;
                CaptionML=[ENU=Queue Folder;
                           ESM=Carpeta de colas;
                           FRC=Dossier attente;
                           ENC=Queue Folder];
                ApplicationArea=#RelationshipMgmt;
                SourceExpr=QueueFolderName;
                Editable=FALSE }

    { 23  ;4   ;Field     ;
                Name=StorageFolderNameFinal;
                CaptionML=[ENU=Storage Folder;
                           ESM=Carpeta de almacenamiento;
                           FRC=Dossier stockage;
                           ENC=Storage Folder];
                ApplicationArea=#RelationshipMgmt;
                SourceExpr=StorageFolderName;
                Editable=FALSE }

    { 29  ;4   ;Field     ;
                Name=IncomingEmailRuleNameFinal;
                CaptionML=[ENU=Incoming Email Rule;
                           ESM=Regla de correo electr¢nico entrante;
                           FRC=RŠgle pour les courriels entrants;
                           ENC=Incoming Email Rule];
                ApplicationArea=#RelationshipMgmt;
                SourceExpr=IncomingEmailRuleName;
                Editable=FALSE }

    { 30  ;4   ;Field     ;
                Name=OutgoingEmailRuleNameFinal;
                CaptionML=[ENU=Outgoing Email Rule;
                           ESM=Regla de correo electr¢nico saliente;
                           FRC=RŠgle pour les courriels sortants;
                           ENC=Outgoing Email Rule];
                ApplicationArea=#RelationshipMgmt;
                SourceExpr=OutgoingEmailRuleName;
                Editable=FALSE }

    { 31  ;4   ;Field     ;
                Name=CreateEmailLoggingJobQueue;
                CaptionML=[ENU=Create Email Logging Job Queue;
                           ESM=Crear cola de proyectos de registro de correo electr¢nico;
                           FRC=Cr‚er une file d'attente des travaux pour la consignation des courriels;
                           ENC=Create Email Logging Job Queue];
                ApplicationArea=#RelationshipMgmt;
                SourceExpr=CreateEmailLoggingJobQueue }

    { 34  ;3   ;Group     ;
                GroupType=Group;
                InstructionalTextML=[ENU=(Note: The creation of public folders and rules may take some time. When the folders are created, the wizard window will close.);
                                     ESM=(Nota: La creaci¢n de carpetas p£blicas y reglas puede tardar algo de tiempo. Cuando se hayan creado las carpetas, se cerrar  la ventana del asistente).;
                                     FRC=(Remarqueÿ: la cr‚ation des dossiers publics et des rŠgles peut prendre un certain temps. La fenˆtre de l'assistant se fermera quand les dossiers seront cr‚‚s.);
                                     ENC=(Note: The creation of public folders and rules may take some time. When the folders are created, the wizard window will close.)] }

  }
  CODE
  {
    VAR
      MediaRepositoryStandard@1040 : Record 9400;
      MediaRepositoryDone@1041 : Record 9400;
      MediaResourcesStandard@1037 : Record 2000000182;
      MediaResourcesDone@1036 : Record 2000000182;
      SetupEmailLogging@1015 : Codeunit 1641;
      ClientTypeManagement@1039 : Codeunit 4;
      Window@1030 : Dialog;
      Step@1008 : 'Intro,LoginPass,PublicFolders,EmailRules,Done';
      Email@1000 : Text[80];
      Password@1001 : Text[30];
      RootQueueStorageFolder@1014 : Text;
      QueueFolderName@1012 : Text;
      StorageFolderName@1011 : Text;
      PublicMailBoxName@1016 : Text;
      IncomingEmailRuleName@1021 : Text;
      OutgoingEmailRuleName@1022 : Text;
      BackEnabled@1002 : Boolean;
      NextEnabled@1003 : Boolean;
      FinishEnabled@1004 : Boolean;
      TopBannerVisible@1042 : Boolean;
      IntroVisible@1005 : Boolean;
      LoginPassVisible@1006 : Boolean;
      DoneVisible@1007 : Boolean;
      PublicFoldersVisible@1013 : Boolean;
      PublicMailBoxTxt@1010 : TextConst 'ENU=Public MailBox;ESM=Buz¢n p£blico;FRC=BoŒte aux lettres publique;ENC=Public MailBox';
      EmailLoggingFolderTxt@1009 : TextConst 'ENU=Email Logging;ESM=Registro de correo electr¢nico;FRC=Connexion … la messagerie;ENC=Email Logging';
      QueueFolderTxt@1017 : TextConst 'ENU=Queue;ESM=Cola;FRC=File d''attente;ENC=Queue';
      StorageFolderTxt@1018 : TextConst 'ENU=Storage;ESM=Almacenamiento;FRC=Stockage;ENC=Storage';
      EmailRulesVisible@1020 : Boolean;
      DefaultFolderSetup@1019 : Boolean;
      CreateIncomingEmailRule@1023 : Boolean;
      CreateOutgoingEmailRule@1024 : Boolean;
      IncomingEmailRuleTxt@1026 : TextConst 'ENU=Log Email Sent to This Organization;ESM=Registrar correo electr¢nico enviado a esta organizaci¢n;FRC=Consigner les courriels envoy‚s … cette organisation;ENC=Log Email Sent to This Organization';
      OutgoingEmailRuleTxt@1025 : TextConst 'ENU=Log Email Sent from This Organization;ESM=Registrar correo electr¢nico enviado desde esta organizaci¢n;FRC=Consigner les courriels envoy‚s … partir de cette organisation;ENC=Log Email Sent from This Organization';
      NAVNotSetUpQst@1027 : TextConst 'ENU=Setup of Email Logging was not finished. \\Are you sure that you want to exit?;ESM=" No se termin¢ la configuraci¢n del registro de correo electr¢nico\\¨Est  seguro de que desea salir?";FRC=La configuration de la connexion … la messagerie n''a pas ‚t‚ men‚e … bien. \\Òtes-vous s–r de vouloir quitterÿ?;ENC=Setup of Email Logging was not finished. \\Are you sure that you want to exit?';
      CreateEmailLoggingJobQueue@1028 : Boolean;
      SetupEmailLogDialogMsg@1029 : TextConst '@@@=This is a message for dialog window. Parameters do not require translation.;ENU=Setup Email Logging    #1################## @2@@@@@@@@@@@@@@@@@@;ESM=Configurar registro de correo electr¢nico    #1################## @2@@@@@@@@@@@@@@@@@@;FRC=Configuration de la connexion … la messagerie    #1################## @2@@@@@@@@@@@@@@@@@@;ENC=Setup Email Logging    #1################## @2@@@@@@@@@@@@@@@@@@';
      InitializingConnectionTxt@1031 : TextConst 'ENU=Initializing connection;ESM=Iniciando conexi¢n;FRC=Initialisation de la connexion;ENC=Initializing connection';
      PublicFoldersCreationTxt@1032 : TextConst 'ENU=Public folders creation;ESM=Creaci¢n de carpetas p£blicas;FRC=Cr‚ation de dossiers publics;ENC=Public folders creation';
      EmailRulesCreationTxt@1033 : TextConst 'ENU=Email rules creation;ESM=Creaci¢n de reglas de correo electr¢nico;FRC=Cr‚ation de rŠgles de messagerie;ENC=Email rules creation';
      UpdatingMarketingSetupTxt@1034 : TextConst 'ENU=Updating Marketing Setup;ESM=Actualizando configuraci¢n de marketing;FRC=Mise … jour de la configuration marketing;ENC=Updating Marketing Setup';
      CreatingEmailLoggingJobQueueTxt@1035 : TextConst 'ENU=Creating email Logging Job Queue;ESM=Creando cola de proyectos de registro de correo electr¢nico;FRC=Cr‚ation de la file d''attente des travaux pour la consignation des courriels;ENC=Creating email Logging Job Queue';
      SkipDeployment@1038 : Boolean;

    LOCAL PROCEDURE NextStep@3(Backwards@1000 : Boolean);
    BEGIN
      IF Backwards THEN
        Step := Step - 1
      ELSE
        Step := Step + 1;

      CASE Step OF
        Step::Intro:
          ShowIntroStep;
        Step::LoginPass:
          BEGIN
            IF Backwards THEN
              SetupEmailLogging.ClosePSConnection;
            ShowLoginPassStep;
          END;
        Step::PublicFolders:
          IF NOT SkipDeployment THEN BEGIN
            Window.OPEN(SetupEmailLogDialogMsg);
            UpdateWindow(InitializingConnectionTxt,1000);
            IF NOT Backwards THEN BEGIN
              SetupEmailLogging.SetDeployCredentials(Email,Password);
              InitializePSExchangeConnection;
            END;
            ShowPublicFoldersStep;
            Window.CLOSE;
          END;
        Step::EmailRules:
          ShowEmailRulesStep;
        Step::Done:
          ShowDoneStep;
      END;
      CurrPage.UPDATE(TRUE);
    END;

    LOCAL PROCEDURE ShowIntroStep@1();
    BEGIN
      ResetWizardControls;
      IntroVisible := TRUE;
      BackEnabled := FALSE;
    END;

    LOCAL PROCEDURE ShowLoginPassStep@9();
    BEGIN
      ResetWizardControls;
      NextEnabled := EmailPasswordNotEmpty;
      LoginPassVisible := TRUE;
    END;

    LOCAL PROCEDURE ShowPublicFoldersStep@4();
    BEGIN
      ResetWizardControls;
      NextEnabled := TRUE;
      PublicFoldersVisible := TRUE;
    END;

    LOCAL PROCEDURE ShowEmailRulesStep@2();
    BEGIN
      ResetWizardControls;
      NextEnabled := TRUE;
      EmailRulesVisible := TRUE;
    END;

    LOCAL PROCEDURE ShowDoneStep@11();
    BEGIN
      ResetWizardControls;
      DoneVisible := TRUE;
      NextEnabled := FALSE;
      FinishEnabled := TRUE;
      CreateEmailLoggingJobQueue := TRUE;
    END;

    LOCAL PROCEDURE ResetWizardControls@10();
    BEGIN
      // Buttons
      BackEnabled := TRUE;
      NextEnabled := TRUE;
      FinishEnabled := FALSE;

      // Tabs
      IntroVisible := FALSE;
      LoginPassVisible := FALSE;
      DoneVisible := FALSE;
      PublicFoldersVisible := FALSE;
      EmailRulesVisible := FALSE;
    END;

    LOCAL PROCEDURE InitializePSExchangeConnection@18();
    BEGIN
      SetupEmailLogging.InitializeExchangePSConnection;
    END;

    [TryFunction]
    LOCAL PROCEDURE CreateExchangePublicFolders@23();
    BEGIN
      UpdateWindow(PublicFoldersCreationTxt,1000);
      SetupEmailLogging.CreatePublicFolders(
        PublicMailBoxName,RootQueueStorageFolder,QueueFolderName,StorageFolderName)
    END;

    LOCAL PROCEDURE CreateMailLoggingRules@43();
    VAR
      QueueEmailAddress@1000 : Text;
    BEGIN
      UpdateWindow(EmailRulesCreationTxt,8000);
      QueueEmailAddress := QueueFolderName + '@' + SetupEmailLogging.GetDomainFromEmail(Email);
      SetupEmailLogging.CreateEmailLoggingRules(QueueEmailAddress,IncomingEmailRuleName,OutgoingEmailRuleName);
    END;

    LOCAL PROCEDURE EmailPasswordNotEmpty@53() : Boolean;
    BEGIN
      EXIT((Email <> '') AND (Password <> ''));
    END;

    LOCAL PROCEDURE UpdateMarketingSetup@21();
    VAR
      MarketingSetup@1000 : Record 5079;
    BEGIN
      UpdateWindow(UpdatingMarketingSetupTxt,6000);
      IF NOT MarketingSetup.GET THEN
        EXIT;

      MarketingSetup.VALIDATE("Exchange Service URL",SetupEmailLogging.GetDomainFromEmail(Email));
      MarketingSetup.VALIDATE("Autodiscovery E-Mail Address",Email);
      MarketingSetup.VALIDATE("Email Batch Size",10);
      MarketingSetup.VALIDATE("Exchange Account User Name",Email);
      MarketingSetup.SetExchangeAccountPassword(Password);
      MarketingSetup.MODIFY;
    END;

    LOCAL PROCEDURE UpdateWindow@5(StepText@1000 : Text;Progress@1001 : Integer);
    BEGIN
      Window.UPDATE(1,StepText);
      Window.UPDATE(2,Progress);
    END;

    LOCAL PROCEDURE LoadTopBanners@40();
    BEGIN
      IF MediaRepositoryStandard.GET('AssistedSetup-NoText-400px.png',FORMAT(ClientTypeManagement.GetCurrentClientType)) AND
         MediaRepositoryDone.GET('AssistedSetupDone-NoText-400px.png',FORMAT(ClientTypeManagement.GetCurrentClientType))
      THEN
        IF MediaResourcesStandard.GET(MediaRepositoryStandard."Media Resources Ref") AND
           MediaResourcesDone.GET(MediaRepositoryDone."Media Resources Ref")
        THEN
          TopBannerVisible := MediaResourcesDone."Media Reference".HASVALUE;
    END;

    [External]
    PROCEDURE SkipDeploymentToExchange@6(Skip@1000 : Boolean);
    BEGIN
      SkipDeployment := Skip;
    END;

    BEGIN
    END.
  }
}

