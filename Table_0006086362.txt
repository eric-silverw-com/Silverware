OBJECT Table 6086362 EM Dimension Inbox
{
  OBJECT-PROPERTIES
  {
    Date=10/26/17;
    Time=[ 1:00:00 AM];
    Version List=EMW16.00.10.2.60.02;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               TestStatusOpen;
               ClearError;
               UpdateRecordGlobalDimValue("Table ID","Document Type","Document No.","Doc. Ref. No.","Dimension Code","Dimension Value Code");
             END;

    OnModify=BEGIN
               TestStatusOpen;
               ClearError;
               UpdateRecordGlobalDimValue("Table ID","Document Type","Document No.","Doc. Ref. No.","Dimension Code","Dimension Value Code");
             END;

    OnDelete=BEGIN
               TestStatusOpen;
               ClearError;
               UpdateRecordGlobalDimValue("Table ID","Document Type","Document No.","Doc. Ref. No.","Dimension Code",'');
             END;

    CaptionML=ENU=EM Dimension Inbox;
  }
  FIELDS
  {
    { 1   ;   ;Table ID            ;Integer       ;CaptionML=ENU=Table ID }
    { 2   ;   ;Document Type       ;Option        ;CaptionML=ENU=Document Type;
                                                   OptionCaptionML=ENU=Budget,Settlement;
                                                   OptionString=Budget,Settlement }
    { 3   ;   ;Document No.        ;Code20        ;CaptionML=ENU=Document No. }
    { 4   ;   ;Doc. Ref. No.       ;Integer       ;TableRelation=IF (Table ID=CONST(6086323)) "Expense Inbox"
                                                                 ELSE IF (Table ID=CONST(6086353)) "Mileage Inbox";
                                                   CaptionML=ENU=Doc. Ref. No. }
    { 10  ;   ;Dimension Code      ;Code20        ;TableRelation=Dimension;
                                                   ValidateTableRelation=No;
                                                   CaptionML=ENU=Dimension Code }
    { 11  ;   ;Dimension Value Code;Code20        ;TableRelation="Dimension Value".Code WHERE (Dimension Code=FIELD(Dimension Code));
                                                   ValidateTableRelation=No;
                                                   CaptionML=ENU=Dimension Value Code;
                                                   NotBlank=Yes }
    { 12  ;   ;Field Code          ;Code20        ;TableRelation="Field Type";
                                                   ValidateTableRelation=No;
                                                   CaptionML=ENU=Field Name }
    { 13  ;   ;Field Value         ;Text250       ;TableRelation="Lookup Value".Code WHERE (Field Type Code=FIELD(Field Code));
                                                   ValidateTableRelation=No;
                                                   CaptionML=ENU=Field Value }
  }
  KEYS
  {
    {    ;Table ID,Document Type,Document No.,Doc. Ref. No.,Dimension Code,Field Code;
                                                   Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text001@1160040001 : TextConst 'ENU=%1 %2 already %3.';

    LOCAL PROCEDURE TestStatusOpen@37();
    VAR
      ExpenseInbox@1160040000 : Record 6086323;
      MileageInbox@1160040001 : Record 6086353;
      ExpHeaderInbox@1160040002 : Record 6086341;
    BEGIN
      CASE "Table ID" OF
        DATABASE::"Expense Inbox":
          BEGIN
            ExpenseInbox.GET("Doc. Ref. No.");
            IF ExpenseInbox.Status = ExpenseInbox.Status::Accepted THEN
              ERROR(Text001,ExpenseInbox.TABLECAPTION,ExpenseInbox."Entry No.",ExpenseInbox.Status);
          END;
        DATABASE::"Mileage Inbox":
          BEGIN
            MileageInbox.GET("Doc. Ref. No.");
            IF MileageInbox.Status = MileageInbox.Status::Accepted THEN
              ERROR(Text001,MileageInbox.TABLECAPTION,MileageInbox."Entry No.",MileageInbox.Status);
          END;
        DATABASE::"Expense Header Inbox":
          BEGIN
            ExpHeaderInbox.GET("Doc. Ref. No.");
            IF ExpHeaderInbox.Status = ExpHeaderInbox.Status::Accepted THEN
              ERROR(Text001,ExpHeaderInbox.TABLECAPTION,ExpHeaderInbox."Entry No.",ExpHeaderInbox.Status);
          END;

      END;
    END;

    LOCAL PROCEDURE ClearError@1160040000();
    VAR
      ExpenseInbox@1160040000 : Record 6086323;
      MileageInbox@1160040001 : Record 6086353;
      ExpHeaderInbox@1160040002 : Record 6086341;
    BEGIN
      CASE "Table ID" OF
        DATABASE::"Expense Inbox":
          BEGIN
            ExpenseInbox.GET("Doc. Ref. No.");
            IF ExpenseInbox.Status = ExpenseInbox.Status::Error THEN BEGIN
              ExpenseInbox.Status := ExpenseInbox.Status::Pending;
              ExpenseInbox.MODIFY;
            END;
          END;
        DATABASE::"Mileage Inbox":
          BEGIN
            MileageInbox.GET("Doc. Ref. No.");
            IF MileageInbox.Status = MileageInbox.Status::Error THEN BEGIN
              MileageInbox.Status := MileageInbox.Status::Pending;
              MileageInbox.MODIFY;
            END;
          END;
        DATABASE::"Expense Header Inbox":
          BEGIN
            ExpHeaderInbox.GET("Doc. Ref. No.");
            IF ExpHeaderInbox.Status = ExpHeaderInbox.Status::Error THEN BEGIN
              ExpHeaderInbox.Status := ExpHeaderInbox.Status::Pending;
              ExpHeaderInbox.MODIFY;
            END;
          END;
      END;
    END;

    PROCEDURE UpdateRecordGlobalDimValue@1160040001(TableID@1160040011 : Integer;DocumentType@1160040010 : Integer;DocumentNo@1160040009 : Code[20];DocRefNo@1160040005 : Integer;DimCode@1160040006 : Code[20];DimValCode@1160040007 : Code[20]);
    VAR
      GLSetup@1160040003 : Record 98;
      ExpInbox@1160040002 : Record 6086323;
      MilInbox@1160040001 : Record 6086353;
      ExpHeaderInbox@1160040008 : Record 6086341;
    BEGIN
      GLSetup.GET;
      IF NOT (DimCode IN [GLSetup."Global Dimension 1 Code",GLSetup."Global Dimension 2 Code"]) THEN
        EXIT;

      CASE TableID OF
        DATABASE::"Expense Inbox":
          BEGIN
            ExpInbox.GET(DocRefNo);
            IF DimCode = GLSetup."Global Dimension 1 Code" THEN
              ExpInbox."Global Dimension 1 Code" := DimValCode;
            IF DimCode = GLSetup."Global Dimension 2 Code" THEN
              ExpInbox."Global Dimension 2 Code" := DimValCode;
            ExpInbox.MODIFY;
          END;

        DATABASE::"Mileage Inbox":
          BEGIN
            MilInbox.GET(DocRefNo);
            IF DimCode = GLSetup."Global Dimension 1 Code" THEN
              MilInbox."Global Dimension 1 Code" := DimValCode;
            IF DimCode = GLSetup."Global Dimension 2 Code" THEN
              MilInbox."Global Dimension 2 Code" := DimValCode;
            MilInbox.MODIFY;
          END;

        DATABASE::"Expense Header Inbox":
          BEGIN
            ExpHeaderInbox.GET(DocRefNo);
            IF DimCode = GLSetup."Global Dimension 1 Code" THEN
              ExpHeaderInbox."Global Dimension 1 Code" := DimValCode;
            IF DimCode = GLSetup."Global Dimension 2 Code" THEN
              ExpHeaderInbox."Global Dimension 2 Code" := DimValCode;
            ExpHeaderInbox.MODIFY;
          END;
      END;
    END;

    BEGIN
    END.
  }
}

