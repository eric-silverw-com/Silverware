OBJECT Page 6086061 Continia User Setup by Company
{
  OBJECT-PROPERTIES
  {
    Date=01/20/17;
    Time=[ 1:00:00 AM];
    Version List=DCW17.00.00.4.07;
  }
  PROPERTIES
  {
    CaptionML=ENU=User Setup by Company;
    InsertAllowed=No;
    DeleteAllowed=No;
    SourceTable=Table2000000006;
    DataCaptionExpr=GetDataCaptionExpr;
    PageType=List;
    OnOpenPage=BEGIN
                 CEMEnabled := CEMUserMgt.IsCEMEnabled(FALSE);
               END;

    OnAfterGetRecord=BEGIN
                       IsCurrCompany := Name = COMPANYNAME;

                       UpdateFields;
                     END;

    OnAfterGetCurrRecord=BEGIN
                           CurrPage.AppUserGrpMember.PAGE.InitPage(Name,CurrUserID);
                           CurrPage.ContiniaUserPermissions.PAGE.InitPage(Name,CurrUserID);
                         END;

    ActionList=ACTIONS
    {
      { 1160040026;  ;ActionContainer;
                      Name=<Action1160040010>;
                      ActionContainerType=RelatedInformation }
      { 1160040025;1 ;Action    ;
                      Name=UserGroups;
                      CaptionML=ENU=Approval User Groups;
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=Users;
                      PromotedCategory=Process;
                      OnAction=VAR
                                 AppUserGrpMemberGrpPage@1160040000 : Page 6085639;
                                 ApprovalUserGroupMember@1160040001 : Record 6085631;
                               BEGIN
                                 CheckHasAccess;

                                 ApprovalUserGroupMember.CHANGECOMPANY(Name);
                                 ApprovalUserGroupMember.SETRANGE("Continia User ID",CurrUserID);
                                 AppUserGrpMemberGrpPage.SetCurrCompanyName(Name);
                                 AppUserGrpMemberGrpPage.SETRECORD(ApprovalUserGroupMember);
                                 AppUserGrpMemberGrpPage.SETTABLEVIEW(ApprovalUserGroupMember);
                                 AppUserGrpMemberGrpPage.RUN;
                               END;
                                }
      { 1160040024;1 ;Action    ;
                      Name=Permissions;
                      CaptionML=ENU=Approval &Permissions;
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=Permission;
                      PromotedCategory=Process;
                      OnAction=VAR
                                 ContiniaUserPermissionsPage@1160040002 : Page 6085638;
                                 ContiniaUserPermission@1160040001 : Record 6085634;
                               BEGIN
                                 CheckHasAccess;

                                 ContiniaUserPermission.CHANGECOMPANY(Name);
                                 ContiniaUserPermission.SETRANGE("Continia User ID",CurrUserID);
                                 ContiniaUserPermissionsPage.SetCurrCompanyName(Name);
                                 ContiniaUserPermissionsPage.SETRECORD(ContiniaUserPermission);
                                 ContiniaUserPermissionsPage.SETTABLEVIEW(ContiniaUserPermission);
                                 ContiniaUserPermissionsPage.RUN;
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1160040000;0;Container;
                ContainerType=ContentArea }

    { 1160040001;1;Group  ;
                Name=Group;
                GroupType=Repeater }

    { 1160040002;2;Field  ;
                SourceExpr=Name;
                Editable=FALSE;
                Style=Strong;
                StyleExpr=IsCurrCompany }

    { 1160040003;2;Field  ;
                CaptionML=ENU=Access;
                SourceExpr=HasAccess;
                OnValidate=BEGIN
                             ContiniaUser.GET(CurrUserID);
                             IF HasAccess THEN
                               ContiniaUser.InsertContiniaUserSetupInComp(Name)
                             ELSE
                               ContiniaUser.DeleteContiniaUserSetupInComp(Name);
                             IF FIND('=') THEN;
                             UpdateFields;
                           END;
                            }

    { 1160040004;2;Field  ;
                SourceExpr=SalesPurchCode;
                CaptionClass='3,'+UserSetup.FIELDCAPTION("Salespers./Purch. Code");
                OnValidate=BEGIN
                             UpdateUserSetupField(UserSetup.FIELDNO("Salespers./Purch. Code"),SalesPurchCode);
                             CurrPage.UPDATE(FALSE);
                           END;

                OnLookup=VAR
                           SalesPurchaser@1160040001 : Record 13;
                           PageID@1160040000 : Integer;
                         BEGIN
                           IF Name <> COMPANYNAME THEN BEGIN
                             SalesPurchaser.CHANGECOMPANY(Name);
                             PageID := PAGE::"Salespeople/Purchasers (Comp.)";
                           END;

                           IF SalesPurchaser.GET(Text) THEN;
                           IF PAGE.RUNMODAL(PageID,SalesPurchaser) = ACTION::LookupOK THEN BEGIN
                             Text := SalesPurchaser.Code;
                             EXIT(TRUE);
                           END;
                         END;
                          }

    { 1160040023;2;Field  ;
                SourceExpr=ApproverID;
                CaptionClass='3,'+UserSetup.FIELDCAPTION("Approver ID");
                OnValidate=BEGIN
                             UpdateUserSetupField(UserSetup.FIELDNO("Approver ID"),ApproverID);
                             CurrPage.UPDATE(FALSE);
                           END;

                OnLookup=VAR
                           ContiniaUserSetup@6085573 : Record 6086002;
                           PageID@1160040002 : Integer;
                         BEGIN
                           IF Name <> COMPANYNAME THEN BEGIN
                             ContiniaUserSetup.CHANGECOMPANY(Name);
                             PageID := PAGE::"Continia User List 2 (Comp.)";
                           END;

                           IF ContiniaUserSetup.GET(Text) THEN;
                           IF PAGE.RUNMODAL(PageID,ContiniaUserSetup) = ACTION::LookupOK THEN BEGIN
                             Text := ContiniaUserSetup."Continia User ID";
                             EXIT(TRUE);
                           END;
                         END;
                          }

    { 6085573;2;Field     ;
                SourceExpr=SecondApproverID;
                CaptionClass='3,'+CompContiniaUserSetup.FIELDCAPTION("4-eyes, 2nd Approver ID");
                OnValidate=BEGIN
                             UpdateContiniaUserSetupField(CompContiniaUserSetup.FIELDNO("4-eyes, 2nd Approver ID"),SecondApproverID);
                             CurrPage.UPDATE(FALSE);
                           END;

                OnLookup=VAR
                           ContiniaUserSetup@1160040001 : Record 6086002;
                           PageID@1160040000 : Integer;
                         BEGIN
                           IF Name <> COMPANYNAME THEN BEGIN
                             ContiniaUserSetup.CHANGECOMPANY(Name);
                             PageID := PAGE::"Continia User List 2 (Comp.)";
                           END;

                           IF ContiniaUserSetup.GET(Text) THEN;
                           IF PAGE.RUNMODAL(PageID,ContiniaUserSetup) = ACTION::LookupOK THEN BEGIN
                             Text := ContiniaUserSetup."Continia User ID";
                             EXIT(TRUE);
                           END;
                         END;
                          }

    { 1160040005;2;Field  ;
                DecimalPlaces=0:0;
                BlankZero=Yes;
                SourceExpr=PurchAmtApvlLimit;
                CaptionClass='3,'+UserSetup.FIELDCAPTION("Purchase Amount Approval Limit");
                OnValidate=BEGIN
                             UpdateUserSetupField(UserSetup.FIELDNO("Purchase Amount Approval Limit"),PurchAmtApvlLimit);
                             CurrPage.UPDATE(FALSE);
                           END;
                            }

    { 1160040006;2;Field  ;
                SourceExpr=UnlimitedPurchApproval;
                CaptionClass='3,'+UserSetup.FIELDCAPTION("Unlimited Purchase Approval");
                OnValidate=BEGIN
                             UpdateUserSetupField(UserSetup.FIELDNO("Unlimited Purchase Approval"),UnlimitedPurchApproval);
                             CurrPage.UPDATE(FALSE);
                           END;
                            }

    { 1160040007;2;Field  ;
                DecimalPlaces=0:0;
                BlankZero=Yes;
                SourceExpr=ReqAmtApvlLimit;
                CaptionClass='3,'+UserSetup.FIELDCAPTION("Request Amount Approval Limit");
                Visible=FALSE;
                OnValidate=BEGIN
                             UpdateUserSetupField(UserSetup.FIELDNO("Request Amount Approval Limit"),ReqAmtApvlLimit);
                             CurrPage.UPDATE(FALSE);
                           END;
                            }

    { 1160040008;2;Field  ;
                SourceExpr=UnlimitedReqApproval;
                CaptionClass='3,'+UserSetup.FIELDCAPTION("Unlimited Request Approval");
                Visible=FALSE;
                OnValidate=BEGIN
                             UpdateUserSetupField(UserSetup.FIELDNO("Unlimited Request Approval"),UnlimitedReqApproval);
                             CurrPage.UPDATE(FALSE);
                           END;
                            }

    { 1160040009;2;Field  ;
                DecimalPlaces=0:0;
                BlankZero=Yes;
                SourceExpr=SalesAmtApvlLimit;
                CaptionClass='3,'+UserSetup.FIELDCAPTION("Sales Amount Approval Limit");
                Visible=FALSE;
                OnValidate=BEGIN
                             UpdateUserSetupField(UserSetup.FIELDNO("Sales Amount Approval Limit"),SalesAmtApvlLimit);
                             CurrPage.UPDATE(FALSE);
                           END;
                            }

    { 1160040010;2;Field  ;
                SourceExpr=UnlimitedSalesApproval;
                CaptionClass='3,'+UserSetup.FIELDCAPTION("Unlimited Sales Approval");
                Visible=FALSE;
                OnValidate=BEGIN
                             UpdateUserSetupField(UserSetup.FIELDNO("Unlimited Sales Approval"),UnlimitedSalesApproval);
                             CurrPage.UPDATE(FALSE);
                           END;
                            }

    { 1160040011;2;Field  ;
                SourceExpr=WebApprovalUser;
                CaptionClass='3,' + CompContiniaUserSetup.FIELDCAPTION("Web Approval User");
                OnValidate=BEGIN
                             UpdateContiniaUserSetupField(CompContiniaUserSetup.FIELDNO("Web Approval User"),WebApprovalUser);
                             CurrPage.UPDATE(FALSE);
                           END;
                            }

    { 1160040012;2;Field  ;
                SourceExpr=CanEditPostingLines;
                CaptionClass='3,' + CompContiniaUserSetup.FIELDCAPTION("Can Edit Posting Lines");
                OnValidate=BEGIN
                             UpdateContiniaUserSetupField(CompContiniaUserSetup.FIELDNO("Can Edit Posting Lines"),CanEditPostingLines);
                             CurrPage.UPDATE(FALSE);
                           END;
                            }

    { 1160040014;2;Field  ;
                SourceExpr=DocumentSearch;
                CaptionClass='3,' + CompContiniaUserSetup.FIELDCAPTION("Document Search");
                OnValidate=BEGIN
                             UpdateContiniaUserSetupField(CompContiniaUserSetup.FIELDNO("Document Search"),DocumentSearch);
                             CurrPage.UPDATE(FALSE);
                           END;
                            }

    { 1160040015;2;Field  ;
                SourceExpr=EMUser;
                CaptionClass='3,' + CompContiniaUserSetup.FIELDCAPTION("Expense Management User");
                Visible=CEMEnabled;
                OnValidate=BEGIN
                             UpdateContiniaUserSetupField(CompContiniaUserSetup.FIELDNO("Expense Management User"),EMUser);
                             CurrPage.UPDATE(FALSE);
                           END;
                            }

    { 1160040016;2;Field  ;
                SourceExpr=VendorNo;
                CaptionClass='3,' + CompContiniaUserSetup.FIELDCAPTION("Vendor No.");
                Visible=CEMEnabled;
                OnValidate=BEGIN
                             UpdateContiniaUserSetupField(CompContiniaUserSetup.FIELDNO("Vendor No."),VendorNo);
                             CurrPage.UPDATE(FALSE);
                           END;

                OnLookup=VAR
                           Vendor@6085574 : Record 23;
                           PageID@6085573 : Integer;
                         BEGIN
                           IF Name <> COMPANYNAME THEN BEGIN
                             Vendor.CHANGECOMPANY(Name);
                             PageID := PAGE::"Vendor List (Comp.)";
                           END;

                           IF Vendor.GET(Text) THEN;
                           IF PAGE.RUNMODAL(PageID,Vendor) = ACTION::LookupOK THEN BEGIN
                             Text := Vendor."No.";
                             EXIT(TRUE);
                           END;
                         END;
                          }

    { 1160040013;2;Field  ;
                SourceExpr=EmployeeNo;
                CaptionClass='3,' + CompContiniaUserSetup.FIELDCAPTION("Employee No.");
                Visible=CEMEnabled;
                OnValidate=BEGIN
                             UpdateContiniaUserSetupField(CompContiniaUserSetup.FIELDNO("Employee No."),EmployeeNo);
                             CurrPage.UPDATE(FALSE);
                           END;

                OnLookup=VAR
                           Employee@1160040000 : Record 5200;
                           PageID@1160040001 : Integer;
                         BEGIN
                           IF Name <> COMPANYNAME THEN BEGIN
                             Employee.CHANGECOMPANY(Name);
                             PageID := PAGE::"Employee List (Comp.)";
                           END;

                           IF Employee.GET(Text) THEN;
                           IF PAGE.RUNMODAL(PageID,Employee) = ACTION::LookupOK THEN BEGIN
                             Text := Employee."No.";
                             EXIT(TRUE);
                           END;
                         END;
                          }

    { 1160040017;2;Field  ;
                SourceExpr=ExpUserGroup;
                CaptionClass='3,' + CompContiniaUserSetup.FIELDCAPTION("Expense User Group");
                Visible=CEMEnabled;
                OnValidate=BEGIN
                             UpdateContiniaUserSetupField(CompContiniaUserSetup.FIELDNO("Expense User Group"),ExpUserGroup);
                             CurrPage.UPDATE(FALSE);
                           END;

                OnLookup=VAR
                           EMUserGroup@6085573 : Record 6086302;
                           PageID@1160040002 : Integer;
                         BEGIN
                           IF Name <> COMPANYNAME THEN BEGIN
                             EMUserGroup.CHANGECOMPANY(Name);
                             PageID := CEMUserMgt.GetEMUserGroupsPageNo;
                           END;

                           IF EMUserGroup.GET(Text) THEN;
                           IF PAGE.RUNMODAL(PageID,EMUserGroup) = ACTION::LookupOK THEN BEGIN
                             Text := EMUserGroup.Code;
                             EXIT(TRUE);
                           END;
                         END;
                          }

    { 1160040018;2;Field  ;
                SourceExpr=ExpReminderCode;
                CaptionClass='3,' + CompContiniaUserSetup.FIELDCAPTION("Expense Reminder Code");
                Visible=CEMEnabled;
                OnValidate=BEGIN
                             UpdateContiniaUserSetupField(CompContiniaUserSetup.FIELDNO("Expense Reminder Code"),ExpReminderCode);
                             CurrPage.UPDATE(FALSE);
                           END;

                OnLookup=VAR
                           EMReminderTerms@6085573 : Record 6086317;
                           PageID@1160040002 : Integer;
                         BEGIN
                           IF Name <> COMPANYNAME THEN BEGIN
                             EMReminderTerms.CHANGECOMPANY(Name);
                             PageID := CEMUserMgt.EMReminderTermsPageNo;
                           END;

                           IF EMReminderTerms.GET(Text) THEN;
                           IF PAGE.RUNMODAL(PageID,EMReminderTerms) = ACTION::LookupOK THEN BEGIN
                             Text := EMReminderTerms.Code;
                             EXIT(TRUE);
                           END;
                         END;
                          }

    { 1160040019;2;Field  ;
                DecimalPlaces=0:0;
                BlankZero=Yes;
                SourceExpr=ExpAmountApprovalLimit;
                CaptionClass='3,' + CompContiniaUserSetup.FIELDCAPTION("Expense Amount Approval Limit");
                Visible=CEMEnabled;
                OnValidate=BEGIN
                             UpdateContiniaUserSetupField(CompContiniaUserSetup.FIELDNO("Expense Amount Approval Limit"),ExpAmountApprovalLimit);
                             CurrPage.UPDATE(FALSE);
                           END;
                            }

    { 1160040020;2;Field  ;
                SourceExpr=UnlimitedExpApproval;
                CaptionClass='3,' + CompContiniaUserSetup.FIELDCAPTION("Unlimited Expense Approval");
                Visible=CEMEnabled;
                OnValidate=BEGIN
                             UpdateContiniaUserSetupField(CompContiniaUserSetup.FIELDNO("Unlimited Expense Approval"),UnlimitedExpApproval);
                             CurrPage.UPDATE(FALSE);
                           END;
                            }

    { 1160040021;2;Field  ;
                BlankZero=Yes;
                SourceExpr=NoOfAppvlSharingToThisUser;
                CaptionClass='3,' + CompContiniaUserSetup.FIELDCAPTION("Shared to this user");
                Editable=FALSE;
                OnDrillDown=BEGIN
                              DrillDownAppvlShareToThisUser;
                              CurrPage.UPDATE(FALSE);
                            END;
                             }

    { 1160040022;2;Field  ;
                BlankZero=Yes;
                SourceExpr=NoOfAppvlSharingToOtherUsers;
                CaptionClass='3,' + CompContiniaUserSetup.FIELDCAPTION("Shared to other users");
                Editable=FALSE;
                OnDrillDown=BEGIN
                              DrillDownAppvlShareToOtherUser;
                              CurrPage.UPDATE(FALSE);
                            END;
                             }

    { 1160040029;0;Container;
                ContainerType=FactBoxArea }

    { 1160040028;1;Part   ;
                Name=AppUserGrpMember;
                PagePartID=Page6085661;
                PartType=Page }

    { 1160040027;1;Part   ;
                Name=ContiniaUserPermissions;
                PagePartID=Page6085662;
                PartType=Page }

  }
  CODE
  {
    VAR
      CompContiniaUserSetup@1160040025 : Record 6086002;
      ContiniaUser@1160040024 : Record 6086001;
      UserSetup@1160040023 : Record 91;
      CEMUserMgt@1160040022 : Codeunit 6085782;
      CEMEnabled@1160040027 : Boolean INDATASET;
      UnlimitedPurchApproval@1160040021 : Boolean;
      UnlimitedReqApproval@1160040020 : Boolean;
      UnlimitedSalesApproval@1160040019 : Boolean;
      SalesPurchCode@1160040018 : Code[10];
      ApproverID@1160040017 : Code[50];
      SecondApproverID@1160040030 : Code[50];
      PurchAmtApvlLimit@1160040016 : Decimal;
      ReqAmtApvlLimit@1160040015 : Decimal;
      SalesAmtApvlLimit@1160040014 : Decimal;
      CurrUserID@1160040013 : Code[50];
      HasAccess@1160040012 : Boolean;
      WebApprovalUser@1160040011 : Boolean;
      CanEditPostingLines@1160040010 : Boolean;
      DocumentSearch@1160040008 : 'All,Own';
      EMUser@1160040007 : Boolean;
      VendorNo@1160040006 : Code[20];
      EmployeeNo@1160040009 : Code[20];
      ExpUserGroup@1160040005 : Code[20];
      ExpReminderCode@1160040004 : Code[10];
      ExpAmountApprovalLimit@1160040003 : Decimal;
      UnlimitedExpApproval@1160040002 : Boolean;
      NoOfAppvlSharingToThisUser@1160040001 : Integer;
      NoOfAppvlSharingToOtherUsers@1160040000 : Integer;
      IsCurrCompany@1160040026 : Boolean INDATASET;
      NoAccessErr1@1160040028 : TextConst 'ENU=%1 does not have access to company %2.';
      NoAccessErr2@1160040029 : TextConst 'ENU=%2 (%1) does not have access to company %3.';

    PROCEDURE UpdateFields@1160040002();
    BEGIN
      HasAccess := FALSE;
      CompContiniaUserSetup.CHANGECOMPANY(Name);
      IF CompContiniaUserSetup.GET(CurrUserID) THEN BEGIN
        HasAccess := TRUE;
        CompContiniaUserSetup.SetCurrCompanyName(Name);
      END ELSE
        CLEAR(CompContiniaUserSetup);

      WebApprovalUser := CompContiniaUserSetup."Web Approval User";
      CanEditPostingLines := CompContiniaUserSetup."Can Edit Posting Lines";
      DocumentSearch := CompContiniaUserSetup."Document Search";
      EMUser := CompContiniaUserSetup."Expense Management User";
      VendorNo := CompContiniaUserSetup."Vendor No.";
      EmployeeNo := CompContiniaUserSetup."Employee No.";
      ExpUserGroup := CompContiniaUserSetup."Expense User Group";
      ExpReminderCode := CompContiniaUserSetup."Expense Reminder Code";
      ExpAmountApprovalLimit := CompContiniaUserSetup."Expense Amount Approval Limit";
      UnlimitedExpApproval := CompContiniaUserSetup."Unlimited Expense Approval";
      NoOfAppvlSharingToThisUser := CompContiniaUserSetup.GetNoOfAppvlShareToThisUser;
      NoOfAppvlSharingToOtherUsers := CompContiniaUserSetup.GetNoOfAppvlShareToOtherUsers;
      SecondApproverID := CompContiniaUserSetup."4-eyes, 2nd Approver ID";

      UserSetup.CHANGECOMPANY(Name);
      IF NOT UserSetup.GET(CompContiniaUserSetup."Continia User ID") THEN
        CLEAR(UserSetup);

      SalesPurchCode := UserSetup."Salespers./Purch. Code";
      ApproverID := UserSetup."Approver ID";
      UnlimitedPurchApproval := UserSetup."Unlimited Purchase Approval";
      PurchAmtApvlLimit := UserSetup."Purchase Amount Approval Limit";
      UnlimitedReqApproval := UserSetup."Unlimited Request Approval";
      ReqAmtApvlLimit := UserSetup."Request Amount Approval Limit";
      UnlimitedSalesApproval := UserSetup."Unlimited Sales Approval";
      SalesAmtApvlLimit := UserSetup."Sales Amount Approval Limit";
    END;

    PROCEDURE SetUSerID@1160040001(NewUserID@1160040000 : Code[50]);
    BEGIN
      CurrUserID := NewUserID;
    END;

    PROCEDURE UpdateContiniaUserSetupField@1160040010(FieldNo@1160040001 : Integer;Value@1160040000 : Variant);
    VAR
      RecRef@1160040002 : RecordRef;
      FieldRef@1160040003 : FieldRef;
    BEGIN
      CompContiniaUserSetup.CHANGECOMPANY(Name);
      CompContiniaUserSetup.GET(CurrUserID);
      CompContiniaUserSetup.SetCurrCompanyName(Name);

      IF FieldNo = CompContiniaUserSetup.FIELDNO("Document Search") THEN BEGIN
        CompContiniaUserSetup.VALIDATE("Document Search",Value);
        CompContiniaUserSetup.MODIFY;
        EXIT;
      END;

      RecRef.GETTABLE(CompContiniaUserSetup);
      FieldRef := RecRef.FIELD(FieldNo);
      IF FORMAT(FieldRef.VALUE) = FORMAT(Value) THEN
        EXIT;

      FieldRef.VALIDATE(Value);
      RecRef.MODIFY;
    END;

    PROCEDURE UpdateUserSetupField@1160040000(FieldNo@1160040001 : Integer;Value@1160040000 : Variant);
    BEGIN
      CompContiniaUserSetup.CHANGECOMPANY(Name);
      CompContiniaUserSetup.GET(CurrUserID);
      CompContiniaUserSetup.SetCurrCompanyName(Name);
      CompContiniaUserSetup.UpdateUserSetupField(FieldNo,Value);
    END;

    PROCEDURE DrillDownAppvlShareToThisUser@1160040007();
    BEGIN
      CompContiniaUserSetup.CHANGECOMPANY(Name);
      CompContiniaUserSetup.GET(CurrUserID);
      CompContiniaUserSetup.SetCurrCompanyName(Name);
      CompContiniaUserSetup.DrillDownAppvlShareToThisUser(Name);
    END;

    PROCEDURE DrillDownAppvlShareToOtherUser@1160040008();
    BEGIN
      CompContiniaUserSetup.CHANGECOMPANY(Name);
      CompContiniaUserSetup.GET(CurrUserID);
      CompContiniaUserSetup.SetCurrCompanyName(Name);
      CompContiniaUserSetup.DrillDownAppvlShareToOtherUser(Name);
    END;

    PROCEDURE GetDataCaptionExpr@1160040003() : Text[80];
    BEGIN
      IF ContiniaUser.GET(CurrUserID) THEN BEGIN
        IF ContiniaUser.Name = '' THEN
          EXIT(CurrUserID)
        ELSE
          EXIT(STRSUBSTNO('%1 - %2',CurrUserID,ContiniaUser.Name));
      END ELSE
        EXIT(CurrUserID);
    END;

    PROCEDURE CheckHasAccess@1160040004();
    BEGIN
      IF NOT HasAccess THEN BEGIN
        ContiniaUser.GET(CurrUserID);
        IF ContiniaUser.Name = '' THEN
          ERROR(NoAccessErr1,CurrUserID,Name)
        ELSE
          ERROR(NoAccessErr2,CurrUserID,ContiniaUser.Name,Name)
      END;
    END;

    BEGIN
    END.
  }
}

