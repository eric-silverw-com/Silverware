OBJECT Codeunit 6086383 Exp. Header Inbox - Process
{
  OBJECT-PROPERTIES
  {
    Date=09/23/16;
    Time=[ 1:00:00 AM];
    Version List=EMW16.00.10.2.50;
  }
  PROPERTIES
  {
    TableNo=6086341;
    OnRun=BEGIN
            ExpHeaderInbox.COPY(Rec);
            Code;
            Rec := ExpHeaderInbox;
          END;

  }
  CODE
  {
    VAR
      ExpHeaderInbox@1160040004 : Record 6086341;
      ExpHeaderInboxCheck@1160040001 : Codeunit 6086384;

    LOCAL PROCEDURE Code@1160040000();
    VAR
      EMSetup@1160040005 : Record 6086300;
      ExpHeader@1160040004 : Record 6086339;
      ExpHeaderValidate@1160040006 : Codeunit 6086381;
    BEGIN
      WITH ExpHeaderInbox DO BEGIN
        IF Status = Status::Accepted THEN
          EXIT;

        IF NOT ExpHeaderInboxCheck.RUN(ExpHeaderInbox) THEN BEGIN
          "Error Text" := COPYSTR(GETLASTERRORTEXT,1,MAXSTRLEN("Error Text"));
          Status := Status::Error;

          IF "Expense Header No." = '' THEN BEGIN
            ExpHeader.SETCURRENTKEY("Exp. Header GUID");
            ExpHeader.SETRANGE("Exp. Header GUID","Exp. Header GUID");
            IF ExpHeader.FINDFIRST THEN BEGIN
              "Expense Header No." := ExpHeader."No.";
              "Expense Header Type" := ExpHeader."Document Type";
            END;
          END;
        END ELSE BEGIN
          "Expense Header Type" := ExpHeaderInboxCheck.GetExpHeaderType;
          "Expense Header No." := ExpHeaderInboxCheck.GetExpHeaderNo;
          "Error Text" := '';
          Status := Status::Accepted;
        END;

        "Created Date/Time" := CURRENTDATETIME;
        "Created by User ID" := USERID;
        MODIFY;

        IF "Expense Header No." <> '' THEN BEGIN
          ExpHeader.GET("Expense Header Type","Expense Header No.");
          ExpHeaderValidate.RUN(ExpHeader);
        END;

      END;
    END;

    BEGIN
    END.
  }
}

