OBJECT Codeunit 6085637 Document File System
{
  OBJECT-PROPERTIES
  {
    Date=01/30/18;
    Time=[ 1:00:00 AM];
    Version List=DCW13.70.00.4.50.05;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      ContiniaCompanySetup@6085574 : Record 6192773;
      DCSetup@1000 : Record 6085573;
      FileSystemMgt@1001 : Codeunit 6085584;
      CurrentCompanyName@6085573 : Text[50];
      GotContiniaCompanySetup@6085575 : Boolean;
      GotDCSetup@1002 : Boolean;

    PROCEDURE GetTiffServerFileName@10(VAR Document@1000 : Record 6085590) FullFilename : Text[1024];
    BEGIN
      GetDCSetup;
      DCSetup.TESTFIELD("Archive File Path");
      FullFilename := GetServerFilePath(Document,Document."No.",DCSetup."Archive File Path",'tiff');
      Document.CreateDirectory(FullFilename);
    END;

    PROCEDURE GetPdfServerFileName@9(VAR Document@1000 : Record 6085590) FullFilename : Text[1024];
    BEGIN
      GetDCSetup;
      DCSetup.TESTFIELD("Archive File Path");
      FullFilename := GetServerFilePath(Document,Document."No.",DCSetup."Archive File Path",'pdf');
      Document.CreateDirectory(FullFilename);
    END;

    PROCEDURE GetEMailServerFileName@8(VAR Document@1000 : Record 6085590) FullFilename : Text[1024];
    VAR
      Document2@6085573 : Record 6085590;
      FilenameGuid@1160040000 : Text[50];
    BEGIN
      IF ISNULLGUID(Document."E-Mail GUID") THEN
        EXIT;

      GetDCSetup;
      DCSetup.TESTFIELD("Archive File Path");

      Document2.SETRANGE("E-Mail GUID",Document."E-Mail GUID");
      Document2.FINDFIRST;

      FullFilename := GetServerFilePath(Document2,Document2."No.",DCSetup."Archive File Path",'eml');

      IF NOT FILE.EXISTS(FullFilename) THEN
        FullFilename := GetServerFilePath(Document,GetEmailGUIDAsText(Document),DCSetup."Archive File Path",'eml');

      Document.CreateDirectory(FullFilename);
    END;

    PROCEDURE GetMiscServerFileName@1(VAR Document@1000 : Record 6085590) FullFilename : Text[1024];
    BEGIN
      GetDCSetup;
      DCSetup.TESTFIELD("Miscellaneous File Path");
      FullFilename := GetServerFilePath(Document,Document."No.",DCSetup."Miscellaneous File Path",Document."File Extension");
      Document.CreateDirectory(FullFilename)
    END;

    PROCEDURE SetTiffFile@3(FilePath@1000 : Text[250];VAR Document@1001 : Record 6085590) : Boolean;
    BEGIN
      IF FilePath <> Document.GetTiffServerFileName THEN
        EXIT(FileSystemMgt.CopyFile(FilePath,Document.GetTiffServerFileName));
    END;

    PROCEDURE SetPdfFile@2(FilePath@1000 : Text[250];VAR Document@1001 : Record 6085590) : Boolean;
    BEGIN
      IF FilePath <> Document.GetPdfServerFileName THEN
        EXIT(FileSystemMgt.CopyFile(FilePath,Document.GetPdfServerFileName));
    END;

    PROCEDURE SetMiscFile@12(FilePath@1000 : Text[250];VAR Document@1001 : Record 6085590) : Boolean;
    BEGIN
      IF FilePath <> Document.GetMiscServerFileName THEN
        EXIT(FileSystemMgt.CopyFile(FilePath,Document.GetMiscServerFileName));
    END;

    PROCEDURE SetEmailFile@7(FilePath@1000 : Text[250];VAR Document@1001 : Record 6085590) : Boolean;
    BEGIN
      IF FilePath <> Document.GetEMailServerFileName THEN
        EXIT(FileSystemMgt.CopyFile(FilePath,Document.GetEMailServerFileName));
    END;

    PROCEDURE HasTiffFile@16(VAR Document@1000 : Record 6085590) : Boolean;
    BEGIN
      EXIT(FILE.EXISTS(Document.GetTiffServerFileName));
    END;

    PROCEDURE HasPdfFile@15(VAR Document@1000 : Record 6085590) : Boolean;
    BEGIN
      EXIT(FILE.EXISTS(Document.GetPdfServerFileName));
    END;

    PROCEDURE HasMiscFile@17(VAR Document@1000 : Record 6085590) : Boolean;
    BEGIN
      EXIT(FILE.EXISTS(Document.GetMiscServerFileName));
    END;

    PROCEDURE HasEmailFile@18(VAR Document@1000 : Record 6085590) : Boolean;
    BEGIN
      EXIT(FILE.EXISTS(Document.GetEMailServerFileName));
    END;

    PROCEDURE ClearTiffFile@14(VAR Document@1001 : Record 6085590) : Boolean;
    BEGIN
      IF HasTiffFile(Document) THEN
        EXIT(FileSystemMgt.DeleteFile(Document.GetTiffServerFileName,TRUE));
    END;

    PROCEDURE ClearPdfFile@13(VAR Document@1001 : Record 6085590) : Boolean;
    BEGIN
      IF HasPdfFile(Document) THEN
        EXIT(FileSystemMgt.DeleteFile(Document.GetPdfServerFileName,TRUE));
    END;

    PROCEDURE ClearMiscFile@6(VAR Document@1001 : Record 6085590) : Boolean;
    BEGIN
      IF HasMiscFile(Document) THEN
        EXIT(FileSystemMgt.DeleteFile(Document.GetMiscServerFileName,TRUE));
    END;

    PROCEDURE ClearEmailFile@5(VAR Document@1001 : Record 6085590) : Boolean;
    VAR
      FullFilename@1000 : Text[1024];
      NewFullFilename@1002 : Text[1024];
    BEGIN
      IF NOT EmailHasMoreDocuments(Document.GetEMailServerFileName,Document) AND HasEmailFile(Document) THEN
        EXIT(FileSystemMgt.DeleteFile(Document.GetEMailServerFileName,TRUE));

      IF HasEmailFile(Document) THEN BEGIN
        FullFilename := GetServerFilePath(Document,Document."No.",DCSetup."Archive File Path",'eml');

        IF FILE.EXISTS(FullFilename) THEN BEGIN
          NewFullFilename := GetServerFilePath(Document,GetEmailGUIDAsText(Document),DCSetup."Archive File Path",'eml');
          FileSystemMgt.MoveFile(FullFilename,NewFullFilename);
        END;
      END;
    END;

    PROCEDURE GetStorageLocation@6085574() : Text[1024];
    VAR
      Document@6085573 : Record 6085590;
    BEGIN
      IF Document.FINDFIRST THEN;

      GetDCSetup;
      GetContiniaCompanySetup;

      WITH DCSetup DO
        EXIT(GetSubDir(Document,"Disk File Directory Structure","Company Code in Archive","Category Code in Archive",
          ContiniaCompanySetup."Company Code",Document.FIELDCAPTION("Document Category Code")));
    END;

    PROCEDURE GetEmailGUIDAsText@1160040016(VAR Document@6085573 : Record 6085590) : Text[50];
    BEGIN
      EXIT(COPYSTR(FORMAT(Document."E-Mail GUID"),2,36));
    END;

    LOCAL PROCEDURE EmailHasMoreDocuments@1160040019(EmailFilePath@1160040003 : Text[1024];Document@1000 : Record 6085590) : Boolean;
    VAR
      Document2@1160040000 : Record 6085590;
      FileSysMgt@1160040002 : Codeunit 6085584;
      EmptyGuid@1160040001 : GUID;
    BEGIN
      WITH Document DO BEGIN
        IF "E-Mail GUID" = EmptyGuid THEN
          EXIT;

        Document2.SETCURRENTKEY("E-Mail GUID");
        Document2.SETRANGE("E-Mail GUID","E-Mail GUID");
        Document2.SETFILTER("No.",'<>%1',"No.");
        IF Document2.FINDSET THEN
          REPEAT
            IF (EmailFilePath = Document2.GetEMailServerFileName) AND
              FILE.EXISTS(Document2.GetTiffServerFileName)
            THEN
              EXIT(TRUE);
          UNTIL Document2.NEXT = 0;
      END;
    END;

    LOCAL PROCEDURE GetServerFilePath@6085573(VAR Document@6085575 : Record 6085590;Identifier@6085573 : Text[50];Path@6085576 : Text[200];Extension@6085574 : Text[10]) FullFilename : Text[1024];
    BEGIN
      IF Identifier = '' THEN
        EXIT;

      FullFilename := STRSUBSTNO('%1%2%3.%4',Path,GetDocSubDir(Document),Identifier,Extension);
    END;

    LOCAL PROCEDURE GetDocSubDir@161024017(Document@161024012 : Record 6085590) : Text[1024];
    BEGIN
      GetContiniaCompanySetup;
      GetDCSetup;

      WITH DCSetup DO
        EXIT(GetSubDir(Document,"Disk File Directory Structure","Company Code in Archive","Category Code in Archive",
          ContiniaCompanySetup."Company Code",Document."Document Category Code"));
    END;

    LOCAL PROCEDURE GetSubDir@161024018(Document@1160040003 : Record 6085590;Structure@161024012 : Integer;CompanyCodeInArchive@1160040000 : Boolean;DocCatCodeInArchive@1000 : Boolean;CompanyCode@1160040002 : Code[10];CategoryCode@1001 : Code[100]) : Text[1024];
    VAR
      Month@161024014 : Code[2];
      Day@161024015 : Code[2];
      Path@1160040001 : Text[1024];
    BEGIN
      IF Document."Import Month" < 10 THEN
        Month := '0' + FORMAT(Document."Import Month")
      ELSE
        Month := FORMAT(Document."Import Month");

      IF Document."Import Day" < 10 THEN
        Day := '0' + FORMAT(Document."Import Day")
      ELSE
        Day := FORMAT(Document."Import Day");

      IF CompanyCodeInArchive AND (CompanyCode <> '') THEN
        Path := CompanyCode + '\';

      IF DocCatCodeInArchive AND (CategoryCode <> '') THEN
        Path := Path + CategoryCode + '\';

      CASE Structure OF
        DCSetup."Disk File Directory Structure"::"One Directory":
          EXIT(Path);
        DCSetup."Disk File Directory Structure"::"Year\Month":
          EXIT(Path + STRSUBSTNO('%1\%2\',Document."Import Year",Month));
        DCSetup."Disk File Directory Structure"::"Year\Month\Day":
          EXIT(Path + STRSUBSTNO('%1\%2\%3\',Document."Import Year",Month,Day));
      END;
    END;

    LOCAL PROCEDURE GetContiniaCompanySetup@6085575();
    BEGIN
      IF NOT GotContiniaCompanySetup THEN
        GotContiniaCompanySetup := ContiniaCompanySetup.GET;
    END;

    LOCAL PROCEDURE GetDCSetup@11();
    BEGIN
      IF NOT GotDCSetup THEN
        GotDCSetup := DCSetup.GET;
    END;

    PROCEDURE SetCurrentCompany@21(NewCompanyName@1000 : Text[50]);
    BEGIN
      CurrentCompanyName := NewCompanyName;
      ContiniaCompanySetup.CHANGECOMPANY(CurrentCompanyName);
    END;

    PROCEDURE SetDCSetup@4(VAR NewDCSetup@1000 : Record 6085573);
    BEGIN
      DCSetup := NewDCSetup;
      GotDCSetup := TRUE;
    END;

    BEGIN
    END.
  }
}

