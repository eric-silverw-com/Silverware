OBJECT Page 521 Application Worksheet
{
  OBJECT-PROPERTIES
  {
    Date=11/27/18;
    Time=11:10:23 PM;
    Version List=;
  }
  PROPERTIES
  {
    CaptionML=ENU=Application Worksheet;
    ApplicationArea=#Basic,#Suite;
    SaveValues=Yes;
    InsertAllowed=No;
    DeleteAllowed=No;
    ModifyAllowed=Yes;
    SourceTable=Table32;
    PageType=Worksheet;
    UsageCategory=Tasks;
    OnOpenPage=BEGIN
                 Apply.SetCalledFromApplicationWorksheet(TRUE);
                 ReapplyTouchedEntries; // in case OnQueryClosePage trigger was not executed due to a sudden crash

                 InventoryPeriod.IsValidDate(InventoryOpenedFrom);
                 IF InventoryOpenedFrom <> 0D THEN
                   IF GETFILTER("Posting Date") = '' THEN
                     SETFILTER("Posting Date",'%1..',CALCDATE('<+1D>',InventoryOpenedFrom))
                   ELSE BEGIN
                     IF GETFILTER("Posting Date") <> STRSUBSTNO('%1..',CALCDATE('<+1D>',InventoryOpenedFrom)) THEN
                       SETFILTER("Posting Date",
                         STRSUBSTNO('%2&%1..',CALCDATE('<+1D>',InventoryOpenedFrom),GETFILTER("Posting Date")))
                   END;

                 UpdateFilterFields;
               END;

    OnFindRecord=VAR
                   Found@1000 : Boolean;
                 BEGIN
                   Found := FIND(Which);
                   IF NOT Found THEN ;
                   EXIT(Found);
                 END;

    OnQueryClosePage=BEGIN
                       IF Apply.AnyTouchedEntries THEN BEGIN
                         IF NOT CONFIRM(Text003) THEN
                           EXIT(FALSE);

                         UnblockItems;
                         Reapplyall;
                       END;

                       EXIT(TRUE);
                     END;

    OnAfterGetCurrRecord=BEGIN
                           UpdateFilterFields;
                         END;

    ActionList=ACTIONS
    {
      { 1900000003;0 ;ActionContainer;
                      ActionContainerType=RelatedInformation }
      { 15      ;1   ;ActionGroup;
                      CaptionML=ENU=V&iew;
                      Image=View }
      { 28      ;2   ;Action    ;
                      Name=AppliedEntries;
                      ShortCutKey=F9;
                      CaptionML=ENU=Applied Entries;
                      ToolTipML=ENU=View the ledger entries that have been applied to this record.;
                      ApplicationArea=#Basic,#Suite;
                      Image=Approve;
                      OnAction=BEGIN
                                 CLEAR(ApplicationsForm);
                                 ApplicationsForm.SetRecordToShow(Rec,Apply,TRUE);
                                 ApplicationsForm.RUN;
                                 InsertUnapplyItem("Item No.");
                                 CurrPage.UPDATE;
                               END;
                                }
      { 16      ;2   ;Action    ;
                      Name=UnappliedEntries;
                      ShortCutKey=Ctrl+F9;
                      CaptionML=ENU=Unapplied Entries;
                      ToolTipML=ENU=View entries that you have unapplied.;
                      ApplicationArea=#Basic,#Suite;
                      Image=Entries;
                      OnAction=BEGIN
                                 CLEAR(ApplicationsForm);
                                 ApplicationsForm.SetRecordToShow(Rec,Apply,FALSE);
                                 ApplicationsForm.LOOKUPMODE := TRUE;
                                 IF ApplicationsForm.RUNMODAL = ACTION::LookupOK THEN
                                   ApplicationsForm.ApplyRec;

                                 CurrPage.UPDATE;
                               END;
                                }
      { 36      ;1   ;ActionGroup;
                      CaptionML=ENU=Ent&ry;
                      Image=Entry }
      { 37      ;2   ;Action    ;
                      AccessByPermission=TableData 348=R;
                      ShortCutKey=Shift+Ctrl+D;
                      CaptionML=ENU=Dimensions;
                      ToolTipML=ENU=View or edit dimensions, such as area, project, or department, that you can assign to sales and purchase documents to distribute costs and analyze transaction history.;
                      ApplicationArea=#Dimensions;
                      Image=Dimensions;
                      OnAction=BEGIN
                                 ShowDimensions;
                                 CurrPage.SAVERECORD;
                               END;
                                }
      { 48      ;2   ;Action    ;
                      ShortCutKey=Ctrl+F7;
                      CaptionML=ENU=&Value Entries;
                      ToolTipML=ENU=View the history of posted amounts that affect the value of the item. Value entries are created for every transaction with the item.;
                      ApplicationArea=#Basic,#Suite;
                      RunObject=Page 5802;
                      RunPageView=SORTING(Item Ledger Entry No.);
                      RunPageLink=Item Ledger Entry No.=FIELD(Entry No.);
                      Image=ValueLedger }
      { 38      ;2   ;Action    ;
                      AccessByPermission=TableData 27=R;
                      CaptionML=ENU=Reservation Entries;
                      ToolTipML=ENU=View the entries for every reservation that is made, either manually or automatically.;
                      ApplicationArea=#Reservation;
                      Image=ReservationLedger;
                      OnAction=BEGIN
                                 ShowReservationEntries(TRUE);
                               END;
                                }
      { 1900000004;0 ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 30      ;1   ;ActionGroup;
                      Name=Functions;
                      CaptionML=ENU=F&unctions;
                      Image=Action }
      { 42      ;2   ;Action    ;
                      CaptionML=ENU=Rea&pply;
                      ToolTipML=ENU=Reapply entries that you have removed.;
                      ApplicationArea=#Basic,#Suite;
                      Image=Action;
                      OnAction=BEGIN
                                 Reapplyall;
                               END;
                                }
      { 3       ;2   ;Action    ;
                      Name=UndoApplications;
                      CaptionML=ENU=Undo Manual Changes;
                      ToolTipML=ENU=Undo your previous application change.;
                      ApplicationArea=#Basic,#Suite;
                      Image=Restore;
                      OnAction=BEGIN
                                 IF Apply.ApplicationLogIsEmpty THEN BEGIN
                                   MESSAGE(NothingToRevertMsg);
                                   EXIT;
                                 END;

                                 IF CONFIRM(RevertAllQst) THEN BEGIN
                                   Apply.UndoApplications;
                                   MESSAGE(RevertCompletedMsg);
                                 END
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 59  ;1   ;Group     ;
                CaptionML=ENU=General }

    { 68  ;2   ;Field     ;
                CaptionML=ENU=Date Filter;
                ToolTipML=ENU=Specifies the date interval by which values are filtered.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=DateFilter;
                OnValidate=VAR
                             TextManagement@1000 : Codeunit 41;
                           BEGIN
                             TextManagement.MakeDateFilter(DateFilter);
                             SETFILTER("Posting Date",DateFilter);
                             DateFilter := GETFILTER("Posting Date");
                             DateFilterOnAfterValidate;
                           END;
                            }

    { 73  ;2   ;Field     ;
                Name=Item Filter;
                CaptionML=ENU=Item Filter;
                ToolTipML=ENU=Specifies a filter to limit the item ledger entries in the first table of the application worksheet to those that have item numbers.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=ItemFilter;
                TableRelation=Item;
                OnValidate=BEGIN
                             ItemFilterOnAfterValidate;
                           END;

                OnLookup=VAR
                           ItemList@1002 : Page 31;
                         BEGIN
                           ItemList.LOOKUPMODE(TRUE);
                           IF ItemList.RUNMODAL = ACTION::LookupOK THEN BEGIN
                             Text := ItemList.GetSelectionFilter;
                             EXIT(TRUE);
                           END;
                         END;
                          }

    { 64  ;2   ;Field     ;
                CaptionML=ENU=Document No. Filter;
                ToolTipML=ENU=Specifies a filter to limit the item ledger entries in the first table of the application worksheet, to those that have document numbers.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=DocumentFilter;
                OnValidate=BEGIN
                             SETFILTER("Document No.",DocumentFilter);
                             DocumentFilter := GETFILTER("Document No.");
                             DocumentFilterOnAfterValidate;
                           END;
                            }

    { 78  ;2   ;Field     ;
                CaptionML=ENU=Location Filter;
                ToolTipML=ENU=Specifies a filter to limit the item ledger entries in the first table of the application worksheet to those that have locations.;
                ApplicationArea=#Location;
                SourceExpr=LocationFilter;
                TableRelation=Location;
                OnValidate=BEGIN
                             SETFILTER("Location Code",LocationFilter);
                             LocationFilter := GETFILTER("Location Code");
                             LocationFilterOnAfterValidate;
                           END;

                OnLookup=VAR
                           LocationList@1002 : Page 15;
                         BEGIN
                           LocationList.LOOKUPMODE(TRUE);
                           IF LocationList.RUNMODAL = ACTION::LookupOK THEN BEGIN
                             Text := LocationList.GetSelectionFilter;
                             EXIT(TRUE);
                           END;
                         END;
                          }

    { 1   ;1   ;Group     ;
                Enabled=TRUE;
                Editable=FALSE;
                GroupType=Repeater }

    { 4   ;2   ;Field     ;
                ToolTipML=ENU=Specifies the number of the item in the entry.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Item No." }

    { 62  ;2   ;Field     ;
                ToolTipML=ENU=Specifies the document number on the entry. The document is the voucher that the entry was based on, for example, a receipt.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Document No." }

    { 26  ;2   ;Field     ;
                ToolTipML=ENU=Specifies the code for the location that the entry is linked to.;
                ApplicationArea=#Location;
                SourceExpr="Location Code" }

    { 6   ;2   ;Field     ;
                ToolTipML=ENU=Specifies the entry's posting date.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Posting Date" }

    { 8   ;2   ;Field     ;
                ToolTipML=ENU=Specifies which type of transaction that the entry is created from.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Entry Type" }

    { 31  ;2   ;Field     ;
                ToolTipML=ENU=Specifies the source type that applies to the source number, shown in the Source No. field.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Source Type" }

    { 44  ;2   ;Field     ;
                ToolTipML=ENU=Specifies what type of document was posted to create the item ledger entry.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Document Type" }

    { 46  ;2   ;Field     ;
                ToolTipML=ENU=Specifies the number of the line on the posted document that corresponds to the item ledger entry.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Document Line No.";
                Visible=FALSE }

    { 76  ;2   ;Field     ;
                ToolTipML=ENU=Specifies the variant of the item on the line.;
                ApplicationArea=#Planning;
                SourceExpr="Variant Code";
                Visible=FALSE }

    { 70  ;2   ;Field     ;
                ToolTipML=ENU=Specifies a serial number if the posted item carries such a number.;
                ApplicationArea=#ItemTracking;
                SourceExpr="Serial No.";
                Visible=FALSE }

    { 72  ;2   ;Field     ;
                ToolTipML=ENU=Specifies a lot number if the posted item carries such a number.;
                ApplicationArea=#ItemTracking;
                SourceExpr="Lot No.";
                Visible=FALSE }

    { 10  ;2   ;Field     ;
                ToolTipML=ENU=Specifies where the entry originated.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Source No." }

    { 39  ;2   ;Field     ;
                ToolTipML=ENU=Specifies a description of the entry.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=Description;
                Visible=FALSE }

    { 18  ;2   ;Field     ;
                ToolTipML=ENU=Specifies the number of units of the item in the item entry.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=Quantity }

    { 20  ;2   ;Field     ;
                ToolTipML=ENU=Specifies the quantity in the Quantity field that remains to be processed.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Remaining Quantity" }

    { 22  ;2   ;Field     ;
                ToolTipML=ENU=Specifies how many units of the item on the line have been invoiced.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Invoiced Quantity" }

    { 40  ;2   ;Field     ;
                ToolTipML=ENU=Specifies how many units of the item on the line have been reserved.;
                ApplicationArea=#Reservation;
                SourceExpr="Reserved Quantity" }

    { 52  ;2   ;Field     ;
                ToolTipML=ENU=Specifies the quantity for this item ledger entry that was shipped and has not yet been returned.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Shipped Qty. Not Returned" }

    { 50  ;2   ;Field     ;
                ToolTipML=ENU=Specifies the adjusted cost, in $, of the quantity posting.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Cost Amount (Actual)" }

    { 66  ;2   ;Field     ;
                CaptionML=ENU=Unit Cost($);
                ToolTipML=ENU="Specifies the cost of one unit of the item. ";
                ApplicationArea=#Basic,#Suite;
                SourceExpr=GetUnitCostLCY;
                Visible=FALSE }

    { 25  ;2   ;Field     ;
                ToolTipML=ENU=Specifies whether the entry has been fully applied to.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=Open }

    { 34  ;2   ;Field     ;
                ToolTipML=ENU=Specifies whether the item in the item ledge entry is positive.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=Positive }

    { 54  ;2   ;Field     ;
                ToolTipML=ENU=Specifies if the quantity on the journal line must be applied to an already-posted entry. In that case, enter the entry number that the quantity will be applied to.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Applies-to Entry";
                Visible=FALSE }

    { 57  ;2   ;Field     ;
                ToolTipML=ENU=Specifies whether there is one or more applied entries, which need to be adjusted.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Applied Entry to Adjust";
                Visible=FALSE }

    { 2   ;2   ;Field     ;
                ToolTipML=ENU=Specifies the number of the entry, as assigned from the specified number series when the entry was created.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Entry No." }

    { 1900000007;0;Container;
                ContainerType=FactBoxArea }

    { 1903523907;1;Part   ;
                ApplicationArea=#Basic,#Suite;
                SubPageLink=Entry No.=FIELD(Entry No.);
                PagePartID=Page9125;
                Visible=FALSE;
                PartType=Page }

  }
  CODE
  {
    VAR
      InventoryPeriod@1010 : Record 5814;
      TempUnapplyItem@1001 : TEMPORARY Record 27;
      Apply@1003 : Codeunit 22;
      ApplicationsForm@1000 : Page 522;
      InventoryOpenedFrom@1011 : Date;
      DateFilter@1015 : Text;
      ItemFilter@1017 : Text;
      LocationFilter@1018 : Text;
      DocumentFilter@1020 : Text;
      Text003@1021 : TextConst 'ENU=After the window is closed, the system will check for and reapply open entries.\Do you want to close the window?';
      RevertAllQst@1002 : TextConst 'ENU=Are you sure that you want to undo all changes?';
      NothingToRevertMsg@1004 : TextConst 'ENU=Nothing to undo.';
      RevertCompletedMsg@1006 : TextConst 'ENU=The changes have been undone.';

    LOCAL PROCEDURE UpdateFilterFields@8();
    BEGIN
      ItemFilter := GETFILTER("Item No.");
      LocationFilter := GETFILTER("Location Code");
      DateFilter := GETFILTER("Posting Date");
      DocumentFilter := GETFILTER("Document No.");
    END;

    LOCAL PROCEDURE Reapplyall@1();
    BEGIN
      Apply.RedoApplications;
      Apply.CostAdjust;
      Apply.ClearApplicationLog;
    END;

    LOCAL PROCEDURE ReapplyTouchedEntries@2();
    BEGIN
      Apply.RestoreTouchedEntries(TempUnapplyItem);

      IF Apply.AnyTouchedEntries THEN BEGIN
        UnblockItems;
        Reapplyall;
      END;
    END;

    [External]
    PROCEDURE SetRecordToShow@7(RecordToSet@1000 : Record 32);
    BEGIN
      Rec := RecordToSet;
    END;

    LOCAL PROCEDURE LocationFilterOnAfterValidate@4176();
    BEGIN
      CurrPage.UPDATE;
    END;

    LOCAL PROCEDURE DateFilterOnAfterValidate@9810();
    BEGIN
      CurrPage.UPDATE;
    END;

    LOCAL PROCEDURE ItemFilterOnAfterValidate@5068();
    BEGIN
      SETFILTER("Item No.",ItemFilter);
      ItemFilter := GETFILTER("Item No.");
      CurrPage.UPDATE;
    END;

    LOCAL PROCEDURE InsertUnapplyItem@3(ItemNo@1000 : Code[20]);
    BEGIN
      WITH TempUnapplyItem DO
        IF NOT GET(ItemNo) THEN BEGIN
          INIT;
          "No." := ItemNo;
          INSERT;
        END;
    END;

    LOCAL PROCEDURE UnblockItems@6();
    VAR
      Item@1000 : Record 27;
    BEGIN
      WITH TempUnapplyItem DO BEGIN
        IF FINDSET THEN
          REPEAT
            Item.GET("No.");
            IF Item."Application Wksh. User ID" = UPPERCASE(USERID) THEN BEGIN
              Item."Application Wksh. User ID" := '';
              Item.MODIFY;
            END;
          UNTIL NEXT = 0;

        DELETEALL;
      END;
    END;

    LOCAL PROCEDURE DocumentFilterOnAfterValidate@7051();
    BEGIN
      CurrPage.UPDATE;
    END;

    BEGIN
    END.
  }
}

