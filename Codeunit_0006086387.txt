OBJECT Codeunit 6086387 Mileage - Complete
{
  OBJECT-PROPERTIES
  {
    Date=05/31/18;
    Time=[ 1:00:00 AM];
    Version List=EMW16.00.10.2.60.06;
  }
  PROPERTIES
  {
    TableNo=6086338;
    OnRun=BEGIN
            ShowError := TRUE;
            Code(Rec);
          END;

  }
  CODE
  {
    VAR
      SendingDocCompleted@1160040000 : TextConst 'ENU=Reopening Mileage...';
      ShowError@1160040001 : Boolean;
      NewerVersionExists@1160040002 : TextConst 'ENU=A newer version might exist in Continia Online. Do you want to synchronize and send again?';

    LOCAL PROCEDURE Code@1160040020(VAR Mileage@1160040000 : Record 6086338);
    VAR
      EMSetup@1160040008 : Record 6086300;
      MilValidate@1160040005 : Codeunit 6086345;
      EMOnlineMgt@1160040002 : Codeunit 6086305;
      EMAppBridge@1160040001 : Codeunit 6086369;
      Window@1160040003 : Dialog;
    BEGIN
      IF Mileage.Status = Mileage.Status::Open THEN
        EXIT;

      Mileage.CheckInboxAndThrowError;

      IF GUIALLOWED AND (Mileage."Settlement No." = '') THEN
        Window.OPEN(STRSUBSTNO(SendingDocCompleted,FORMAT(Mileage."Entry No.")));

      IF Mileage."Settlement No." = '' THEN BEGIN
        EMAppBridge.CancelApprovalRequest(DATABASE::Mileage,FORMAT(Mileage."Entry No."));
        Mileage.GET(Mileage."Entry No.");
      END;

      IF Mileage.Status = Mileage.Status::"Pending Expense User" THEN
        IF HasPermissionToSync AND (Mileage."Settlement No." = '') AND GUIALLOWED THEN BEGIN
          IF NOT EMOnlineMgt.DeprecateMileage(Mileage."Mileage GUID",Mileage."Continia Online Version No.",FALSE) THEN BEGIN
            IF CONFIRM(NewerVersionExists) THEN BEGIN
              CODEUNIT.RUN(CODEUNIT::"EM Online Mgt.");
              Mileage.GET(Mileage."Entry No.");
            END;

            EMOnlineMgt.DeprecateMileage(Mileage."Mileage GUID",Mileage."Continia Online Version No.",TRUE);
          END;
        END ELSE
          EMOnlineMgt.DeprecateMileage(Mileage."Mileage GUID",Mileage."Continia Online Version No.",ShowError);

      //A SHADOW EXIST FOR HISTORY PURPOSE. MAKE SURE THAT ONE IS FORGOTTEN (NO ERROR)
      IF Mileage.Status > Mileage.Status::"Pending Expense User" THEN
        EMOnlineMgt.DeprecateMileage(Mileage."Mileage GUID",'',FALSE);

      Mileage.Status := Mileage.Status::Open;
      Mileage."Mileage Completed" := TRUE;
      Mileage.SetSkipSendToExpUser(TRUE);
      Mileage.MODIFY(TRUE);

      MilValidate.RUN(Mileage);

      IF GUIALLOWED AND (Mileage."Settlement No." = '') THEN
        Window.CLOSE;
    END;

    PROCEDURE SetShowError@1160040000(ShowErrValue@1160040000 : Boolean);
    BEGIN
      ShowError := ShowErrValue;
    END;

    PROCEDURE HasPermissionToSync@1160040002() : Boolean;
    VAR
      EMSetup@1160040001 : Record 6086300;
      ExpNavVersionMgt@1160040000 : Codeunit 6086319;
    BEGIN
      IF EMSetup.GET THEN
        IF EMSetup."Approval Administrator Role ID" <> '' THEN
          IF ExpNavVersionMgt.HasPermission(EMSetup."Approval Administrator Role ID") THEN
            EXIT(TRUE);

      IF ExpNavVersionMgt.HasPermission('EM-SUPER') THEN
        EXIT(TRUE);

      IF ExpNavVersionMgt.HasPermission('SUPER') THEN
        EXIT(TRUE);
    END;

    BEGIN
    END.
  }
}

