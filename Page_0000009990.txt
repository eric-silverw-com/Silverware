OBJECT Page 9990 Code Coverage
{
  OBJECT-PROPERTIES
  {
    Date=11/27/18;
    Time=11:12:07 PM;
    Version List=;
  }
  PROPERTIES
  {
    Permissions=;
    Editable=Yes;
    CaptionML=ENU=Code Coverage;
    ApplicationArea=#Advanced;
    InsertAllowed=No;
    DeleteAllowed=No;
    ModifyAllowed=No;
    SourceTable=Table2000000049;
    PageType=Worksheet;
    UsageCategory=Administration;
    OnInit=BEGIN
             RequiredCoveragePercent := 90;
           END;

    OnOpenPage=BEGIN
                 CodeCoverageRunning := FALSE;
               END;

    OnAfterGetRecord=BEGIN
                       NoofLines := 0;
                       LinesHit := 0;
                       LinesNotHit := 0;
                       Indent := 2;

                       CodeLine := Line;

                       CASE "Line Type" OF
                         "Line Type"::Object:
                           // Sum object coverage
                           BEGIN
                             CoveragePercent := CodeCoverageMgt.ObjectCoverage(Rec,NoofLines,LinesHit) * 100;
                             LinesNotHit := NoofLines - LinesHit;
                             Indent := 0
                           END;
                         "Line Type"::"Trigger/Function":
                           // Sum method coverage
                           BEGIN
                             CoveragePercent := CodeCoverageMgt.FunctionCoverage(Rec,NoofLines,LinesHit) * 100;
                             LinesNotHit := NoofLines - LinesHit;
                             Indent := 1
                           END
                         ELSE BEGIN
                           IF "No. of Hits" > 0 THEN
                             CoveragePercent := 100
                           ELSE
                             CoveragePercent := 0;
                         END;
                       END;

                       SetStyles;
                     END;

    ActionList=ACTIONS
    {
      { 15      ;    ;ActionContainer;
                      Name=Code Coverage Actions;
                      ActionContainerType=ActionItems }
      { 14      ;1   ;Action    ;
                      Name=Start;
                      CaptionML=ENU=Start;
                      ApplicationArea=#All;
                      Promoted=Yes;
                      Enabled=NOT CodeCoverageRunning;
                      PromotedIsBig=Yes;
                      Image=Start;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 CodeCoverageMgt.Start(TRUE);
                                 CodeCoverageRunning := TRUE;
                               END;
                                }
      { 10      ;1   ;Action    ;
                      Name=Refresh;
                      CaptionML=ENU=Refresh;
                      ToolTipML=ENU=Refresh the page.;
                      ApplicationArea=#All;
                      Promoted=Yes;
                      Enabled=CodeCoverageRunning;
                      PromotedIsBig=Yes;
                      Image=Refresh;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 CodeCoverageMgt.Refresh;
                               END;
                                }
      { 9       ;1   ;Action    ;
                      Name=Stop;
                      CaptionML=ENU=Stop;
                      ApplicationArea=#All;
                      Promoted=Yes;
                      Enabled=CodeCoverageRunning;
                      PromotedIsBig=Yes;
                      Image=Stop;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 CodeCoverageMgt.Stop;
                                 TotalCoveragePercent := CodeCoverageMgt.ObjectsCoverage(Rec,TotalNoofLines,TotalLinesHit) * 100;
                                 CodeCoverageRunning := FALSE;
                               END;
                                }
      { 16      ;    ;ActionContainer;
                      Name=Related Information;
                      ActionContainerType=RelatedInformation }
      { 17      ;1   ;Action    ;
                      Name=Load objects;
                      CaptionML=ENU=Load objects;
                      ApplicationArea=#All;
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=AddContacts;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 PAGE.RUN(PAGE::"Code Coverage Object");
                               END;
                                }
      { 21      ;1   ;Action    ;
                      CaptionML=ENU=Load country objects;
                      ApplicationArea=#All;
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=AddContacts;
                      PromotedCategory=Process;
                      OnAction=VAR
                                 Object@1000 : Record 2000000001;
                               BEGIN
                                 ObjectIdFilter := '10000..99999|1000000..98999999';
                                 Object.SETFILTER(ID,ObjectIdFilter);
                                 CODECOVERAGEINCLUDE(Object);
                                 SETFILTER("Object ID",ObjectIdFilter)
                               END;
                                }
      { 18      ;1   ;Action    ;
                      Name=DeleteNonMarked;
                      CaptionML=ENU=Delete Lines for Non-Marked Objects;
                      ApplicationArea=#All;
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=Delete;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 CodeCoverageMgt.CleanupCodeCoverage;
                               END;
                                }
      { 11      ;    ;ActionContainer;
                      Name=Code Coverage Export;
                      ActionContainerType=Reports }
      { 12      ;1   ;Action    ;
                      Name=Export to XML;
                      CaptionML=ENU=Export to XML;
                      ApplicationArea=#All;
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=Export;
                      PromotedCategory=Report;
                      OnAction=VAR
                                 CodeCoverage@1002 : Record 2000000049;
                                 CodeCoverageSummary@1000 : XMLport 9990;
                               BEGIN
                                 CodeCoverage.COPYFILTERS(Rec);
                                 CodeCoverageSummary.SETTABLEVIEW(CodeCoverage);
                                 CodeCoverageSummary.RUN;
                               END;
                                }
      { 13      ;1   ;Action    ;
                      Name=Backup/Restore;
                      CaptionML=ENU=Backup/Restore;
                      ToolTipML=ENU=Back up or restore the database.;
                      ApplicationArea=#All;
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=Export;
                      PromotedCategory=Report;
                      OnAction=VAR
                                 CodeCoverageDetailed@1000 : XMLport 9991;
                               BEGIN
                                 CodeCoverageDetailed.RUN;
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1   ;0   ;Container ;
                ContainerType=ContentArea }

    { 22  ;1   ;Group     ;
                GroupType=Group }

    { 26  ;2   ;Field     ;
                Name=ObjectIdFilter;
                CaptionML=ENU=Object Id Filter;
                ToolTipML=ENU=Specifies the object ID filter that applies when tracking which part of the application code has been exercised during test activity.;
                ApplicationArea=#All;
                SourceExpr=ObjectIdFilter;
                OnValidate=BEGIN
                             SETFILTER("Object ID",ObjectIdFilter);
                             TotalCoveragePercent := CodeCoverageMgt.ObjectsCoverage(Rec,TotalNoofLines,TotalLinesHit) * 100;
                             CurrPage.UPDATE(FALSE);
                           END;
                            }

    { 25  ;2   ;Field     ;
                Name=ObjectTypeFilter;
                CaptionML=ENU=Object Type Filter;
                ToolTipML=ENU=Specifies the object type filter that applies when tracking which part of the application code has been exercised during test activity.;
                ApplicationArea=#All;
                SourceExpr=ObjectTypeFilter;
                OnValidate=BEGIN
                             SETFILTER("Object Type",ObjectTypeFilter);
                             TotalCoveragePercent := CodeCoverageMgt.ObjectsCoverage(Rec,TotalNoofLines,TotalLinesHit);
                             CurrPage.UPDATE(FALSE);
                           END;
                            }

    { 30  ;2   ;Field     ;
                Name=RequiredCoverage;
                CaptionML=ENU=Required Coverage %;
                ToolTipML=ENU=Specifies the extent to which the application code is covered by tests.;
                ApplicationArea=#All;
                SourceExpr=RequiredCoveragePercent;
                OnValidate=BEGIN
                             CurrPage.UPDATE(FALSE);
                           END;
                            }

    { 23  ;2   ;Field     ;
                Name=TotalNoofLines;
                CaptionML=ENU=Total # Lines;
                ToolTipML=ENU=Specifies the total number of lines, when tracking which part of the application code has been exercised during test activity.;
                ApplicationArea=#All;
                SourceExpr=TotalNoofLines;
                Editable=FALSE }

    { 24  ;2   ;Field     ;
                Name=TotalCoveragePercent;
                CaptionML=ENU=Total Coverage %;
                ToolTipML=ENU=Specifies the extent to which the application code is covered by tests.;
                ApplicationArea=#All;
                DecimalPlaces=2:2;
                SourceExpr=TotalCoveragePercent;
                Editable=FALSE }

    { 2   ;1   ;Group     ;
                CaptionML=ENU=Object;
                Editable=FALSE;
                IndentationColumnName=Indent;
                ShowAsTree=Yes;
                GroupType=Repeater }

    { 19  ;2   ;Field     ;
                Name=CodeLine;
                CaptionML=ENU=Code;
                ToolTipML=ENU=Specifies which part of the application code has been exercised during test activity.;
                ApplicationArea=#All;
                SourceExpr=CodeLine }

    { 6   ;2   ;Field     ;
                Name=CoveragePercent;
                CaptionML=ENU=Coverage %;
                ToolTipML=ENU=Specifies the percentage applied to the code coverage line.;
                ApplicationArea=#All;
                SourceExpr=CoveragePercent;
                StyleExpr=CoveragePercentStyle }

    { 7   ;2   ;Field     ;
                Name=LineType;
                CaptionML=ENU=Line Type;
                ToolTipML=ENU=Specifies the line type, when tracking which part of the application code has been exercised during test activity.;
                ApplicationArea=#All;
                SourceExpr="Line Type" }

    { 3   ;2   ;Field     ;
                Name=ObjectType;
                CaptionML=ENU=Object Type;
                ToolTipML=ENU=Specifies the average coverage of all code lines inside the object, when tracking which part of the application code has been exercised during test activity.;
                ApplicationArea=#All;
                SourceExpr="Object Type" }

    { 4   ;2   ;Field     ;
                Name=ObjectID;
                CaptionML=ENU=Object ID;
                ToolTipML=ENU=Specifies the ID of the object.;
                ApplicationArea=#All;
                SourceExpr="Object ID" }

    { 5   ;2   ;Field     ;
                Name=LineNo;
                CaptionML=ENU=Line No.;
                ToolTipML=ENU=Specifies the line number, when tracking which part of the application code has been exercised during test activity.;
                ApplicationArea=#All;
                SourceExpr="Line No." }

    { 20  ;2   ;Field     ;
                Name=NoofLines;
                CaptionML=ENU=No. of Lines;
                ToolTipML=ENU=Specifies the number of lines, when tracking which part of the application code has been exercised during test activity.;
                ApplicationArea=#All;
                SourceExpr=NoofLines }

    { 8   ;2   ;Field     ;
                Name=No. of Hits;
                CaptionML=ENU=No. of Hits;
                ToolTipML=ENU=Specifies the number of hits, when tracking which part of the application code has been exercised during test activity.;
                ApplicationArea=#All;
                SourceExpr="No. of Hits" }

    { 28  ;2   ;Field     ;
                Name=LinesHit;
                CaptionML=ENU=No. of Hit Lines;
                ToolTipML=ENU=Specifies the number of hit lines, when tracking which part of the application code has been exercised during test activity.;
                ApplicationArea=#All;
                SourceExpr=LinesHit }

    { 27  ;2   ;Field     ;
                Name=LinesNotHit;
                CaptionML=ENU=No. of Skipped Lines;
                ToolTipML=ENU=Specifies the number of skipped lines, when tracking which part of the application code has been exercised during test activity.;
                ApplicationArea=#All;
                SourceExpr=LinesNotHit }

  }
  CODE
  {
    VAR
      CodeCoverageMgt@1000 : Codeunit 9990;
      LinesHit@1008 : Integer;
      LinesNotHit@1007 : Integer;
      Indent@1006 : Integer;
      CodeCoverageRunning@1001 : Boolean INDATASET;
      CodeLine@1010 : Text[1024];
      NoofLines@1005 : Integer INDATASET;
      CoveragePercent@1004 : Decimal;
      TotalNoofLines@1011 : Integer;
      TotalCoveragePercent@1009 : Decimal;
      TotalLinesHit@1003 : Integer;
      ObjectIdFilter@1013 : Text;
      ObjectTypeFilter@1012 : Text;
      RequiredCoveragePercent@1015 : Integer;
      CoveragePercentStyle@1016 : Text;

    LOCAL PROCEDURE SetStyles@5();
    BEGIN
      IF "Line Type" = "Line Type"::Empty THEN
        CoveragePercentStyle := 'Standard'
      ELSE
        IF CoveragePercent < RequiredCoveragePercent THEN
          CoveragePercentStyle := 'Unfavorable'
        ELSE
          CoveragePercentStyle := 'Favorable';
    END;

    BEGIN
    END.
  }
}

