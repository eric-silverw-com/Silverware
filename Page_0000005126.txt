OBJECT Page 5126 Create Opportunity
{
  OBJECT-PROPERTIES
  {
    Date=11/27/18;
    Time=11:10:54 PM;
    Version List=;
  }
  PROPERTIES
  {
    CaptionML=ENU=Create Opportunity;
    InsertAllowed=No;
    DeleteAllowed=No;
    LinksAllowed=No;
    SourceTable=Table5092;
    DataCaptionExpr=Caption;
    PageType=Card;
    OnInit=BEGIN
             SalespersonCodeEditable := TRUE;
             WizardCampaignDescriptionEdita := TRUE;
             WizardContactNameEditable := TRUE;
           END;

    OnOpenPage=BEGIN
                 IsOnMobile := ClientTypeManagement.GetCurrentClientType = CLIENTTYPE::Phone;
               END;

    OnAfterGetRecord=BEGIN
                       WizardContactNameOnFormat(FORMAT("Wizard Contact Name"));
                     END;

    OnQueryClosePage=BEGIN
                       IF CloseAction IN [ACTION::OK,ACTION::LookupOK] THEN
                         FinishPage;
                     END;

    ActionList=ACTIONS
    {
      { 1900000004;0 ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 10      ;1   ;Action    ;
                      Name=FinishWizard;
                      CaptionML=ENU=&Finish;
                      ToolTipML=ENU=Finish creating the opportunity.;
                      ApplicationArea=#RelationshipMgmt;
                      Promoted=Yes;
                      Visible=IsOnMobile;
                      InFooterBar=Yes;
                      Image=Approve;
                      OnAction=BEGIN
                                 FinishPage;
                                 CurrPage.CLOSE;
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 1   ;1   ;Group     ;
                Name=General;
                CaptionML=ENU=General;
                GroupType=Group }

    { 23  ;2   ;Field     ;
                ToolTipML=ENU=Specifies the description of the opportunity.;
                ApplicationArea=#RelationshipMgmt;
                SourceExpr=Description }

    { 12  ;2   ;Field     ;
                ToolTipML=ENU=Specifies the date that the opportunity was created.;
                ApplicationArea=#RelationshipMgmt;
                SourceExpr="Creation Date" }

    { 20  ;2   ;Field     ;
                ToolTipML=ENU=Specifies the priority of the opportunity. There are three options:;
                ApplicationArea=#RelationshipMgmt;
                SourceExpr=Priority }

    { 7   ;2   ;Field     ;
                Lookup=No;
                CaptionML=ENU=Contact;
                ToolTipML=ENU=Specifies the contact who is involved in this opportunity.;
                ApplicationArea=#RelationshipMgmt;
                SourceExpr="Wizard Contact Name";
                TableRelation=Contact;
                Editable=WizardContactNameEditable;
                OnAssistEdit=VAR
                               Cont@1102601001 : Record 5050;
                             BEGIN
                               IF (GETFILTER("Contact No.") = '') AND (GETFILTER("Contact Company No.") = '') THEN
                                 IF (Cont."No." = '') AND ("Segment Description" = '') THEN BEGIN
                                   IF Cont.GET("Contact No.") THEN ;
                                   IF PAGE.RUNMODAL(0,Cont) = ACTION::LookupOK THEN BEGIN
                                     VALIDATE("Contact No.",Cont."No.");
                                     "Wizard Contact Name" := Cont.Name;
                                   END;
                                 END;
                             END;
                              }

    { 4   ;2   ;Field     ;
                CaptionML=ENU=Salesperson;
                ToolTipML=ENU=Specifies the salesperson who is responsible for the opportunity.;
                ApplicationArea=#Suite,#RelationshipMgmt;
                SourceExpr="Salesperson Code";
                Editable=SalespersonCodeEditable }

    { 2   ;2   ;Field     ;
                CaptionML=ENU=Sales Cycle;
                ToolTipML=ENU=Specifies which sales cycle will be used to process this opportunity;
                ApplicationArea=#RelationshipMgmt;
                SourceExpr="Sales Cycle Code" }

    { 5   ;2   ;Field     ;
                Lookup=No;
                CaptionML=ENU=Campaign;
                ToolTipML=ENU=Specifies the campaign that the opportunity is related to. The description is copied from the campaign card.;
                ApplicationArea=#RelationshipMgmt;
                SourceExpr="Wizard Campaign Description";
                TableRelation=Campaign;
                Importance=Additional;
                Editable=WizardCampaignDescriptionEdita;
                OnAssistEdit=VAR
                               Campaign@1102601001 : Record 5071;
                             BEGIN
                               IF GETFILTER("Campaign No.") = '' THEN
                                 IF PAGE.RUNMODAL(0,Campaign) = ACTION::LookupOK THEN BEGIN
                                   VALIDATE("Campaign No.",Campaign."No.");
                                   "Wizard Campaign Description" := Campaign.Description;
                                 END;
                             END;
                              }

    { 3   ;2   ;Field     ;
                Lookup=No;
                CaptionML=ENU=Segment;
                ToolTipML=ENU=Specifies a description of the segment that is related to the opportunity. The description is copied from the segment card.;
                ApplicationArea=#RelationshipMgmt;
                SourceExpr="Segment Description";
                TableRelation="Segment Header";
                Importance=Additional;
                Editable=FALSE;
                OnAssistEdit=VAR
                               SegmentHeader@1102601000 : Record 5076;
                             BEGIN
                               IF SegHeader."No." = '' THEN
                                 IF PAGE.RUNMODAL(0,SegmentHeader) = ACTION::LookupOK THEN BEGIN
                                   VALIDATE("Segment No.",SegmentHeader."No.");
                                   "Segment Description" := SegmentHeader.Description;
                                 END;
                             END;
                              }

    { 34  ;1   ;Group     ;
                Name=Estimates;
                CaptionML=ENU=Estimates;
                GroupType=Group }

    { 37  ;2   ;Field     ;
                CaptionML=ENU=Activate the First Stage.;
                ToolTipML=ENU=Specifies if the opportunity is to be activated. If you select the check box, then you can fill out the remainder of the fields on this page. In the Opportunity Card window, the status is set to In Progress.;
                ApplicationArea=#RelationshipMgmt;
                SourceExpr="Activate First Stage";
                OnValidate=BEGIN
                             IF NOT "Activate First Stage" THEN BEGIN
                               "Wizard Estimated Value (LCY)" := 0;
                               "Wizard Chances of Success %" := 0;
                               "Wizard Estimated Closing Date" := 0D;
                             END;
                           END;
                            }

    { 25  ;2   ;Field     ;
                CaptionML=ENU=Estimated Sales Value ($);
                ToolTipML=ENU=Specifies the value in the wizard for the opportunity. You can specify an estimated value of the opportunity in local currency in this field.;
                ApplicationArea=#RelationshipMgmt;
                SourceExpr="Wizard Estimated Value (LCY)";
                Enabled="Activate First Stage" }

    { 27  ;2   ;Field     ;
                CaptionML=ENU=Chances of Success (%);
                ToolTipML=ENU=Specifies the value in the wizard for the opportunity. You can specify a percentage completion estimate in this field.;
                ApplicationArea=#RelationshipMgmt;
                DecimalPlaces=0:0;
                SourceExpr="Wizard Chances of Success %";
                MaxValue=100;
                Enabled="Activate First Stage" }

    { 31  ;2   ;Field     ;
                CaptionML=ENU=Estimated Closing Date;
                ToolTipML=ENU=Specifies a closing date for the opportunity from the wizard.;
                ApplicationArea=#RelationshipMgmt;
                SourceExpr="Wizard Estimated Closing Date";
                Enabled="Activate First Stage" }

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=(Multiple)';
      Text001@1001 : TextConst 'ENU=untitled';
      Cont@1003 : Record 5050;
      SalesPurchPerson@1004 : Record 13;
      Campaign@1005 : Record 5071;
      SegHeader@1006 : Record 5076;
      ClientTypeManagement@1007 : Codeunit 4;
      WizardContactNameEditable@19024232 : Boolean INDATASET;
      SalespersonCodeEditable@19071610 : Boolean INDATASET;
      WizardCampaignDescriptionEdita@19073396 : Boolean INDATASET;
      IsOnMobile@1002 : Boolean;

    LOCAL PROCEDURE Caption@1() : Text[260];
    VAR
      CaptionStr@1000 : Text[260];
    BEGIN
      IF Cont.GET(GETFILTER("Contact Company No.")) THEN
        CaptionStr := COPYSTR(Cont."No." + ' ' + Cont.Name,1,MAXSTRLEN(CaptionStr));
      IF Cont.GET(GETFILTER("Contact No.")) THEN
        CaptionStr := COPYSTR(CaptionStr + ' ' + Cont."No." + ' ' + Cont.Name,1,MAXSTRLEN(CaptionStr));
      IF SalesPurchPerson.GET(GETFILTER("Salesperson Code")) THEN
        CaptionStr := COPYSTR(CaptionStr + ' ' + SalesPurchPerson.Code + ' ' + SalesPurchPerson.Name,1,MAXSTRLEN(CaptionStr));
      IF Campaign.GET(GETFILTER("Campaign No.")) THEN
        CaptionStr := COPYSTR(CaptionStr + ' ' + Campaign."No." + ' ' + Campaign.Description,1,MAXSTRLEN(CaptionStr));
      IF SegHeader.GET(GETFILTER("Segment No.")) THEN
        CaptionStr := COPYSTR(CaptionStr + ' ' + SegHeader."No." + ' ' + SegHeader.Description,1,MAXSTRLEN(CaptionStr));
      IF CaptionStr = '' THEN
        CaptionStr := Text001;

      EXIT(CaptionStr);
    END;

    LOCAL PROCEDURE WizardContactNameOnFormat@19031225(Text@19070643 : Text[1024]);
    BEGIN
      IF SegHeader.GET(GETFILTER("Segment No.")) THEN
        Text := Text000;
    END;

    LOCAL PROCEDURE FinishPage@4();
    BEGIN
      CheckStatus;
      FinishWizard;
    END;

    BEGIN
    END.
  }
}

