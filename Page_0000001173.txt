OBJECT Page 1173 Document Attachment Details
{
  OBJECT-PROPERTIES
  {
    Date=11/27/18;
    Time=11:10:35 PM;
    Version List=;
  }
  PROPERTIES
  {
    Editable=Yes;
    CaptionML=ENU=Attached Documents;
    SourceTable=Table1173;
    DelayedInsert=Yes;
    SourceTableView=SORTING(ID,Table ID);
    PageType=Worksheet;
    OnInit=BEGIN
             FlowFieldsEditable := TRUE;
           END;

    OnNewRecord=BEGIN
                  "File Name" := SelectFileTxt;
                END;

    ActionList=ACTIONS
    {
      { 7       ;    ;ActionContainer;
                      CaptionML=ENU=Actions;
                      ActionContainerType=ActionItems }
      { 8       ;1   ;Action    ;
                      Name=Preview;
                      CaptionML=ENU=Preview;
                      ToolTipML=ENU=Get a preview of the attachment.;
                      ApplicationArea=#All;
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=Export;
                      PromotedCategory=Process;
                      Scope=Repeater;
                      OnAction=BEGIN
                                 IF "File Name" <> '' THEN
                                   Export(TRUE);
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1   ;0   ;Container ;
                ContainerType=ContentArea }

    { 2   ;1   ;Group     ;
                Name=Group;
                GroupType=Repeater }

    { 4   ;2   ;Field     ;
                Name=Name;
                ToolTipML=ENU=Specifies the filename of the attachment.;
                ApplicationArea=#All;
                SourceExpr="File Name";
                Editable=FALSE;
                OnDrillDown=VAR
                              TempBlob@1002 : Record 99008535;
                              FileManagement@1001 : Codeunit 419;
                              FileName@1000 : Text;
                            BEGIN
                              IF "Document Reference ID".HASVALUE THEN
                                Export(TRUE)
                              ELSE BEGIN
                                FileName := FileManagement.BLOBImportWithFilter(TempBlob,ImportTxt,FileName,STRSUBSTNO(FileDialogTxt,FilterTxt),FilterTxt);
                                IF FileName <> '' THEN
                                  SaveAttachment(FromRecRef,FileName,TempBlob);
                                CurrPage.UPDATE(FALSE);
                              END;
                            END;
                             }

    { 10  ;2   ;Field     ;
                ToolTipML=ENU=Specifies the file extension of the attachment.;
                ApplicationArea=#All;
                SourceExpr="File Extension";
                Editable=FALSE }

    { 5   ;2   ;Field     ;
                ToolTipML=ENU=Specifies the type of document that the attachment is.;
                ApplicationArea=#All;
                SourceExpr="File Type";
                Editable=FALSE }

    { 3   ;2   ;Field     ;
                ToolTipML=ENU=Specifies the user who attached the document.;
                ApplicationArea=#All;
                SourceExpr=User;
                Editable=FALSE }

    { 6   ;2   ;Field     ;
                ToolTipML=ENU=Specifies the date when the document was attached.;
                ApplicationArea=#All;
                SourceExpr="Attached Date" }

    { 9   ;2   ;Field     ;
                ToolTipML=ENU=Specifies if the attachment must flow to transactions.;
                ApplicationArea=#All;
                SourceExpr="Document Flow Purchase";
                CaptionClass=GetCaptionClass(9);
                Visible=PurchaseDocumentFlow;
                Editable=FlowFieldsEditable }

    { 11  ;2   ;Field     ;
                ToolTipML=ENU=Specifies if the attachment must flow to transactions.;
                ApplicationArea=#All;
                SourceExpr="Document Flow Sales";
                CaptionClass=GetCaptionClass(11);
                Visible=SalesDocumentFlow;
                Editable=FlowFieldsEditable }

  }
  CODE
  {
    VAR
      FromRecRef@1009 : RecordRef;
      SalesDocumentFlow@1002 : Boolean;
      FileDialogTxt@1003 : TextConst '@@@="%1=file types, such as *.txt or *.docx";ENU=Attachments (%1)|%1';
      FilterTxt@1001 : TextConst '@@@={Locked};ENU="*.jpg;*.jpeg;*.bmp;*.png;*.gif;*.tiff;*.tif;*.pdf;*.docx;*.doc;*.xlsx;*.xls;*.pptx;*.ppt;*.msg;*.xml;*.*"';
      ImportTxt@1000 : TextConst 'ENU=Attach a document.';
      SelectFileTxt@1004 : TextConst 'ENU=Select File...';
      PurchaseDocumentFlow@1005 : Boolean;
      FlowToPurchTxt@1006 : TextConst 'ENU=Flow to Purch. Trx';
      FlowToSalesTxt@1007 : TextConst 'ENU=Flow to Sales Trx';
      FlowFieldsEditable@1008 : Boolean;

    LOCAL PROCEDURE GetCaptionClass@2(FieldNo@1000 : Integer) : Text;
    BEGIN
      IF SalesDocumentFlow AND PurchaseDocumentFlow THEN
        CASE FieldNo OF
          9:
            EXIT(FlowToPurchTxt);
          11:
            EXIT(FlowToSalesTxt);
        END;
    END;

    PROCEDURE OpenForRecRef@3(RecRef@1000 : RecordRef);
    VAR
      FieldRef@1001 : FieldRef;
      RecNo@1002 : Code[20];
      DocType@1003 : 'Quote,Order,Invoice,Credit Memo,Blanket Order,Return Order';
      LineNo@1004 : Integer;
    BEGIN
      RESET;

      FromRecRef := RecRef;

      SETRANGE("Table ID",RecRef.NUMBER);

      IF RecRef.NUMBER = DATABASE::Item THEN BEGIN
        SalesDocumentFlow := TRUE;
        PurchaseDocumentFlow := TRUE;
      END;

      CASE RecRef.NUMBER OF
        DATABASE::Customer,
        DATABASE::"Sales Header",
        DATABASE::"Sales Line",
        DATABASE::"Sales Invoice Header",
        DATABASE::"Sales Invoice Line",
        DATABASE::"Sales Cr.Memo Header",
        DATABASE::"Sales Cr.Memo Line":
          SalesDocumentFlow := TRUE;
        DATABASE::Vendor,
        DATABASE::"Purchase Header",
        DATABASE::"Purchase Line",
        DATABASE::"Purch. Inv. Header",
        DATABASE::"Purch. Inv. Line",
        DATABASE::"Purch. Cr. Memo Hdr.",
        DATABASE::"Purch. Cr. Memo Line":
          PurchaseDocumentFlow := TRUE;
      END;

      CASE RecRef.NUMBER OF
        DATABASE::Customer,
        DATABASE::Vendor,
        DATABASE::Item,
        DATABASE::Employee,
        DATABASE::"Fixed Asset",
        DATABASE::Job,
        DATABASE::Resource:
          BEGIN
            FieldRef := RecRef.FIELD(1);
            RecNo := FieldRef.VALUE;
            SETRANGE("No.",RecNo);
          END;
      END;

      CASE RecRef.NUMBER OF
        DATABASE::"Sales Header",
        DATABASE::"Sales Line",
        DATABASE::"Purchase Header",
        DATABASE::"Purchase Line":
          BEGIN
            FieldRef := RecRef.FIELD(1);
            DocType := FieldRef.VALUE;
            SETRANGE("Document Type",DocType);

            FieldRef := RecRef.FIELD(3);
            RecNo := FieldRef.VALUE;
            SETRANGE("No.",RecNo);

            FlowFieldsEditable := FALSE;
          END;
      END;

      CASE RecRef.NUMBER OF
        DATABASE::"Sales Line",
        DATABASE::"Purchase Line":
          BEGIN
            FieldRef := RecRef.FIELD(4);
            LineNo := FieldRef.VALUE;
            SETRANGE("Line No.",LineNo);
          END;
      END;

      CASE RecRef.NUMBER OF
        DATABASE::"Sales Invoice Header",
        DATABASE::"Sales Cr.Memo Header",
        DATABASE::"Purch. Inv. Header",
        DATABASE::"Purch. Cr. Memo Hdr.":
          BEGIN
            FieldRef := RecRef.FIELD(3);
            RecNo := FieldRef.VALUE;
            SETRANGE("No.",RecNo);

            FlowFieldsEditable := FALSE;
          END;
      END;

      CASE RecRef.NUMBER OF
        DATABASE::"Sales Invoice Line",
        DATABASE::"Sales Cr.Memo Line",
        DATABASE::"Purch. Inv. Line",
        DATABASE::"Purch. Cr. Memo Line":
          BEGIN
            FieldRef := RecRef.FIELD(3);
            RecNo := FieldRef.VALUE;
            SETRANGE("No.",RecNo);

            FieldRef := RecRef.FIELD(4);
            LineNo := FieldRef.VALUE;
            SETRANGE("Line No.",LineNo);

            FlowFieldsEditable := FALSE;
          END;
      END;
    END;

    BEGIN
    END.
  }
}

