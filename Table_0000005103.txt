OBJECT Table 5103 Interaction Tmpl. Language
{
  OBJECT-PROPERTIES
  {
    Date=09/19/18;
    Time=12:00:00 PM;
    Version List=NAVW113.00;
  }
  PROPERTIES
  {
    OnDelete=BEGIN
               RemoveAttachment(FALSE);
             END;

    OnRename=VAR
               InteractionTemplate@1000 : Record 5064;
             BEGIN
               InteractionTemplate.GET("Interaction Template Code");
               IF xRec."Language Code" <> "Language Code" THEN
                 IF InteractionTemplate."Language Code (Default)" = xRec."Language Code" THEN BEGIN
                   InteractionTemplate."Language Code (Default)" := "Language Code";
                   InteractionTemplate.MODIFY;
                 END;
             END;

    CaptionML=[ENU=Interaction Tmpl. Language;
               ESM=Idioma plant. interacci¢n;
               FRC=Langue modŠle interaction;
               ENC=Interaction Tmpl. Language];
    LookupPageID=Page5154;
  }
  FIELDS
  {
    { 1   ;   ;Interaction Template Code;Code10   ;TableRelation="Interaction Template";
                                                   CaptionML=[ENU=Interaction Template Code;
                                                              ESM=C¢d. plantilla interacci¢n;
                                                              FRC=Code modŠle interaction;
                                                              ENC=Interaction Template Code];
                                                   Editable=No }
    { 2   ;   ;Language Code       ;Code10        ;TableRelation=Language;
                                                   CaptionML=[ENU=Language Code;
                                                              ESM=C¢d. idioma;
                                                              FRC=Code langue;
                                                              ENC=Language Code] }
    { 3   ;   ;Description         ;Text50        ;CaptionML=[ENU=Description;
                                                              ESM=Descripci¢n;
                                                              FRC=Description;
                                                              ENC=Description] }
    { 4   ;   ;Attachment No.      ;Integer       ;CaptionML=[ENU=Attachment No.;
                                                              ESM=N§ anexo;
                                                              FRC=Nø piŠce jointe;
                                                              ENC=Attachment No.] }
    { 5   ;   ;Custom Layout Code  ;Code20        ;TableRelation="Custom Report Layout" WHERE (Report ID=CONST(5084));
                                                   OnValidate=BEGIN
                                                                CALCFIELDS("Custom Layout Description");
                                                              END;

                                                   CaptionML=[ENU=Custom Layout Code;
                                                              ESM=C¢digo de dise¤o personalizado;
                                                              FRC=Code de pr‚sentation personnalis‚e;
                                                              ENC=Custom Layout Code] }
    { 6   ;   ;Custom Layout Description;Text250  ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Custom Report Layout".Description WHERE (Code=FIELD(Custom Layout Code)));
                                                   CaptionML=[ENU=Custom Layout Description;
                                                              ESM=Descripci¢n de dise¤o personalizado;
                                                              FRC=Description pr‚sentation personnalis‚e;
                                                              ENC=Custom Layout Description];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Interaction Template Code,Language Code ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      AttachmentRecord@1003 : Record 5062;
      ClientTypeManagement@1077 : Codeunit 4;
      Text000@1002 : TextConst 'ENU=You have canceled the create process.;ESM=Ha cancelado el proceso crear.;FRC=Vous avez annul‚ la cr‚ation.;ENC=You have cancelled the create process.';
      Text001@1001 : TextConst 'ENU=Replace existing attachment?;ESM=¨Sustituir el anexo actual?;FRC=Remplacer la piŠce jointe existante?;ENC=Replace existing attachment?';
      Text002@1000 : TextConst 'ENU=You have canceled the import process.;ESM=Ha cancelado el proceso importar.;FRC=Vous avez annul‚ l''import.;ENC=You have cancelled the import process.';
      Text005@1004 : TextConst 'ENU=Export Attachment;ESM=Exportar anexo;FRC=Exporter piŠce jointe;ENC=Export Attachment';

    PROCEDURE CreateAttachment@1();
    VAR
      Attachment@1000 : Record 5062;
      InteractTmplLanguage@1004 : Record 5103;
      WordManagement@1002 : Codeunit 5054;
      NewAttachNo@1003 : Integer;
    BEGIN
      IF "Attachment No." <> 0 THEN BEGIN
        IF Attachment.GET("Attachment No.") THEN
          Attachment.TESTFIELD("Read Only",FALSE);
        IF NOT CONFIRM(Text001,FALSE) THEN
          EXIT;
      END;

      IF "Custom Layout Code" = '' THEN
        IF ClientTypeManagement.GetCurrentClientType IN [CLIENTTYPE::Web,CLIENTTYPE::Tablet,CLIENTTYPE::Phone] THEN BEGIN
          IF Attachment.ImportAttachmentFromClientFile('',FALSE,FALSE) THEN
            NewAttachNo := Attachment."No.";
        END ELSE
          NewAttachNo :=
            WordManagement.CreateWordAttachment("Interaction Template Code" + ' ' + Description,"Language Code")
      ELSE
        NewAttachNo := CreateHTMLCustomLayoutAttachment;

      IF NewAttachNo <> 0 THEN BEGIN
        IF "Attachment No." <> 0 THEN
          RemoveAttachment(FALSE);
        "Attachment No." := NewAttachNo;
        IF InteractTmplLanguage.GET("Interaction Template Code","Language Code") THEN
          MODIFY
        ELSE
          INSERT;
      END ELSE
        ERROR(Text000);
    END;

    [External]
    PROCEDURE CreateHTMLCustomLayoutAttachment@8() : Integer;
    VAR
      Attachment@1000 : Record 5062;
    BEGIN
      Attachment.INIT;
      Attachment."Storage Type" := Attachment."Storage Type"::Embedded;
      Attachment."File Extension" := 'HTML';
      Attachment.INSERT(TRUE);
      Attachment.WriteHTMLCustomLayoutAttachment('',"Custom Layout Code");
      EXIT(Attachment."No.");
    END;

    PROCEDURE OpenAttachment@2();
    VAR
      Attachment@1000 : Record 5062;
    BEGIN
      IF "Attachment No." = 0 THEN
        EXIT;
      Attachment.GET("Attachment No.");
      Attachment.OpenAttachment("Interaction Template Code" + ' ' + Description,FALSE,"Language Code");
    END;

    PROCEDURE CopyFromAttachment@3();
    VAR
      InteractTmplLanguage@1000 : Record 5103;
      Attachment@1001 : Record 5062;
      AttachmentManagement@1002 : Codeunit 5052;
      NewAttachNo@1003 : Integer;
    BEGIN
      IF Attachment.GET("Attachment No.") THEN
        Attachment.TESTFIELD("Read Only",FALSE);

      IF "Attachment No." <> 0 THEN BEGIN
        IF NOT CONFIRM(Text001,FALSE) THEN
          EXIT;
        RemoveAttachment(FALSE);
        "Attachment No." := 0;
        MODIFY;
        COMMIT;
      END;

      InteractTmplLanguage.SETFILTER("Attachment No.",'<>%1',0);
      IF PAGE.RUNMODAL(0,InteractTmplLanguage) = ACTION::LookupOK THEN BEGIN
        NewAttachNo := AttachmentManagement.InsertAttachment(InteractTmplLanguage."Attachment No.");
        IF NewAttachNo <> 0 THEN BEGIN
          "Attachment No." := NewAttachNo;
          MODIFY;
        END;
      END;
    END;

    PROCEDURE ImportAttachment@5();
    VAR
      Attachment@1000 : Record 5062;
    BEGIN
      IF "Attachment No." <> 0 THEN BEGIN
        IF Attachment.GET("Attachment No.") THEN
          Attachment.TESTFIELD("Read Only",FALSE);
        IF NOT CONFIRM(Text001,FALSE) THEN
          EXIT;
      END;

      IF Attachment.ImportAttachmentFromClientFile('',FALSE,FALSE) THEN BEGIN
        "Attachment No." := Attachment."No.";
        MODIFY;
      END ELSE
        ERROR(Text002);
    END;

    PROCEDURE ExportAttachment@6();
    VAR
      TempBlob@1002 : Record 99008535;
      MarketingSetup@1004 : Record 5079;
      FileMgt@1003 : Codeunit 419;
      FileName@1001 : Text[1024];
      FileFilter@1005 : Text;
      ExportToFile@1006 : Text;
    BEGIN
      MarketingSetup.GET;
      ExportToFile := '';
      IF NOT AttachmentRecord.GET("Attachment No.") THEN
        EXIT;

      CASE AttachmentRecord."Storage Type" OF
        AttachmentRecord."Storage Type"::Embedded:
          BEGIN
            AttachmentRecord.CALCFIELDS("Attachment File");
            IF AttachmentRecord."Attachment File".HASVALUE THEN BEGIN
              FileName := "Interaction Template Code" + '.' + AttachmentRecord."File Extension";
              TempBlob.Blob := AttachmentRecord."Attachment File";
              FileMgt.BLOBExport(TempBlob,FileName,TRUE);
            END;
          END;
        AttachmentRecord."Storage Type"::"Disk File":
          BEGIN
            IF MarketingSetup."Attachment Storage Type" = MarketingSetup."Attachment Storage Type"::"Disk File" THEN
              MarketingSetup.TESTFIELD("Attachment Storage Location");
            FileFilter :=
              UPPERCASE(AttachmentRecord."File Extension") +
              ' (*.' + AttachmentRecord."File Extension" + ')|*.' + AttachmentRecord."File Extension";
            ExportToFile := "Interaction Template Code" + '.' + AttachmentRecord."File Extension";
            DOWNLOAD(
              AttachmentRecord."Storage Pointer" + '\' + FORMAT(AttachmentRecord."No.") + '.',Text005,'',FileFilter,ExportToFile);
          END;
      END;
    END;

    PROCEDURE RemoveAttachment@7(Prompt@1000 : Boolean);
    VAR
      Attachment@1001 : Record 5062;
    BEGIN
      IF Attachment.GET("Attachment No.") THEN
        IF Attachment.RemoveAttachment(Prompt) THEN BEGIN
          "Attachment No." := 0;
          MODIFY;
        END;
    END;

    BEGIN
    END.
  }
}

