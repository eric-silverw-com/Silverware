OBJECT Page 2198 O365 Unit Of Measure Card
{
  OBJECT-PROPERTIES
  {
    Date=11/27/18;
    Time=11:10:47 PM;
    Version List=;
  }
  PROPERTIES
  {
    CaptionML=ENU=Price per;
    SourceTable=Table204;
    DataCaptionExpr=Description;
    OnQueryClosePage=VAR
                       UnitOfMeasure@1001 : Record 204;
                     BEGIN
                       IF NOT (CloseAction IN [ACTION::OK,ACTION::LookupOK]) THEN
                         EXIT(TRUE);

                       IF DescriptionInCurrentLanguage = GetDescriptionInCurrentLanguage THEN
                         EXIT(TRUE);

                       // Do not insert a new empty record
                       IF (Code = '') AND (DescriptionInCurrentLanguage = '') THEN
                         EXIT(TRUE);

                       IF UnitOfMeasure.GET(UPPERCASE(DescriptionInCurrentLanguage)) THEN
                         ERROR(UnitOfMeasureAlredyExistsErr,DescriptionInCurrentLanguage);

                       IF Code = '' THEN
                         InsertNewUnitOfMeasure
                       ELSE
                         RenameUnitOfMeasureRemoveTranslations;
                     END;

    OnAfterGetCurrRecord=BEGIN
                           DescriptionInCurrentLanguage := GetDescriptionInCurrentLanguage;
                         END;

    ActionList=ACTIONS
    {
      { 4       ;    ;ActionContainer;
                      ActionContainerType=NewDocumentItems }
    }
  }
  CONTROLS
  {
    { 3   ;0   ;Container ;
                ContainerType=ContentArea }

    { 2   ;1   ;Field     ;
                ToolTipML=ENU=Specifies a code for the unit of measure that is shown on the item and resource cards where it is used.;
                ApplicationArea=#Basic,#Suite,#Invoicing;
                SourceExpr=Code;
                Visible=FALSE }

    { 1   ;1   ;Field     ;
                CaptionML=ENU=Description;
                ToolTipML=ENU=Specifies a description of the unit of measure.;
                ApplicationArea=#Basic,#Suite,#Invoicing;
                SourceExpr=DescriptionInCurrentLanguage;
                OnValidate=BEGIN
                             IF DescriptionInCurrentLanguage = '' THEN
                               DescriptionInCurrentLanguage := GetDescriptionInCurrentLanguage;
                           END;
                            }

  }
  CODE
  {
    VAR
      UnitOfMeasureAlredyExistsErr@1000 : TextConst '@@@="%1=The unit of measure description";ENU=You already have a measure with the name %1.';
      DescriptionInCurrentLanguage@1001 : Text[10];

    LOCAL PROCEDURE RenameUnitOfMeasureRemoveTranslations@1();
    VAR
      UnitOfMeasureTranslation@1000 : Record 5402;
    BEGIN
      IF Code <> '' THEN BEGIN
        UnitOfMeasureTranslation.SETRANGE(Code,Code);
        UnitOfMeasureTranslation.DELETEALL(TRUE);
      END;

      VALIDATE(Description,DescriptionInCurrentLanguage);
      MODIFY(TRUE);
      RENAME(DescriptionInCurrentLanguage);
    END;

    LOCAL PROCEDURE InsertNewUnitOfMeasure@2();
    BEGIN
      VALIDATE(Code,DescriptionInCurrentLanguage);
      VALIDATE(Description,DescriptionInCurrentLanguage);

      INSERT(TRUE);
    END;

    BEGIN
    END.
  }
}

