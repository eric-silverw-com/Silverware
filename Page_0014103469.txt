OBJECT Page 14103469 EasyPDF Attachment
{
  OBJECT-PROPERTIES
  {
    Date=04/12/16;
    Time=[ 7:36:26 PM];
    Modified=Yes;
    Version List=EZP2.22.00;
  }
  PROPERTIES
  {
    CaptionML=ENU=Easy PDF Attachment;
    InsertAllowed=No;
    ModifyAllowed=No;
    SourceTable=Table14103466;
    DataCaptionExpr=EasyPDF.Basename(ServerFilePath);
    PageType=NavigatePage;
    PromotedActionCategoriesML=ENU=New,Process,Report,Attachment;
    ShowFilter=No;
    OnOpenPage=BEGIN

                 PageIsEditable := CurrPage.EDITABLE;
                 AddIsVisible := (Rec.GETFILTER(AttachmentType) <> '0') AND (Rec.GETFILTER(AttachmentType) <> '1');
               END;

    OnClosePage=BEGIN

                  // Clean up any temporary files we may have created

                  IF Rec.FIND('-') THEN
                    REPEAT
                      IF ClientTempFilePath <> '' THEN
                      BEGIN
                        FM.DeleteClientFile(ClientTempFilePath);
                        ClientTempFilePath := '';
                        Rec.MODIFY;
                      END;
                    UNTIL NEXT = 0;
                END;

    ActionList=ACTIONS
    {
      { 1000000003;  ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 1000000004;1 ;Action    ;
                      Name=ActionAdd;
                      CaptionML=ENU=New;
                      Promoted=Yes;
                      Visible=AddIsVisible;
                      Enabled=PageIsEditable;
                      PromotedIsBig=Yes;
                      Image=New;
                      PromotedCategory=Category4;
                      OnAction=VAR
                                 Path@1000000001 : Text[1024];
                                 EasyPDFAttachment@1000000000 : Record 14103466;
                                 BLOBRef@1000000003 : Record 99008535;
                               BEGIN

                                 Path := FM.OpenFileDialog('Select Attachment', '', FM.GetToFilterText('',''));
                                 IF Path <> '' THEN
                                 BEGIN

                                   EasyPDFAttachment.INIT;
                                   IF GETFILTER(AttachmentType) <> '' THEN
                                     EVALUATE(EasyPDFAttachment.AttachmentType, Rec.GETFILTER(AttachmentType));
                                   EasyPDFAttachment.Code := Rec.GETFILTER(Code);
                                   EasyPDFAttachment.UserId := GETFILTER(UserId);
                                   EasyPDFAttachment.BatchId := Rec.GETFILTER(BatchId);
                                   IF GETFILTER(BatchEntryNo) <> '' THEN
                                     EVALUATE(EasyPDFAttachment.BatchEntryNo, GETFILTER(BatchEntryNo));
                                   IF AddAsBlob THEN
                                   BEGIN
                                     EasyPDF.BLOBImportClient(BLOBRef,Path);
                                     EasyPDFAttachment.Blob := BLOBRef.Blob;
                                   END ELSE
                                     EasyPDFAttachment.ServerFilePath := CopyAttachmentToServer(Path);
                                   EasyPDFAttachment.Filename := EasyPDF.Basename(Path);
                                   EasyPDFAttachment.INSERT;
                                 END;
                               END;
                                }
      { 1000000006;1 ;Action    ;
                      Name=ActionDelete;
                      CaptionML=ENU=Delete;
                      Promoted=Yes;
                      Enabled=PageIsEditable;
                      PromotedIsBig=Yes;
                      Image=Delete;
                      PromotedCategory=Category4;
                      OnAction=VAR
                                 EasyPDFAttachment@1000000000 : Record 14103466;
                               BEGIN

                                 IF CONFIRM('Delete selected attachments?') THEN
                                 BEGIN
                                   EasyPDFAttachment.COPY(Rec);
                                   CurrPage.SETSELECTIONFILTER(EasyPDFAttachment);
                                   EasyPDFAttachment.DELETEALL(TRUE);
                                 END;
                               END;
                                }
      { 1000000005;1 ;Action    ;
                      Name=ActionShow;
                      CaptionML=ENU=Show;
                      Promoted=Yes;
                      Enabled=TRUE;
                      PromotedIsBig=Yes;
                      Image=Find;
                      PromotedCategory=Category4;
                      OnAction=BEGIN

                                 EasyPDF.ShowAttachment(Rec);
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1000000000;;Container;
                Name=Container;
                ContainerType=ContentArea }

    { 1000000001;1;Group  ;
                Name=Group;
                CaptionML=ENU=Attachments;
                GroupType=Repeater }

    { 1000000002;2;Field  ;
                Name=FileName;
                CaptionML=ENU=File Name;
                SourceExpr=Filename }

  }
  CODE
  {
    VAR
      EasyPDF@1000000001 : Codeunit 37062300;
      TEXT001@1000000002 : TextConst 'ENU=File not found - %1';
      PageIsEditable@1000000005 : Boolean INDATASET;
      FM@1000000000 : Codeunit 419;
      AddIsVisible@1000000003 : Boolean INDATASET;
      AddAsBlob@1000000004 : Boolean;

    PROCEDURE CopyAttachmentToServer@1000000000(FilePath@1000000000 : Text[260]) : Text[260];
    VAR
      FileName@1000000001 : Text[80];
      DestPath@1000000002 : Text[260];
      DestFile@1000000005 : Text[260];
      EasyPDFSetup@1000000003 : Record 14103460;
      SysIoFileClient@1000000004 : DotNet "'mscorlib'.System.IO.File" RUNONCLIENT;
      SysIoDirectoryServer@1000000006 : DotNet "'mscorlib'.System.IO.Directory";
    BEGIN

      IF NOT SysIoFileClient.Exists(FilePath) THEN
        ERROR('Create Attachment Error. \File not found: %1', FilePath);

      EasyPDF.GetSetup(EasyPDFSetup);

      FileName := EasyPDF.Basename(FilePath);
      DestPath := EasyPDFSetup.ServerTempPath + EasyPDF.Basename(UserId);
      DestFile := DestPath + '\' + FileName;

      SysIoDirectoryServer.CreateDirectory(DestPath);
      EasyPDF.CopyToServer(FilePath, DestPath, FileName);

      EXIT(DestFile);
    END;

    PROCEDURE SetAddAsBlob@1000000001(Flag@1000000000 : Boolean);
    BEGIN
      AddAsBlob := Flag;
    END;

    BEGIN
    END.
  }
}

