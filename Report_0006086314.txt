OBJECT Report 6086314 Batch Post Settlements
{
  OBJECT-PROPERTIES
  {
    Date=10/25/17;
    Time=[ 1:00:00 AM];
    Version List=EMW17.00.00.2.60.02;
  }
  PROPERTIES
  {
    CaptionML=ENU=Batch Post Settlements;
    ProcessingOnly=Yes;
    OnPreReport=BEGIN
                  IF ReplacePostingDate AND (PostingDateReq = 0D) THEN
                    ERROR(Text000);
                END;

    OnPostReport=BEGIN
                   Window.CLOSE;

                   IF CounterOK <> 0 THEN
                     MESSAGE(Text004,CounterOK,Counter);
                 END;

  }
  DATASET
  {
    { 1   ;    ;DataItem;                    ;
               DataItemTable=Table6086339;
               DataItemTableView=SORTING(Posted,Continia User ID,Status);
               OnPreDataItem=BEGIN
                               SETRANGE(Posted,FALSE);
                               SETRANGE(Status,Status::Released);

                               CounterTotal := COUNT;

                               Window.OPEN(Text001 + Text002 + Text003);
                             END;

               OnAfterGetRecord=VAR
                                  Settlement2@1000 : Record 6086339;
                                BEGIN
                                  Counter := Counter + 1;
                                  Mileage.SETRANGE("Settlement No.","No.");
                                  IF Mileage.FINDSET THEN
                                    REPEAT
                                      CODEUNIT.RUN(CODEUNIT::"Mileage-Validate",Mileage);
                                      IF Mileage.HasErrorComment(FALSE) OR Mileage.HasWarningComment(FALSE) THEN
                                        CurrReport.SKIP;
                                    UNTIL Mileage.NEXT = 0;

                                  Expense.SETRANGE("Settlement No.","No.");
                                  IF Expense.FINDSET THEN
                                    WITH Expense DO
                                      REPEAT
                                        IF AllocationsExists THEN BEGIN
                                          CALCFIELDS("Allocated Amount (LCY)");
                                          IF "Allocated Amount (LCY)" <> "Amount (LCY)" THEN
                                            CurrReport.SKIP;
                                        END;

                                        CODEUNIT.RUN(CODEUNIT::"Expense-Validate",Expense);
                                        IF HasErrorComment(FALSE) OR HasWarningComment(FALSE) THEN
                                          CurrReport.SKIP;

                                      UNTIL NEXT = 0;
                                  Window.UPDATE(1,ROUND(Counter / CounterTotal * 10000,1));

                                  COMMIT;

                                  Settlement2 := "Expense Header";
                                  IF SettlementCheck.RUN(Settlement2) THEN BEGIN
                                    TempSettlementToPost.COPY(Settlement2);
                                    TempSettlementToPost.INSERT;

                                    IF MARKEDONLY THEN
                                      MARK(FALSE);
                                  END;
                                END;

               ReqFilterFields=Continia User ID,Posting Date }

    { 2   ;    ;DataItem;                    ;
               DataItemTable=Table2000000026;
               DataItemTableView=SORTING(Number);
               OnPreDataItem=BEGIN
                               CounterTotal := TempSettlementToPost.COUNT;
                               SETRANGE(Number,1,CounterTotal);
                             END;

               OnAfterGetRecord=BEGIN
                                  IF Number = 1 THEN
                                    TempSettlementToPost.FINDFIRST
                                  ELSE
                                    TempSettlementToPost.NEXT;

                                  "Expense Header".GET(TempSettlementToPost."Document Type",TempSettlementToPost."No.");
                                  Window.UPDATE(2,ROUND(Number / CounterTotal * 10000,1));
                                  IF ReplacePostingDate THEN BEGIN
                                    TempSettlementToPost."Posting Date" := PostingDateReq;
                                    TempSettlementToPost.UpdatePostingDate(PostingDatePolicy);
                                  END;
                                  SettlementPost.RunWithoutCheck(TempSettlementToPost);

                                  CounterOK := CounterOK + 1;
                                END;
                                 }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      OnOpenPage=BEGIN
                   PostingDateEditable := PostingDatePolicy = PostingDatePolicy::"Specific Date";
                 END;

    }
    CONTROLS
    {
      { 5   ;0   ;Container ;
                  ContainerType=ContentArea }

      { 4   ;1   ;Group     ;
                  CaptionML=ENU=Options }

      { 3   ;2   ;Field     ;
                  CaptionML=ENU=Replace Posting Date;
                  SourceExpr=ReplacePostingDate }

      { 1160040000;2;Field  ;
                  CaptionML=ENU=Posting Date Policy;
                  OptionCaptionML=ENU=First Expense/Mileage Date,Last Expense/Mileage Date,Specific Date;
                  SourceExpr=PostingDatePolicy;
                  OnValidate=BEGIN
                               IF PostingDatePolicy <> PostingDatePolicy::"Specific Date" THEN
                                 CLEAR(PostingDateReq);

                               PostingDateEditable := PostingDatePolicy = PostingDatePolicy::"Specific Date";
                             END;
                              }

      { 2   ;2   ;Field     ;
                  CaptionML=ENU=Posting Date;
                  SourceExpr=PostingDateReq;
                  Editable=PostingDateEditable }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      SettlementPost@1010 : Codeunit 6086338;
      SettlementCheck@1001 : Codeunit 6086349;
      TempSettlementToPost@1000 : TEMPORARY Record 6086339;
      Window@1009 : Dialog;
      Counter@1008 : Integer;
      CounterOK@1007 : Integer;
      CounterTotal@1006 : Integer;
      PostingDateReq@1005 : Date;
      ReplacePostingDate@1004 : Boolean;
      ReplaceDocumentDate@1003 : Boolean;
      Text000@1015 : TextConst 'ENU=Please enter the posting date.';
      Text001@1014 : TextConst 'ENU=Posting Settlements...';
      Text002@1013 : TextConst 'ENU=\Checking lines @1@@@@@@@@@@@@@@@@@@@@@@@@@@';
      Text003@1012 : TextConst 'ENU=\Posting lines  @2@@@@@@@@@@@@@@@@@@@@@@@@@@';
      Expense@1002 : Record 6086320;
      Mileage@1016 : Record 6086338;
      PostingDatePolicy@1160040002 : 'First Expense/Mileage Date,Last Expense/Mileage Date,Specific Date';
      PostingDateEditable@1018 : Boolean INDATASET;
      Text004@1017 : TextConst 'ENU=%1 Settlement out of a total of %2 have been posted.';

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

