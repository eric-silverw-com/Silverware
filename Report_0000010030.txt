OBJECT Report 10030 Budget Amount by Period
{
  OBJECT-PROPERTIES
  {
    Date=11/22/17;
    Time=12:00:00 PM;
    Version List=NAVNA11.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Budget Amount by Period;
               ESM=Imp. ppto. por per¡odo;
               FRC=Montant budget par p‚riode;
               ENC=Budget Amount by Period];
    ProcessingOnly=Yes;
    OnPreReport=BEGIN
                  GLSetup.GET;
                  GlobalDim1Code := GLSetup."Global Dimension 1 Code";
                  GlobalDim2Code := GLSetup."Global Dimension 2 Code";
                END;

  }
  DATASET
  {
    { 6710;    ;DataItem;                    ;
               DataItemTable=Table15;
               DataItemTableView=SORTING(No.);
               MaxIteration=1;
               OnPreDataItem=BEGIN
                               IF DistributeAmt = 0 THEN
                                 ERROR(Text001);
                               IF BudgetDate = 0D THEN
                                 ERROR(Text002);
                               IF NoPeriods = 0 THEN
                                 ERROR(Text003);
                             END;

               ReqFilterFields=No.,Global Dimension 1 Filter,Global Dimension 2 Filter,Budget Filter }

    { 5444;    ;DataItem;                    ;
               DataItemTable=Table2000000026;
               DataItemTableView=SORTING(Number);
               OnPreDataItem=BEGIN
                               CASE Rounding OF
                                 Rounding::Pennies:
                                   Precision := 0.05;
                                 Rounding::Dollars:
                                   Precision := 1;
                                 Rounding::Hundreds:
                                   Precision := 100;
                                 Rounding::Thousands:
                                   Precision := 1000;
                                 Rounding::Millions:
                                   Precision := 1000000;
                               END;

                               PercentChg := PercentChg / 100;
                               IF PercentChg <= 1 THEN
                                 PercentChg := 1 + PercentChg;
                               IF AmountType = AmountType::"Total Amount" THEN BEGIN
                                 IF PercentChg <> 1 THEN
                                   BaseAmount := DistributeAmt / (((PercentChg - POWER(PercentChg,NoPeriods)) / (1 - PercentChg) ) + 1)
                                 ELSE
                                   BaseAmount := DistributeAmt / NoPeriods;
                               END ELSE
                                 BaseAmount := DistributeAmt;

                               GLBudgetEntry.RESET;
                               IF GLBudgetEntry.FINDLAST THEN
                                 EntryNo := GLBudgetEntry."Entry No.";
                             END;

               OnAfterGetRecord=VAR
                                  TempDimSetEntry@1020000 : TEMPORARY Record 480;
                                  DimensionValue@1020001 : Record 349;
                                  DimMgt@1020002 : Codeunit 408;
                                BEGIN
                                  CurrentPeriod := CurrentPeriod + 1;
                                  IF CurrentPeriod > NoPeriods THEN
                                    CurrReport.BREAK
                                    ;
                                  IF CurrentPeriod > 1 THEN BEGIN
                                    BaseAmount := BaseAmount * PercentChg;
                                    BudgetDate := NextDate(BudgetDate);
                                  END;
                                  EntryNo := EntryNo + 10000;
                                  GLBudgetEntry.INIT;
                                  GLBudgetEntry."Entry No." := EntryNo;
                                  GLBudgetEntry."Budget Name" := "G/L Account".GETFILTER("Budget Filter");
                                  GLBudgetEntry."G/L Account No." := "G/L Account"."No.";
                                  GLBudgetEntry.Date := BudgetDate;
                                  IF "G/L Account".GETFILTER("Global Dimension 1 Filter") <> '' THEN BEGIN
                                    GLBudgetEntry."Global Dimension 1 Code" := "G/L Account".GETRANGEMIN("Global Dimension 1 Filter");
                                    IF DimensionValue.GET(GlobalDim1Code,GLBudgetEntry."Global Dimension 1 Code") THEN BEGIN
                                      TempDimSetEntry.INIT;
                                      TempDimSetEntry."Dimension Code" := DimensionValue."Dimension Code";
                                      TempDimSetEntry."Dimension Value Code" := DimensionValue.Code;
                                      TempDimSetEntry."Dimension Value ID" := DimensionValue."Dimension Value ID";
                                      TempDimSetEntry.INSERT;
                                    END;
                                  END;
                                  IF "G/L Account".GETFILTER("Global Dimension 2 Filter") <> '' THEN BEGIN
                                    GLBudgetEntry."Global Dimension 2 Code" := "G/L Account".GETRANGEMIN("Global Dimension 2 Filter");
                                    IF DimensionValue.GET(GlobalDim2Code,GLBudgetEntry."Global Dimension 2 Code") THEN BEGIN
                                      TempDimSetEntry.INIT;
                                      TempDimSetEntry."Dimension Code" := DimensionValue."Dimension Code";
                                      TempDimSetEntry."Dimension Value Code" := DimensionValue.Code;
                                      TempDimSetEntry."Dimension Value ID" := DimensionValue."Dimension Value ID";
                                      TempDimSetEntry.INSERT;
                                    END;
                                  END;
                                  GLBudgetEntry."Dimension Set ID" := DimMgt.GetDimensionSetID(TempDimSetEntry);
                                  GLBudgetEntry.Amount := ROUND(BaseAmount,Precision);
                                  GLBudgetEntry.INSERT;
                                END;
                                 }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
      OnOpenPage=BEGIN
                   IF PeriodLength = 0 THEN
                     PeriodLength := PeriodLength::Month;
                   IF Rounding = 0 THEN
                     Rounding := Rounding::Dollars;
                   IF NoPeriods = 0 THEN
                     NoPeriods := 12;
                 END;

    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[ENU=Options;
                             ESM=Opciones;
                             FRC=Options;
                             ENC=Options] }

      { 7   ;2   ;Field     ;
                  CaptionML=[ENU=Budget Beginning Date;
                             ESM=Fecha inicial ppto.;
                             FRC=Date de d‚but budget;
                             ENC=Budget Beginning Date];
                  ToolTipML=[ENU=Specifies the starting date of the first budget period to be created.;
                             ESM=Especifica la fecha de inicio del periodo del primer presupuesto que se debe crear.;
                             FRC=Sp‚cifie la date de d‚but de la premiŠre p‚riode budg‚taire … cr‚er.;
                             ENC=Specifies the starting date of the first budget period to be created.];
                  ApplicationArea=#Suite;
                  SourceExpr=BudgetDate }

      { 5   ;2   ;Field     ;
                  CaptionML=[ENU=No. of Periods;
                             ESM=N§ de periodos;
                             FRC=Nombre de p‚riodes;
                             ENC=No. of Periods];
                  ToolTipML=[ENU=Specifies the number of budget periods to be created.;
                             ESM=Especifica n£mero de periodos de presupuesto que se deben crear.;
                             FRC=Sp‚cifie le nombre de p‚riodes budg‚taires … cr‚er.;
                             ENC=Specifies the number of budget periods to be created.];
                  ApplicationArea=#Suite;
                  SourceExpr=NoPeriods }

      { 10  ;2   ;Field     ;
                  CaptionML=[ENU=Period Length;
                             ESM=Longitud periodo;
                             FRC=Dur‚e de la p‚riode;
                             ENC=Period Length];
                  ToolTipML=[ENU=Specifies the length of each period, for example, enter "1M" for one month.;
                             ESM=Especifica la longitud de cada periodo. Por ejemplo, escriba "1M" para un mes.;
                             FRC=Sp‚cifie la longueur de chaque p‚riode, par exemple, entrez ®ÿ1Mÿ¯ pour un mois.;
                             ENC=Specifies the length of each period, for example, enter "1M" for one month.];
                  OptionCaptionML=[ENU=,Day,Week,Month,Quarter,Year,Accounting Period;
                                   ESM=,D¡a,Semana,Mes,Trimestre,A¤o,Periodo contable;
                                   FRC=,Jour,Semaine,Mois,Trimestre,Ann‚e,P‚riode comptable;
                                   ENC=,Day,Week,Month,Quarter,Year,Accounting Period];
                  ApplicationArea=#Suite;
                  SourceExpr=PeriodLength }

      { 3   ;2   ;Field     ;
                  CaptionML=[ENU=Budget Amount;
                             ESM=Imp. ppto.;
                             FRC=Montant budget;
                             ENC=Budget Amount];
                  ToolTipML=[ENU="Specifies the budgeted amount. ";
                             ESM="Especifica el importe presupuestado. ";
                             FRC="Sp‚cifie le montant budg‚t‚. ";
                             ENC="Specifies the budgeted amount. "];
                  ApplicationArea=#Suite;
                  SourceExpr=DistributeAmt }

      { 12  ;2   ;Field     ;
                  CaptionML=[ENU=Amount Type;
                             ESM=Tipo importe;
                             FRC=Type de montant;
                             ENC=Amount Type];
                  ToolTipML=[ENU=Specifies what is shown in the Budget Amount field. Total Amount: The amount is the total of all of the periods to be created. Beginning Amount: The amount is the budget for the first period to be created.;
                             ESM=Especifica lo que se muestra en el campo Importe presupuesto. Importe total: el importe es el total de todos los periodos que se van a crear. Importe inicial: el importe corresponde al presupuesto del primer periodo que se va a crear.;
                             FRC=Sp‚cifie ce qui est affich‚ dans le champ Montant budget. Montant totalÿ: le montant est le total de toutes les p‚riodes … cr‚er. Montant de d‚partÿ: le montant est le budget pour la premiŠre p‚riode … cr‚er.;
                             ENC=Specifies what is shown in the Budget Amount field. Total Amount: The amount is the total of all of the periods to be created. Beginning Amount: The amount is the budget for the first period to be created.];
                  OptionCaptionML=[ENU=Total Amount,Beginning Amount;
                                   ESM=Importe total,Importe inicial;
                                   FRC=Montant total,Montant de d‚part;
                                   ENC=Total Amount,Beginning Amount];
                  ApplicationArea=#Suite;
                  SourceExpr=AmountType }

      { 8   ;2   ;Field     ;
                  CaptionML=[ENU=Period Percent Change;
                             ESM=Cambio porcen. per¡odo;
                             FRC=Pourcentage changement de la p‚riode;
                             ENC=Period Percent Change];
                  ToolTipML=[ENU=Specifies if you want the budget to increase in relation to the previous period. For example, if the budget amount is $100, and the amount type is Beginning Amount, and the period percent change is 10, then the first period will have a budget of $100, the second period will have a budget of $110, the third will have a budget of $121.;
                             ESM=Especifica si desea que el presupuesto se incremente en relaci¢n con el periodo anterior. Por ejemplo, si el importe presupuestario es 100 USD, el tipo de importe es Importe inicial y el cambio de porcentaje de periodo es 10, el primer periodo tendr  un presupuesto de 100 USD, el segundo, de 110 USD, el tercero, de 121 USD, etc.;
                             FRC=Sp‚cifie si vous souhaitez que le budget augmente par rapport … la p‚riode pr‚c‚dente. Par exemple, si le montant du budget correspond … 100ÿ$, que le type de montant est Montant de d‚part et que le pourcentage changement de la p‚riode est 10, la premiŠre p‚riode pr‚sentera un budget de 100ÿ$, la deuxiŠme p‚riode un budget de 110ÿ$ et la troisiŠme un budget de 121ÿ$.;
                             ENC=Specifies if you want the budget to increase in relation to the previous period. For example, if the budget amount is $100, and the amount type is Beginning Amount, and the period percent change is 10, then the first period will have a budget of $100, the second period will have a budget of $110, the third will have a budget of $121.];
                  ApplicationArea=#Suite;
                  SourceExpr=PercentChg }

      { 14  ;2   ;Field     ;
                  CaptionML=[ENU=Round To;
                             ESM=Redondear a;
                             FRC=Arrondir …;
                             ENC=Round To];
                  ToolTipML=[ENU=Specifies if you want the results in the report to be rounded to the nearest penny (hundredth of a unit), dollar (unit), or thousand dollars (units). The results are in US dollars, unless you use an additional reporting currency.;
                             ESM=Especifica si desea que los resultados del informe se redondeen al centavo (c‚ntimo de unidad), d¢lar (unidad) o millar de d¢lares (unidades) m s cercano. Los resultados se muestran en USD, a menos que use una divisa de notificaci¢n adicional.;
                             FRC=Sp‚cifie si vous souhaitez que les r‚sultats du rapport soient arrondis au penny le plus proche (centiŠme d'une unit‚), au dollar (unit‚) ou au millier de dollars (unit‚s). Les r‚sultats sont en dollars US, sauf si vous utilisez une devise de report additionnelle.;
                             ENC=Specifies if you want the results in the report to be rounded to the nearest penny (hundredth of a unit), dollar (unit), or thousand dollars (units). The results are in CA Dollars, unless you use an additional reporting currency.];
                  OptionCaptionML=[ENU=,Pennies,Dollars,Hundreds,Thousands,Millions;
                                   ESM=,Centavos,Pesos,Cientos,Miles,Millones;
                                   FRC=,Cents,Dollars,Centaines,Milliers,Millions;
                                   ENC=,Pennies,Dollars,Hundreds,Thousands,Millions];
                  ApplicationArea=#Suite;
                  SourceExpr=Rounding }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      GLBudgetEntry@1000 : Record 96;
      AcctPeriod@1001 : Record 50;
      GLSetup@1020001 : Record 98;
      DistributeAmt@1002 : Decimal;
      BaseAmount@1003 : Decimal;
      PercentChg@1004 : Decimal;
      Precision@1005 : Decimal;
      BudgetDate@1006 : Date;
      AmountType@1007 : 'Total Amount,Beginning Amount';
      Rounding@1008 : ',Pennies,Dollars,Hundreds,Thousands,Millions';
      PeriodLength@1009 : ',Day,Week,Month,Quarter,Year,Accounting Period';
      NoPeriods@1010 : Integer;
      CurrentPeriod@1011 : Integer;
      EntryNo@1012 : Integer;
      Text000@1014 : TextConst 'ENU=There are not enough accounting periods set up to complete this budget.;ESM=No hay suficientes per¡odos contables para completar este presupuesto.;FRC=Il n''y a pas assez de p‚riodes comptables configur‚es pour compl‚ter ce budget.;ENC=There are not enough accounting periods set up to complete this budget.';
      Text001@1015 : TextConst 'ENU=Please enter an amount to budget.;ESM=Inserte un importe para ppto.;FRC=Entrez un montant au budget.;ENC=Please enter an amount to budget.';
      Text002@1016 : TextConst 'ENU=Please enter a beginning date.;ESM=Introduzca una fecha inicial.;FRC=Entrez une date de d‚but;ENC=Please enter a beginning date.';
      Text003@1017 : TextConst 'ENU=Please enter the number of periods to budget.;ESM=Inserte el n§ de per¡odos para presupuestar.;FRC=Entrez un nombre de p‚riodes au budget.;ENC=Please enter the number of periods to budget.';
      GlobalDim1Code@1020002 : Code[20];
      GlobalDim2Code@1020000 : Code[20];

    [External]
    PROCEDURE NextDate@1(CurrentDate@1000 : Date) : Date;
    VAR
      CalculatedDate@1001 : Date;
    BEGIN
      CASE PeriodLength OF
        PeriodLength::Day:
          CalculatedDate := CALCDATE('<+1D>',CurrentDate);
        PeriodLength::Week:
          CalculatedDate := CALCDATE('<+1W>',CurrentDate);
        PeriodLength::Month:
          CalculatedDate := CALCDATE('<+1M>',CurrentDate);
        PeriodLength::Quarter:
          CalculatedDate := CALCDATE('<+1Q>',CurrentDate);
        PeriodLength::Year:
          CalculatedDate := CALCDATE('<+1Y>',CurrentDate);
        PeriodLength::"Accounting Period":
          BEGIN
            AcctPeriod.SETFILTER("Starting Date",'>%1',CurrentDate);
            IF NOT AcctPeriod.FINDFIRST THEN
              ERROR(Text000);
            CalculatedDate := AcctPeriod."Starting Date";
          END;
      END;
      EXIT(CalculatedDate);
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

