OBJECT Table 570 G/L Account Category
{
  OBJECT-PROPERTIES
  {
    Date=09/19/18;
    Time=12:00:00 PM;
    Version List=NAVW113.00;
  }
  PROPERTIES
  {
    DataCaptionFields=Description;
    OnDelete=VAR
               GLAccount@1000 : Record 15;
             BEGIN
               IF "System Generated" THEN
                 ERROR(CannotDeleteSystemGeneratedErr,Description);
               GLAccount.SETRANGE("Account Subcategory Entry No.","Entry No.");
               IF GLAccount.FINDFIRST THEN
                 ERROR(CategoryUsedOnAccountErr,TABLECAPTION,Description,GLAccount.TABLECAPTION,GLAccount."No.");
               DeleteChildren("Entry No.");
             END;

    CaptionML=[ENU=G/L Account Category;
               ESM=Categor¡a de cuenta;
               FRC=Cat‚gorie de compte du grand livre;
               ENC=G/L Account Category];
  }
  FIELDS
  {
    { 1   ;   ;Entry No.           ;Integer       ;AutoIncrement=Yes;
                                                   CaptionML=[ENU=Entry No.;
                                                              ESM=N§ mov.;
                                                              FRC=Nø ‚criture;
                                                              ENC=Entry No.] }
    { 2   ;   ;Parent Entry No.    ;Integer       ;CaptionML=[ENU=Parent Entry No.;
                                                              ESM=N§ movimiento principal;
                                                              FRC=Nø s‚quence parent;
                                                              ENC=Parent Entry No.] }
    { 3   ;   ;Sibling Sequence No.;Integer       ;CaptionML=[ENU=Sibling Sequence No.;
                                                              ESM=N§ secuencia del mismo nivel;
                                                              FRC=Nø s‚quence sour;
                                                              ENC=Sibling Sequence No.] }
    { 4   ;   ;Presentation Order  ;Text100       ;CaptionML=[ENU=Presentation Order;
                                                              ESM=Orden presentaci¢n;
                                                              FRC=Ordre de pr‚sentation;
                                                              ENC=Presentation Order] }
    { 5   ;   ;Indentation         ;Integer       ;CaptionML=[ENU=Indentation;
                                                              ESM=Indentar;
                                                              FRC=Indentation;
                                                              ENC=Indentation] }
    { 6   ;   ;Description         ;Text80        ;CaptionML=[ENU=Description;
                                                              ESM=Descripci¢n;
                                                              FRC=Description;
                                                              ENC=Description] }
    { 7   ;   ;Account Category    ;Option        ;OnValidate=BEGIN
                                                                IF "Account Category" IN ["Account Category"::Income,"Account Category"::"Cost of Goods Sold","Account Category"::Expense]
                                                                THEN BEGIN
                                                                  "Income/Balance" := "Income/Balance"::"Income Statement";
                                                                  "Additional Report Definition" := "Additional Report Definition"::" ";
                                                                END ELSE
                                                                  "Income/Balance" := "Income/Balance"::"Balance Sheet";
                                                                IF Description = '' THEN
                                                                  Description := FORMAT("Account Category");
                                                                UpdatePresentationOrder;
                                                              END;

                                                   CaptionML=[ENU=Account Category;
                                                              ESM=Categor¡a de cuenta;
                                                              FRC=Cat‚gorie du compte;
                                                              ENC=Account Category];
                                                   OptionCaptionML=[ENU=,Assets,Liabilities,Equity,Income,Cost of Goods Sold,Expense;
                                                                    ESM=,Activos,Pasivo,Recursos propios,Ingresos,Costo de las mercanc¡as vendidas,Gasto;
                                                                    FRC=,Immobilisations,Emprunts et dettes,Capitaux propres,Revenus,Co–t des marchandises vendues,D‚penses;
                                                                    ENC=,Assets,Liabilities,Equity,Income,Cost of Goods Sold,Expense];
                                                   OptionString=,Assets,Liabilities,Equity,Income,Cost of Goods Sold,Expense;
                                                   BlankZero=Yes }
    { 8   ;   ;Income/Balance      ;Option        ;OnValidate=BEGIN
                                                                UpdatePresentationOrder;
                                                              END;

                                                   CaptionML=[ENU=Income/Balance;
                                                              ESM=Ingresos/Saldo;
                                                              FRC=Gestion/Bilan;
                                                              ENC=Income/Balance];
                                                   OptionCaptionML=[ENU=Income Statement,Balance Sheet;
                                                                    ESM=Balance de ingresos,Balance;
                                                                    FRC=Gestion,Bilan;
                                                                    ENC=Income Statement,Balance Sheet];
                                                   OptionString=Income Statement,Balance Sheet;
                                                   Editable=No }
    { 9   ;   ;Additional Report Definition;Option;OnValidate=BEGIN
                                                                IF "Additional Report Definition" <> "Additional Report Definition"::" " THEN
                                                                  TESTFIELD("Income/Balance","Income/Balance"::"Balance Sheet");
                                                              END;

                                                   CaptionML=[ENU=Additional Report Definition;
                                                              ESM=Definici¢n de informe adicional;
                                                              FRC=D‚finition de rapport suppl‚mentaire;
                                                              ENC=Additional Report Definition];
                                                   OptionCaptionML=[ENU=" ,Operating Activities,Investing Activities,Financing Activities,Cash Accounts,Retained Earnings,Distribution to Shareholders";
                                                                    ESM=" ,Actividades de explotaci¢n,Actividades de inversi¢n,Actividades de financiaci¢n,Cuentas de efectivo,Ganancias retenidas,Distribuci¢n a accionistas";
                                                                    FRC=" ,Activit‚s d'exploitation,Activit‚s d'investissement,Activit‚s de financement,Comptes de tr‚sorerie,R‚serves,R‚partition aux actionnaires";
                                                                    ENC=" ,Operating Activities,Investing Activities,Financing Activities,Cash Accounts,Retained Earnings,Distribution to Shareholders"];
                                                   OptionString=[ ,Operating Activities,Investing Activities,Financing Activities,Cash Accounts,Retained Earnings,Distribution to Shareholders] }
    { 11  ;   ;System Generated    ;Boolean       ;CaptionML=[ENU=System Generated;
                                                              ESM=Generado por el sistema;
                                                              FRC=G‚n‚r‚ par le systŠme;
                                                              ENC=System Generated] }
    { 12  ;   ;Has Children        ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Exist("G/L Account Category" WHERE (Parent Entry No.=FIELD(Entry No.)));
                                                   CaptionML=[ENU=Has Children;
                                                              ESM=Tiene hijos;
                                                              FRC=A des enfants;
                                                              ENC=Has Children] }
  }
  KEYS
  {
    {    ;Entry No.                               ;Clustered=Yes }
    {    ;Presentation Order,Sibling Sequence No.  }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      NewCategoryTxt@1001 : TextConst 'ENU=<Enter a Name>;ESM=<especificar un nombre>;FRC=<Entrer un nom>;ENC=<Enter a Name>';
      CannotDeleteSystemGeneratedErr@1019 : TextConst '@@@="%1 = a category value, e.g. ""Assets""";ENU=%1 is a system generated category and cannot be deleted.;ESM=%1 es una categor¡a generada por el sistema y no se puede eliminar.;FRC=%1 est une cat‚gorie g‚n‚r‚e par le systŠme et ne peut pas ˆtre supprim‚e.;ENC=%1 is a system generated category and cannot be deleted.';
      NoAccountsInFilterErr@1002 : TextConst '@@@="%1 = either ''Balance Sheet'' or ''Income Statement''";ENU=There are no G/L Accounts in the filter of type %1.;ESM=No hay cuentas en el filtro de tipo %1.;FRC=Il n''existe aucun compte g‚n‚ral dans le filtre du type %1.;ENC=There are no G/L Accounts in the filter of type %1.';
      CategoryUsedOnAccountErr@1003 : TextConst '@@@="%1=account category table name, %2=category description, %3=g/l account table name, %4=g/l account number.";ENU=You cannot delete %1 %2 because it is used in %3 %4.;ESM=No puede eliminar %1 %2 porque se usa en %3 %4.;FRC=Vous ne pouvez pas supprimer l''enregistrement %1 %2, car il est utilis‚ dans %3 %4.;ENC=You cannot delete %1 %2 because it is used in %3 %4.';

    [External]
    PROCEDURE UpdatePresentationOrder@4();
    VAR
      GLAccountCategory@1000 : Record 570;
      PresentationOrder@1001 : Text;
    BEGIN
      IF "Entry No." = 0 THEN
        EXIT;
      GLAccountCategory := Rec;
      IF "Sibling Sequence No." = 0 THEN
        "Sibling Sequence No." := "Entry No." * 10000 MOD 2000000000;
      Indentation := 0;
      PresentationOrder := COPYSTR(FORMAT(1000000 + "Sibling Sequence No."),2);
      WHILE GLAccountCategory."Parent Entry No." <> 0 DO BEGIN
        Indentation += 1;
        GLAccountCategory.GET(GLAccountCategory."Parent Entry No.");
        PresentationOrder := COPYSTR(FORMAT(1000000 + GLAccountCategory."Sibling Sequence No."),2) + PresentationOrder;
      END;
      CASE "Account Category" OF
        "Account Category"::Assets:
          PresentationOrder := '0' + PresentationOrder;
        "Account Category"::Liabilities:
          PresentationOrder := '1' + PresentationOrder;
        "Account Category"::Equity:
          PresentationOrder := '2' + PresentationOrder;
        "Account Category"::Income:
          PresentationOrder := '3' + PresentationOrder;
        "Account Category"::"Cost of Goods Sold":
          PresentationOrder := '4' + PresentationOrder;
        "Account Category"::Expense:
          PresentationOrder := '5' + PresentationOrder;
      END;
      "Presentation Order" := COPYSTR(PresentationOrder,1,MAXSTRLEN("Presentation Order"));
      MODIFY;
    END;

    [External]
    PROCEDURE InitializeDataSet@1();
    BEGIN
      CODEUNIT.RUN(CODEUNIT::"G/L Account Category Mgt.");
    END;

    [External]
    PROCEDURE InsertRow@6() : Integer;
    VAR
      GLAccountCategoryMgt@1000 : Codeunit 570;
    BEGIN
      EXIT(GLAccountCategoryMgt.AddCategory("Entry No.","Parent Entry No.","Account Category",NewCategoryTxt,FALSE,0));
    END;

    LOCAL PROCEDURE Move@13(Steps@1002 : Integer);
    VAR
      GLAccountCategory@1000 : Record 570;
      SiblingOrder@1001 : Integer;
    BEGIN
      IF "Entry No." = 0 THEN
        EXIT;
      GLAccountCategory := Rec;
      GLAccountCategory.SETRANGE("Parent Entry No.","Parent Entry No.");
      GLAccountCategory.SETRANGE("Account Category","Account Category");
      GLAccountCategory.SETCURRENTKEY("Presentation Order","Sibling Sequence No.");
      IF GLAccountCategory.NEXT(Steps) = 0 THEN
        EXIT;
      SiblingOrder := "Sibling Sequence No.";
      "Sibling Sequence No." := GLAccountCategory."Sibling Sequence No.";
      GLAccountCategory."Sibling Sequence No." := SiblingOrder;
      GLAccountCategory.UpdatePresentationOrder;
      GLAccountCategory.MODIFY;
      UpdatePresentationOrder;
      MODIFY;
      UpdateDescendants(Rec);
      UpdateDescendants(GLAccountCategory);
    END;

    [External]
    PROCEDURE MoveUp@11();
    BEGIN
      Move(-1);
    END;

    [External]
    PROCEDURE MoveDown@14();
    BEGIN
      Move(1);
    END;

    LOCAL PROCEDURE ChangeAncestor@10(ChangeToChild@1002 : Boolean);
    VAR
      GLAccountCategory@1000 : Record 570;
    BEGIN
      IF "Entry No." = 0 THEN
        EXIT;
      GLAccountCategory := Rec;
      IF ChangeToChild THEN BEGIN
        GLAccountCategory.SETRANGE("Parent Entry No.","Parent Entry No.");
        GLAccountCategory.SETRANGE(Indentation,Indentation);
        GLAccountCategory.SETCURRENTKEY("Presentation Order","Sibling Sequence No.");
        IF GLAccountCategory.NEXT(-1) = 0 THEN
          EXIT;
        "Parent Entry No." := GLAccountCategory."Entry No."
      END ELSE
        IF GLAccountCategory.GET("Parent Entry No.") THEN
          "Parent Entry No." := GLAccountCategory."Parent Entry No."
        ELSE
          EXIT;
      UpdatePresentationOrder;
      MODIFY;
      UpdateDescendants(Rec);
    END;

    LOCAL PROCEDURE UpdateDescendants@12(ParentGLAccountCategory@1000 : Record 570);
    VAR
      GLAccountCategory@1001 : Record 570;
    BEGIN
      IF ParentGLAccountCategory."Entry No." = 0 THEN
        EXIT;
      GLAccountCategory.SETRANGE("Parent Entry No.",ParentGLAccountCategory."Entry No.");
      IF GLAccountCategory.FINDSET THEN
        REPEAT
          GLAccountCategory."Income/Balance" := ParentGLAccountCategory."Income/Balance";
          GLAccountCategory."Account Category" := ParentGLAccountCategory."Account Category";
          GLAccountCategory.UpdatePresentationOrder;
          UpdateDescendants(GLAccountCategory);
        UNTIL GLAccountCategory.NEXT = 0;
    END;

    [External]
    PROCEDURE MakeChildOfPreviousSibling@15();
    BEGIN
      ChangeAncestor(TRUE);
    END;

    [External]
    PROCEDURE MakeSiblingOfParent@16();
    BEGIN
      ChangeAncestor(FALSE);
    END;

    [External]
    PROCEDURE DeleteRow@9();
    BEGIN
      IF "Entry No." = 0 THEN
        EXIT;
      DeleteChildren("Entry No.");
      DELETE(TRUE);
    END;

    LOCAL PROCEDURE DeleteChildren@7(ParentEntryNo@1001 : Integer);
    VAR
      GLAccountCategory@1000 : Record 570;
    BEGIN
      GLAccountCategory.SETRANGE("Parent Entry No.",ParentEntryNo);
      IF GLAccountCategory.FINDSET THEN
        REPEAT
          GLAccountCategory.DeleteRow;
        UNTIL GLAccountCategory.NEXT = 0;
    END;

    [External]
    PROCEDURE MapAccounts@8();
    BEGIN
    END;

    [External]
    PROCEDURE ValidateTotaling@5(NewTotaling@1001 : Text);
    VAR
      GLAccount@1000 : Record 15;
      OldTotaling@1002 : Text;
    BEGIN
      OldTotaling := GetTotaling;
      IF NewTotaling = OldTotaling THEN
        EXIT;
      IF NewTotaling <> '' THEN BEGIN
        GLAccount.SETFILTER("No.",NewTotaling);
        GLAccount.SETRANGE("Income/Balance","Income/Balance");
        GLAccount.LOCKTABLE;
        IF NOT GLAccount.FINDSET THEN
          ERROR(NoAccountsInFilterErr,"Income/Balance");
        IF OldTotaling <> '' THEN
          ClearGLAccountSubcategoryEntryNo(OldTotaling,"Income/Balance");
        REPEAT
          GLAccount.VALIDATE("Account Subcategory Entry No.","Entry No.");
          GLAccount.MODIFY(TRUE);
        UNTIL GLAccount.NEXT = 0;
      END ELSE
        ClearGLAccountSubcategoryEntryNo(OldTotaling,"Income/Balance");
    END;

    LOCAL PROCEDURE ClearGLAccountSubcategoryEntryNo@3(Filter@1000 : Text;IncomeBalance@1002 : Integer);
    VAR
      GLAccount@1001 : Record 15;
    BEGIN
      GLAccount.SETFILTER("No.",Filter);
      GLAccount.SETRANGE("Income/Balance",IncomeBalance);
      GLAccount.MODIFYALL("Account Subcategory Entry No.",0);
    END;

    PROCEDURE LookupTotaling@22();
    VAR
      GLAccount@1001 : Record 15;
      GLAccList@1000 : Page 18;
      OldTotaling@1002 : Text;
    BEGIN
      GLAccount.SETRANGE("Income/Balance","Income/Balance");
      OldTotaling := GetTotaling;
      IF OldTotaling <> '' THEN BEGIN
        GLAccount.SETFILTER("No.",OldTotaling);
        IF GLAccount.FINDFIRST THEN
          GLAccList.SETRECORD(GLAccount);
        GLAccount.SETRANGE("No.");
      END;
      GLAccList.SETTABLEVIEW(GLAccount);
      GLAccList.LOOKUPMODE(TRUE);
      IF GLAccList.RUNMODAL = ACTION::LookupOK THEN
        ValidateTotaling(GLAccList.GetSelectionFilter);
    END;

    [External]
    PROCEDURE PositiveNormalBalance@19() : Boolean;
    BEGIN
      EXIT("Account Category" IN ["Account Category"::Expense,"Account Category"::Assets,"Account Category"::"Cost of Goods Sold"]);
    END;

    [External]
    PROCEDURE GetBalance@17() : Decimal;
    VAR
      GLEntry@1000 : Record 17;
      GLAccountCategory@1001 : Record 570;
      Balance@1002 : Decimal;
      TotalingStr@1004 : Text;
    BEGIN
      CALCFIELDS("Has Children");
      IF "Has Children" THEN BEGIN
        GLAccountCategory.SETRANGE("Parent Entry No.","Entry No.");
        IF GLAccountCategory.FINDSET THEN
          REPEAT
            Balance += GLAccountCategory.GetBalance;
          UNTIL GLAccountCategory.NEXT = 0;
      END;
      TotalingStr := GetTotaling;
      IF TotalingStr = '' THEN
        EXIT(Balance);
      GLEntry.SETFILTER("G/L Account No.",TotalingStr);
      GLEntry.CALCSUMS(Amount);
      EXIT(Balance + GLEntry.Amount);
    END;

    PROCEDURE GetTotaling@2() : Text[250];
    VAR
      GLAccount@1000 : Record 15;
      SelectionFilterManagement@1001 : Codeunit 46;
    BEGIN
      GLAccount.SETRANGE("Account Subcategory Entry No.","Entry No.");
      EXIT(COPYSTR(SelectionFilterManagement.GetSelectionFilterForGLAccount(GLAccount),1,250));
    END;

    BEGIN
    END.
  }
}

