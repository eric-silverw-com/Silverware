OBJECT Codeunit 6085619 Export Continia Users
{
  OBJECT-PROPERTIES
  {
    Date=09/03/18;
    Time=[ 1:00:00 AM];
    Version List=DCW13.70.00.4.50.07;
  }
  PROPERTIES
  {
    OnRun=VAR
            ContiniaCompanySetup@6085573 : Record 6192773;
            ContiniaWebPortal@1160040000 : Record 6192774;
            ContiniaUser@1160040002 : Record 6086001;
            ContiniaUser2@1160040001 : Record 6086001;
            CEM@1160040004 : Codeunit 6085782;
          BEGIN
            Code;

            ContiniaCompanySetup.GET;
            IF NOT CEM.IsCEMEnabled(FALSE) THEN
              ContiniaCompanySetup.TESTFIELD("Web Portal Code");

            IF ContiniaWebPortal.GET(ContiniaCompanySetup."Web Portal Code") AND
              (ContiniaWebPortal."Welcome E-Mails" = ContiniaWebPortal."Welcome E-Mails"::"Send automatically")
            THEN
              SendWelcomeEmail
            ELSE BEGIN
              ContiniaUser.SETCURRENTKEY("Send Welcome E-mail");
              ContiniaUser.SETRANGE("Send Welcome E-mail",TRUE);
              IF NOT ContiniaUser.ISEMPTY THEN
                MESSAGE(Text025,ContiniaWebPortal.TABLECAPTION);
            END;

            CEM.SendWelcomeEmail2;
          END;

  }
  CODE
  {
    VAR
      Text000@1160040020 : TextConst 'ENU=Welcome to Continia Online';
      Text001@1160040012 : TextConst 'ENU=Dear %1';
      Text002@1160040013 : TextConst 'ENU=You have now been created as a user for Continia Document Capture. Before you get started please use the link below to activate your user account.';
      Text003@1160040014 : TextConst 'ENU=Account Details';
      Text004@1160040015 : TextConst 'ENU=Username: %1';
      Text005@1160040016 : TextConst 'ENU=Password:';
      Text006@1160040018 : TextConst 'ENU=Click here to specify and activate your account';
      Text007@1160040017 : TextConst 'ENU=When your account has been activated you can access Continia Online here: %1';
      Text008@1160040019 : TextConst 'ENU=Best regards,';
      Text009@1160040010 : TextConst 'ENU=%1 users exported.';
      Text015@1160040002 : TextConst 'ENU=No users were exported.';
      COMgt@1160040007 : Codeunit 6192772;
      XMLLib@1160040011 : Codeunit 6085573;
      HttpActions@1160040009 : 'PUT,DELETE,GET';
      Protocol@1160040008 : 'IMAP,ContiniaMail';
      Text017@1160040021 : TextConst 'ENU=You have now been created as a user for Continia Online.';
      Text018@1160040022 : TextConst 'ENU=Your Windows password';
      Text019@1160040023 : TextConst 'ENU=Your account has already been activated and you can access Continia Online here: %1';
      Text020@1160040024 : TextConst 'ENU=Sending Welcome E-mails...\@1@@@@@@@@@@@@@@@@@@@@@@@@@@';
      Text021@1160040027 : TextConst 'ENU=Exporting Continia users...';
      Text022@1160040026 : TextConst 'ENU=Exporting Companies';
      Text023@1160040033 : TextConst 'ENU=Exporting Users';
      Text024@1160040028 : TextConst 'ENU=Uploading Users';
      Text025@1160040001 : TextConst 'ENU=One or more users have not received a welcome e-mail, because welcome e-mails has been set up to be send manually in %1.';
      LicenseExpiredErr@116004004 : TextConst 'ENU=%1 has not been activated correctly for company %2. Please activate or deactivate the company from the %1 Setup card.';
      CompanyNotActivatedErr@6085574 : TextConst 'ENU=There is something wrong with the Continia setup.\\Please reactivate your Continia product before you can continue.';
      ConflictingWebportalSetupUrlEr@6085575 : TextConst 'ENU=%1 must be unique for each %2.\\Please update the Continia Web Portal setup and export Continia Users again.';
      ConflictingCompCodeOnPortalErr@6085573 : TextConst 'ENU=Company %1 and %2 have been assigned the same %3 (%4) in %5.\\%3 must be unique for all companies using the same web site (%6 in %5).';

    LOCAL PROCEDURE Code@1160040037() : Boolean;
    VAR
      Company@1160040012 : Record 2000000006;
      CompanyNeedReactivation@1160040031 : TEMPORARY Record 2000000006;
      ContiniaUser@1160040011 : Record 6086001;
      ContiniaUserSetup@1160040014 : Record 6086002;
      ContiniaCompanySetup@6085573 : Record 6192773;
      ContiniaWebPortal@1160040017 : Record 6192774;
      ContiniaWebPortal2@6085574 : Record 6192774;
      AboutDC@1160040015 : Codeunit 6085589;
      CODC@1160040000 : Codeunit 6085617;
      CEM@1160040010 : Codeunit 6085782;
      COLMgt@1160040027 : Codeunit 6192773;
      ContiniaWebPortalList@6085575 : Page 6192774;
      CloudXmlDoc@1160040021 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlDocument";
      OnPremXmlDoc@1160040022 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlDocument";
      CloudUsersNode@1160040019 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
      OnPremUsersNode@1160040008 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
      CloudDatabaseNode@1160040020 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
      OnPremDatabaseNode@1160040007 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
      CloudWindowsNode@1160040023 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
      OnPremWindowsNode@1160040006 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
      CloudUserNode@1160040024 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
      OnPremUserNode@1160040005 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
      CloudCompaniesNode@1160040025 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
      OnPremCompaniesNode@1160040016 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
      Window@1160040013 : Dialog;
      TotalCount@1160040028 : Integer;
      RecCount@1160040001 : Integer;
      ExportCount@1160040029 : Integer;
      UseCloudApproval@1160040004 : Boolean;
      ExportOnPrem@1160040002 : Boolean;
      ExportCloud@1160040003 : Boolean;
      DCBuildVersion@1160040009 : Text[30];
      CEMBuildVersion@1160040018 : Text[30];
      XMLDocsToUpload@1160040026 : Integer;
      ActivationState@1160040030 : 'NeverActivated,NeedReactivation,Activated,OfflineStarted';
    BEGIN
      ContiniaCompanySetup.GET;
      IF NOT CEM.IsCEMEnabled(FALSE) THEN
        ContiniaCompanySetup.TESTFIELD("Web Portal Code");

      IF GUIALLOWED THEN BEGIN
        Window.OPEN(CreateDialogText);
        TotalCount := Company.COUNT;
      END;

      IF ContiniaWebPortal.GET(ContiniaCompanySetup."Web Portal Code") AND
        NOT ContiniaWebPortal."Use Continia Online Web Portal"
      THEN BEGIN
        ContiniaWebPortal.TESTFIELD("Web Site Url");

        ContiniaWebPortal2.SETRANGE("Use Continia Online Web Portal",FALSE);
        ContiniaWebPortal2.SETRANGE("Web Site Url",ContiniaWebPortal."Web Site Url");
        IF ContiniaWebPortal2.COUNT > 1 THEN
          ERROR(ConflictingWebportalSetupUrlEr,ContiniaWebPortal2.FIELDCAPTION("Web Site Url"),ContiniaWebPortal2.TABLECAPTION);
      END;

      IF Company.FINDSET THEN
        REPEAT
          IF GUIALLOWED THEN BEGIN
            RecCount += 1;

            Window.UPDATE(1,CalcProgress(TotalCount,RecCount));
          END;

          CheckCompanySetup(Company.Name);

          IF SameWebPortal(Company.Name) THEN BEGIN
            IF (COLMgt.GetCompanyActivationState('CDC',Company.Name) <> ActivationState::NeedReactivation) AND
               (COLMgt.GetCompanyActivationState('CEM',Company.Name) <> ActivationState::NeedReactivation)
            THEN BEGIN
              IF COLMgt.GetCompanyActivationState('CDC',Company.Name) = ActivationState::Activated THEN
                Company.MARK(TRUE);

              IF COLMgt.GetCompanyActivationState('CEM',Company.Name) = ActivationState::Activated THEN
                Company.MARK(TRUE);
            END;
          END;
        UNTIL Company.NEXT = 0;

      Company.MARKEDONLY(TRUE);
      IF Company.COUNT > 0 THEN BEGIN
        DCBuildVersion := AboutDC.BuildVersion;
        CEMBuildVersion := CEM.CEMBuildVersion;

        ContiniaUser.SETFILTER("E-Mail",'<>%1','');
        IF ContiniaUser.FINDSET THEN BEGIN
          IF GUIALLOWED THEN BEGIN
            RecCount := 0;
            TotalCount := ContiniaUser.COUNT;
          END;

          REPEAT
            IF GUIALLOWED THEN BEGIN
              RecCount += 1;
              Window.UPDATE(2,CalcProgress(TotalCount,RecCount));
            END;

            CLEAR(CloudUserNode);
            CLEAR(OnPremUserNode);
            IF Company.FINDSET THEN
              REPEAT
                ContiniaUserSetup.CHANGECOMPANY(Company.Name);
                IF ContiniaUserSetup.GET(ContiniaUser."User ID") THEN BEGIN
                  ContiniaCompanySetup.CHANGECOMPANY(Company.Name);
                  IF ContiniaCompanySetup.GET THEN BEGIN
                    UseCloudApproval := (ContiniaWebPortal.GET(ContiniaCompanySetup."Web Portal Code") AND
                      ContiniaWebPortal."Use Continia Online Web Portal");
                    ExportOnPrem := (ContiniaWebPortal.GET(ContiniaCompanySetup."Web Portal Code") AND
                      NOT ContiniaWebPortal."Use Continia Online Web Portal");
                    ExportCloud := UseCloudApproval OR CEM.IsCEMEnabled(FALSE);
                  END ELSE BEGIN
                    UseCloudApproval := FALSE;
                    ExportOnPrem := FALSE;
                    ExportCloud := FALSE;
                  END;

                  IF ContiniaUserSetup."Expense Management User" OR
                    (ContiniaUserSetup."Web Approval User" AND
                    ContiniaWebPortal.GET(ContiniaCompanySetup."Web Portal Code") AND
                    ContiniaWebPortal."Use Continia Online Web Portal")
                  THEN BEGIN
                    IF ExportCloud THEN BEGIN
                      IF ISNULL(CloudXmlDoc) THEN
                        CreateUsersXml(ContiniaWebPortal,DCBuildVersion,CEMBuildVersion,
                          CloudXmlDoc,CloudUsersNode,CloudWindowsNode,CloudDatabaseNode);

                      IF ISNULL(CloudUserNode) THEN
                        IF IsWindowsUser(ContiniaUser) THEN
                          CreateUserXml(CloudWindowsNode,ContiniaWebPortal,ContiniaUser,CloudUserNode,CloudCompaniesNode)
                        ELSE
                          CreateUserXml(CloudDatabaseNode,ContiniaWebPortal,ContiniaUser,CloudUserNode,CloudCompaniesNode);

                      CreateCompanyXml(CloudCompaniesNode,Company,
                        ContiniaUserSetup."Expense Management User",ContiniaUserSetup."Web Approval User" AND UseCloudApproval,
                          ContiniaUserSetup."Expense User Group");
                    END;
                  END;

                  // Export users to OnPrem Web Approval if needed
                  IF ExportOnPrem AND ContiniaUserSetup."Web Approval User" THEN BEGIN
                    ContiniaCompanySetup.TESTFIELD("Web Portal Code");
                    ContiniaWebPortal.GET(ContiniaCompanySetup."Web Portal Code");
                    IF ISNULL(OnPremXmlDoc) THEN
                      CreateUsersXml(ContiniaWebPortal,DCBuildVersion,CEMBuildVersion,
                        OnPremXmlDoc,OnPremUsersNode,OnPremWindowsNode,OnPremDatabaseNode);

                    IF ISNULL(OnPremUserNode) THEN
                      IF IsWindowsUser(ContiniaUser) THEN
                        CreateUserXml(OnPremWindowsNode,ContiniaWebPortal,ContiniaUser,OnPremUserNode,OnPremCompaniesNode)
                      ELSE
                        CreateUserXml(OnPremDatabaseNode,ContiniaWebPortal,ContiniaUser,OnPremUserNode,OnPremCompaniesNode);

                    CreateCompanyXml(OnPremCompaniesNode,Company,FALSE,ContiniaUserSetup."Web Approval User",'');
                  END;
                END;
              UNTIL Company.NEXT = 0;

            IF NOT (ISNULL(OnPremUserNode) AND ISNULL(CloudUserNode)) THEN
              ExportCount += 1;
          UNTIL ContiniaUser.NEXT = 0;

          IF ExportCount <> 0 THEN BEGIN
            IF GUIALLOWED THEN BEGIN
              IF NOT ISNULL(CloudXmlDoc) AND NOT ISNULL(OnPremXmlDoc) THEN
                XMLDocsToUpload := 2
              ELSE
                XMLDocsToUpload := 1;

              IF XMLDocsToUpload = 1 THEN
                Window.UPDATE(3,5000)
              ELSE
                Window.UPDATE(3,3333);
            END;

            IF NOT ISNULL(CloudXmlDoc) THEN BEGIN
              COMgt.ExecuteRequest(CODC.GetCOUserUrl,HttpActions::PUT,TRUE,CloudXmlDoc);
              SaveActivateLinks(CloudXmlDoc);
              COMMIT;

              IF GUIALLOWED THEN
                IF XMLDocsToUpload = 1 THEN
                  Window.UPDATE(3,10000)
                ELSE
                  Window.UPDATE(3,6667);
            END;

            IF NOT ISNULL(OnPremXmlDoc) THEN BEGIN
              COMgt.ExecuteRequestWithoutToken(CODC.GetUserUrl,HttpActions::PUT,TRUE,OnPremXmlDoc);
              SaveActivateLinks(OnPremXmlDoc);
              COMMIT;

              IF GUIALLOWED THEN
                Window.UPDATE(3,10000);
            END;
          END;
        END;
      END;

      IF GUIALLOWED THEN
        Window.CLOSE;

      IF ExportCount = 0 THEN
        MESSAGE(Text015)
      ELSE
        MESSAGE(Text009,ExportCount);
    END;

    LOCAL PROCEDURE CheckCompanySetup@1160040057(_CompanyName@1160040000 : Text[30]);
    VAR
      ContiniaCompanySetup@6085573 : Record 6192773;
      CCSetup2@6085575 : Record 6192773;
      ContiniaWebPortal@1160040003 : Record 6192774;
      ContiniaUser@1160040005 : Record 6086001;
      ContiniaUserSetup@1160040002 : Record 6086002;
      Comp@6085574 : Record 2000000006;
      EmptyGUID@1160040004 : GUID;
      Stop@1160040006 : Boolean;
    BEGIN
      ContiniaUserSetup.CHANGECOMPANY(_CompanyName);
      ContiniaUserSetup.SETRANGE("Web Approval User",TRUE);
      IF ContiniaUserSetup.ISEMPTY THEN
        EXIT;

      ContiniaCompanySetup.CHANGECOMPANY(_CompanyName);
      IF NOT ContiniaCompanySetup.GET THEN
        EXIT;

      IF ContiniaCompanySetup."Company GUID" = EmptyGUID THEN
        ERROR(CompanyNotActivatedErr);

      IF ContiniaUserSetup.FINDSET THEN
        REPEAT
          ContiniaUser.GET(ContiniaUserSetup."Continia User ID");
          IF IsWindowsUser(ContiniaUser) AND ContiniaWebPortal.GET(ContiniaCompanySetup."Web Portal Code") THEN BEGIN
            ContiniaWebPortal.TESTFIELD("Windows Web Service URL");
            ContiniaWebPortal.TESTFIELD("Local Domain Name");
            Stop := TRUE;
          END;
        UNTIL (ContiniaUserSetup.NEXT = 0) OR Stop;

      Stop := FALSE;
      IF ContiniaUserSetup.FINDSET THEN
        REPEAT
          ContiniaUser.GET(ContiniaUserSetup."Continia User ID");
          IF ContiniaUser."NAV Login Type" = ContiniaUser."NAV Login Type"::Database THEN BEGIN
            IF ContiniaWebPortal.GET(ContiniaCompanySetup."Web Portal Code") THEN
              ContiniaWebPortal.TESTFIELD("Database Web Service URL");
            Stop := TRUE;
          END;
        UNTIL (ContiniaUserSetup.NEXT = 0) OR Stop;

      Comp.SETFILTER(Name,'<>%1',_CompanyName);
      IF Comp.FINDSET THEN
        REPEAT
          CCSetup2.CHANGECOMPANY(Comp.Name);
          IF CCSetup2.GET AND
            ((CCSetup2."Web Portal Code" = ContiniaCompanySetup."Web Portal Code") AND
            (ContiniaCompanySetup."Web Portal Code" <> '')) AND
            (CCSetup2."Company Code" = ContiniaCompanySetup."Company Code")
          THEN
            ERROR(ConflictingCompCodeOnPortalErr,_CompanyName,Comp.Name,ContiniaCompanySetup.FIELDCAPTION("Company Code"),
              ContiniaCompanySetup."Company Code",ContiniaCompanySetup.TABLECAPTION,
              ContiniaCompanySetup.FIELDCAPTION("Web Portal Code"));
        UNTIL Comp.NEXT = 0;
    END;

    LOCAL PROCEDURE CreateUsersXml@1160040042(ContiniaWebPortal@1160040002 : Record 6192774;DCBuildVersion@1160040003 : Text[30];CEMBuildVersion@1160040006 : Text[30];VAR XmlDoc@1160040001 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlDocument";VAR UsersNode@1160040000 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";VAR WindowsNode@1160040004 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";VAR DatabaseNode@1160040005 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode");
    BEGIN
      XmlDoc := XmlDoc.XmlDocument;
      UsersNode := XmlDoc.CreateNode('element','Users','');
      XMLLib.SetXmlNodeAttr(UsersNode,'Version','2');
      XmlDoc.AppendChild(UsersNode);

      XMLLib.CreateXmlNode(UsersNode,'DatabaseLogins','',DatabaseNode);
      XMLLib.SetXmlNodeAttr(DatabaseNode,'WebServiceUrl',ContiniaWebPortal."Database Web Service URL");
      XMLLib.SetXmlNodeAttr(DatabaseNode,'Tenant',ContiniaWebPortal.Tenant);
      XMLLib.SetXmlNodeAttr(DatabaseNode,'DCVersion',DCBuildVersion);
      XMLLib.SetXmlNodeAttr(DatabaseNode,'EMVersion',CEMBuildVersion);

      XMLLib.CreateXmlNode(UsersNode,'WindowsLogins','',WindowsNode);
      XMLLib.SetXmlNodeAttr(WindowsNode,'WebServiceUrl',ContiniaWebPortal."Windows Web Service URL");
      XMLLib.SetXmlNodeAttr(WindowsNode,'Tenant',ContiniaWebPortal.Tenant);
      XMLLib.SetXmlNodeAttr(WindowsNode,'DCVersion',DCBuildVersion);
      XMLLib.SetXmlNodeAttr(WindowsNode,'EMVersion',CEMBuildVersion);
    END;

    LOCAL PROCEDURE CreateUserXml@1160040047(VAR ParentNode@1160040001 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";ContiniaWebPortal@1160040004 : Record 6192774;ContiniaUser@1160040003 : Record 6086001;VAR UserNode@1160040000 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";VAR CompaniesNode@1160040002 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode") : Boolean;
    VAR
      WebServiceKey@1160040005 : Text[250];
    BEGIN
      XMLLib.CreateXmlNode(ParentNode,'User','',UserNode);
      XMLLib.CreateXmlNode(UserNode,'Companies','',CompaniesNode);

      IF IsWindowsUser(ContiniaUser) THEN BEGIN
        IF STRPOS(ContiniaUser."User ID",'\') > 0 THEN
          XMLLib.SetXmlNodeAttr(UserNode,'WindowsUsername',ContiniaUser."User ID")
        ELSE
          XMLLib.SetXmlNodeAttr(UserNode,'WindowsUsername',ContiniaWebPortal."Local Domain Name" + '\' + ContiniaUser."User ID");
      END ELSE BEGIN
        WebServiceKey := COMgt.GetUserWebServiceKey(ContiniaUser."User ID");
        XMLLib.SetXmlNodeAttr(UserNode,'WebServiceKey',WebServiceKey);
      END;

      XMLLib.SetXmlNodeAttr(UserNode,'Email',ContiniaUser."E-Mail");
      XMLLib.SetXmlNodeAttr(UserNode,'Username',ContiniaUser."User ID");
      XMLLib.SetXmlNodeAttr(UserNode,'FullName',ContiniaUser.GetName);
    END;

    LOCAL PROCEDURE CreateCompanyXml@1160040048(VAR CompaniesNode@1160040000 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";Company@1160040002 : Record 2000000006;IsEMUser@1160040004 : Boolean;IsWAUser@1160040003 : Boolean;EMUserGroup@1160040006 : Code[20]) : Boolean;
    VAR
      CompanyNode@1160040001 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
      ContiniaCompanySetup@6085573 : Record 6192773;
    BEGIN
      ContiniaCompanySetup.CHANGECOMPANY(Company.Name);
      ContiniaCompanySetup.GET;

      XMLLib.CreateXmlNode(CompaniesNode,'Company','',CompanyNode);
      XMLLib.SetXmlNodeAttr(CompanyNode,'Name',Company.Name);
      XMLLib.SetXmlNodeAttr(CompanyNode,'Code',ContiniaCompanySetup."Company Code");
      XMLLib.SetXmlNodeAttr(CompanyNode,'Guid',ContiniaCompanySetup."Company GUID");
      IF IsWAUser THEN
        XMLLib.SetXmlNodeAttr(CompanyNode,'WAUser',XMLLib.Bool2Text(IsWAUser));
      IF IsEMUser THEN
        XMLLib.SetXmlNodeAttr(CompanyNode,'EMUser',XMLLib.Bool2Text(IsEMUser));
      IF EMUserGroup <> '' THEN
        XMLLib.SetXmlNodeAttr(CompanyNode,'EMUserGroup',EMUserGroup);
    END;

    PROCEDURE SaveActivateLinks@1160040000(VAR XmlDoc@1160040001 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlDocument");
    VAR
      ContiniaUser@1160040000 : Record 6086001;
      XMLNodeList@1160040004 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNodeList";
      ActivationLink@1160040006 : Text[1024];
      EMActivationLink@1160040002 : Text[1024];
      Username@1160040007 : Text[1024];
      i@1160040005 : Integer;
    BEGIN
      IF NOT XmlDoc.HasChildNodes THEN
        EXIT;

      XMLNodeList := XmlDoc.DocumentElement.SelectNodes('User');

      FOR i := 0 TO XMLNodeList.Count - 1 DO BEGIN
        COMgt.GetAttribAsText(XMLNodeList,'Username',i,Username);
        COMgt.GetAttribAsText(XMLNodeList,'Link',i,ActivationLink);
        COMgt.GetAttribAsText(XMLNodeList,'EMLink',i,EMActivationLink);

        IF ContiniaUser.GET(Username) THEN BEGIN
          ContiniaUser."Activation Link (EM)" := EMActivationLink;
          ContiniaUser."Activation Link" := ActivationLink;
          ContiniaUser.MODIFY(TRUE);
        END;
      END;
    END;

    PROCEDURE SendWelcomeEmail@1160040002();
    VAR
      ContiniaCompanySetup@1160040002 : Record 6192773;
      ContiniaWebPortal@6086300 : Record 6192774;
      ContiniaUser@1160040001 : Record 6086001;
      ContiniaUser2@1160040004 : Record 6086001;
      SmtpMailMgt@1160040000 : Codeunit 6085598;
      ContiniaUserMgt@1160040007 : Codeunit 6085781;
      Window@1160040005 : Dialog;
      TotalCount@1160040010 : Integer;
      RecCount@1160040009 : Integer;
    BEGIN
      ContiniaUser.SETCURRENTKEY("Send Welcome E-mail");
      ContiniaUser.SETRANGE("Send Welcome E-mail",TRUE);
      IF ContiniaUser.FINDSET(TRUE,TRUE) THEN BEGIN
        ContiniaCompanySetup.GET;
        ContiniaCompanySetup.TESTFIELD("Web Portal Code");
        ContiniaWebPortal.GET(ContiniaCompanySetup."Web Portal Code");

        IF GUIALLOWED THEN BEGIN
          Window.OPEN(Text020);
          TotalCount := ContiniaUser.COUNT;
        END;

        REPEAT
          IF ContiniaUser.IsWebApprovalUser(TRUE) THEN BEGIN
            IF GUIALLOWED THEN BEGIN
              RecCount += 1;
              Window.UPDATE(1,CalcProgress(TotalCount,RecCount));
            END;

            SmtpMailMgt.AddTo(ContiniaUser.Name,ContiniaUser."E-Mail");
            SmtpMailMgt.SetSubject(Text000);

            SmtpMailMgt.AppendToBody(STRSUBSTNO('<b>%1</b>',STRSUBSTNO(Text001,ContiniaUser.Name)),TRUE);
            SmtpMailMgt.AppendLineBreakToBody;

            IF IsWindowsUser(ContiniaUser) THEN BEGIN
              SmtpMailMgt.AppendToBody(Text017,TRUE);
              SmtpMailMgt.AppendLineBreakToBody;
              SmtpMailMgt.AppendToBody(STRSUBSTNO('<b>%1</b>',Text003),TRUE);
              IF ContiniaWebPortal."Use Continia Online Web Portal" THEN
                SmtpMailMgt.AppendToBody(STRSUBSTNO(Text004,ContiniaUser."E-Mail"),TRUE)
              ELSE
                SmtpMailMgt.AppendToBody(STRSUBSTNO(Text004,ContiniaUserMgt.GetNAVUserID(ContiniaUser."User ID")),TRUE);
              SmtpMailMgt.AppendToBody(STRSUBSTNO('%1 %2',Text005,Text018),TRUE);
              SmtpMailMgt.AppendLineBreakToBody;
              SmtpMailMgt.AppendToBody(STRSUBSTNO(Text019,ContiniaWebPortal."Web Site Url"),TRUE);
            END ELSE BEGIN
              ContiniaUser.TESTFIELD("Activation Link");
              SmtpMailMgt.AppendToBody(Text002,TRUE);
              SmtpMailMgt.AppendLineBreakToBody;
              SmtpMailMgt.AppendToBody(STRSUBSTNO('<b>%1</b>',Text003),TRUE);
              IF ContiniaWebPortal."Use Continia Online Web Portal" THEN
                SmtpMailMgt.AppendToBody(STRSUBSTNO(Text004,ContiniaUser."E-Mail"),TRUE)
              ELSE
                SmtpMailMgt.AppendToBody(STRSUBSTNO(Text004,ContiniaUserMgt.GetNAVUserID(ContiniaUser."User ID")),TRUE);
              SmtpMailMgt.AppendToBody(STRSUBSTNO('%1<a href=%3>%2</a>',Text005,Text006,ContiniaUser."Activation Link"),TRUE);
              SmtpMailMgt.AppendLineBreakToBody;
              SmtpMailMgt.AppendToBody(STRSUBSTNO(Text007,ContiniaWebPortal."Web Site Url"),TRUE);
            END;

            SmtpMailMgt.AppendLineBreakToBody;
            SmtpMailMgt.AppendToBody(Text008,TRUE);
            SmtpMailMgt.AppendToBody('Continia Software A/S',TRUE);

            IF NOT SmtpMailMgt.Send THEN
              SmtpMailMgt.DisplayLastError;

            CLEAR(SmtpMailMgt);

            ContiniaUser2.GET(ContiniaUser."User ID");
            ContiniaUser2."Send Welcome E-mail" := FALSE;
            ContiniaUser2.MODIFY(TRUE);
            COMMIT;
          END;
        UNTIL ContiniaUser.NEXT = 0;

        IF GUIALLOWED THEN
          Window.CLOSE;
      END;
    END;

    PROCEDURE CalcProgress@161024017(TotalCount@161024012 : Integer;Index@161024013 : Integer) : Integer;
    BEGIN
      EXIT(ROUND(Index / TotalCount * 10000,1,'>'));
    END;

    PROCEDURE CreateDialogText@1160040003() : Text[1024];
    VAR
      SubText@1160040004 : ARRAY [8] OF Text[100];
    BEGIN
      SubText[1] := Text022;
      SubText[2] := Text023;
      SubText[3] := Text024;
      EXIT(CreateDialogText2(
        '%1\' +
        '%2 @1@@@@@@@@@@@@@\' +
        '%3 @2@@@@@@@@@@@@@\' +
        '%4 @3@@@@@@@@@@@@@',
        Text021,
        SubText));
    END;

    PROCEDURE CreateDialogText2@1160040005(MainText@1160040000 : Text[250];CaptionText@1160040005 : Text[100];SubText@1160040001 : ARRAY [8] OF Text[100]) DialogText : Text[1024];
    VAR
      NewSubText@1160040004 : ARRAY [8] OF Text[100];
      i@1160040003 : Integer;
      MaxSubTextLenght@1160040002 : Integer;
    BEGIN
      FOR i := 1 TO 8 DO
        IF STRLEN(SubText[i]) > MaxSubTextLenght THEN
          MaxSubTextLenght := STRLEN(SubText[i]);

      FOR i := 1 TO 8 DO
        NewSubText[i] := SubText[i] + PADSTR('',MaxSubTextLenght - STRLEN(SubText[i]),' ');

      DialogText := STRSUBSTNO(MainText,CaptionText,NewSubText[1],NewSubText[2],NewSubText[3],NewSubText[3],NewSubText[5],
        NewSubText[6],NewSubText[7],NewSubText[8]);
    END;

    LOCAL PROCEDURE SameWebPortal@6085574(CompanyName@6085573 : Text[30]) : Boolean;
    VAR
      ContiniaCompanySetup@6085574 : Record 6192773;
      ContiniaCompanySetup2@6085575 : Record 6192773;
    BEGIN
      ContiniaCompanySetup.GET;
      ContiniaCompanySetup2.CHANGECOMPANY(CompanyName);
      IF NOT ContiniaCompanySetup2.GET THEN
        EXIT(FALSE);

      IF ContiniaCompanySetup."Client ID" <> ContiniaCompanySetup2."Client ID" THEN
        EXIT(FALSE);

      EXIT(ContiniaCompanySetup."Web Portal Code" = ContiniaCompanySetup2."Web Portal Code");
    END;

    PROCEDURE IsWindowsUser@1160040001(ContiniaUser@1160040000 : Record 6086001) : Boolean;
    BEGIN
      IF (ContiniaUser."NAV Login Type" = ContiniaUser."NAV Login Type"::Windows) AND
        (COMgt.GetUserWebServiceKey(ContiniaUser."User ID") = '')
      THEN
        EXIT(TRUE);
    END;

    BEGIN
    END.
  }
}

