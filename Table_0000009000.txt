OBJECT Table 9000 User Group
{
  OBJECT-PROPERTIES
  {
    Date=09/19/18;
    Time=12:00:00 PM;
    Version List=NAVW113.00;
  }
  PROPERTIES
  {
    DataPerCompany=No;
    OnInsert=BEGIN
               TESTFIELD(Code);
               Customized := TRUE;
             END;

    OnModify=BEGIN
               Customized := TRUE;
             END;

    OnDelete=VAR
               UserGroupMember@1000 : Record 9001;
             BEGIN
               UserGroupMember.SETRANGE("User Group Code",Code);
               IF NOT UserGroupMember.ISEMPTY THEN
                 ERROR(HasMembersErr);
             END;

    OnRename=BEGIN
               Customized := TRUE;
             END;

    ReplicateData=No;
    CaptionML=[ENU=User Group;
               ESM=Grupo de usuarios;
               FRC=Groupe d'utilisateurs;
               ENC=User Group];
    LookupPageID=Page9830;
  }
  FIELDS
  {
    { 1   ;   ;Code                ;Code20        ;CaptionML=[ENU=Code;
                                                              ESM=C¢digo;
                                                              FRC=Code;
                                                              ENC=Code];
                                                   NotBlank=Yes }
    { 2   ;   ;Name                ;Text50        ;CaptionML=[ENU=Name;
                                                              ESM=Nombre;
                                                              FRC=Nom;
                                                              ENC=Name] }
    { 3   ;   ;Default Profile ID  ;Code30        ;TableRelation="All Profile"."Profile ID";
                                                   CaptionML=[ENU=Default Profile ID;
                                                              ESM=Id. de perfil predeterminado;
                                                              FRC=Code de profil par d‚faut;
                                                              ENC=Default Profile ID] }
    { 4   ;   ;Assign to All New Users;Boolean    ;OnValidate=VAR
                                                                UserGroup@1000 : Record 9000;
                                                                PermissionManager@1001 : Codeunit 9002;
                                                              BEGIN
                                                                IF NOT PermissionManager.SoftwareAsAService THEN
                                                                  EXIT;

                                                                IF "Assign to All New Users" = TRUE THEN
                                                                  EXIT;

                                                                // When deselecting this option, if the last auto-assign user group is deselected,
                                                                // it means that new users will get no permissions
                                                                // and therefore they will not be able to login.
                                                                UserGroup.SETRANGE("Assign to All New Users",TRUE);
                                                                UserGroup.SETFILTER(Code,'<>%1',Code);
                                                                IF UserGroup.COUNT = 0 THEN
                                                                  IF NOT CONFIRM(NewUsersCannotLoginQst) THEN
                                                                    ERROR('');
                                                              END;

                                                   CaptionML=[ENU=Assign to All New Users;
                                                              ESM=Asignar a todos los usuarios nuevos;
                                                              FRC=Assigner … tous les nouveaux utilisateurs;
                                                              ENC=Assign to All New Users] }
    { 5   ;   ;Customized          ;Boolean       ;CaptionML=[ENU=Customized;
                                                              ESM=Personalizado;
                                                              FRC=Personnalis‚;
                                                              ENC=Customized];
                                                   Editable=No }
    { 6   ;   ;Default Profile App ID;GUID        ;CaptionML=[ENU=Default Profile App ID;
                                                              ESM=Id. de aplicaci¢n de perfil predeterminado;
                                                              FRC=Code d'application de profil par d‚faut;
                                                              ENC=Default Profile App ID] }
    { 7   ;   ;Default Profile Scope;Option       ;CaptionML=[ENU=Default Profile Scope;
                                                              ESM=µmbito de perfil predeterminado;
                                                              FRC=Port‚e du profil par d‚faut;
                                                              ENC=Default Profile Scope];
                                                   OptionCaptionML=[ENU=System,Tenant;
                                                                    ESM=Sistema,Suscriptor;
                                                                    FRC=SystŠme,Abonn‚;
                                                                    ENC=System,Tenant];
                                                   OptionString=System,Tenant }
  }
  KEYS
  {
    {    ;Code                                    ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      HasMembersErr@1002 : TextConst 'ENU=You cannot delete this user group because it has members.;ESM=No es posible eliminar este grupo de usuarios porque tiene miembros.;FRC=Vous ne pouvez pas supprimer ce groupe d''utilisateurs car il contient des membres.;ENC=You cannot delete this user group because it has members.';
      NewUsersCannotLoginQst@1000 : TextConst 'ENU=You have not specified a user group that will be assigned automatically to new users. If users are not assigned a user group, they cannot sign in. \\Do you want to continue?;ESM=No especific¢ el grupo de usuarios al que se asignar n autom ticamente los nuevos usuarios. Si estos usuarios no se asignan a un grupo, no podr n iniciar sesi¢n. \\¨Quiere continuar?;FRC=Vous n''avez sp‚cifi‚ aucun groupe utilisateurs … automatiquement affecter aux nouveaux utilisateurs. Si ces derniers ne sont affect‚s … aucun groupe utilisateurs, il ne leur sera pas possible de se connecter. \\Souhaitez-vous continuerÿ?;ENC=You have not specified a user group that will be assigned automatically to new users. If users are not assigned a user group, they cannot sign in. \\Do you want to continue?';

    [External]
    PROCEDURE IsUserMember@14(VAR User@1001 : Record 2000000120;SelectedCompany@1000 : Text[30]) : Boolean;
    VAR
      UserGroupMember@1002 : Record 9001;
    BEGIN
      UserGroupMember.SETRANGE("User Group Code",Code);
      UserGroupMember.SETRANGE("User Security ID",User."User Security ID");
      UserGroupMember.SETRANGE("Company Name",SelectedCompany);
      EXIT(NOT UserGroupMember.ISEMPTY);
    END;

    [External]
    PROCEDURE SetUserGroupMembership@7(VAR User@1001 : Record 2000000120;NewUserGroupMembership@1003 : Boolean;SelectedCompany@1000 : Text[30]);
    VAR
      UserGroupMember@1002 : Record 9001;
    BEGIN
      IF UserGroupMember.GET(Code,User."User Security ID",SelectedCompany) THEN BEGIN
        IF NOT NewUserGroupMembership THEN
          UserGroupMember.DELETE(TRUE);
        EXIT;
      END;
      IF NOT NewUserGroupMembership THEN
        EXIT;
      UserGroupMember.INIT;
      UserGroupMember."User Group Code" := Code;
      UserGroupMember."User Security ID" := User."User Security ID";
      UserGroupMember."Company Name" := SelectedCompany;
      UserGroupMember.INSERT(TRUE);
    END;

    PROCEDURE ExportUserGroups@1(FileName@1000 : Text) : Text;
    VAR
      TempBlob@1004 : Record 99008535;
      FileManagement@1003 : Codeunit 419;
      ExportImportUserGroups@1002 : XMLport 9000;
      OutStr@1001 : OutStream;
    BEGIN
      TempBlob.INIT;
      TempBlob.Blob.CREATEOUTSTREAM(OutStr);
      ExportImportUserGroups.SETTABLEVIEW(Rec);
      ExportImportUserGroups.SETDESTINATION(OutStr);
      ExportImportUserGroups.EXPORT;
      IF FileName = '' THEN
        EXIT(FileManagement.BLOBExport(TempBlob,TABLECAPTION + '.xml',TRUE));
      EXIT(FileManagement.BLOBExport(TempBlob,FileName,FALSE));
    END;

    PROCEDURE ImportUserGroups@2(FileName@1000 : Text);
    VAR
      TempBlob@1004 : Record 99008535;
      FileManagement@1003 : Codeunit 419;
      ExportImportUserGroups@1002 : XMLport 9000;
      InStr@1001 : InStream;
    BEGIN
      TempBlob.INIT;
      IF FileManagement.BLOBImport(TempBlob,FileName) = '' THEN
        EXIT;
      TempBlob.Blob.CREATEINSTREAM(InStr);
      ExportImportUserGroups.SETSOURCE(InStr);
      ExportImportUserGroups.IMPORT;
    END;

    BEGIN
    END.
  }
}

