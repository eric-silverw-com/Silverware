OBJECT Table 6702 Booking Sync
{
  OBJECT-PROPERTIES
  {
    Date=11/27/18;
    Time=11:15:27 PM;
    Version List=;
  }
  PROPERTIES
  {
    Permissions=TableData 1261=rimd;
    CaptionML=ENU=Booking Sync;
  }
  FIELDS
  {
    { 1   ;   ;Primary Key         ;Code10        ;CaptionML=ENU=Primary Key }
    { 2   ;   ;Booking Mailbox Address;Text80     ;CaptionML=ENU=Booking Mailbox Address }
    { 3   ;   ;Booking Mailbox Name;Text250       ;CaptionML=ENU=Booking Mailbox Name }
    { 4   ;   ;User ID             ;Code50        ;TableRelation=User."User Name";
                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   DataClassification=EndUserIdentifiableInformation;
                                                   CaptionML=ENU=User ID;
                                                   NotBlank=Yes }
    { 5   ;   ;Last Customer Sync  ;DateTime      ;CaptionML=ENU=Last Customer Sync;
                                                   Editable=Yes }
    { 6   ;   ;Last Service Sync   ;DateTime      ;CaptionML=ENU=Last Service Sync }
    { 7   ;   ;Enabled             ;Boolean       ;CaptionML=ENU=Enabled }
    { 8   ;   ;Sync Customers      ;Boolean       ;CaptionML=ENU=Sync Customers }
    { 9   ;   ;Customer Filter     ;BLOB          ;CaptionML=ENU=Customer Filter }
    { 10  ;   ;Customer Template Code;Code10      ;TableRelation="Customer Template".Code;
                                                   CaptionML=ENU=Customer Template Code }
    { 12  ;   ;Sync Services       ;Boolean       ;CaptionML=ENU=Sync Services }
    { 13  ;   ;Item Filter         ;BLOB          ;CaptionML=ENU=Item Filter }
    { 14  ;   ;Item Template Code  ;Code10        ;TableRelation="Config. Template Header".Code WHERE (Table ID=FILTER(27));
                                                   CaptionML=ENU=Item Template Code }
  }
  KEYS
  {
    {    ;Primary Key                             ;Clustered=Yes }
    {    ;Booking Mailbox Address                  }
  }
  FIELDGROUPS
  {
  }
  CODE
  {

    [External]
    PROCEDURE GetCustomerFilter@1() FilterText : Text;
    VAR
      ReadStream@1000 : InStream;
    BEGIN
      CALCFIELDS("Customer Filter");
      "Customer Filter".CREATEINSTREAM(ReadStream);
      ReadStream.READTEXT(FilterText);
    END;

    [External]
    PROCEDURE GetItemFilter@2() FilterText : Text;
    VAR
      ReadStream@1000 : InStream;
    BEGIN
      CALCFIELDS("Item Filter");
      "Item Filter".CREATEINSTREAM(ReadStream);
      ReadStream.READTEXT(FilterText);
    END;

    [External]
    PROCEDURE SaveCustomerFilter@6(FilterText@1000 : Text);
    VAR
      WriteStream@1001 : OutStream;
    BEGIN
      CLEAR("Customer Filter");
      "Customer Filter".CREATEOUTSTREAM(WriteStream);
      WriteStream.WRITETEXT(FilterText);
      CLEAR("Last Customer Sync");
      MODIFY;
    END;

    [External]
    PROCEDURE SaveItemFilter@7(FilterText@1000 : Text);
    VAR
      WriteStream@1001 : OutStream;
    BEGIN
      CLEAR("Item Filter");
      "Item Filter".CREATEOUTSTREAM(WriteStream);
      WriteStream.WRITETEXT(FilterText);
      CLEAR("Last Service Sync");
      MODIFY;
    END;

    [External]
    PROCEDURE IsSetup@3() : Boolean;
    VAR
      PermissionManager@1000 : Codeunit 9002;
    BEGIN
      IF PermissionManager.SoftwareAsAService THEN
        EXIT(GET AND ("Last Service Sync" <> 0DT));
    END;

    BEGIN
    END.
  }
}

